
Функция ПолучитьHTMLКанбанТЗ(стПараметры = Неопределено, стНастройки = Неопределено) Экспорт

    Если стПараметры = Неопределено Тогда
		стПараметры = ПолучитьСтруктураКанбана();		
	КонецЕсли; 
	
	Если стНастройки = Неопределено Тогда
		
		стНастройки = Новый Структура();
		стНастройки.Вставить("ШиринаКолонкиТЗКанбан", 	250);
		стНастройки.Вставить("РазмерШрифтаТЗКанбан", 	16);
		стНастройки.Вставить("Сортировка", 				0);
		стНастройки.Вставить("Сотрудник", 				Справочники.Сотрудники.ПустаяСсылка());
		стНастройки.Вставить("Проект", 					Справочники.Проекты.ПустаяСсылка());
		стНастройки.Вставить("Отбор", 					Неопределено);
		
	КонецЕсли;
	
	стДанныеДляВывода = ПолучитьДанныеДляВывода(стПараметры, стНастройки); 
	
	мДанныеДляПоиска = Новый Массив;
	Для каждого х Из стДанныеДляВывода.ДанныеКарточек Цикл
	    мДанныеДляПоиска.Добавить(Новый Структура("Наименование, Ресурс", х.Наименование, х.Ресурс));
	КонецЦикла;
	
	Возврат Новый Структура("КодHTML, мДанныеДляПоиска", СформироватьИнтерфейс(стДанныеДляВывода, стПараметры, стНастройки), мДанныеДляПоиска);
	
КонецФункции // ПолучитьHTMLКанбанТЗ()

Функция СформироватьИнтерфейс(стДанныеДляВывода, стПараметры, стНастройки)

	Шаблон = ПолучитьШаблонHTML();
	
	Kanban = СформироватьКанбан(стДанныеДляВывода, стПараметры, стНастройки);
	
	Корзина = "";
	
	ШиринаКолонки 	= стНастройки.ШиринаКолонкиТЗКанбан;
	РазмерШрифта	= стНастройки.РазмерШрифтаТЗКанбан;
	
	css_body 				= ПолучитьМакет_css_body();
	css_kanban_container 	= ПолучитьМакет_css_kanban_container();
	css_category 			= ПолучитьМакет_css_category();
	css_items_container 	= ПолучитьМакет_css_items_container();
	css_item 				= ПолучитьМакет_css_item();
	JS 						= ПолучитьМакет_js();
	
	Добавить_JS(JS, стПараметры);

	css_category 			= СтрЗаменить(css_category, "%250%", 	ШиринаКолонки);
	css_category 			= СтрЗаменить(css_category, "&250&", 	Цел(ШиринаКолонки * 0.6));
	css_category 			= СтрЗаменить(css_category, "#16#", 	РазмерШрифта);

	CSS = css_body + css_kanban_container + css_category + css_items_container + css_item; 
	
	Возврат СтрШаблон(Шаблон, CSS, Kanban, Корзина, JS);
	
КонецФункции 

Процедура Добавить_JS(JS, стПараметры)

	стрЗамены = "";
	
	стрШаблон = "const source_%1 = document.getElementById(""%2"");
				|source_%1.addEventListener(""dragstart"", () => source_id = ""%2"");
				|
				|source_%1.addEventListener(""drop"", () => console.log(source_id));
				|
				|";
    
	сч = 1;
	
	Для каждого х Из стПараметры.мКолонки Цикл
	
		стрЗамены = стрЗамены + стрШаблон(стрШаблон, сч, х.УИД);
		сч = сч + 1;
	
	КонецЦикла;
	
	JS = стрШаблон(JS, стрЗамены);

КонецПроцедуры

Функция СформироватьКанбан(стДанныеДляВывода, стПараметры, стНастройки)

	Категории 	= Новый Массив;
	стОтбор 	= Новый Структура("Колонка", );

	ШаблонКатегории 				= ПолучитьШаблонКатегории();
	ШаблонКатегорииБезПеремещения 	= ПолучитьШаблонКатегорииБезПеремещения();
 	ШаблонЭлемента 					= ПолучитьШаблонЭлемента();
 	ШаблонЭлементаБезПеремещения	= ПолучитьШаблонЭлементаБезПеремещения();
	
	Для каждого ТекущаяКолонка Из стПараметры.мКолонки Цикл   //УИД, ИмяСобытия, ЛимитКарточек, ЛимитЧасов, Наименование, Цвет, ЗапрещенИнтерактивныйПереход, ЗапрещенИнтерактивныйУход
		
		текШаблонЭлемента = ?(ТекущаяКолонка.ЗапрещенИнтерактивныйУход, ШаблонЭлементаБезПеремещения, ШаблонЭлемента);	

		стОтбор.Колонка = ТекущаяКолонка.УИД;
		стДанныеКатегории = СформироватьЗадачи(стОтбор,  стДанныеДляВывода, стПараметры, текШаблонЭлемента, стНастройки);	
		
		текШаблонКатегории = ?(ТекущаяКолонка.ЗапрещенИнтерактивныйПереход, ШаблонКатегорииБезПеремещения, ШаблонКатегории);	
		
		Категории.Добавить(СтрШаблон(текШаблонКатегории, ТекущаяКолонка.УИД, ТекущаяКолонка.Наименование, стДанныеКатегории.СтрокаHTML 
						, "", "149, 18, 18" , стДанныеКатегории.ВсегоЗадач));
		
	КонецЦикла;
	
	Если Не ПустаяСтрока(стПараметры.СтруктураДоски.ПутьКФайлуКартинкиКорзины) Тогда
		
		Категории.Добавить(СтрШаблон(ПолучитьШаблонФинишногоБлока(), стПараметры.СтруктураДоски.ПутьКФайлуКартинкиКорзины)); 
		
	КонецЕсли;
	
	Возврат СтрСоединить(Категории, Символы.ПС + Символы.ВК);

КонецФункции  

Функция СформироватьЗадачи(стОтбор, стДанныеДляВывода, стПараметры, ШаблонЭлемента, стНастройки)
	
	ЗадачиКатегории 	= Новый Массив;
	тзДанныеДляВывода 	= стДанныеДляВывода.ДанныеКарточек;
	тзДанныеТегов 		= стДанныеДляВывода.ДанныеТегов;
	ВсегоЗадач			= 0;
	tabindex = 1;
	
	Для каждого ТекущаяЯчейка Из тзДанныеДляВывода.НайтиСтроки(стОтбор) Цикл
		
		Если стНастройки.Отбор = ТекущаяЯчейка.УИД Тогда
		
			tabindex = 0;	
		
		Иначе
		
			tabindex = 1;	
		
		КонецЕсли;
		
		ЗадачиКатегории.Добавить(СтрШаблон(ШаблонЭлемента
			, ТекущаяЯчейка.УИД, ТекущаяЯчейка.Дорожка, ТекущаяЯчейка.Наименование, Формат(ТекущаяЯчейка.КрайняяДата, "ДФ=dd.MM.yy"), tabindex));
		ВсегоЗадач = ВсегоЗадач + 1;

	КонецЦикла;
	
	Возврат Новый Структура("СтрокаHTML, ВсегоЗадач", СтрСоединить(ЗадачиКатегории, Символы.ПС + Символы.ВК), ВсегоЗадач);
	
КонецФункции // ВывестиКЗадачи(ТекущаяКолонка.УИД, мДанныеДляВывода, СтрокаHTML, стПараметры)

Функция ПолучитьДанныеДляВывода(стСтруктураКанбана, стНастройки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанКарточки.УИД КАК УИД,
		|	КанбанКарточки.Наименование КАК Наименование,
		|	КанбанКарточки.Ресурс КАК Ресурс,
		|	КанбанКарточки.Цвет КАК Цвет
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрСведений.КанбанКарточки КАК КанбанКарточки
		|		?ЛЕВОЕ? СОЕДИНЕНИЕ РегистрСведений.КанбанРазделители КАК КанбанРазделители
		|		ПО КанбанКарточки.УИД = КанбанРазделители.УИДКарточка
		|ГДЕ
		|	КанбанКарточки.Доска = &Доска
		|	И ВЫБОР
		|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ КанбанРазделители.Проект = &Проект
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Сотрудник = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ КанбанРазделители.Сотрудник = &Сотрудник
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КанбанДоски.НомерВерсии КАК НомерВерсии
		|ИЗ
		|	РегистрСведений.КанбанДоски КАК КанбанДоски
		|ГДЕ
		|	КанбанДоски.УИД = &Доска
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.УИД КАК УИД,
		|	вт.Наименование КАК Наименование,
		|	вт.Ресурс КАК Ресурс,
		|	ТИПЗНАЧЕНИЯ(вт.Ресурс) КАК ТипЗначения,
		|	вт.Цвет КАК Цвет,
		|	КанбанПереходКарточкиСрезПоследних.УИДКудаДорожка КАК Дорожка,
		|	КанбанПереходКарточкиСрезПоследних.УИДКудаКолонка КАК Колонка
		|ИЗ
		|	вт КАК вт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КанбанПереходКарточки.СрезПоследних(
		|				,
		|				НЕ(УИДКудаДорожка = """"
		|						ИЛИ УИДКудаКолонка = """"
		|						ИЛИ УИДКудаКолонка = ""00000000-0000-0000-0000-000000000000"")) КАК КанбанПереходКарточкиСрезПоследних
		|		ПО вт.УИД = КанбанПереходКарточкиСрезПоследних.УИДКарточка
		|
		|УПОРЯДОЧИТЬ ПО
		|	КанбанПереходКарточкиСрезПоследних.НомерВКолонке
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КанбанТеги.УИД КАК УИД,
		|	КанбанТеги.НомерТега КАК НомерТега,
		|	КанбанТеги.Тег КАК Тег
		|ИЗ
		|	РегистрСведений.КанбанТеги КАК КанбанТеги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт КАК вт
		|		ПО КанбанТеги.УИД = вт.УИД
		|
		|УПОРЯДОЧИТЬ ПО
		|	вт.Наименование";
	
	Запрос.УстановитьПараметр("Доска", 		стСтруктураКанбана.СтруктураДоски.УИДДоски);
	Запрос.УстановитьПараметр("Сотрудник", 	стНастройки.Сотрудник);
	Запрос.УстановитьПараметр("Проект", 	стНастройки.Проект); 

	Если стНастройки.Сортировка = 1 Тогда
		Запрос.Текст = СтрЗаменить(запрос.Текст, "КанбанПереходКарточкиСрезПоследних.НомерВКолонке", "вт.Наименование");	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(стНастройки.Сотрудник) ИЛИ ЗначениеЗаполнено(стНастройки.Проект) Тогда
		Запрос.Текст = СтрЗаменить(запрос.Текст, "?ЛЕВОЕ?", "ВНУТРЕННЕЕ");
	Иначе
		Запрос.Текст = СтрЗаменить(запрос.Текст, "?ЛЕВОЕ?", "ЛЕВОЕ");
	КонецЕсли;
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		стСтруктураКанбана.СтруктураДоски.НомерВерсии = Выборка.НомерВерсии;	
	КонецЦикла;
	
	ДанныеКарточек = Результат[2].Выгрузить();
	
	ПолучитьКрайниеДаты(ДанныеКарточек);
	
	Если стНастройки.Сортировка = 2 Тогда
	
		ДанныеКарточек.Сортировать("КрайняяДата");	
	
	КонецЕсли;
	
	Возврат Новый Структура("ДанныеКарточек, ДанныеТегов", ДанныеКарточек, Результат[3].Выгрузить());
	
КонецФункции // ПолучитьДанныеДляВывода()

Процедура ПолучитьКрайниеДаты(ДанныеКарточек)

	ДанныеКарточек.Колонки.Добавить("КрайняяДата", Новый ОписаниеТипов("Дата"));
	ДанныеКарточек.Индексы.Добавить("Ресурс");
	
	мКомплекты 		= Новый Массив;
	
	Для каждого х Из ДанныеКарточек Цикл
		мКомплекты.Добавить(х.Ресурс);
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплекта.Комплект КАК Комплект,
		|	ДатыКомплекта.КрайТЗ КАК КрайТЗ
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|ГДЕ
		|	ДатыКомплекта.Комплект В(&Комплект)";
	
	Запрос.УстановитьПараметр("Комплект", мКомплекты);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НайденаяСтрока = ДанныеКарточек.Найти(Выборка.Комплект, "Ресурс");
		
		Если НЕ НайденаяСтрока = Неопределено Тогда
			НайденаяСтрока.КрайняяДата = Выборка.КрайТЗ;	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСтруктураКанбана() 
	
	СтруктураДоски = Новый Структура("УИДДоски, Наименование, НомерВерсии, ПутьКФайлуКартинкиКорзины, ПерваяЗагрузка, Отбор", "", "", 0, "", Истина, Неопределено);
	
	СтруктураКанбана = Новый Структура("СтруктураДоски, мКолонки, мДорожки", СтруктураДоски, Новый Массив, Новый Массив);
	
	ИмяДоскиДляТЗ = омКанбанДоскиНаСервереПовтор.ПолучитьИмяДоски("НаименованиеДоскиДляТЗ");
	
	Если ПустаяСтрока(ИмяДоскиДляТЗ) Тогда
		Возврат СтруктураКанбана;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КанбанДоски.УИД КАК УИДДоски,
	|	КанбанДоски.Наименование КАК Наименование,
	|	КанбанДоски.НомерВерсии КАК НомерВерсии,
	|	КанбанДоски.ИмяКартинкиКорзины КАК ИмяКартинкиКорзины
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	РегистрСведений.КанбанДоски КАК КанбанДоски
	|ГДЕ
	|	КанбанДоски.Наименование = &Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КанбанДорожки.УИД КАК УИД,
	|	КанбанДорожки.Наименование КАК Наименование,
	|	КанбанДорожки.Цвет КАК Цвет,
	|	КанбанДорожки.ЛимитКарточек КАК ЛимитКарточек
	|ИЗ
	|	вт КАК вт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КанбанДорожки КАК КанбанДорожки
	|		ПО вт.УИДДоски = КанбанДорожки.Доска
	|
	|УПОРЯДОЧИТЬ ПО
	|	КанбанДорожки.НомерПоПорядку
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КанбанКолонки.УИД КАК УИД,
	|	КанбанКолонки.ИмяСобытия КАК ИмяСобытия,
	|	КанбанКолонки.ЛимитКарточек КАК ЛимитКарточек,
	|	КанбанКолонки.ЛимитЧасов КАК ЛимитЧасов,
	|	КанбанКолонки.Наименование КАК Наименование,
	|	КанбанКолонки.Цвет КАК Цвет,
	|	КанбанКолонки.ЗапрещенИнтерактивныйПереход КАК ЗапрещенИнтерактивныйПереход,
	|	КанбанКолонки.ЗапрещенИнтерактивныйУход КАК ЗапрещенИнтерактивныйУход
	|ИЗ
	|	вт КАК вт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КанбанКолонки КАК КанбанКолонки
	|		ПО вт.УИДДоски = КанбанКолонки.Доска
	|
	|УПОРЯДОЧИТЬ ПО
	|	КанбанКолонки.НомерПоПорядку
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт.УИДДоски КАК УИДДоски,
	|	вт.Наименование КАК Наименование,
	|	вт.НомерВерсии КАК НомерВерсии,
	|	вт.ИмяКартинкиКорзины КАК ИмяКартинкиКорзины
	|ИЗ
	|	вт КАК вт";
	
	Запрос.УстановитьПараметр("Наименование", ИмяДоскиДляТЗ);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл  
		СтруктураДорожки = Новый Структура("УИД, ИмяСобытия, ЛимитКарточек,  Наименование, Цвет", );
		ЗаполнитьЗначенияСвойств(СтруктураДорожки, Выборка);
		СтруктураКанбана.мДорожки.Добавить(СтруктураДорожки);
	КонецЦикла;
	
	Выборка = Результат[2].Выбрать();
	Пока Выборка.Следующий() Цикл  
		СтруктураКолонки = Новый Структура("УИД, ИмяСобытия, ЛимитКарточек, ЛимитЧасов, Наименование, Цвет, ЗапрещенИнтерактивныйПереход, ЗапрещенИнтерактивныйУход", );
		ЗаполнитьЗначенияСвойств(СтруктураКолонки, Выборка);
		СтруктураКанбана.мКолонки.Добавить(СтруктураКолонки);
	КонецЦикла;
	
	Выборка = Результат[3].Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтруктураКанбана.СтруктураДоски, Выборка);
		
		Если НЕ ПустаяСтрока(Выборка.ИмяКартинкиКорзины) И ПустаяСтрока(СтруктураКанбана.СтруктураДоски.ПутьКФайлуКартинкиКорзины) Тогда
			
			ПутьКФайлу = КаталогВременныхФайлов() + Выборка.ИмяКартинкиКорзины + ".png";
			Файл = Новый Файл(ПутьКФайлу); 
			Если НЕ Файл.Существует() Тогда
				БиблиотекаКартинок[Выборка.ИмяКартинкиКорзины].Записать(ПутьКФайлу);	
			КонецЕсли; 
			
			СтруктураКанбана.СтруктураДоски.ПутьКФайлуКартинкиКорзины = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПутьКФайлу));
			
			УдалитьФайлы(ПутьКФайлу);
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураКанбана;
	
КонецФункции // ПолучитьУИДДДоски()

Функция ПолучитьУИДКолонки(ИмяКолонки) Экспорт
	
	УИДДоски = омКанбанДоскиНаСервереПовтор.ПолучитьУИДДоски("УИДДоскиДляТЗ");
	
	Возврат омКанбанДоскиНаСервереПовтор.ПолучитьУИДКолонки(УИДДоски, ИмяКолонки);
	
КонецФункции // ПолучитьУИДКолонки()

Функция ПереместитьРеквизитВОжидание(Реквизит, Комментарий = "") Экспорт

	УИДДоски 		= омКанбанДоскиНаСервереПовтор.ПолучитьУИДДоски("УИДДоскиДляТЗ");
	УИДКудаКолонка 	= омКанбанДоскиНаСервереПовтор.ПолучитьУИДКолонки(УИДДоски, "_Ожидание_ТЗ");
	
	УИДКарточки = ПереместитьРеквизитВПереопределенное(Реквизит, УИДДоски, УИДКудаКолонка, Комментарий);
	
	Если НЕ ПустаяСтрока(УИДКарточки) Тогда
	
		омКанбанДоскиНаСервере.СоздатьЗадачуКонтрольЗадачи(УИДКарточки);	
	
	КонецЕсли;

КонецФункции // переместитьРеквизитВОжидание()

Функция ПереместитьРеквизитВВозврат(Реквизит, Комментарий = "") Экспорт

	УИДДоски 		= омКанбанДоскиНаСервереПовтор.ПолучитьУИДДоски("УИДДоскиДляТЗ");
	УИДКудаКолонка 	= омКанбанДоскиНаСервереПовтор.ПолучитьУИДКолонки(УИДДоски, "_Возврат_ТЗ");
	
	ПереместитьРеквизитВПереопределенное(Реквизит, УИДДоски, УИДКудаКолонка, Комментарий); 

КонецФункции // переместитьРеквизитВОжидание()

Функция ПереместитьРеквизитВПереопределенное(Реквизит, УИДДоски, УИДКудаКолонка, Комментарий)

	УстановитьПривилегированныйРежим(Истина);
	
	УИДДоски 	= омКанбанДоскиНаСервереПовтор.ПолучитьУИДДоски("УИДДоскиДляТЗ");
	УИДКарточка = омКанбанДоскиНаСервере.ПолучитьУИДКарточки(УИДДоски, Реквизит);
	
	Если ПустаяСтрока(УИДКарточка) Тогда
		Возврат "";	
	КонецЕсли;
	
	стПараметрыПерехода = омКанбанНачальноеЗаполнение.ПолучитьСтруктуруДляЭлементовКанбана("ПереходКарточки", Комментарий);
	
	стПараметрыПерехода.Доска 			= УИДДоски;
	стПараметрыПерехода.НомерВКолонке 	= -1;
	стПараметрыПерехода.УИДКудаКолонка 	= УИДКудаКолонка;
	стПараметрыПерехода.УИДКудаДорожка 	= омКанбанДоскиНаСервереПовтор.ПолучитьУИДДорожки(УИДДоски);
	стПараметрыПерехода.УИДКарточка 	= УИДКарточка;
	
	УИДПерехода = омКанбанНачальноеЗаполнение.СоздатьДвижениеКарточки(стПараметрыПерехода); 
	
	Возврат УИДКарточка;

КонецФункции // переместитьРеквизитВОжидание()

Функция ПеревестиРеквизитВЗавершенный(Реквизит, Комментарий = "") Экспорт
	
	УИДДоски 	= омКанбанДоскиНаСервереПовтор.ПолучитьУИДДоски("УИДДоскиДляТЗ");
    омКанбанДоскиНаСервере.ПеревестиКарточкуВВыполнено(УИДДоски, Реквизит, Комментарий); 
	

КонецФункции // ПеревестиРеквизитВЗавершенный()

Функция ПолучитьКартинкуКорзины(ПутьККорзине) Экспорт

	Возврат "<div class=""category"" data-category-id=""00000000-0000-0000-0000-000000000000"" ondragover=""event.preventDefault()"" ondrop=""onDrop(event)"">
			| <img src=""" + ПутьККорзине + """>
			|</div>";
	

КонецФункции // ПолучитьКартинкуКорзины()

Функция ПолучитУИДРесурса(Ресурс) Экспорт

	УИДДоски = омКанбанДоскиНаСервереПовтор.ПолучитьУИДДоски("УИДДоскиДляТЗ");
	Возврат омКанбанДоскиНаСервере.ПолучитьУИДКарточки(УИДДоски, Ресурс);

КонецФункции // ПолучитУИДРесурса()


#Область HTML

Функция ПолучитьШаблонHTML() Экспорт
	
	ШаблонHTML = "
	|<!DOCTYPE html>
	|<html lang=""ru"">
	|<head>
	|  <meta charset=""UTF-8"">
	|  <meta name=""viewport"" content=""width=device-width, initial-scale=1, maximum-scale=1, user-scalable=no, minimal-ui"">
	|  <title>Kanban Board</title>
	|  <style>
	|    %1
	|  </style>
	|</head>
	|<body>
	|
	|<div class=""kanban-container"">
	|%2
	|</div>
	|
	|%3
	|%4
	|
	|</body>
	|</html>";
	
	Возврат ШаблонHTML;
	
КонецФункции

Функция ПолучитьШаблонКатегории() Экспорт
	
	ШаблонHTML = "
	|<div class=""category"" data-category-id=""%1"" id=""%1"" ondragover=""event.preventDefault()"" ondrop=""onDrop(event)"" style=""background:rgba(%5, 0.5);"">
	|  <div class=""category-header"">
    |    <h3>%2</h3>
    |    <p class=""total-sum"">Всего задач: %6</p>
    |  </div>
	|  <div class=""items-container"">
	|    %3
	|    %4
	|  </div>
	|</div>";
	
	Возврат ШаблонHTML;
КонецФункции

Функция ПолучитьШаблонКатегорииБезПеремещения() Экспорт
	
	ШаблонHTML = "
	|<div class=""category"" data-category-id=""%1"" id=""%1"" ondrop=""onDrop(event)"" style=""background:rgba(%5, 0.5);"">
	|  <div class=""category-header"">
    |    <h3>%2</h3>
    |    <p class=""total-sum"">Всего задач: %6</p>
    |  </div>
	|  <div class=""items-container"">
	|    %3
	|    %4
	|  </div>
	|</div>";
	
	Возврат ШаблонHTML;
КонецФункции

Функция ПолучитьШаблонЭлемента() Экспорт
	
	ШаблонHTML = "
      |<div class=""item"" draggable=""true"" data-id=""%1"" data-track=""%2"" ondragstart=""onDragStart(event)"" ondragend=""onDragEnd(event)"" onclick=""onClickItem(event)"" ondblclick=""onDbClickItem(event)"" tabindex=""%5"">
      |  <h4 class=""item-header"">%3</h4>
      |  <p class=""item-desk"">
      |    Крайняя дата: %4
      |  </p>
      |</div>";
	
	Возврат ШаблонHTML;
КонецФункции 

Функция ПолучитьШаблонЭлементаБезПеремещения()

	ШаблонHTML = "
      |<div class=""item"" draggable=""true"" data-id=""%1"" data-track=""%2"" ondragstart=""offDragStart(event)"" ondragend=""onDragEnd(event)"" onclick=""onClickItem(event)"" ondblclick=""onDbClickItem(event)"" tabindex=""%5"">
      |  <h4 class=""item-header"">%3</h4>
      |  <p class=""item-desk"">
      |    Крайняя дата: %4
      |  </p>
      |</div>";
	
	Возврат ШаблонHTML;

КонецФункции // ПолучитьШаблонЭлементаБезПеремещения()


Функция ПолучитьШаблонКнопкиЗагрузки() Экспорт
	
	ШаблонHTML = "<button class=""load-more-button"" onclick=""onLoadMoreClick(this)"">Загрузить еще</button>";
	
	Возврат ШаблонHTML;
КонецФункции

Функция ПолучитьШаблонФинишногоБлока()
	
	Возврат "
	|<div class=""category"" data-category-id=""100"" ondragover=""event.preventDefault()"" ondrop=""onDrop(event)"">
	|	<div class=""items-container"">
	|		<div class=""item"" draggable=""true"" data-id=""00000000-0000-0000-0000-000000000000"" data-track=""00000000-0000-0000-0000-000000000000"" ondragstart=""onDragStart(event)"" ondragend=""onDragEnd(event)"" onclick=""onClickItem(event)"" ondblclick=""onDbClickItem(event)"">
	|			<div class=""item"" ondragover=""event.preventDefault()"" ondrop=""onDrop(event)"" data-category-id="""">
	|		   	<img src=""%1"">
	|			</div>
	|		</div>
	|	</div>
	|</div>";
КонецФункции // ПолучитьШаблонФинишногоБлока()

#КонецОбласти 

#Область Шаблоны

Функция ПолучитьМакет_css_item()
	
	///* Стили для каждой задачи */
	//.item {
	//  background: white; /* Устанавливает белый фоновый цвет для задачи */
	//  padding: 10px; /* Добавляет внутренние отступы в 10px со всех сторон задачи */
	//  margin: 5px 2px; /* Добавляет внешние отступы: 5px сверху и снизу, 2px слева и справа */
	//  border-radius: 3px; /* Скругляет углы задачи с радиусом 3px */
	//  cursor: grab; /* Изменяет курсор на "руку", указывая на возможность перетаскивания */
	//}
	
	///* Стили для цены задачи */
	//.item-price {
	//  padding: 2px; /* Добавляет внутренние отступы в 2px со всех сторон */
	//  margin: 0; /* Убирает внешние отступы */
	//}
	//
	///* Стили для заголовка задачи */
	//.item-header {
	//  padding: 2px; /* Добавляет внутренние отступы в 2px со всех сторон */
	//  margin: 0; /* Убирает внешние отступы */
	//}
	
	///* Стили для описания задачи */
	//.item-desk {
	//  padding: 2px; /* Добавляет внутренние отступы в 2px со всех сторон */
	//  margin: 0; /* Убирает внешние отступы */
	//}
	
	Возврат "
	|    .item {
	|      background: white;
	|      padding: 10px;
	|      margin: 5px 2px;
	|      border-radius: 3px;
	|      cursor: grab;
	|    }
	|    .item-price {
	|      padding: 2px;
	|      margin: 0;
	|    }
	|    .item-header {
	|      padding: 2px;
	|      margin: 0;
	|    }
	|    .item-desk {
	|      padding: 2px;
	|      margin: 0;
	|    }
	|";
	
КонецФункции // ПолучитьМакет_css_item()

Функция ПолучитьМакет_css_items_container()
	
	///* Стили для контейнера задач */
	//.items-container {
	//  flex-grow: 1; /* Позволяет контейнеру задач занимать все доступное пространство внутри Flexbox-контейнера (.category) */
	//  overflow-y: auto; /* Добавляет вертикальную полосу прокрутки, если содержимое превышает высоту контейнера */
	//  max-height: 100%; /* Устанавливает максимальную высоту контейнера задач в 100% от родительского элемента (.category) */
	//  scrollbar-width: thin; /* Устанавливает тонкую ширину скроллбара для браузеров, поддерживающих это свойство (например, Firefox) */
	//  scrollbar-color: rgba(0, 0, 0, 0.3) rgba(0, 0, 0, 0.1); /* Устанавливает цвет ползунка и дорожки скроллбара для браузеров, поддерживающих это свойство (например, Firefox) */
	//}
	
	///* Стилизация скроллбара для WebKit-браузеров (Chrome, Safari, Edge) */
	//.items-container::-webkit-scrollbar {
	//  width: 10px; /* Устанавливает ширину вертикального скроллбара */
	//}
	
	//.items-container::-webkit-scrollbar-track {
	//  background: rgba(0, 0, 0, 0.1); /* Устанавливает полупрозрачный фон для дорожки скроллбара */
	//  border-radius: 5px; /* Скругляет углы дорожки скроллбара */
	//}
	
	//.items-container::-webkit-scrollbar-thumb {
	//  background-color: rgba(0, 0, 0, 0.3); /* Устанавливает полупрозрачный цвет для ползунка скроллбара */
	//  border-radius: 5px; /* Скругляет углы ползунка скроллбара */
	//  border: 2px solid rgba(0, 0, 0, 0.1); /* Добавляет границу вокруг ползунка с небольшим отступом */
	//}
	
	Возврат "
	|    .items-container {
	|      flex-grow: 1;
	|      overflow-y: auto;
	|      max-height: 100%;
	|      scrollbar-width: thin;
	|      scrollbar-color: rgba(0, 0, 0, 0.3) rgba(0, 0, 0, 0.1);
	|    }
	|    .items-container::-webkit-scrollbar {
	|      width: 10px;
	|    }
	|    .items-container::-webkit-scrollbar-track {
	|      background: rgba(0, 0, 0, 0.1);
	|      border-radius: 5px;
	|    }
	|    .items-container::-webkit-scrollbar-thumb {
	|      background-color: rgba(0, 0, 0, 0.3);
	|      border-radius: 5px;
	|      border: 2px solid rgba(0, 0, 0, 0.1); 
	|    }
	|"	
	
КонецФункции // ПолучитьМакет_css_items_container()

Функция ПолучитьМакет_css_category() // Стили для каждой категории (колонки)
	
	//  background: rgba(152, 152, 152, 0.3); /* Устанавливает полупрозрачный серый фоновый цвет для колонки */
	//  margin: 5px; /* Добавляет внешние отступы в 5px со всех сторон колонки */
	//  padding: 10px; /* Добавляет внутренние отступы в 10px со всех сторон колонки */
	//  border-radius: 5px; /* Скругляет углы колонки с радиусом 5px */
	//  width: 250px; /* Устанавливает фиксированную ширину колонки в 250px */
	//  min-width: 250px; /* Устанавливает минимальную ширину колонки в 250px */
	//  height: fit-content; /* Устанавливает высоту колонки по содержимому */
	//  display: flex; /* Устанавливает Flexbox для размещения элементов внутри колонки (заголовок, задачи, кнопка) */
	//  flex-direction: column; /* Располагает элементы внутри колонки вертикально */
	//  max-height: 90vh; /* Устанавливает максимальную высоту колонки в 90% высоты окна просмотра */
	//}
	
	///* Стили для заголовка категории (h3) */
	//.category-header h3 {
	//  margin-top: 0; /* Убирает верхний внешний отступ у заголовка h3 */
	//  margin-bottom: 0; /* Убирает нижний внешний отступ у заголовка h3 */
	//  padding: 3px; /* Добавляет внутренние отступы в 3px со всех сторон заголовка h3 */
	//}
	
	///* Стили для параграфа в заголовке категории (p) */
	//.category-header p {
	//  margin-top: 0; /* Убирает верхний внешний отступ у параграфа p */
	//  margin-bottom: 0; /* Убирает нижний внешний отступ у параграфа p */
	//  padding: 3px; /* Добавляет внутренние отступы в 3px со всех сторон параграфа p */
	//}
	
	///* Стили для контейнера заголовка категории */
	//.category-header{
	//  background-color: rgba(255, 255, 255, 0.5); /* Устанавливает полупрозрачный белый фоновый цвет */
	//  border-radius: 3px; /* Скругляет углы контейнера заголовка с радиусом 3px */
	//  text-align: center; /* Выравнивает текст внутри контейнера по центру */ 
	
	Возврат "    
	|    .category {
	|      background: rgba(152, 152, 152, 0.3);
	|      margin: 5px;
	|      padding: 3px;
	|      border-radius: 2px;
	|      width: %250%px;
	|      min-width: &250&px;
	|      font-size: #16#px;
	|      height: fit-content;
	|      display: flex;
	|      flex-direction: column;
	|      max-height: 90vh;
	|    }
	|    .category-header h3 {
	|      margin-top: 0;
	|      margin-bottom: 0;
	|      padding: 3px;
	|    }
	|    .category-header p {
	|      margin-top: 0;
	|      margin-bottom: 0;
	|      padding: 3px;
	|    }
	|    .category-header{
	|      background-color: rgba(255, 255, 255, 0.5);
	|      border-radius: 3px;
	|      text-align: center;
	|    }"	
	
КонецФункции // ПолучитьМакет_css_category()()

Функция ПолучитьМакет_css_body() // Общие стили для body 
	
	//font-family: Arial, sans-serif; /* Устанавливает шрифт Arial или любой доступный sans-serif для всего документа */
	//background: #b8b8b8 url('https://i.pinimg.com/originals/46/6c/4d/466c4d0ec2824e15ff7def79f8ec2789.png') no-repeat center center fixed; /* Устанавливает ярко-оранжевый фоновый цвет и фоновое изображение, которое не повторяется, центрируется и фиксируется при прокрутке */
	//background-size: cover; /* Масштабирует фоновое изображение так, чтобы оно покрывало всю область body */
	//margin: 0; /* Убирает внешние отступы со всех сторон элемента body */
	//padding: 0; /* Убирает внутренние отступы со всех сторон элемента body */
	//display: flex; /* Устанавливает Flexbox для размещения дочерних элементов body */
	//height: 100vh; /* Устанавливает высоту body равной 100% высоты окна просмотра (viewport) */
	//align-items: stretch; /* Растягивает дочерние элементы Flexbox по поперечной оси (по высоте в данном случае) */
	//justify-content: left; /* Центрирует дочерние элементы Flexbox по главной оси (влево в данном случае) */
	
	Возврат  " 
	|    body {
	|      font-family: Arial, sans-serif;
	//|      background: #b8b8b8 url('https://i.pinimg.com/originals/46/6c/4d/466c4d0ec2824e15ff7def79f8ec2789.png') no-repeat center center fixed;
	|      background-size: cover;
	|      margin: 0;
	|      padding: 0;
	|      display: flex;
	|      height: 100vh;
	|      align-items: stretch;
	|      justify-content: left;
	|    }"
	
КонецФункции // ПолучитьМакет_css_body()

Функция ПолучитьМакет_css_kanban_container() // Стили для контейнера Kanban 
	//display: flex; /* Устанавливает Flexbox для размещения колонок Kanban */
	//overflow-x: auto; /* Добавляет горизонтальную полосу прокрутки, если содержимое превышает ширину контейнера */
	// overflow-y: hidden; /* Скрывает вертикальную полосу прокрутки */
	// padding: 10px; /* Добавляет внутренние отступы в 10px со всех сторон контейнера */
	
	Возврат "    
|    .kanban-container {
|      display: flex;
|      overflow-x: auto;
|      overflow-y: hidden;
|      padding: 10px;
|    }"

КонецФункции // ПолучитьМакет_css_kanban_container()

Функция ПолучитьМакет_js()

Возврат "<script>
|  // Переменная для хранения ссылки на перетаскиваемый элемент
|  let draggedItem = null;
|  let source_id = null;
|
|  function onClickItem(event) {
|    const eventData = {
|      event: ""ClickItem"", // тип события
|      itemId: event.currentTarget.dataset.id, // ID элемента
|      trackId: event.currentTarget.dataset.track, // Дорожка элемента
|    };
|    pushEvent(eventData);
|  }
|
|  function onDbClickItem(event) {
|    const eventData = {
|      event: ""DbClickItem"", // тип события
|      itemId: event.currentTarget.dataset.id, // ID элемента
|    };
|    pushEvent(eventData);
|  }
|
|  /**
|   * Функция вызывается при начале перетаскивания элемента.
|   * @param {Event} event - Событие dragstart
|   */
|  function onDragStart(event) {
|    // Сохраняем ссылку на перетаскиваемый элемент
|    draggedItem = event.target;
|
|    // Сохраняем ID перетаскиваемого элемента в объект dataTransfer
|    event.dataTransfer.setData(""text/plain"", event.target.dataset.id + ';' + event.target.dataset.track);
|
|    // Скрываем элемент во время перетаскивания (для улучшения UX)
|    setTimeout(() => {
|      event.target.style.display = ""none"";
|    }, 0);
|  }
|
|  /**
|   * Функция вызывается при завершении перетаскивания элемента (успешном или нет).
|   * @param {Event} event - Событие dragend
|   */
|  function onDragEnd(event) {
|    // Восстанавливаем видимость элемента в любом случае
|    event.target.style.display = ""block"";
|  }
|  /**
|   * Функция запрещает перемещение.
|   * @param {Event} event - Событие dragend
|   */
|	function offDragStart(event){
|		event.preventDefault();
|	}
|
|
|  /**
|   * Функция вызывается, когда элемент перетаскивается над целевой областью.
|   * @param {Event} event - Событие dragover
|   */
|  function onDragOver(event) {
|    // Разрешаем дроп (по умолчанию браузеры запрещают дроп)
|    event.preventDefault();
|  }
|
|  /**
|   * Функция вызывается, когда элемент отпускается в целевой области.
|   * @param {Event} event - Событие drop
|   */
|  function onDrop(event) {
|    // Отменяем стандартное поведение браузера
|    event.preventDefault();
|
|    // Получаем ID категории, куда был перемещен элемент
|    const categoryId = event.currentTarget.dataset.categoryId;
|
|    // Находим контейнер задач (.items-container) внутри целевой категории
|    const targetContainer = event.currentTarget.querySelector('.items-container');
|
|    // Получаем ID перетаскиваемого элемента из объекта dataTransfer
|    
|	let allSymbols = event.dataTransfer.getData(""text/plain"").split(';')
|
|	 const itemId = allSymbols[0];  
|    const trackId = allSymbols[1];
|
|    // Находим все задачи в целевом контейнере
|    const items = Array.from(targetContainer.querySelectorAll('.item'));
|
|    // Определяем позицию курсора относительно элементов
|    const mouseY = event.clientY; // Позиция курсора по вертикали
|    let insertBeforeIndex = -1; // Индекс элемента, перед которым нужно вставить задачу
|
|    // Проходим по всем задачам в контейнере
|    for (let i = 0; i < items.length; i++) {
|      const itemRect = items[i].getBoundingClientRect(); // Получаем размеры и позицию элемента
|      if (mouseY < itemRect.top + itemRect.height / 2) {
|        // Если курсор находится выше середины элемента, вставляем перед ним
|        insertBeforeIndex = i;
|        break;
|      }
|    }
|
|    // Находим кнопку ""Загрузить еще"" в контейнере, если она есть
|    const loadMoreButton = targetContainer.querySelector('.load-more-button');
|
|    // Если индекс не найден, вставляем элемент в конец списка (перед кнопкой ""Загрузить еще"", если она есть)
|    if (insertBeforeIndex === -1) {
|      if (loadMoreButton) {
|        targetContainer.insertBefore(draggedItem, loadMoreButton);
|      } else {
|        targetContainer.appendChild(draggedItem);
|      }
|    } else {
|      // Вставляем элемент перед найденным элементом
|      targetContainer.insertBefore(draggedItem, items[insertBeforeIndex]);
|    }
|
|    // Восстанавливаем видимость элемента
|    draggedItem.style.display = ""block"";
|    draggedItem = null;
|
|    // Создаем событие и помещаем его в очередь
|    const eventData = {
|      event: ""DropItem"", // тип события
|      itemId: itemId, // ID перетаскиваемого элемента
|      categoryId: categoryId, // ID категории, куда был перемещен элемент 
|      trackId: trackId, //ID дорожки
|      insertBeforeIndex: insertBeforeIndex // Индекс элемента
|    };
|    pushEvent(eventData);
|  }
|
|  var queue = Array() // Очередь событий
|  var queueHead = -1 // Указатель на начало очереди
|  var queueTail = -1 // Указатель на конец очереди
|  var totalEventCount = 0 //Количество обработанных событий
|
|  /**
|   * Функция для добавления события в очередь.
|   * @param {Object} dataEvent - Объект с данными события
|   */
|  function pushEvent(dataEvent) {
|    if (queueHead === queueTail && queueHead > -1) { //Если очередь пуста, сбросим указатели
|      queueHead = queueTail = -1
|    }
|    queue[queueTail + 1] = JSON.stringify(dataEvent) //Преобразуем данные в JSON
|    queueTail += 1
|    totalEventCount += 1
|
|
|    // Логируем добавленное событие
|    console.log(""Добавлено событие:"", dataEvent);
|  }
|
|  function getEvent(index) {
|    return queue[index]
|  }
|
|function scrollIntoView(id){
|	getElementById('id').scrollIntoView()
|}
|	
|var box = document.querySelector("".category"");
|var md = false;
|var startY = 0;
|var scrollTop = box.scrollTop;
|var handler = function handler(e) {
|  if (e.type == ""mousedown"") {
|    startY = e.clientY;
|    scrollTop = box.scrollTop;
|    md = true;
|  } else if (e.type == ""mousemove"") {
|    if (!md) return;
|    box.scrollTop = scrollTop + startY - e.clientY;
|  } else {
|    md = false;
|  }
|};
|box.addEventListener(""mousedown"", handler);
|box.addEventListener(""mousemove"", handler);
|box.addEventListener(""mouseout"", handler);
|box.addEventListener(""mouseup"", handler);
|
|%1
|</script>";
КонецФункции // ПолучитьМакет_js()

#КонецОбласти