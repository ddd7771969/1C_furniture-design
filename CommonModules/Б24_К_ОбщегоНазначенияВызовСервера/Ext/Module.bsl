
#Область Локализация

Функция ПолучитьЛокализациюКонфигурации() Экспорт
	
	// UKR
	// BEL
	// KAZ
	Возврат "RUS";
	
КонецФункции

Функция ПолучитьЛокализациюБитрикс24() Экспорт
	
	// ua
	// en
	
	Возврат "ru";
	
КонецФункции

Функция ПолучитьНазваниеСтраны() Экспорт
	Возврат "РОССИЯ";	
КонецФункции

Функция ПолучитьСлужебноеНазваниеКонфигурации() Экспорт
	
	Возврат "CA 2";
	
КонецФункции

Функция ПолучитьНазваниеМодуля() Экспорт
	
	Возврат "Коннектор к Битрикс24";
	
КонецФункции

Функция ПолучитьИмяРасширения() Экспорт
	
	Возврат "Битрикс24КомплексКА";
	
КонецФункции

Функция ПолучитьВалютуПоКоду(КодВалюты) Экспорт
	
	Если КодВалюты = "840" Тогда
		Результат = "USD";
	ИначеЕсли КодВалюты = "978" Тогда
		Результат = "EUR";
	ИначеЕсли КодВалюты = "980" Тогда
		Результат = "UAH";
	ИначеЕсли КодВалюты = "933" Тогда
		Результат = "BYN";
	ИначеЕсли КодВалюты = "398" Тогда
		Результат = "KZT";
	ИначеЕсли КодВалюты = "643" Тогда
		Результат = "RUB";   
	ИначеЕсли КодВалюты = "156" Тогда
		Результат = "CNY";   
	Иначе
		Результат = "RUB";
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция ПолучитьКодПоВалюте(Валюта) Экспорт
	
	Если Валюта = "USD" Тогда
		Результат = "840";
	ИначеЕсли Валюта = "EUR" Тогда
		Результат = "978";
	ИначеЕсли Валюта = "UAH" Тогда
		Результат = "980";
	ИначеЕсли Валюта = "BYN" Тогда
		Результат = "933";
	ИначеЕсли Валюта = "KZT" Тогда
		Результат = "398";
	ИначеЕсли Валюта = "RUB" Тогда
		Результат = "643";    
	ИначеЕсли Валюта = "CNY" Тогда
		Результат = "156";    
	Иначе
		Результат = "643";
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция ПолучитьКодТелефонаСтраны() Экспорт
	
	//+375 - Беларусь
	//+7 - РФ и К
	
	Возврат "7";
	
КонецФункции

#КонецОбласти


#Область ПроверкаВерсий

Функция Версия() Экспорт
	
	Результат = "";
	
	УстановитьПривилегированныйРежим(Истина);
	НайденныеРасширения = РасширенияКонфигурации.Получить(Новый Структура("Имя", ПолучитьИмяРасширения()));
	Если НайденныеРасширения.Количество() > 0 Тогда
		Результат = НайденныеРасширения[0].Версия;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция ПроверкаОбновленияДанныхМодуляОбменаСБитрикс24() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПроверитьСоздатьПрофилиГруппДоступа();
	
	Версия = Версия();
	
	КлючХранилищаВерсии = "ВерсияМодуляБиблиотеки";

	ПредыдущаяВерсия =  ПолучитьЗначениеХранилищаНастроек(КлючХранилищаВерсии);
	
	ПервыйЗапуск = НЕ ПредыдущаяВерсия = Версия;
	
	Если НЕ ПервыйЗапуск Тогда
		Возврат Истина;
	КонецЕсли;
	
	Пока ПредыдущаяВерсия <> Версия Цикл
	
		ПредыдущаяВерсия =  ПолучитьЗначениеХранилищаНастроек(КлючХранилищаВерсии);
	
		Если НЕ ЗначениеЗаполнено(ПредыдущаяВерсия) Тогда
			
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, Версия)
			
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.0" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.0.0.1");
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.1" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.0.0.2");
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.2" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.0.0.3");
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.3" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.0.0.4");
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.4" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.0.0.5");
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.5" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.0.0.6");
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.6" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.0.0.7");
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.7" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.0.0.8");
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.8" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.0.0.9");
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.9" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.0.0.10");
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.10" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.0.0.11");
		ИначеЕсли ПредыдущаяВерсия = "1.0.0.11" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.0");
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.0" Тогда  
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.1");  
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.1" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.2");
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.2" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.3");
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.3" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.4");
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.4" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.5");
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.5" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.6");
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.6" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.7"); 
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.7" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.8");     
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.8" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.9");  
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.9" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.10");    
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.10" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.11");    
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.11" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.12");    
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.12" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.14");
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.14" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.17");
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.17" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.18"); 
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.18" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.20"); 
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.20" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.21");  
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.21" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "1.1.0.22");
		ИначеЕсли ПредыдущаяВерсия = "1.1.0.22" Тогда 
			
			Б24_КБ_ОбщегоНазначенияВызовСервера.ПереходНаВерсию2();
			Б24_КС_ОбщегоНазначенияВызовСервера.ПереходНаВерсию2();

			ПереходНаВерсию2(КлючХранилищаВерсии, "2.0.0.0"); 
			
		ИначеЕсли ПредыдущаяВерсия = "2.0.0.0" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "2.0.0.1");
		ИначеЕсли ПредыдущаяВерсия = "2.0.0.1" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "2.0.0.2");    
		ИначеЕсли ПредыдущаяВерсия = "2.0.0.2" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "2.0.0.3");  
		ИначеЕсли ПредыдущаяВерсия = "2.0.0.3" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.0.0");
		ИначеЕсли ПредыдущаяВерсия = "3.0.0.0" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.0.1");  
		ИначеЕсли ПредыдущаяВерсия = "3.0.0.1" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.0.2");   
		ИначеЕсли ПредыдущаяВерсия = "3.0.0.2" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.0.3");   
		ИначеЕсли ПредыдущаяВерсия = "3.0.0.3" Тогда       
			
			Б24_КС_ОбщегоНазначенияВызовСервера.ПереходНаВерсию3010();
			
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.1.0");  
			
		ИначеЕсли ПредыдущаяВерсия = "3.0.1.0" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.1.1");
		ИначеЕсли ПредыдущаяВерсия = "3.0.1.1" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.1.2");     
		ИначеЕсли ПредыдущаяВерсия = "3.0.1.2" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.1.5");  
		ИначеЕсли ПредыдущаяВерсия = "3.0.1.5" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.1.6");  
		ИначеЕсли ПредыдущаяВерсия = "3.0.1.6" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.1.8");  
		ИначеЕсли ПредыдущаяВерсия = "3.0.1.8" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.1.9");  
		ИначеЕсли ПредыдущаяВерсия = "3.0.1.9" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.1.12");
		ИначеЕсли ПредыдущаяВерсия = "3.0.1.12" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.1.13"); 
		ИначеЕсли ПредыдущаяВерсия = "3.0.1.13" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.1.14");    
		ИначеЕсли ПредыдущаяВерсия = "3.0.1.14" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.0.1.15");    
		ИначеЕсли ПредыдущаяВерсия = "3.0.1.15" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.0.1");    
		ИначеЕсли ПредыдущаяВерсия = "3.1.0.1" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.0.2");    
		ИначеЕсли ПредыдущаяВерсия = "3.1.0.2" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.0.3"); 
		ИначеЕсли ПредыдущаяВерсия = "3.1.0.3" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.0.4"); 
		ИначеЕсли ПредыдущаяВерсия = "3.1.0.4" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.0.12");
		ИначеЕсли ПредыдущаяВерсия = "3.1.0.12" Тогда 
			
			Б24_КС_ОбщегоНазначенияВызовСервера.ПереходНаВерсию3115();
			
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.0.15");    
			
		ИначеЕсли ПредыдущаяВерсия = "3.1.0.15" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.0.20"); 
		ИначеЕсли ПредыдущаяВерсия = "3.1.0.20" Тогда 
			ПереходНаВерсию31030(КлючХранилищаВерсии, "3.1.0.30");
		ИначеЕсли ПредыдущаяВерсия = "3.1.0.30" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.0.40");   
		ИначеЕсли ПредыдущаяВерсия = "3.1.0.40" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.0.45");   
		ИначеЕсли ПредыдущаяВерсия = "3.1.0.45" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.0.46");  
		ИначеЕсли ПредыдущаяВерсия = "3.1.0.46" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.1.1");  
		ИначеЕсли ПредыдущаяВерсия = "3.1.1.1" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.1.2");  
		ИначеЕсли ПредыдущаяВерсия = "3.1.1.2" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.1.5");   
		ИначеЕсли ПредыдущаяВерсия = "3.1.1.5" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.1.6");   
		ИначеЕсли ПредыдущаяВерсия = "3.1.1.6" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.1.7");   
		ИначеЕсли ПредыдущаяВерсия = "3.1.1.7" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "3.1.1.8");      
		ИначеЕсли ПредыдущаяВерсия = "3.1.1.8" Тогда   
			
			Б24_КС_ОбщегоНазначенияВызовСервера.ПереходНаВерсию4000();
			
			ПереходНаВерсию4000(КлючХранилищаВерсии, "4.0.0.0");
			
		ИначеЕсли ПредыдущаяВерсия = "4.0.0.0" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "4.0.0.1");   
		ИначеЕсли ПредыдущаяВерсия = "4.0.0.1" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "4.0.0.2"); 
		ИначеЕсли ПредыдущаяВерсия = "4.0.0.2" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "4.0.0.3"); 
		ИначеЕсли ПредыдущаяВерсия = "4.0.0.3" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "4.0.0.4");  
		ИначеЕсли ПредыдущаяВерсия = "4.0.0.4" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "4.0.0.5");    
		ИначеЕсли ПредыдущаяВерсия = "4.0.0.5" Тогда 
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, "4.0.0.7");    
		Иначе  
			                        
			УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, Версия);
			
			Возврат Истина;
			
		КонецЕсли;		
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьСлужебныеДанныеДляПроверкиНаОбновление() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	
	СлужебныеДанные = Новый Структура;
	СлужебныеДанные.Вставить("ВерсияПлатформы1С"			, СистемнаяИнформация.ВерсияПриложения);
	СлужебныеДанные.Вставить("НазваниеПрикладногоРешения"	, Метаданные.Синоним);
	СлужебныеДанные.Вставить("ИмяПрикладногоРешения"		, Метаданные.Имя);
	СлужебныеДанные.Вставить("ВерсияПрикладногоРешения"		, Метаданные.Версия);
	СлужебныеДанные.Вставить("ВерсияМодуляСинхронизации"	, Версия());
	СлужебныеДанные.Вставить("МодульПервогоПоколения"		, Ложь);	
	СлужебныеДанные.Вставить("ХешСумма"						, "");	

	ДанныеОМодуле = Новый Структура;
	ДанныеОМодуле.Вставить("ИдПрикладногоРешения"		, "1C_Complex");
	ДанныеОМодуле.Вставить("МинимальныйРелиз" 			, "");
	ДанныеОМодуле.Вставить("МодульНеОпределен" 			, Ложь);
	ДанныеОМодуле.Вставить("СсылкаНаУстановленныйМодуль", "");
	ДанныеОМодуле.Вставить("МодульДоработан" 			, Ложь);
	
	ДанныеОМодуле.Вставить("ЭтоРасширение" 				, Истина);
	ДанныеОМодуле.Вставить("ИдСтраны" 					, "RU");
	ДанныеОМодуле.Вставить("ИдМодуля" 					, "CA_2");
	
	
	Результат = Новый Структура;
	Результат.Вставить("СлужебныеДанные", СлужебныеДанные);
	Результат.Вставить("ДанныеОМодуле"	, ДанныеОМодуле);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьИнформациюОМодулях(ДанныеОМодуле) Экспорт
	
	Результат = Неопределено;
	
	НастройкиПодключения = Новый Структура;
	Б24_К_RestApiВызовСервера.РазобратьАдресСайта("https://util.1c-bitrix.ru/onec/modules_info.php", НастройкиПодключения);	
	
	Соединение = Б24_К_RestApiВызовСервера.УстановитьСоединениеССервером(НастройкиПодключения);
	
	ПустойПараметр = "&LastParam=last";    // 1С может резать символы, поэтому добавляем параметр, который можно спокойно резать.
	
	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.АдресРесурса = "onec/modules_info.php";
	
	ТелоHTTPЗапроса = "q=1&module="+ДанныеОМодуле.ИдПрикладногоРешения+"&country="+ДанныеОМодуле.ИдСтраны+"&app="+ДанныеОМодуле.ИдМодуля+ ПустойПараметр;	
	
	// Module - IdModule (символьный код раздела 1 уровня),
	// Country - IdCountry (символьный код раздела 2 уровня),
	// App - IdAppSolution (символьный код раздела 2 уровня).	
	
	
	HTTPЗапрос.Заголовки.Вставить("Content-Type"	,"application/x-www-form-urlencoded; charset=utf-8");
	HTTPЗапрос.Заголовки.Вставить("Content-Length"	, Формат(СтрДлина(ТелоHTTPЗапроса), "ЧГ=0"));
	
	HTTPЗапрос.УстановитьТелоИзСтроки(ТелоHTTPЗапроса);
	
	Попытка   
		
		Ответ 				= Соединение.ОтправитьДляОбработки(HTTPЗапрос);	
		ОтветСтрокой 		= Ответ.ПолучитьТелоКакСтроку();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ОтветСтрокой);
		
		ИнформацияОМодулях =  ПрочитатьJSON(ЧтениеJSON, Истина);
		
		Результат = ИнформацияОМодулях.Получить("result").Получить("modules");
		
	Исключение
		ОбщегоНазначения.СообщитьПользователю(ИнформацияОбОшибке().Описание);
	КонецПопытки;
	
	Возврат Результат; 
	
КонецФункции


Процедура ПроверитьСоздатьПрофилиГруппДоступа()
	
	Попытка               
		
		ИмяПрофиля = "(Коннектор к Битрикс24) Пользователь"; 
		НайденныйПрофиль = Справочники.ПрофилиГруппДоступа.НайтиПоНаименованию(ИмяПрофиля);
		Если НайденныйПрофиль.Пустая() Тогда
			
			НовыйПрофиль = Справочники.ПрофилиГруппДоступа.СоздатьЭлемент();
			НовыйПрофиль.Наименование = ИмяПрофиля;
			НовыйПрофиль.Комментарий = "Под профилем осуществляется работа с модулем интеграции без прав настройки";
			НовыйПрофиль.Роли.Добавить().Роль = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Роли.Найти("Б24_К_Пользователь"));
			
			НоваНазначение = НовыйПрофиль.Назначение.Добавить();
			НоваНазначение.ТипПользователей = Справочники.Пользователи.ПустаяСсылка();
			
			НовыйПрофиль.Записать();
		КонецЕсли;
		
		ИмяПрофиля = "(Коннектор к Битрикс24) Администратор"; 
		НайденныйПрофиль = Справочники.ПрофилиГруппДоступа.НайтиПоНаименованию(ИмяПрофиля);
		Если НайденныйПрофиль.Пустая() Тогда
			
			НовыйПрофиль = Справочники.ПрофилиГруппДоступа.СоздатьЭлемент();
			НовыйПрофиль.Наименование = ИмяПрофиля;
			НовыйПрофиль.Комментарий = "Под профилем осуществляются настройки модуля интеграции";
			НовыйПрофиль.Роли.Добавить().Роль = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Роли.Найти("Б24_К_Администратор"));
			
			НоваНазначение = НовыйПрофиль.Назначение.Добавить();
			НоваНазначение.ТипПользователей = Справочники.Пользователи.ПустаяСсылка();
			
			НовыйПрофиль.Записать();
		КонецЕсли;
		
	Исключение
		
		ОбщегоНазначения.СообщитьПользователю("Не удалось создать профиль пользователя '(Коннектор к Битрикс24) Пользователь'");
		
	КонецПопытки;
	
КонецПроцедуры



Процедура ПереходНаВерсию2(КлючХранилищаВерсии, ВерсияОбновления)
			
	ОбщегоНазначения.СообщитьПользователю("Осуществляется переход базовой подсистемы модуля интеграции с Битрикс24 на версию 2.0.0.0");
			
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТипДанных = Выборка.ТипДанных; 
		
		СтруктураНастроек = Новый Структура;
		СтруктураНастроек.Вставить("Портал", Выборка.Портал);
		
		Если ТипДанных = Перечисления.Б24_К_ТипыДанныхДляОбменаСПорталом.ДелоКомпании 
			ИЛИ ТипДанных = Перечисления.Б24_К_ТипыДанныхДляОбменаСПорталом.ДелоКонтакта
			ИЛИ ТипДанных = Перечисления.Б24_К_ТипыДанныхДляОбменаСПорталом.ДелоЛида
			ИЛИ ТипДанных = Перечисления.Б24_К_ТипыДанныхДляОбменаСПорталом.ДелоСделки
			ИЛИ ТипДанных = Перечисления.Б24_К_ТипыДанныхДляОбменаСПорталом.ДелоСчета
			ИЛИ ТипДанных = Перечисления.Б24_К_ТипыДанныхДляОбменаСПорталом.ДелоСмартПроцесса Тогда
			
			РегистрыСведений.Б24_К_ИдентификаторыДел.ЗаписатьИдБ24Объекта(СтруктураНастроек, Выборка.ТипДанных
			, Выборка.Объект, Выборка.Идентификатор,  Выборка.ДополнительныйИдентификатор);  
		КонецЕсли;
		
	КонецЦикла;

	УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, ВерсияОбновления);  
			
КонецПроцедуры

Процедура ПереходНаВерсию3010()
	
	Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Попытка  
		
			Настройки = Выборка.Настройки.Получить();    
			
			Если Настройки = Неопределено Тогда
				Продолжить;
			КонецЕсли;    
			
#Область ЗаполнениеПоПредложениямНоменклатуре     
			ЕстьПредложенияНоменклатура = Ложь;    
			Для каждого ТекЭлемент Из Настройки.Сущности Цикл
				Если ТекЭлемент.ВидСущности = "Товар2" Тогда  
					ЕстьТовары2 = Истина;
				КонецЕсли;
				Если ТекЭлемент.ВидСущности = "Предложение"  И ТекЭлемент.ИмяОбъекта = "Номенклатура" Тогда
					ЕстьПредложенияНоменклатура = Истина;
				КонецЕсли;
			КонецЦикла;
			
			СтруктураДанныхКонструктораИнтеграцийПоУмолчанию = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСтруктуруДанныхКонструктораИнтеграцийПоУмолчанию();   
			
			мНовыеСущности = Новый Массив;
			Для каждого ТекЭлемент Из СтруктураДанныхКонструктораИнтеграцийПоУмолчанию.Сущности Цикл
				Если НЕ ЕстьПредложенияНоменклатура И ТекЭлемент.ВидСущности = "Предложение" И ТекЭлемент.ИмяОбъекта = "Номенклатура" Тогда
					мНовыеСущности.Добавить(ТекЭлемент);
					Настройки.Сущности.Добавить(ТекЭлемент); 
				КонецЕсли;  
			КонецЦикла;

			ДанныеСущности = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСоответствияВыгрузкиПоУмолчанию(мНовыеСущности);
			Для каждого ТекЭлемент Из ДанныеСущности Цикл
				Настройки.СоответствияПолейСущностейДляВыгрузкиНаПортал.Добавить(ТекЭлемент);		
			КонецЦикла;         
			
			ДанныеСущности = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСоответствияЗагрузкиПоУмолчанию(мНовыеСущности); 
			Для каждого ТекЭлемент Из ДанныеСущности Цикл
				Настройки.СоответствияПолейСущностейДляЗагрузкиВ1С.Добавить(ТекЭлемент);		
			КонецЦикла;  
			
			ДанныеСущности = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьОтборыПоВыгрузкеДанныхНаПорталПоУмолчанию(мНовыеСущности);
			Для каждого ТекЭлемент Из ДанныеСущности Цикл
				Настройки.ОтборыДляВыгрузкиНаПортал.Добавить(ТекЭлемент);		
			КонецЦикла;    
	#КонецОбласти
			
			ТекЗапись = Выборка.ПолучитьМенеджерЗаписи();
			ТекЗапись.Настройки = Новый ХранилищеЗначения(Настройки);
			ТекЗапись.Записать();
			
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Не удалось конвертировать настройки синхронизации для настройки подключения: " + Строка(Выборка.НастройкаПодключения));
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПереходНаВерсию31030(КлючХранилищаВерсии, ВерсияОбновления)
			
	ОбщегоНазначения.СообщитьПользователю("Осуществляется переход базовой подсистемы модуля интеграции с Битрикс24 на версию 3.1.0.30");
			
	НаборЗаписей = РегистрыСведений.Б24_К_ОчередьФоновыхЗаданий.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();

	УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, ВерсияОбновления);  
			
КонецПроцедуры

Процедура ПереходНаВерсию4000(КлючХранилищаВерсии, ВерсияОбновления)
	
	ОбщегоНазначения.СообщитьПользователю("Осуществляется переход базовой подсистемы модуля интеграции с Битрикс24 на версию 4.0.0.0");
	
	ОбщиеНастройки = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки");
	Если ОбщиеНастройки = Неопределено Тогда 
		ОбщиеНастройки = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОбщиеНастройкиПоУмолчанию();
	КонецЕсли;
	
	Константы.Б24_К_ПодключениеПоHTTP.Установить(ОбщиеНастройки.СпособВзаимодействияСБитрикс24 = "Через push&pull сервер");

	УстановитьЗначениеВХранилищаНастроек(КлючХранилищаВерсии, ВерсияОбновления);  
			
КонецПроцедуры

#КонецОбласти


#Область ПроцедурыИФункцииПоРаботеСФС

Функция ПолучитьТипПлатформы() Экспорт
	
	ПлатформаWindows = Ложь;
	
#ЕСЛИ НЕ МобильноеПриложениеСервер Тогда 	
		
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ПлатформаWindows = СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86
	ИЛИ СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64;
		
#КОНЕЦЕСЛИ	
	
	Возврат ПлатформаWindows;

КонецФункции

// Функция - возвращает путь к файлам/папкам, в зависимости от ОС
//
// Параметры:
//  ПлатформаWindows - 	 Признак того, что эта ОС - Windows 
//  Путь			 - 	 Адрес к файлу/папке 
// Возвращаемое значение:
//   Адрес к файлу/папке 
Функция ПолучитьПутьДляПлатформы(ПлатформаWindows, Путь) Экспорт
	
	Если ПлатформаWindows Тогда
		ЧтоМенять = "/";
		НаЧтоМенять = "\";
	Иначе
		ЧтоМенять = "\";
		НаЧтоМенять = "/";
	КонецЕсли;
	
	Путь = СтрЗаменить(Путь, ЧтоМенять, НаЧтоМенять);
	
	Возврат Путь;
	
КонецФункции

// Функция - проверяет, существует ли указанный файл
//
// Параметры:
//  ИмяФайла - 	 Имя проверяемого файла 
// Возвращаемое значение:
//   Истина, если файл существует 
Функция СуществуетФайл(ИмяФайла) Экспорт
	ВыбФайл = Новый Файл(ИмяФайла);
	Если ВыбФайл.Существует()  Тогда  
		Возврат Истина;
	КонецЕсли;  
	Возврат Ложь;
КонецФункции

// Функция - очищает содержимое каталога 
//
// Параметры:
//  Каталог			 - 	 Каталог, в котором очищаются файлы 
//  ПараметрыОбмена	 - 	 Настройки узла обмена  
// Возвращаемое значение:
//   Истина, если каталог очищен 
Функция КаталогОчищен(Каталог, ПараметрыОбмена = Неопределено) Экспорт
	
	Попытка
		
		УдалитьФайлы(Каталог, "*.*");
		
	Исключение
		
		ОбщегоНазначения.СообщитьПользователю("Не удалось очистить каталог обмена: (" + Каталог + ")");	
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьРасширениеФайлаПоНаименованию(ИмяФайла) Экспорт
	
	Если Сред(ИмяФайла, СтрДлина(ИмяФайла)-3,1) = "." Тогда
		Расширение = Прав(ИмяФайла, 4);	
	ИначеЕсли Сред(ИмяФайла, СтрДлина(ИмяФайла)-4,1) = "." Тогда
		Расширение = Прав(ИмяФайла, 5);
	ИначеЕсли Сред(ИмяФайла, СтрДлина(ИмяФайла)-2,1) = "." Тогда
		Расширение = Прав(ИмяФайла, 3);
	Иначе
		Расширение = "";	
	КонецЕсли;
	
	Возврат Расширение;
	
КонецФункции

Функция ПолучитьАдресФайлаТома(ПлатформаWindows, ИнформацияФайла) Экспорт

	АдресФайла = "";
	
	Если НЕ ЗначениеЗаполнено(ИнформацияФайла.ПутьКФайлу) Тогда Возврат АдресФайла КонецЕсли; 
	Если ПлатформаWindows И НЕ ЗначениеЗаполнено(ИнформацияФайла.ТомПолныйПутьWindows) Тогда Возврат АдресФайла КонецЕсли; 
	Если НЕ ПлатформаWindows И НЕ ЗначениеЗаполнено(ИнформацияФайла.ТомПолныйПутьLinux) Тогда Возврат АдресФайла КонецЕсли; 
	
	АдресФайла = ПолучитьПутьДляПлатформы(ПлатформаWindows, ?(ПлатформаWindows, ИнформацияФайла.ТомПолныйПутьWindows, ИнформацияФайла.ТомПолныйПутьLinux) + "\" + ИнформацияФайла.ПутьКФайлу);
	Возврат АдресФайла;

КонецФункции  

#КонецОбласти


#Область РаботаСXML

Функция ПолучитьЗначениеПоXML(ТипОбъекта, Идентификатор) Экспорт
	
	Если ТипЗнч(Идентификатор) <> Тип("Строка") Тогда
		Возврат Идентификатор;
	КонецЕсли;
	
	ТипыИсключенияНаВерсиюДанных = Новый Массив;
	ТипыИсключенияНаВерсиюДанных.Добавить(Тип("Число"));	
	ТипыИсключенияНаВерсиюДанных.Добавить(Тип("Строка"));	
	ТипыИсключенияНаВерсиюДанных.Добавить(Тип("Дата"));	
	ТипыИсключенияНаВерсиюДанных.Добавить(Тип("Булево"));	
	
	Попытка
		
		Результат = Неопределено;
		
		ПрефиксыВнешнихКодов1С = ПолучитьПрефиксВнешнихКодов1С();
		Для каждого ТекКлюч Из ПрефиксыВнешнихКодов1С Цикл
			
			ДлинаПрефикса = СтрДлина(ТекКлюч.Ключ);
			Если Лев(Идентификатор, ДлинаПрефикса) = ТекКлюч.Ключ Тогда
				
				Результат = ПолучитьXMLЗначение(ТекКлюч.Значение, Прав(Идентификатор, СтрДлина(Идентификатор)-ДлинаПрефикса));
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если Результат = Неопределено Тогда 
			Результат = ПолучитьXMLЗначение(ТипОбъекта, Идентификатор);
		КонецЕсли;
		                    
		Если ЗначениеЗаполнено(Результат) И ТипыИсключенияНаВерсиюДанных.Найти(ТипОбъекта) = Неопределено Тогда
			
			Результат = ?(ЗначениеЗаполнено(Результат.ВерсияДанных), Результат, Неопределено);
			
			Если Результат = Неопределено И ТипОбъекта = Тип("СправочникСсылка.Номенклатура") Тогда
				Результат = ПолучитьЗначениеПоXML(Тип("СправочникСсылка.ХарактеристикиНоменклатуры"), Идентификатор);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Результат = Неопределено И ТипыИсключенияНаВерсиюДанных.Найти(ТипОбъекта) = Неопределено И Метаданные.Перечисления.Содержит(Метаданные.НайтиПоТипу(ТипОбъекта)) Тогда
			Результат = XMLЗначение(ТипОбъекта, Идентификатор);
		КонецЕсли;
		
	Исключение
		
		Результат = Неопределено;
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьXMLЗначение(ТипОбъекта, Идентификатор)
	
	Если ТипЗнч(Идентификатор) <> Тип("Строка") Тогда
		Возврат Идентификатор;
	КонецЕсли;
	
	Результат = Неопределено;

	ТипыИсключенияНаВерсиюДанных = Новый Массив;
	ТипыИсключенияНаВерсиюДанных.Добавить(Тип("Число"));	
	ТипыИсключенияНаВерсиюДанных.Добавить(Тип("Строка"));	
	ТипыИсключенияНаВерсиюДанных.Добавить(Тип("Дата"));	
	ТипыИсключенияНаВерсиюДанных.Добавить(Тип("Булево"));	
	
	Если ТипыИсключенияНаВерсиюДанных.Найти(ТипОбъекта) = Неопределено Тогда
	
		Если СтрДлина(Идентификатор) = 36 Тогда
			Результат = XMLЗначение(ТипОбъекта, Идентификатор);
		КонецЕсли;
		
	Иначе
		
		Если Идентификатор = "" Тогда
			Если ТипОбъекта = Тип("Число") Тогда
				Результат = 0;
			ИначеЕсли ТипОбъекта = Тип("Строка") Тогда
				Результат = "";
			Иначе
				Результат = Неопределено;
			КонецЕсли;
		Иначе
			Результат = XMLЗначение(ТипОбъекта, Идентификатор);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьЗначениеПоИмениИИдентификатору(ПолноеИмя, ИдентификаторОбъекта) Экспорт
	
	НайденныйОбъектСсылка = Неопределено;
	
	Если Лев(ПолноеИмя, 9) = "Документ." Тогда  
		НайденныйОбъектСсылка  = ПолучитьXMLЗначение(Тип(СтрЗаменить(ПолноеИмя, "Документ.", "ДокументСсылка.")), ИдентификаторОбъекта);
	ИначеЕсли Лев(ПолноеИмя, 11) = "Справочник."Тогда   
		НайденныйОбъектСсылка  = ПолучитьXMLЗначение(Тип(СтрЗаменить(ПолноеИмя, "Справочник.", "СправочникСсылка.")), ИдентификаторОбъекта);
	КонецЕсли; 
	
	Возврат НайденныйОбъектСсылка;
	
КонецФункции


Функция ПолучитьОписаниеПрефиксовВнешнихКодов1С() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("Организации"				, "ORG_");	
	Результат.Вставить("БанковскиеСчета"			, "B_ORG_");	
	Результат.Вставить("КонтактныеЛица"				, "CL_");	
	Результат.Вставить("ОперацияПоПлатежнымКартам"	, "PC_");	
	Результат.Вставить("ПоступлениеВКассу"			, "CR_");	
	Результат.Вставить("ПоступлениеНаСчет"			, "SA_");	
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьПрефиксВнешнихКодов1С()
	
	Результат = Новый Структура;
	Результат.Вставить("ORG_"	, Тип("СправочникСсылка.Организации"));	
	//Результат.Вставить("B_ORG_"	, Тип("СправочникСсылка.БанковскиеСчетаОрганизаций"));	
	//Результат.Вставить("CL_"	, Тип("СправочникСсылка.КонтактныеЛицаПартнеров"));	
	//Результат.Вставить("PC_"	, Тип("ДокументСсылка.ОперацияПоПлатежнойКарте"));	
	//Результат.Вставить("CR_"	, Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));	
	//Результат.Вставить("SA_"	, Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств"));	

	Возврат Результат;
	
КонецФункции

Функция ПолучитьПрефиксВнешнихКодовБитрикс24() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("КонтактныеЛица"				, "#");	
	Результат.Вставить("Проекты"						, "S_");	
	Результат.Вставить("Заказы"						, "Z_");	
	Результат.Вставить("Счета"						, "B_");	
	Результат.Вставить("ОперацияПоПлатежнымКартам"	, "PC_");	
	Результат.Вставить("ПоступлениеВКассу"			, "CR_");	
	Результат.Вставить("ПоступлениеНаСчет"			, "SA_");	
	
	Возврат Результат;
	
КонецФункции


Функция ПолучитьНоменклатуруИХарактеристикуПоВнешнемуИдентификатору(ВнешнийИдентификатор) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Объект"				, Справочники.Номенклатура.ПустаяСсылка());	
	Результат.Вставить("ПодчиненныйОбъект"	, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());	
	
	Попытка
		
		ПозицияРазделителя = СтрНайти(ВнешнийИдентификатор, "#");
		Если ПозицияРазделителя >0 Тогда
			ПерваяЧасть 	= Лев(ВнешнийИдентификатор, ПозицияРазделителя-1);
			ВтораяЧасть 	= Прав(ВнешнийИдентификатор, СтрДлина(ВнешнийИдентификатор)-ПозицияРазделителя-СтрДлина("#")+1);
		Иначе
			ПерваяЧасть 	= ВнешнийИдентификатор;
			ВтораяЧасть		= "";
		КонецЕсли;
		
		СтруктураСтроки = Новый Структура;
		СтруктураСтроки.Вставить("ПерваяЧасть", ПерваяЧасть);
		СтруктураСтроки.Вставить("ВтораяЧасть", ВтораяЧасть);
		
		Если СтруктураСтроки.ВтораяЧасть = "" Тогда
			Результат.Объект 			= ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Номенклатура"), СтруктураСтроки.ПерваяЧасть); 	
			Результат.ПодчиненныйОбъект = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка(); 	
		Иначе
			Результат.Объект 			= ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Номенклатура"), СтруктураСтроки.ПерваяЧасть); 	
			Результат.ПодчиненныйОбъект = ПолучитьЗначениеПоXML(Тип("СправочникСсылка.ХарактеристикиНоменклатуры"), СтруктураСтроки.ВтораяЧасть); 
		КонецЕсли;
	Исключение
		Результат = Неопределено;	
	КонецПопытки;
	
	Возврат Результат;	
	
КонецФункции

Функция ПроверитьУдалитьПрефиксИдОбъекта(Идентификатор) Экспорт
	
	ИдБитрикс24	 = Идентификатор;
	Для каждого ТекКлюч Из ПолучитьПрефиксВнешнихКодовБитрикс24() Цикл
		
		Если СтрНайти(ИдБитрикс24, ТекКлюч.Значение) > 0 Тогда
			ИдБитрикс24 = Прав(ИдБитрикс24, СтрДлина(ИдБитрикс24) - СтрДлина(ТекКлюч.Значение)); 	
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИдБитрикс24;
	
КонецФункции

#КонецОбласти


#Область РаботаСОбщимиНастройками

Функция ПолучитьЗначениеХранилищаНастроек(Настройка, НаименованиеКлюча = "", Пользователь = "Общие") Экспорт
	
УстановитьПривилегированныйРежим(Истина);

	Результат = Неопределено;
	
	СохраненныеНастройки = ХранилищеОбщихНастроек.Загрузить(ПолучитьНазваниеМодуля(), Настройка, , Пользователь);
	Если СохраненныеНастройки = Неопределено Тогда 
		СохраненныеНастройки = ХранилищеОбщихНастроек.Загрузить("1С:Бэкофис 2.0", Настройка, , Пользователь);
	КонецЕсли;    
	
	Если ТипЗнч(СохраненныеНастройки) = Тип("Структура") И НЕ НаименованиеКлюча = "" Тогда

		Если СохраненныеНастройки.Свойство(НаименованиеКлюча) <> Ложь Тогда
			Результат = СохраненныеНастройки[НаименованиеКлюча];	
		Иначе
			СохраненныеНастройки.Вставить(НаименованиеКлюча, Неопределено);
			УстановитьЗначениеВХранилищаНастроек(Настройка, СохраненныеНастройки);
		КонецЕсли;	
	Иначе         		
		
		Если СохраненныеНастройки = Неопределено Тогда
			УстановитьЗначениеВХранилищаНастроек(Настройка, Неопределено);
		Иначе
			Результат = СохраненныеНастройки;	
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат; 
	
КонецФункции

Процедура УстановитьЗначениеВХранилищаНастроек(Настройка, ЗначениеНастройки, Пользователь = "Общие") Экспорт
	
УстановитьПривилегированныйРежим(Истина);
	
	ХранилищеОбщихНастроек.Сохранить(ПолучитьНазваниеМодуля(), Настройка, ЗначениеНастройки, , Пользователь);
	
КонецПроцедуры

#КонецОбласти


#Область Логирование

Процедура ДобавитьВЛог(СтруктураНастроек, ТипСообщения, пСобытие, СсылкаНаОбъект = Неопределено) Экспорт
	
	ТекущаяДата = ТекущаяДатаСеанса();
		
	Если ТипЗнч(СтруктураНастроек) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	Если НЕ СтруктураНастроек.Свойство("Логирование") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураНастроек.Свойство("ИнтерактивныйЗапуск") Тогда
		Если СтруктураНастроек.Логирование.Измерение1 <> "Служебные операции" Тогда        
			
			Если НЕ Лев(пСобытие, 26)= "Тело запроса HTTP запроса:" И НЕ Лев(пСобытие, 16)= "Ответ с портала:" Тогда
				СообщитьПользователю(пСобытие, СсылкаНаОбъект, Формат(ТекущаяДата, "ДЛФ=T"), ТипСообщения);
			КонецЕсли;
			
		КонецЕсли;   
	КонецЕсли;
	
	Если СтрДлина(пСобытие) > 1048576 Тогда        //1 мб
		ТексСообщения = Лев(пСобытие, 1048576);	
	Иначе
		ТексСообщения = пСобытие;	
	КонецЕсли;
	
	СтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения + 1;
	
	НовыйЭлемент = РегистрыСведений.Б24_К_Логирование.СоздатьМенеджерЗаписи();
	НовыйЭлемент.НастройкаПодключения	= СтруктураНастроек.НастройкаПодключения;
	НовыйЭлемент.Период 				= ТекущаяДата;
	НовыйЭлемент.ВремяСообщения 		= ТекущаяДата;
	НовыйЭлемент.НомерСообщения 		= СтруктураНастроек.Логирование.НомерСообщения;
	НовыйЭлемент.ВремяЗапуска 			= СтруктураНастроек.Логирование.ВремяЗапуска;
	НовыйЭлемент.Измерение1 			= СтруктураНастроек.Логирование.Измерение1;
	НовыйЭлемент.Измерение2 			= СтруктураНастроек.Логирование.Измерение2;
	НовыйЭлемент.Измерение3 			= СтруктураНастроек.Логирование.Измерение3;
	НовыйЭлемент.Измерение4 			= СтруктураНастроек.Логирование.Измерение4;
	НовыйЭлемент.Измерение5 			= СтруктураНастроек.Логирование.Измерение5;
	НовыйЭлемент.Измерение6 			= СтруктураНастроек.Логирование.Измерение6;
	НовыйЭлемент.ТипСообщения 			= ТипСообщения;
	НовыйЭлемент.Сообщение 				= ТексСообщения;
	НовыйЭлемент.Записать();
	
	//Если СтруктураНастроек.Логирование.Свойство("СообщениеВЧат") И НЕ СтруктураНастроек.Логирование.СообщениеВЧат 
	//	И ТипСообщения <> СтруктураНастроек.ТипыСообщений.Информация И СтруктураНастроек.ОповещатьПользователейБитрикс24 И СтруктураНастроек.НастройкиПодключения.Свойство("Сервер") Тогда  
	//  	ДобавитьСообщениеВСлужебныйЧат(СтруктураНастроек, СтруктураНастроек.НастройкаПодключения, ТексСообщения);
	//КонецЕсли;
	//
	Если ТипСообщения <> СтруктураНастроек.ТипыСообщений.Информация И СтруктураНастроек.ОбщиеНастройки.ХранитьИнформациюОбОшибках = Истина Тогда
		
		ОписаниеИзмерения = ?(ЗначениеЗаполнено(СтруктураНастроек.Логирование.Измерение1), СтруктураНастроек.Логирование.Измерение1, "");
		ОписаниеИзмерения = ОписаниеИзмерения + ?(ЗначениеЗаполнено(СтруктураНастроек.Логирование.Измерение2), "\\" + СтруктураНастроек.Логирование.Измерение2, "");
		ОписаниеИзмерения = ОписаниеИзмерения + ?(ЗначениеЗаполнено(СтруктураНастроек.Логирование.Измерение3), "\\" + СтруктураНастроек.Логирование.Измерение3, "");
		ОписаниеИзмерения = ОписаниеИзмерения + ?(ЗначениеЗаполнено(СтруктураНастроек.Логирование.Измерение4), "\\" + СтруктураНастроек.Логирование.Измерение4, "");
		
		НовыйЭлемент = РегистрыСведений.Б24_К_ЖурналОшибок.СоздатьМенеджерЗаписи();
		НовыйЭлемент.Получатель				= "Почта";       //получать всегда. Он же отображается в журнале.
		НовыйЭлемент.НастройкаПодключения	= СтруктураНастроек.НастройкаПодключения;
		НовыйЭлемент.Операция				= СтруктураНастроек.Операция;
		НовыйЭлемент.ВремяЗапуска 			= СтруктураНастроек.Логирование.ВремяЗапуска;
		НовыйЭлемент.НомерСообщения 		= СтруктураНастроек.Логирование.НомерСообщения;
		НовыйЭлемент.Сообщение 				= ТексСообщения;
		НовыйЭлемент.ТипСообщения 			= ТипСообщения;
		НовыйЭлемент.ДатаСообщения 			= ТекущаяДата;
		
		НовыйЭлемент.ОписаниеИзмерения 		= ОписаниеИзмерения;
		
		НовыйЭлемент.Записать();
		
		Если СтруктураНастроек.ОбщиеНастройки.ОповещатьООшибкахПользователей1С Тогда
			
			Для Каждого ТекПользователь Из СтруктураНастроек.ОбщиеНастройки.ОповещающиесяПользователи1С Цикл
				Если ЗначениеЗаполнено(ТекПользователь.Значение) Тогда
					
					Если НЕ ЗначениеЗаполнено(НовыйЭлемент.Операция) Тогда НовыйЭлемент.Операция = "Общие" КонецЕсли;
					
					НовыйЭлемент = РегистрыСведений.Б24_К_ЖурналОшибок.СоздатьМенеджерЗаписи();
					НовыйЭлемент.Получатель				= ТекПользователь.Значение;
					НовыйЭлемент.НастройкаПодключения	= СтруктураНастроек.НастройкаПодключения;
					НовыйЭлемент.Операция				= СтруктураНастроек.Операция;
					НовыйЭлемент.ВремяЗапуска 			= СтруктураНастроек.Логирование.ВремяЗапуска;
					НовыйЭлемент.НомерСообщения 		= СтруктураНастроек.Логирование.НомерСообщения;
					
					НовыйЭлемент.Сообщение 			= ТексСообщения;
					НовыйЭлемент.ТипСообщения 		= ТипСообщения;
					НовыйЭлемент.ДатаСообщения 		= ТекущаяДата;
					
					НовыйЭлемент.ОписаниеИзмерения 	= ОписаниеИзмерения;
					
					НовыйЭлемент.Записать();
					
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры 

Процедура СообщитьПользователю(Знач ТекстСообщенияПользователю, Знач КлючДанных = Неопределено,	Знач Поле = "",
	Знач ПутьКДанным = "", Отказ = Ложь) Экспорт
	
	ЭтоОбъект = Ложь;
	
	Если КлючДанных <> Неопределено
		И XMLТипЗнч(КлючДанных) <> Неопределено Тогда
		
		ТипЗначенияСтрокой = XMLТипЗнч(КлючДанных).ИмяТипа;
		ЭтоОбъект = СтрНайти(ТипЗначенияСтрокой, "Object.") > 0;
	КонецЕсли;
	
	текСообщение = Новый СообщениеПользователю;
	текСообщение.Текст = ТекстСообщенияПользователю;
	текСообщение.Поле = Поле;
	
	Если ЭтоОбъект Тогда
		текСообщение.УстановитьДанные(КлючДанных);
	Иначе
		текСообщение.КлючДанных = КлючДанных;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ПутьКДанным) Тогда
		текСообщение.ПутьКДанным = ПутьКДанным;
	КонецЕсли;
	
	текСообщение.Сообщить();
	
КонецПроцедуры

#КонецОбласти


#Область ОповещениеОбОшибках

Процедура ПроверкаОтправкаПисемСОшибками() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОповещатьООшибкахНаПочту 		= ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "ОповещатьООшибкахНаПочту");
	УчетнаяЗаписьЭлектроннойПочты 	= ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "УчетнаяЗаписьЭлектроннойПочты");
	ЭлектронныеПочтыПолучатели 		= ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "ЭлектронныеПочтыПолучатели");
	ЭлектронныеПочтыПолучатели 		= ?(ЭлектронныеПочтыПолучатели = Неопределено, Новый СписокЗначений, ЭлектронныеПочтыПолучатели);
	
	Если ОповещатьООшибкахНаПочту <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	мНастройкиПодключений = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьМассивНастроекПодключения();
	
	Если мНастройкиПодключений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ЖурналОшибок.Получатель КАК Получатель,
	|	Б24_К_ЖурналОшибок.НастройкаПодключения КАК НастройкаПодключения,
	|	Б24_К_ЖурналОшибок.Операция КАК Операция,
	|	Б24_К_ЖурналОшибок.ВремяЗапуска КАК ВремяЗапуска,
	|	Б24_К_ЖурналОшибок.НомерСообщения КАК НомерСообщения,
	|	Б24_К_ЖурналОшибок.Сообщение КАК Сообщение,
	|	ВЫБОР
	|		КОГДА Б24_К_ЖурналОшибок.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.Б24_К_ТипыСообщений.Ошибка)
	|			ТОГДА ""Ошибка""
	|		ИНАЧЕ ""Критическая ошибка""
	|	КОНЕЦ КАК ТипСообщения,
	|	Б24_К_ЖурналОшибок.ДатаСообщения КАК ДатаСообщения,
	|	Б24_К_ЖурналОшибок.ОписаниеИзмерения КАК ОписаниеИзмерения
	|ПОМЕСТИТЬ ВТ_Ошибки
	|ИЗ
	|	РегистрСведений.Б24_К_ЖурналОшибок КАК Б24_К_ЖурналОшибок
	|ГДЕ
	|	Б24_К_ЖурналОшибок.Просмотрено = ЛОЖЬ
	|	И Б24_К_ЖурналОшибок.Обработано = ЛОЖЬ";
	Запрос.Выполнить();
	
	Для Каждого ТекНастройка Из мНастройкиПодключений Цикл
		
		Если ЗначениеЗаполнено(УчетнаяЗаписьЭлектроннойПочты) Тогда
			
			СообщениеВПисьме = "";
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("НастройкаПодключения"	, ТекНастройка);
			Запрос.УстановитьПараметр("Получатель"				, "Почта");
			
			Запрос.Текст = "ВЫБРАТЬ *
			|ИЗ
			|	ВТ_Ошибки КАК ВТ_Ошибки
			|ГДЕ
			|	ВТ_Ошибки.НастройкаПодключения = &НастройкаПодключения
			|	И ВТ_Ошибки.Получатель = &Получатель
			|
			|УПОРЯДОЧИТЬ ПО
			|	ВТ_Ошибки.НомерСообщения";
			
			ВыполненныйЗапрос = Запрос.Выполнить();
			
			Если НЕ ВыполненныйЗапрос.Пустой() Тогда
				
				ВыборкаОшибок = ВыполненныйЗапрос.Выбрать();
				
				СообщениеВПисьме = "<p><font size=""5""><b>Ошибки синхронизации</b></font></p>";
				СообщениеВПисьме = СообщениеВПисьме + "<table align=""left"" border=""1"" cellpadding=""1"" cellspacing=""1"" style=""width: 1400px"">";
				СообщениеВПисьме = СообщениеВПисьме + "<thead><tr><th scope=""col"">Тип ошибки</th><th scope=""col"">Дата</th><th scope=""col"">Описание ошибки</th><th scope=""col"">Измерение</th></tr></thead><tbody>";
				
				Пока ВыборкаОшибок.Следующий() Цикл
					СообщениеВПисьме = СообщениеВПисьме + "<tr>";
					СообщениеВПисьме = СообщениеВПисьме + "<td>" + ВыборкаОшибок.ТипСообщения + "</td>"; 
					СообщениеВПисьме = СообщениеВПисьме + "<td>" + ВыборкаОшибок.ДатаСообщения + "</td>"; 
					СообщениеВПисьме = СообщениеВПисьме + "<td>" + ВыборкаОшибок.Сообщение + "</td>"; 
					СообщениеВПисьме = СообщениеВПисьме + "<td>" + ВыборкаОшибок.ОписаниеИзмерения + "</td>"; 
					
					СообщениеВПисьме = СообщениеВПисьме + "</tr>";
				КонецЦикла;
				
				СообщениеВПисьме = СообщениеВПисьме + "</tbody></table>";
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СообщениеВПисьме) Тогда
				
				ПолучателиПисем = Новый Массив;
				Для каждого ТекПочта Из ЭлектронныеПочтыПолучатели Цикл
					ПолучателиПисем.Добавить(Новый Структура("Адрес, Представление", ТекПочта.Значение, ТекПочта.Значение));	
				КонецЦикла;
				
				ПараметрыОтправки = Новый Структура;
				ПараметрыОтправки.Вставить("Кому"				, ПолучателиПисем);
				ПараметрыОтправки.Вставить("Тема"				, "Ошибки синхронизации с Битрикс24. Настройка: " + Строка(ТекНастройка));
				ПараметрыОтправки.Вставить("Тело"				, СообщениеВПисьме);
				ПараметрыОтправки.Вставить("ОбрабатыватьТексты"	, Истина);
				ПараметрыОтправки.Вставить("ТипТекста"			, Перечисления.ТипыТекстовЭлектронныхПисем.HTML);
				
				ЕстьОшибкаПриОтправлении = Ложь;
				
				Попытка
					РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(УчетнаяЗаписьЭлектроннойПочты, ПараметрыОтправки);
				Исключение
					ЗаписьЖурналаРегистрации("ОтправкаПисемСОшибкамиСинхронизации", УровеньЖурналаРегистрации.Ошибка,,, "Не удалось отправить письма по учетке:" + Строка(УчетнаяЗаписьЭлектроннойПочты));
					ЗаписьЖурналаРегистрации("ОтправкаПисемСОшибкамиСинхронизации", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					ЕстьОшибкаПриОтправлении = Истина;
				КонецПопытки;
				
				Если НЕ ЕстьОшибкаПриОтправлении Тогда
					
					ВыборкаОшибок = ВыполненныйЗапрос.Выбрать();		
					
					Пока ВыборкаОшибок.Следующий() Цикл
						МенеджерЗаписи = РегистрыСведений.Б24_К_ЖурналОшибок.СоздатьМенеджерЗаписи();
						ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ВыборкаОшибок);
						МенеджерЗаписи.Прочитать();
						МенеджерЗаписи.Просмотрено = Истина;
						МенеджерЗаписи.Записать();	
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Функция ЭтотПользовательПолучаетОповещения() Экспорт

	Результат = Ложь;
	
	ОповещатьООшибкахПользователей1С = ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "ОповещатьООшибкахПользователей1С");
	
	Если ОповещатьООшибкахПользователей1С = Истина Тогда
		
		ОповещающиесяПользователи1С = ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "ОповещающиесяПользователи1С");
		
		Если ТипЗнч(ОповещающиесяПользователи1С) = Тип("СписокЗначений") Тогда
			Если ОповещающиесяПользователи1С.НайтиПоЗначению(Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьТекущегоПользователя()) <> Неопределено Тогда	
				Результат = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьМассивОшибокСообщений() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	ТипыСообщений = ПолучитьТипыСообщений();
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Получатель", Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьТекущегоПользователя());
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ЖурналОшибок.Получатель КАК Получатель,
	|	Б24_К_ЖурналОшибок.НастройкаПодключения КАК НастройкаПодключения,
	|	Б24_К_ЖурналОшибок.Операция КАК Операция,
	|	Б24_К_ЖурналОшибок.ВремяЗапуска КАК ВремяЗапуска,
	|	Б24_К_ЖурналОшибок.НомерСообщения КАК НомерСообщения,
	|	Б24_К_ЖурналОшибок.Сообщение КАК Сообщение,
	|	Б24_К_ЖурналОшибок.Просмотрено КАК Просмотрено,
	|	Б24_К_ЖурналОшибок.ТипСообщения КАК ТипСообщения,
	|	Б24_К_ЖурналОшибок.ДатаСообщения КАК ДатаСообщения,
	|	Б24_К_ЖурналОшибок.ОписаниеИзмерения КАК ОписаниеИзмерения,
	|	Б24_К_ЖурналОшибок.Обработано КАК Обработано
	|ИЗ
	|	РегистрСведений.Б24_К_ЖурналОшибок КАК Б24_К_ЖурналОшибок
	|ГДЕ
	|	Б24_К_ЖурналОшибок.Получатель = &Получатель
	|	И Б24_К_ЖурналОшибок.Просмотрено = ЛОЖЬ";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда 
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НовоеСообщение = Новый Структура;
			НовоеСообщение.Вставить("Получатель"			, Выборка.Получатель); 
			НовоеСообщение.Вставить("НастройкаПодключения"	, Выборка.НастройкаПодключения); 
			НовоеСообщение.Вставить("Операция"				, Выборка.Операция); 
			НовоеСообщение.Вставить("ВремяЗапуска"			, Выборка.ВремяЗапуска); 
			НовоеСообщение.Вставить("НомерСообщения"		, Выборка.НомерСообщения); 
			НовоеСообщение.Вставить("Сообщение"				, Выборка.Сообщение); 
			НовоеСообщение.Вставить("Просмотрено"			, Выборка.Просмотрено); 
			НовоеСообщение.Вставить("ТипСообщения"			, Выборка.ТипСообщения); 
			НовоеСообщение.Вставить("ДатаСообщения"			, Выборка.ДатаСообщения); 
			НовоеСообщение.Вставить("ОписаниеИзмерения"		, Выборка.ОписаниеИзмерения); 
			НовоеСообщение.Вставить("Обработано"			, Выборка.Обработано); 
			НовоеСообщение.Вставить("ТипСообщенияСтрокой"	, ?(Выборка.ТипСообщения = ТипыСообщений.Ошибка, "Ошибка", "Критическая ошибка")); 
			
			Результат.Добавить(НовоеСообщение);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьИдентификаторЧата(СтруктураНастроек, НастройкаПодключения, ОбновитьИдЧата = Ложь)
	
	ИдентификаторСлужебногоЧата = Неопределено;
	
	ЕстьЧат = Ложь;
	мИдентификаторы = ПолучитьЗначениеХранилищаНастроек("ИдентификаторыСлужебногоЧата"); 
	Если ТипЗнч(мИдентификаторы) = Тип("Соответствие") Тогда
		ИдентификаторСлужебногоЧата = мИдентификаторы.Получить(НастройкаПодключения);  
		Если ЗначениеЗаполнено(ИдентификаторСлужебногоЧата) Тогда   
        	ЕстьЧат = Истина;
		КонецЕсли;
	КонецЕсли;
	
	
	Если НЕ ЕстьЧат ИЛИ ОбновитьИдЧата Тогда  
		
		ТелоHTTPЗапроса = "";
		Для Каждого ТекСтрока Из НастройкаПодключения.ПолучателиДанныхОбОшибках Цикл    
			
			Если Не ЗначениеЗаполнено(ТекСтрока.ИдентификаторПользователяБитрикс24) Тогда
				Продолжить;
			КонецЕсли;
			
			ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&USERS[]="+ТекСтрока.ИдентификаторПользователяБитрикс24; 	
			
		КонецЦикла;
		
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&COLOR=SAND";
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&TITLE=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("Чат с ошибками синхронизации 1С: " + Строка(НастройкаПодключения));
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&DESCRIPTION=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("Чат с ошибками синхронизации 1С: " + Строка(НастройкаПодключения));
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&AVATAR=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Base64Строка(БиблиотекаКартинок.Б24_К_ИконкаПоиска.ПолучитьДвоичныеДанные()));
		
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/im.chat.add", ТелоHTTPЗапроса);       
		Если СтруктураОтвета <> Неопределено Тогда   
			ИдентификаторСлужебногоЧата = Формат(СтруктураОтвета.Получить("result"), "ЧГ=0");          
			
			Если ТипЗнч(мИдентификаторы) = Тип("Соответствие") Тогда
				мИдентификаторы.Вставить(НастройкаПодключения, ИдентификаторСлужебногоЧата)	
			Иначе
				мИдентификаторы = Новый Соответствие;
				мИдентификаторы.Вставить(НастройкаПодключения, ИдентификаторСлужебногоЧата)	
			КонецЕсли;
			
			УстановитьЗначениеВХранилищаНастроек("ИдентификаторыСлужебногоЧата", мИдентификаторы); 
		КонецЕсли;
	КонецЕсли;

	Возврат ИдентификаторСлужебногоЧата;
	
КонецФункции

Процедура ОбновитьЧат(НастройкаПодключения) Экспорт
	
	СтруктураНастроек =  СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСлужебногоЧата = Неопределено;
	
	ЕстьЧат = Ложь;
	мИдентификаторы = ПолучитьЗначениеХранилищаНастроек("ИдентификаторыСлужебногоЧата"); 
	Если ТипЗнч(мИдентификаторы) = Тип("Соответствие") Тогда
		ИдентификаторСлужебногоЧата = мИдентификаторы.Получить(НастройкаПодключения);  
		Если ЗначениеЗаполнено(ИдентификаторСлужебногоЧата) Тогда   
			ЕстьЧат = Истина;
		КонецЕсли;
	КонецЕсли;
	
	
	Если НЕ ЕстьЧат Тогда  
		
		ТелоHTTPЗапроса = "";
		Для Каждого ТекСтрока Из НастройкаПодключения.ПолучателиДанныхОбОшибках Цикл   
			
			Если Не ЗначениеЗаполнено(ТекСтрока.ИдентификаторПользователяБитрикс24) Тогда
				Продолжить;
			КонецЕсли;
			
			ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&USERS[]="+ТекСтрока.ИдентификаторПользователяБитрикс24; 	  
			
		КонецЦикла;
		
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&COLOR=SAND";
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&TITLE=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("Чат с ошибками синхронизации 1С: " + Строка(НастройкаПодключения));
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&DESCRIPTION=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("Чат с ошибками синхронизации 1С: " + Строка(НастройкаПодключения));
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&AVATAR=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Base64Строка(БиблиотекаКартинок.Б24_К_ИконкаПоиска.ПолучитьДвоичныеДанные()));
		
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/im.chat.add", ТелоHTTPЗапроса);       
		Если СтруктураОтвета <> Неопределено Тогда   
			ИдентификаторСлужебногоЧата = Формат(СтруктураОтвета.Получить("result"), "ЧГ=0");          
			
			Если ТипЗнч(мИдентификаторы) = Тип("Соответствие") Тогда
				мИдентификаторы.Вставить(НастройкаПодключения, ИдентификаторСлужебногоЧата)	
			Иначе
				мИдентификаторы = Новый Соответствие;
				мИдентификаторы.Вставить(НастройкаПодключения, ИдентификаторСлужебногоЧата)	
			КонецЕсли;
			
			УстановитьЗначениеВХранилищаНастроек("ИдентификаторыСлужебногоЧата", мИдентификаторы); 
		КонецЕсли;
	Иначе
		
		ТелоHTTPЗапроса = "&DIALOG_ID=chat"+ИдентификаторСлужебногоЧата;	
		
		лСтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/im.chat.user.list", ТелоHTTPЗапроса, Ложь); 
		Если лСтруктураОтвета <> Неопределено Тогда
			
			error_description 	= лСтруктураОтвета.Получить("error_description");  
			Если error_description = "Вы не можете отправлять сообщения в указанный чат" Тогда
				ИдентификаторСлужебногоЧата = ПолучитьИдентификаторЧата(СтруктураНастроек, НастройкаПодключения, Истина); 
				ОбновитьЧат(НастройкаПодключения);	
				Возврат;
			КонецЕсли;
			
			result = лСтруктураОтвета.Получить("result");
			Если ТипЗнч(result) = Тип("Массив") Тогда      
				
				мДобавить = Новый Массив;   
				Для каждого ТекСтрока Из НастройкаПодключения.ПолучателиДанныхОбОшибках Цикл      
					
					Если Не ЗначениеЗаполнено(ТекСтрока.ИдентификаторПользователяБитрикс24) Тогда
						Продолжить;
					КонецЕсли;
					
					Если result.Найти(Число(ТекСтрока.ИдентификаторПользователяБитрикс24)) = Неопределено Тогда
						мДобавить.Добавить(ТекСтрока.ИдентификаторПользователяБитрикс24);	
					КонецЕсли;
					
				КонецЦикла;	
					
				мУдалить = Новый Массив;
				Для каждого ТекЭлемент Из result Цикл   
					ИдПользователя = Формат(ТекЭлемент,"ЧГ=0"); 	
					Если НастройкаПодключения.ПолучателиДанныхОбОшибках.Найти(ИдПользователя) = Неопределено Тогда	
						мУдалить.Добавить(ИдПользователя);		
					КонецЕсли;	
					
				КонецЦикла;    
				
				Если мДобавить.Количество() > 0 Тогда	
					ТелоHTTPЗапроса = "&CHAT_ID="+ИдентификаторСлужебногоЧата;	
					Для каждого ТекЭлемент Из мДобавить Цикл   
						ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&USERS[]="+ТекЭлемент; 	
					КонецЦикла;
					лСтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/im.chat.user.add", ТелоHTTPЗапроса, Ложь); 
				КонецЕсли;  
				
				Если мУдалить.Количество() > 0 Тогда	
					Для каждого ТекЭлемент Из мУдалить Цикл   
						ТелоHTTPЗапроса = "&CHAT_ID="+ИдентификаторСлужебногоЧата+"&USER_ID="+ТекЭлемент;	
						лСтруктураОтвета 	= Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/im.chat.user.delete", ТелоHTTPЗапроса, Ложь); 
					КонецЦикла;
				КонецЕсли;  
				
			КонецЕсли;  
			
		КонецЕсли;	
			
	КонецЕсли;
	
КонецПроцедуры   

Процедура ДобавитьСообщениеВСлужебныйЧат(СтруктураНастроек, НастройкаПодключения, Сообщение) Экспорт   

	Если СтруктураНастроек.Свойство("ФильтрыСообщенийОбОшибках") Тогда  
		
		ВыгружатьОшибку = Истина;    
		
		Для каждого ТекЭлемент Из СтруктураНастроек.ФильтрыСообщенийОбОшибках Цикл
			Если ТекЭлемент <> "" И СтрНайти(Сообщение, ТекЭлемент) > 0 Тогда
				ВыгружатьОшибку = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ВыгружатьОшибку Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураНастроек.Логирование.СообщениеВЧат = Истина;
	
	ИдентификаторСлужебногоЧата = ПолучитьИдентификаторЧата(СтруктураНастроек, НастройкаПодключения);
	Если ИдентификаторСлужебногоЧата <> Неопределено Тогда
		
		ТелоHTTPЗапроса = "&DIALOG_ID=chat"+ИдентификаторСлужебногоЧата+"&MESSAGE="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Сообщение);;	
		
		лСтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/im.message.add", ТелоHTTPЗапроса, Ложь); 
		Если лСтруктураОтвета <> Неопределено Тогда
			
			error_description 	= лСтруктураОтвета.Получить("error_description");  
			Если error_description = "Вы не можете отправлять сообщения в указанный чат" Тогда
				ИдентификаторСлужебногоЧата = ПолучитьИдентификаторЧата(СтруктураНастроек, НастройкаПодключения, Истина); 
				ДобавитьСообщениеВСлужебныйЧат(СтруктураНастроек, НастройкаПодключения, Сообщение);	
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	СтруктураНастроек.Логирование.СообщениеВЧат = Ложь;
	
КонецПроцедуры

#КонецОбласти


#Область РаботаСМетаданнымиИРеквизитами

Функция ПолучитьРеквизитОбъекта(Объект, НазваниеРеквизита) Экспорт
	
	Возврат Объект[НазваниеРеквизита]; 
	
КонецФункции

Функция ПолучитьЗначениеРеквизитаОбъекта(Объект, ИмяРеквизита) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, ИмяРеквизита); 
	
КонецФункции


Функция ПолучитьИмяОбъектаПоСинониму(СинонимОбъекта) Экспорт
	
	Результат = Неопределено;
	
	Если СинонимОбъекта = "Поступление безналичных д.с." Тогда
		Результат = "ПоступлениеБезналичныхДенежныхСредств";
		Возврат Результат;
	ИначеЕсли СинонимОбъекта = "Эквайринговая операция" Тогда
		Результат = "ОперацияПоПлатежнойКарте";
		Возврат Результат;
	ИначеЕсли СинонимОбъекта = "Ком. предложение клиенту" Тогда
		Результат = "КоммерческоеПредложениеКлиенту";
		Возврат Результат;  
	ИначеЕсли СинонимОбъекта = "Сверка взаиморасчетов" Тогда
		Результат = "СверкаВзаиморасчетов";
		Возврат Результат;    
	КонецЕсли;
	
	Если СтрНайти(СинонимОбъекта, "Документ: ") > 0 Тогда     
		
		ОбработанныйСинонимОбъекта = СтрЗаменить(СинонимОбъекта, "Документ: ", "");
		Для Каждого ТекДокумент Из Метаданные.Документы Цикл
			Если ТекДокумент.Синоним = ОбработанныйСинонимОбъекта Тогда 
				Результат = ТекДокумент.Имя;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли СтрНайти(СинонимОбъекта, "Справочник: ") > 0 Тогда  
		
		ОбработанныйСинонимОбъекта = СтрЗаменить(СинонимОбъекта, "Справочник: ", "");  
		Для Каждого ТекДокумент Из Метаданные.Справочники Цикл
			Если ТекДокумент.Синоним = ОбработанныйСинонимОбъекта Тогда 
				Результат = ТекДокумент.Имя;
				Прервать;
			КонецЕсли;	
		КонецЦикла;   
		
	Иначе //для поддержки старого
		
		Для Каждого ТекДокумент Из Метаданные.Документы Цикл
			Если ТекДокумент.Синоним = СинонимОбъекта Тогда 
				Результат = ТекДокумент.Имя;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьОписаниеИспользуемыхДокументов() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("АктВыполненныхРабот"						, "Акт выполненных работ для клиента"	);
	Результат.Вставить("ВозвратТоваровОтКлиента"					, "Возврат товаров от клиента"			);
	Результат.Вставить("ВозвратТоваровПоставщику"					, "Возврат товаров поставщику"			);
	Результат.Вставить("ЗаказКлиента"								, "Заказ клиента"						);
	Результат.Вставить("ЗаказПоставщику"							, "Заказ поставщику"					);
	Результат.Вставить("ПриходныйКассовыйОрдер"						, "Приходный кассовый ордер"			);
	Результат.Вставить("ПоступлениеБезналичныхДенежныхСредств"		, "Поступление безналичных д.с."		);
	Результат.Вставить("ОперацияПоПлатежнойКарте"					, "Эквайринговая операция"				);
	Результат.Вставить("ПриобретениеТоваровУслуг"					, "Приобретение товаров и услуг"		);
	Результат.Вставить("РеализацияТоваровУслуг"						, "Реализация товаров и услуг"			);
	Результат.Вставить("СверкаВзаиморасчетов"						, "Сверка взаиморасчетов"				);
	Результат.Вставить("КоммерческоеПредложениеКлиенту"				, "Ком. предложение клиенту"			);
	Результат.Вставить("СчетНаОплатуКлиенту"						, "Счет на оплату клиенту"				);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьИдентификаторыИспользуемыхДокументов() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("АктВыполненныхРабот"					, "1");
	Результат.Вставить("ВозвратТоваровОтКлиента"				, "2");
	Результат.Вставить("ВозвратТоваровПоставщику"				, "3");
	Результат.Вставить("ЗаказПоставщику"						, "4");
	Результат.Вставить("ПриходныйКассовыйОрдер"					, "5");
	Результат.Вставить("ПоступлениеБезналичныхДенежныхСредств"	, "6");
	Результат.Вставить("ОперацияПоПлатежнойКарте"				, "7");
	Результат.Вставить("ПриобретениеТоваровУслуг"				, "8");
	Результат.Вставить("РеализацияТоваровУслуг"					, "9");
	Результат.Вставить("СверкаВзаиморасчетов"					, "10");
	Результат.Вставить("ЗаказКлиента"							, "11");
	Результат.Вставить("КоммерческоеПредложениеКлиенту"			, "12");
	Результат.Вставить("СчетНаОплатуКлиенту"					, "14");
	
	Возврат Результат;
	
КонецФункции
  
Функция ПолучитьСсылкуНаОбъектИзНавигационнойСсылки(НавигационнаяСсылка) Экспорт
    
    ПерваяТочка = Найти(НавигационнаяСсылка, "e1cib/data/");
    ВтораяТочка = Найти(НавигационнаяСсылка, "?ref=");
    
    ПредставлениеТипа   = Сред(НавигационнаяСсылка, ПерваяТочка + 11, ВтораяТочка - ПерваяТочка - 11);
    ШаблонЗначения = ЗначениеВСтрокуВнутр(ПредопределенноеЗначение(ПредставлениеТипа + ".ПустаяСсылка"));
    ЗначениеСсылки = СтрЗаменить(ШаблонЗначения, "00000000000000000000000000000000", Сред(НавигационнаяСсылка, ВтораяТочка + 5));
	
	Возврат ЗначениеИзСтрокиВнутр(ЗначениеСсылки);
    
КонецФункции 

#КонецОбласти


#Область РаботаСНастройками

Функция СформироватьСтруктуруНастроек(НастройкаПодключения, ДляСервера = Ложь) Экспорт
	
	СтруктураНастроек = Новый Структура;	
	СтруктураНастроек.Вставить("НастройкаПодключения"		, НастройкаПодключения);
	СтруктураНастроек.Вставить("ДатаФормирования"			, ТекущаяДатаСеанса());
	
	СтруктураНастроек.Вставить("ТипыСообщений"				, ПолучитьТипыСообщений());
	СтруктураНастроек.Вставить("ТипыОбъектовОбмена"			, ПолучитьТипыДанныхДляОбменаСПорталом());  
	СтруктураНастроек.Вставить("ТипыОперацийСинхронизации"	, ПолучитьТипыОпераций());
	СтруктураНастроек.Вставить("ТипыКонтрагентов"			, ПолучитьТипыКонтрагентов());
	
	СтруктураНастроек.Вставить("ПлатформаWindows"			, ПолучитьТипПлатформы());	
	
	СтруктураНастроек.Вставить("Портал"						, НастройкаПодключения.Портал);  
	СтруктураНастроек.Вставить("ИдентификаторПодключения"	, НастройкаПодключения.ИдентификаторПодключения);   
	СтруктураНастроек.Вставить("НеОбновлятьДелаБитрикс24"	, НастройкаПодключения.НеОбновлятьДелаБитрикс24);  
	СтруктураНастроек.Вставить("НеВыгружатьАдресаРеквизитов", НастройкаПодключения.НеВыгружатьАдресаРеквизитов);
	СтруктураНастроек.Вставить("НеЗагружатьАдресаРеквизитов", НастройкаПодключения.НеЗагружатьАдресаРеквизитов);   
	
	СтруктураНастроек.Вставить("ПараметрКоннектора"			, "");
	СтруктураНастроек.Вставить("Операция"					, "Общие"); 	
	СтруктураНастроек.Вставить("ПодкаталогФайлов"			, "import_files");
	СтруктураНастроек.Вставить("ВремяЗапускаВМиллисекундах"	, ТекущаяУниверсальнаяДатаВМиллисекундах());	

	//СтруктураНастроек.Вставить("ОповещатьПользователейБитрикс24", НастройкаПодключения.ОповещатьПользователейБитрикс24ОбОшибках);
	//СтруктураНастроек.Вставить("ФильтрыСообщенийОбОшибках"	, НастройкаПодключения.ФильтрыСообщенийОбОшибках.ВыгрузитьКолонку("КлючевойТекст")); 
	
	СтруктураНастроек.Вставить("ТипыКонтрагентовДляКомпаний"	, Новый Массив);
	СтруктураНастроек.Вставить("ТипыКонтрагентовДляКонтактов"	, Новый Массив);
	
	//Если НастройкаПодключения.ЗависимостиКлиентовОтТиповКонтрагентов.Количество() = 0 Тогда
	//	
	//	ЗависимостиКлиентовОтТиповКонтрагентовПоУмолчанию = Справочники.Б24_К_НастройкиПодключения.ЗависимостиКлиентовОтТиповКонтрагентовПоУмолчанию();
	//	
	//	Для каждого ТекЭлемент Из ЗависимостиКлиентовОтТиповКонтрагентовПоУмолчанию.ТипыКонтрагентовДляКомпаний Цикл
	//		СтруктураНастроек.ТипыКонтрагентовДляКомпаний.Добавить(ТекЭлемент);	
	//	КонецЦикла;
	//	
	//	Для каждого ТекЭлемент Из ЗависимостиКлиентовОтТиповКонтрагентовПоУмолчанию.ТипыКонтрагентовДляКонтактов Цикл
	//		СтруктураНастроек.ТипыКонтрагентовДляКонтактов.Добавить(ТекЭлемент);	
	//	КонецЦикла;
	//	
	//Иначе
	//	
	//	Для каждого ТекСтрока Из НастройкаПодключения.ЗависимостиКлиентовОтТиповКонтрагентов Цикл
	//		Если ТекСтрока.ВидКлиента = "Контакт" Тогда
	//			СтруктураНастроек.ТипыКонтрагентовДляКонтактов.Добавить(ТекСтрока.Тип);	
	//		Иначе
	//			СтруктураНастроек.ТипыКонтрагентовДляКомпаний.Добавить(ТекСтрока.Тип);	
	//		КонецЕсли; 
	//	КонецЦикла;     
	//	
	//КонецЕсли;
	
	//Если ДляСервера Тогда	
	//	СтруктураНастроек.Вставить("ПресетыШаблоновРеквизитов"		, НастройкаПодключения.ПресетыШаблоновРеквизитов.Выгрузить()); 	
	//	СтруктураНастроек.Вставить("ПресетыСтавокНДС"				, НастройкаПодключения.ПресетыСтавокНДС.Выгрузить()); 	
	//	
	//	СтруктураНастроек.Вставить("ПресетыТиповКомпаний", НастройкаПодключения.ПресетыТиповКлиентов.ВыгрузитьКолонки());
	//	СтруктураНастроек.Вставить("ПресетыТиповКонтактов", НастройкаПодключения.ПресетыТиповКлиентов.ВыгрузитьКолонки());
	//	
	//	Для каждого ТекСтрока Из НастройкаПодключения.ПресетыТиповКлиентов Цикл
	//		Если ТекСтрока.ВидКлиента = "Контакт" Тогда
	//			НоваяСтрока = СтруктураНастроек.ПресетыТиповКонтактов.Добавить();	
	//		Иначе
	//			НоваяСтрока = СтруктураНастроек.ПресетыТиповКомпаний.Добавить();	
	//		КонецЕсли;   
	//		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
	//	КонецЦикла;     
	//КонецЕсли;
	
	ОбщиеНастройки 	= ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки");
	ОбщиеНастройки = ?(ОбщиеНастройки = Неопределено, Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОбщиеНастройкиПоУмолчанию(), ОбщиеНастройки);
	СтруктураНастроек.Вставить("ОбщиеНастройки"	, ОбщиеНастройки); 	

	Логирование = Новый Структура;     
	Логирование.Вставить("СообщениеВЧат"	, Ложь);
	Логирование.Вставить("НомерСообщения"	, 0);
	Логирование.Вставить("ВремяЗапуска"		, Формат(ПолучитьТекущуюДатуСеанса(), "ДЛФ=T"));
	Логирование.Вставить("Измерение1"		, "Формирование базовых настроек");
	Логирование.Вставить("Измерение2"		, "");
	Логирование.Вставить("Измерение3"		, "");
	Логирование.Вставить("Измерение4"		, "");
	Логирование.Вставить("Измерение5"		, "");
	Логирование.Вставить("Измерение6"		, "");
	СтруктураНастроек.Вставить("Логирование", Логирование); 	
	
	Если НЕ Б24_К_RestApiВызовСервера.ДобавлениеНастроекАвторизации(СтруктураНастроек) Тогда
		ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось получить настройки подключения. Выгрузка пакета невозможна.");		
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ Б24_К_RestApiВызовСервера.ПолучитьТокенДляСоединения(СтруктураНастроек) Тогда
		ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось получить временный ключ соединения. Выгрузка пакета невозможна.");
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураНастроек.НастройкиПодключения = Б24_К_RestApiВызовСервера.ПолучитьНастройкиПодключения(СтруктураНастроек);
	
	РегистрыСведений.Б24_К_Логирование.ОчиститьИзмеренияЛогирования(СтруктураНастроек);

	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	
	//Возврат Новый ФиксированнаяСтруктура(СтруктураНастроек); 	
	Возврат СтруктураНастроек; 	
	
КонецФункции

#КонецОбласти


#Область ФоновыеЗадания

Процедура ЗапуститьЗадание(Метод, Наименование, ПараметрыЗапроса, Приоритет = 1, ОтдельныйПоток = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Метод) Тогда
		Возврат;
	КонецЕсли;
	
	УникальныйИдентификатор = Новый УникальныйИдентификатор;
	
	КлючЗадания = Строка(УникальныйИдентификатор);
	
	ПараметрыЗапроса.Вставить("КлючЗадания", КлючЗадания);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = Наименование;
	ПараметрыВыполнения.КлючФоновогоЗадания 		= КлючЗадания; 
	ПараметрыВыполнения.ОжидатьЗавершение 			= 0;
	
	Если ОтдельныйПоток Тогда   
		Если ЗначениеЗаполнено(Метод) Тогда ДлительныеОперации.ВыполнитьВФоне(Метод, ПараметрыЗапроса, ПараметрыВыполнения) КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ОчередьФоновыхЗаданий.КлючЗадания КАК КлючЗадания
	|ИЗ
	|	РегистрСведений.Б24_К_ОчередьФоновыхЗаданий КАК Б24_К_ОчередьФоновыхЗаданий
	|ГДЕ
	|	Б24_К_ОчередьФоновыхЗаданий.КоличествоПопыток < 3";
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	ФоновоеЗаданиеЗапущено = Ложь;   
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл

			мФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("Ключ, Состояние", Выборка.КлючЗадания, СостояниеФоновогоЗадания.Активно));
			Если мФоновыеЗадания.Количество() > 0 Тогда
				ФоновоеЗаданиеЗапущено = Истина;	
				Прервать;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
	Если ФоновоеЗаданиеЗапущено Тогда  
		НоваяЗапись = РегистрыСведений.Б24_К_ОчередьФоновыхЗаданий.СоздатьМенеджерЗаписи();
		НоваяЗапись.КлючЗадания 			= КлючЗадания; 
		НоваяЗапись.Метод 					= Метод;
		НоваяЗапись.Наименование 			= Наименование;
		НоваяЗапись.ПередаваемыеПараметры 	= Новый ХранилищеЗначения(ПараметрыЗапроса);
		НоваяЗапись.Приоритет 				= Приоритет;
		НоваяЗапись.Записать();
	Иначе
		ДлительныеОперации.ВыполнитьВФоне(Метод, ПараметрыЗапроса, ПараметрыВыполнения);
	КонецЕсли;
	
КонецПроцедуры	

Процедура ПроверитьФоновоеЗаданиеВОчереди(Параметры) Экспорт

	НоваяЗапись = РегистрыСведений.Б24_К_ОчередьФоновыхЗаданий.СоздатьМенеджерЗаписи(); 
	НоваяЗапись.КлючЗадания = Параметры.КлючЗадания;
	НоваяЗапись.Прочитать();
	НоваяЗапись.Удалить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КлючЗадания", Параметры.КлючЗадания);
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	Б24_К_ОчередьФоновыхЗаданий.КлючЗадания КАК КлючЗадания,
	|	Б24_К_ОчередьФоновыхЗаданий.Метод КАК Метод
	|ИЗ
	|	РегистрСведений.Б24_К_ОчередьФоновыхЗаданий КАК Б24_К_ОчередьФоновыхЗаданий
	|ГДЕ
	|	Б24_К_ОчередьФоновыхЗаданий.КоличествоПопыток < 3
	|
	|УПОРЯДОЧИТЬ ПО
	|	Б24_К_ОчередьФоновыхЗаданий.Приоритет";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	Если ВыполненныйЗапрос.Пустой() Тогда 
		Возврат;
	КонецЕсли;  
	
	Выборка = ВыполненныйЗапрос.Выбрать();
	
	Если НЕ ЗначениеЗаполнено(Выборка.Метод) Тогда
		ТекЗапись = РегистрыСведений.Б24_К_ОчередьФоновыхЗаданий.СоздатьМенеджерЗаписи(); 
		ТекЗапись.КлючЗадания = Выборка.КлючЗадания;
		ТекЗапись.Прочитать();   
		ТекЗапись.Удалить();
		Возврат;
	КонецЕсли;
	
	ФоновоеЗаданиеЗапущено = Ложь;
	
	мФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("Ключ, Состояние", Выборка.КлючЗадания, СостояниеФоновогоЗадания.Активно));
	Если мФоновыеЗадания.Количество() > 0 Тогда
		ФоновоеЗаданиеЗапущено = Истина;	
		Возврат;
	КонецЕсли;
	
	Если НЕ ФоновоеЗаданиеЗапущено Тогда
		
		УникальныйИдентификатор = Новый УникальныйИдентификатор;
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
		ПараметрыВыполнения.КлючФоновогоЗадания 		= Выборка.КлючЗадания;
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = ТекЗапись.Наименование;
		ПараметрыВыполнения.ОжидатьЗавершение = 0;

		ДлительныеОперации.ВыполнитьВФоне(ТекЗапись.Метод, ТекЗапись.ПередаваемыеПараметры.Получить(), ПараметрыВыполнения);  
		
		ТекЗапись = РегистрыСведений.Б24_К_ОчередьФоновыхЗаданий.СоздатьМенеджерЗаписи(); 
		ТекЗапись.КлючЗадания = Выборка.КлючЗадания;
		ТекЗапись.Прочитать();
		ТекЗапись.КоличествоПопыток = ТекЗапись.КоличествоПопыток + 1;
		ТекЗапись.Записать();
		
	КонецЕсли;  
	
КонецПроцедуры


Функция ПолучитьИспользуемыеРеглЗадания() Экспорт 
	
	Результат = Новый Структура();	
	Результат.Вставить("МониторингРТ"			, "Б24_К_МониторингРТ");	 
	Результат.Вставить("СинхронизацияДанных"	, "Б24_К_СинхронизацияДанных");
	Результат.Вставить("СинхронизацияДанныхСМ"	, "Б24_К_СинхронизацияДанныхСМ");
	Результат.Вставить("РассылкаПисемСОшибками"	, "Б24_К_РассылкаПисемСОшибками");
	Результат.Вставить("СПАРК"					, "Б24_К_СПАРК");	 
	Результат.Вставить("РоботыБ24"				, "Б24_К_РоботыБ24");	 
	Результат.Вставить("ТриггерыБ24"			, "Б24_К_ТриггерыБ24");	 
	Результат.Вставить("ТриггерыБ24"			, "Б24_К_ТриггерыБ24");	 
	Результат.Вставить("ВыгрузкаВРежимеРВ"		, "Б24_К_ВыгрузкаВРежимеРВ");	 
	Результат.Вставить("ВыгрузкаВРежимеРВСМ"	, "Б24_К_ВыгрузкаВРежимеРВСМ");	 
	Результат.Вставить("ОчисткаЛога"			, "Б24_К_ОчисткаЛога");	 
	Результат.Вставить("ВыгрузкаОтчетов"		, "Б24_КЭ_ВыгрузкаОтчетов");	 
	
	Результат.Вставить("ВыгрузкаОстатков"		, "Б24_СУ_ВыгрузкаОстатков");	 
	Результат.Вставить("ВыгрузкаЦен"			, "Б24_СУ_ВыгрузкаЦен");	 
	Результат.Вставить("ВыгрузкаОтгрузок"		, "Б24_СУ_ВыгрузкаОтгрузок");	 
	Результат.Вставить("ВыгрузкаКартинок"		, "Б24_СУ_ВыгрузкаКартинок");	 
	
	Возврат Результат; 
	
КонецФункции

Функция ПолучитьИменаИспользуемыхРеглЗаданий() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("МониторингРТ"			, "(Битрикс24) Мониторинг проверки соединения с Битрикс24");
	Результат.Вставить("СинхронизацияДанных"	, "(Битрикс24) Синхронизация с Битрикс24: ");
	Результат.Вставить("СинхронизацияДанныхСМ"	, "(Битрикс24) Синхронизация смарт-процессов Битрикс24: ");
	Результат.Вставить("РассылкаПисемСОшибками"	, "(Битрикс24) Выполнение оповещения об ошибках во время синхронизации с Битрикс24");
	Результат.Вставить("СПАРК"					, "(Битрикс24) Выгрузка информации по 1СПАРК Риски");	
	Результат.Вставить("РоботыБ24"				, "(Битрикс24) Проверка роботов Битрикс24");	
	Результат.Вставить("ТриггерыБ24"			, "(Битрикс24) Выгрузка триггеров Битрикс24");	
	Результат.Вставить("ВыгрузкаВРежимеРВ"		, "(Битрикс24) Выгрузка элементов в режиме реального времени");	
	Результат.Вставить("ВыгрузкаВРежимеРВСМ"	, "(Битрикс24) Выгрузка элементов смарт процессов в режиме реального времени");	
	Результат.Вставить("ОчисткаЛога"			, "(Битрикс24) Удаление устаревших записей истории взаимодействия с Битрикс24");	
	Результат.Вставить("ВыгрузкаОтчетов"		, "(Битрикс24) Выгрузка отчетов в ЖЛ по расписанию");	
	
	Результат.Вставить("БитриксПоискЛист"		, "(Битрикс24) Получение списка клиентов для поиска");	
	Результат.Вставить("БитриксПоискЗагрузка"	, "(Битрикс24) Загрузка клиента");	
	Результат.Вставить("ПодборТовараЛист"		, "(Битрикс24) Получение списка товаров для поиска");	
	Результат.Вставить("ПодборТовараЗагрузка"	, "(Битрикс24) Загрузка товара");	
	
	Результат.Вставить("ВыгрузкаОстатков"		, "(Битрикс24) Выгрузка остатков");	
	Результат.Вставить("ВыгрузкаЦен"			, "(Битрикс24) Выгрузка цен");	
	Результат.Вставить("ВыгрузкаОтгрузок"		, "(Битрикс24) Выгрузка цен");	
	Результат.Вставить("ВыгрузкаКартинок"		, "(Битрикс24) Выгрузка картинок");	
	
	Возврат Результат; 
	
КонецФункции

Функция СейчасВыполняетсяЗадание(НазваниеЗадания) Экспорт
	
	НачалоОтбораФоновыхЗаданий = ТекущаяДатаСеанса() - 60*60*12;
	                      
	мФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("Начало, Состояние, Наименование", НачалоОтбораФоновыхЗаданий, СостояниеФоновогоЗадания.Активно, НазваниеЗадания));

	Если мФоновыеЗадания.Количество() > 0 Тогда

		Возврат Истина;		
		
	КонецЕсли;
	
	Возврат Ложь;   	
	
КонецФункции

#КонецОбласти


#Область ПредопределенныеАлгоритмы

Функция ПолучитьСтруктуруПараметровПредопределенногоАлгоритма() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Результат"				, Неопределено); 
	Результат.Вставить("СтруктураНастроек"		, Неопределено); 
	Результат.Вставить("Объект1С"				, Неопределено); 
	Результат.Вставить("ОбъектПортала"			, Неопределено); 
	Результат.Вставить("СтруктураДанных"		, Неопределено); 
	Результат.Вставить("СтрокаТаблицыОбъекта1С"	, Неопределено); 
	
	Результат.Вставить("НастройкаПодключения"	, Неопределено); 
	Результат.Вставить("ПараметрыРобота"		, Неопределено); 
	Результат.Вставить("ИнформацияОСущности"	, Неопределено); 
	Результат.Вставить("ТипСущности"			, Неопределено); 
	Результат.Вставить("Основание"				, Неопределено); 
	Результат.Вставить("НовыйОбъект"			, Неопределено); 
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьЗначениеПредопределенногоАлгоритма(Алгоритм, ПараметрыАлгоритма, ТипАлгоритма) Экспорт
	
	Если ТипАлгоритма = "Функция" Тогда
		Результат = ПараметрыАлгоритма.Результат;
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
Попытка	
		
	Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		
		АлгоритмКлюча = стрЗаменить(Алгоритм, "[Результат]"	, "Параметры.Результат");
		АлгоритмКлюча = стрЗаменить(АлгоритмКлюча, "[СтруктураНастроек]"		, "Параметры.СтруктураНастроек");
		АлгоритмКлюча = стрЗаменить(АлгоритмКлюча, "[Объект1С]"					, "Параметры.Объект1С");
		АлгоритмКлюча = стрЗаменить(АлгоритмКлюча, "[СтрокаТаблицыОбъекта1С]"	, "Параметры.СтрокаТаблицыОбъекта1С");
		АлгоритмКлюча = стрЗаменить(АлгоритмКлюча, "[ОбъектПортала]"			, "Параметры.ОбъектПортала");
		АлгоритмКлюча = стрЗаменить(АлгоритмКлюча, "[СтруктураДанных]"			, "Параметры.СтруктураДанных");
		
		АлгоритмКлюча = стрЗаменить(АлгоритмКлюча, "[НастройкаПодключения]"		, "Параметры.НастройкаПодключения");
		АлгоритмКлюча = стрЗаменить(АлгоритмКлюча, "[ПараметрыРобота]"			, "Параметры.ПараметрыРобота");
		АлгоритмКлюча = стрЗаменить(АлгоритмКлюча, "[ИнформацияОСущности]"		, "Параметры.ИнформацияОСущности");
		АлгоритмКлюча = стрЗаменить(АлгоритмКлюча, "[ТипСущности]"				, "Параметры.ТипСущности");
		
		ОбщегоНазначения.ВыполнитьВБезопасномРежиме(АлгоритмКлюча, ПараметрыАлгоритма); 
		
		Если ТипАлгоритма = "Функция" Тогда
			Результат = ПараметрыАлгоритма.Результат;		
		КонецЕсли;
		
	КонецЕсли;	

Исключение
	
	ЗаписьЖурналаРегистрации("ОтправкаПисемСОшибкамиСинхронизации", УровеньЖурналаРегистрации.Ошибка,,, "Не удалось выполнить пользовательский алгоритм");
	ЗаписьЖурналаРегистрации("ОтправкаПисемСОшибкамиСинхронизации", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	
КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


#Область ПрочиеПроцедурыИФункции

Функция ПолучитьТипыКонтрагентов() Экспорт
	
	СтруктураДанных = Новый Структура;
	Для каждого ТекЭлемент Из Метаданные.Перечисления.ЮрФизЛицо.ЗначенияПеречисления Цикл
		СтруктураДанных.Вставить(ТекЭлемент.Имя, Перечисления.ЮрФизЛицо[ТекЭлемент.Имя]);
	КонецЦикла;
	
	Результат = Новый ФиксированнаяСтруктура(СтруктураДанных);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТипыСообщений() Экспорт
	
	СтруктураТиповСообщений = Новый Структура;
	Для каждого ТекЭлемент Из Метаданные.Перечисления.Б24_К_ТипыСообщений.ЗначенияПеречисления Цикл
		СтруктураТиповСообщений.Вставить(ТекЭлемент.Имя, Перечисления.Б24_К_ТипыСообщений[ТекЭлемент.Имя]);
	КонецЦикла;
	
	Результат = Новый ФиксированнаяСтруктура(СтруктураТиповСообщений);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТипыДанныхДляОбменаСПорталом() Экспорт
	
	СтруктураТиповДанных = Новый Структура;
	Для каждого ТекЭлемент Из Метаданные.Перечисления.Б24_К_ТипыДанныхДляОбменаСПорталом.ЗначенияПеречисления Цикл
		СтруктураТиповДанных.Вставить(ТекЭлемент.Имя, Перечисления.Б24_К_ТипыДанныхДляОбменаСПорталом[ТекЭлемент.Имя]);
	КонецЦикла;
	
	Результат = Новый ФиксированнаяСтруктура(СтруктураТиповДанных);
	
	Возврат Результат;
	
КонецФункции


Функция ПолучитьТипыОпераций() Экспорт
	
	лСтруктура = Новый Структура;
	лСтруктура.Вставить("Компании"			, "Компании");	
	лСтруктура.Вставить("Контакты"			, "Контакты");
	лСтруктура.Вставить("Реквизиты"			, "Реквизиты");
	лСтруктура.Вставить("БанковскиеСчета"	, "БанковскиеСчета");
	
	лСтруктура.Вставить("Счета"				, "Счета");
	лСтруктура.Вставить("Сделки"			, "Сделки");
	лСтруктура.Вставить("Заказы"			, "Заказы");
	лСтруктура.Вставить("Отгрузки"			, "Отгрузки");
	лСтруктура.Вставить("Оплаты"			, "Оплаты");
	лСтруктура.Вставить("Подразделения"		, "Подразделения");
	
	лСтруктура.Вставить("СвойстваКонтактов"	, "СвойстваКонтактов");
	лСтруктура.Вставить("СвойстваКомпаний"	, "СвойстваКомпаний");
	лСтруктура.Вставить("СвойстваСделок"	, "СвойстваСделок");
	лСтруктура.Вставить("СвойстваСчетов"	, "СвойстваСчетов");
	лСтруктура.Вставить("СвойстваЗаказов"	, "СвойстваЗаказов");
	
	
	лСтруктура.Вставить("ГруппыТоваров"		, "ГруппыТоваров");
	лСтруктура.Вставить("ЕдиницыИзмерения"	, "ЕдиницыИзмерения");
	лСтруктура.Вставить("СвойстваТоваров"	, "СвойстваТоваров");
	
	лСтруктура.Вставить("Товары"			, "Товары");
	лСтруктура.Вставить("Предложения"			, "Предложения");
	лСтруктура.Вставить("СвойстваПредложений"	, "СвойстваПредложений");   
	
	лСтруктура.Вставить("КартинкиФайлыТоваров"		, "КартинкиФайлыТоваров");
	лСтруктура.Вставить("КартинкиФайлыПредложений"	, "КартинкиФайлыПредложений");
	
	лСтруктура.Вставить("Прайсы"	, "Прайсы");
	лСтруктура.Вставить("Цены"		, "Цены");
	лСтруктура.Вставить("Остатки"	, "Остатки"); 
	
	лСтруктура.Вставить("УдалитьЗаказы"		, "УдалитьЗаказы");
	лСтруктура.Вставить("УдалитьОтгрузки"	, "УдалитьОтгрузки");
	
	Результат = Новый ФиксированнаяСтруктура(лСтруктура);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТекущуюДатуСеанса() Экспорт
	
	Возврат ТекущаяДатаСеанса();
	
КонецФункции

Функция ПолучитьИспользуемыеТипыСвойствБитрикс24() Экспорт
	
	ПоддерживаемыеТипы = Новый Массив;
	ПоддерживаемыеТипы.Добавить("string");
	ПоддерживаемыеТипы.Добавить("boolean");
	ПоддерживаемыеТипы.Добавить("integer");
	ПоддерживаемыеТипы.Добавить("double");
	ПоддерживаемыеТипы.Добавить("datetime");
	ПоддерживаемыеТипы.Добавить("date");
	ПоддерживаемыеТипы.Добавить("employee");
	ПоддерживаемыеТипы.Добавить("enumeration");
	ПоддерживаемыеТипы.Добавить("url");
	
	Возврат ПоддерживаемыеТипы;	
	
КонецФункции

Функция ПреобразоватьТаблицуВМассив(ТзнДанных) Экспорт
	
	Результат = Новый Массив;
	
	Для каждого ТекСтрока Из ТзнДанных Цикл
		
		СтрокаСтруктурой = Новый Структура;
		
		Для Каждого ТекКолонка Из ТзнДанных.Колонки Цикл
			СтрокаСтруктурой.Вставить(ТекКолонка.Имя, ТекСтрока[ТекКолонка.Имя]); 
		КонецЦикла;
		
		Результат.Добавить(СтрокаСтруктурой);
		
	КонецЦикла;
	
	Возврат Результат;	
	
КонецФункции

// Процедура - выполняет таймаут 
//
// Параметры:
//  ДлительностьСек	 - 	 длительность таймаута (сек) 
Процедура Таймаут(ДлительностьСек) Экспорт
	
	ТекущийСеансИнформационнойБазы = ПолучитьТекущийСеансИнформационнойБазы();
	ФоновоеЗадание = ТекущийСеансИнформационнойБазы.ПолучитьФоновоеЗадание();
	
	Если ФоновоеЗадание = Неопределено Тогда
		Параметры = Новый Массив;
		Параметры.Добавить(ДлительностьСек);
		ФоновоеЗадание = ФоновыеЗадания.Выполнить("ОбщегоНазначенияБТС.Пауза", Параметры);
	КонецЕсли;
		
	ФоновоеЗадание.ОжидатьЗавершенияВыполнения(ДлительностьСек);
	
КонецПроцедуры

Функция ПолучитьМассивРазрешенныхРасширенийКартинок() Экспорт
	
	РазрешенныеТипыКартинок = Новый Массив;
	РазрешенныеТипыКартинок.Добавить("gif");
	РазрешенныеТипыКартинок.Добавить("jpg");
	РазрешенныеТипыКартинок.Добавить("jpeg");
	РазрешенныеТипыКартинок.Добавить("png");
	РазрешенныеТипыКартинок.Добавить(".gif");
	РазрешенныеТипыКартинок.Добавить(".jpg");
	РазрешенныеТипыКартинок.Добавить(".jpeg");
	РазрешенныеТипыКартинок.Добавить(".png");
	РазрешенныеТипыКартинок.Добавить("");
	
	Возврат  РазрешенныеТипыКартинок;
	
КонецФункции

Функция ОбъектМодифицирован(Объект, МассивРеквизитовИсключений, МассивДляПроверки = Неопределено, ПроверятьТабличныеЧасти = Истина, ПроверятьСтандартные = Истина) Экспорт 
    
    Ссылка = Объект.Ссылка;
    МетаданныеОбъекта = Ссылка.Метаданные();
    
	Для каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл
		
		Если МассивРеквизитовИсключений.Найти(Реквизит.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если МассивДляПроверки <> Неопределено Тогда
			Если МассивДляПроверки.Найти(Реквизит.Имя) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		
        Если Объект[Реквизит.Имя] <> Ссылка[Реквизит.Имя] Тогда
            Возврат Истина;
        КонецЕсли;
    КонецЦикла;
    
    Если ПроверятьСтандартные Тогда
   		Для каждого Реквизит Из МетаданныеОбъекта.СтандартныеРеквизиты Цикл
        	Если Объект[Реквизит.Имя] <> Ссылка[Реквизит.Имя] Тогда
            	Возврат Истина;
        	КонецЕсли;
    	КонецЦикла;
    КонецЕсли;
    
    Если ПроверятьТабличныеЧасти Тогда
        Для каждого ТЧ Из МетаданныеОбъекта.ТабличныеЧасти Цикл
            
            Если Объект[ТЧ.Имя].Количество() <> Ссылка[ТЧ.Имя].Количество() Тогда
                Возврат Истина;
            КонецЕсли;
                        
            Для Индекс = 0 По Объект[ТЧ.Имя].Количество()-1 Цикл
                ОбъектСтрокаТЧ = Объект[ТЧ.Имя][Индекс];
                СсылкаСтрокаТЧ = Ссылка[ТЧ.Имя][Индекс];
                Для каждого РеквизитТЧ Из ТЧ.Реквизиты Цикл
                    Если ОбъектСтрокаТЧ[РеквизитТЧ.Имя] <> СсылкаСтрокаТЧ[РеквизитТЧ.Имя] Тогда
                        Возврат Истина;
                    КонецЕсли;
                КонецЦикла;
            КонецЦикла;
            
        КонецЦикла;        
    КонецЕсли;
    
    Возврат Ложь;
    
КонецФункции

Функция ПолучитьМассивРеквизитовДляПроверкиМодифицированности() Экспорт
	
	Возврат Неопределено;
	
	Результат = Новый Массив;
	Результат.Добавить("Организация");
	Результат.Добавить("Контрагент");
	Результат.Добавить("Партнер");
	Результат.Добавить("СуммаДокумента");
	Результат.Добавить("Валюта");
	Результат.Добавить("НоменклатураДоставки");
	
	Возврат Результат;
	
КонецФункции

Функция КонвертацияДесятичногоЧислаВШестнадцатеричное(Знач пDec) Экспорт
	
	Разрядность = 16;
	стр16Число = "";
	
	Пока пDec <> 0 Цикл
		
		Поз =пDec % Разрядность;
		стр16Число = Сред("0123456789ABCDEF", Поз + 1, 1) + стр16Число;
		пDec = Цел(пDec / Разрядность);
		
	КонецЦикла;
	
	Возврат стр16Число; 
	
КонецФункции 

Функция КонвертацияШестнадцатеричногоЧислаВДесятичное(Знач пHex) Экспорт
	
	Разрядность = 16;
	пHex = СокрЛП(пHex);
	МаксСтепень = СтрДлина(пHex) - 1;
	ч10Число = 0;
	счСимв = 1;
	
	Пока МаксСтепень >=0 Цикл
		
		пHexСимвол = Сред(пHex, счСимв, 1);
		ПредставлениеHex = Найти("0123456789ABCDEF", пHexСимвол) - 1;
		ч10Число = ч10Число + ПредставлениеHex * Pow(Разрядность, МаксСтепень);
		МаксСтепень = МаксСтепень - 1;
		счСимв = счСимв + 1;
		
	КонецЦикла;
	
	Возврат ч10Число;	
	
КонецФункции   

Процедура СортироватьМассив(Массив, Направление, Свойство = Неопределено) Экспорт 
	
    СравнениеЗначений = Новый СравнениеЗначений; 
	
    Для Индекс = 0 По Массив.ВГраница() Цикл
		Для ИндексЭлемента = 0 По Массив.ВГраница() - Индекс - 1 Цикл
			
            Если Свойство = Неопределено Тогда
                Значение1 = Массив[ИндексЭлемента];
                Значение2 = Массив[ИндексЭлемента + 1];
            Иначе
                Значение1 = Массив[ИндексЭлемента][Свойство];
                Значение2 = Массив[ИндексЭлемента + 1][Свойство];
			КонецЕсли;  
			
            РезультатСравнения = СравнениеЗначений.Сравнить(Значение1, Значение2);
            Если РезультатСравнения > 0 И Направление = НаправлениеСортировки.Возр
                ИЛИ РезультатСравнения < 0 И Направление = НаправлениеСортировки.Убыв Тогда
                ТекущийЭлемент = Массив[ИндексЭлемента];
                Массив[ИндексЭлемента] = Массив[ИндексЭлемента + 1];
                Массив[ИндексЭлемента + 1] = ТекущийЭлемент;
			КонецЕсли;  
			
        КонецЦикла;
	КонецЦикла; 
	
КонецПроцедуры     


Функция РазобратьФИО(Знач ПредставлениеФИО) Экспорт

	Если СтрНайти(ПредставлениеФИО, "ИП") > 0 Тогда  
		
		ПредставлениеФИО = СтрЗаменить(ПредставлениеФИО, "ИП", "");   
		
		ДанныеФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(ПредставлениеФИО);                       
		ДанныеФИО.Вставить("ИП", Истина);

		
	Иначе
		ДанныеФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(ПредставлениеФИО);   
		ДанныеФИО.Вставить("ИП", Ложь);
	КонецЕсли;   
	
	ДанныеФИО.Фамилия = ?(ЗначениеЗаполнено(ДанныеФИО.Фамилия), ДанныеФИО.Фамилия, "");
	ДанныеФИО.Отчество = ?(ЗначениеЗаполнено(ДанныеФИО.Отчество), ДанныеФИО.Отчество, "");
	
	Возврат ДанныеФИО;
	
КонецФункции      

Функция ПреобразоватьФИО(Фамилия, Имя, Отчество, ЭтоИП = Ложь) Экспорт 
	
	Результат = Новый Структура;
	Результат.Вставить("Фамилия"	, Фамилия);
	Результат.Вставить("Имя"		, Имя);
	Результат.Вставить("Отчество"	, Отчество);
	Результат.Вставить("ИП"			, ЭтоИП);
	
	Если ЭтоИП = Истина Тогда
		Результат.Фамилия 	= "ИП " + Результат.Фамилия;
		Результат.Отчество 	= СтрЗаменить(Результат.Отчество, "ИП", "");	
		Результат.Отчество 	= СокрЛП(Результат.Отчество);	
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Функция ЭтоПростойТип(Тип) Экспорт
	
	Возврат Тип = Тип("Булево")
	Или Тип = Тип("Дата")
	Или Тип = Тип("Строка")
	Или Тип = Тип("Число")
	Или Тип = Тип("УникальныйИдентификатор")
	Или Тип = Тип("ХранилищеЗначения");
	
КонецФункции

Функция НовоеЗащищенноеСоединение() Экспорт
	
	//Новый ЗащищенноеСоединениеOpenSSL(СертификатКлиента, Новый СертификатыУдостоверяющихЦентровОС());
	
	Возврат ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	
КонецФункции

Функция ПолучитьСтавкуНДСПоСтавке(Ставка) Экспорт
	
	СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка();
	
	Если Ставка = "Без НДС" Тогда
		СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
	Иначе
		Выборка = Справочники.СтавкиНДС.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Ставка = Ставка И НЕ Выборка.РасчетнаяСтавка И НЕ Выборка.Ссылка = Справочники.СтавкиНДС.БезНДС Тогда
				СтавкаНДС = Выборка.Ссылка;
			КонецЕсли;
			
		КонецЦикла;    
	КонецЕсли;
	
	Возврат СтавкаНДС;
	
КонецФункции       

Функция ПолучитьСтавкуПоНДС(СсылкаНаСтавкуНДС) Экспорт
	
	Если СсылкаНаСтавкуНДС = Справочники.СтавкиНДС.БезНДС Тогда
		Возврат "";
	Иначе
		Возврат СсылкаНаСтавкуНДС.Ставка; 
	КонецЕсли;
	
КонецФункции     

#КонецОбласти
           