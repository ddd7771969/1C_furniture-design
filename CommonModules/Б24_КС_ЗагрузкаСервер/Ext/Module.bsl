
#Область ПользовательскиеПоля  

Функция ЗагрузитьОбновитьПользовательскиеПоляОбъектов(пСтруктураНастроек, мДанных, ТипОбъектаОбмена) Экспорт
	
	ИспользуемыеТипыСвойствБитрикс24 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьИспользуемыеТипыСвойствБитрикс24();
	НаименованияСлужебныхСвойствБ24		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаименованияСлужебныхСвойствБ24();
	ЕстьДопРеквизиты = Истина;
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	Если ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКомпании Тогда
		ТипЗначенияСвойств 	= СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваКомпании; 
		НаборСвойств		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Партнеры_Общие");	
		ОкончаниеНазвания	= " (Контрагенты)";
		ТипДляЛога = "Компания";	
	ИначеЕсли ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКонтакта Тогда
		ТипЗначенияСвойств 	= СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваКонтакта;
		НаборСвойств		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Партнеры_Общие");
		ОкончаниеНазвания	= " (Контрагенты)";
		ТипДляЛога = "Контакт";	
	ИначеЕсли ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСделки Тогда
		ТипЗначенияСвойств 	= СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваСделки; 	
		НаборСвойств		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Документ_ЗаказКлиента");	
		ОкончаниеНазвания	= " (Заказы покупателей)";
		ТипДляЛога = "Проект";	
	ИначеЕсли ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоЗаказа Тогда
		ТипЗначенияСвойств 	= СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваЗаказа; 	
		НаборСвойств		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Документ_ЗаказКлиента");	
		ОкончаниеНазвания	= " (Заказы покупателей)";
		ТипДляЛога = "Заказ";	
	ИначеЕсли ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСчета Тогда
		ТипЗначенияСвойств 	= СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваСчета;
		НаборСвойств		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Документ_СчетНаОплатуКлиенту");
		ОкончаниеНазвания	= " (Счет на оплату)";
		ТипДляЛога = "Счет";	
	ИначеЕсли ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСчета2 Тогда
		ТипЗначенияСвойств 	= СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваСчета2;
		НаборСвойств		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Документ_СчетНаОплатуКлиенту");
		ОкончаниеНазвания	= " (Счет на оплату)";
		ТипДляЛога = "Счет";	
		// Используется в других конфигурациях 
		// ЕстьДопРеквизиты 	= Ложь;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"				, ТипОбъектаОбмена);
	Запрос.УстановитьПараметр("ТипДанныхЗначенийСвойств", ТипЗначенияСвойств);
	Запрос.УстановитьПараметр("НаборСвойств"			, НаборСвойств);
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийСвойств
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗначенийСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Объект,
	|	ДополнительныеРеквизитыИСведения.Заголовок КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НаименованияСвойств
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияСвойствОбъектов.Ссылка КАК Объект,
	|	ЗначенияСвойствОбъектов.Владелец КАК Свойство,
	|	ЗначенияСвойствОбъектов.Наименование КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НаименованияЗначенийСвойств
	|ИЗ
	|	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Свойство,
	|	Идентификатор";
	
	Запрос.Выполнить();
	
	ЗагруженныеОбъекты = Новый Массив;	
	
	Для каждого ТекШапкаЭлемента Из мДанных Цикл	
		
		Если ТипЗнч(ТекШапкаЭлемента) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ТекЭлемент = ТекШапкаЭлемента.Получить("field"); 
		
		ИдЭлемента	= Формат(ТекЭлемент.Получить("id"),"ЧГ=0");
		ДополнительныйИдЭлемента = ТекЭлемент.Получить("fieldName");
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);
		
		ПользовательскийТип = ТекЭлемент.Получить("userTypeId");    
		Если ИспользуемыеТипыСвойствБитрикс24.Найти(ПользовательскийТип) = Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;
		КонецЕсли;
		
		Наименование = "";
		НаименованияНаФормах = ТекЭлемент.Получить("editFormLabel");
		Если НаименованияНаФормах <> Неопределено Тогда
			Наименование = НаименованияНаФормах.Получить(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЛокализациюБитрикс24());		
		КонецЕсли;
		
		Если Наименование = "" ИЛИ Наименование = Неопределено Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не найдено наименование для свойства с Ид:" + ИдЭлемента);
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			Продолжить;	
		КонецЕсли;
		
		Если НаименованияСлужебныхСвойствБ24.Найти(Наименование) <> Неопределено Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Свойство: " + Наименование + " не будет добавлено/обновлено. т.к. является служебным");
			Продолжить;
		КонецЕсли;
		
		ВнешнийКод 	= Строка(ТекЭлемент.Получить("xmlId"));
		
		Если ТекЭлемент.Получить("multiple") = "Y" Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Свойство: " + Наименование + " не будет добавлено/обновлено. т.к. является множественным");
			Продолжить;
		КонецЕсли;
		
#Область ПоискСозданиеСвойства	
		Свойство = Неопределено;
		Свойство = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Идентификаторы", ИдЭлемента);	
		
		Если ЗначениеЗаполнено(ВнешнийКод)И НЕ ЗначениеЗаполнено(Свойство)  Тогда
			Свойство = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"), ВнешнийКод);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Свойство)  Тогда
			Свойство = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_НаименованияСвойств", Наименование);	
		КонецЕсли;
		
		ЭтоНовый = Истина;
		
		Если Свойство = Неопределено Тогда
			Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.СоздатьЭлемент();
			Свойство.НаборСвойств = НаборСвойств;
			
			Если Наименование = "Внешний URL" Тогда
				Свойство.Доступен 	= Ложь;
				Свойство.Виден 		= Ложь;
			КонецЕсли;
			
		Иначе
			
			ЭтоНовый = Ложь;
			Свойство = Свойство.ПолучитьОбъект();
			
		КонецЕсли;
#КонецОбласти	
		
		Если ЭтоНовый Тогда
			Свойство.Наименование 	= Наименование + ОкончаниеНазвания;
			Свойство.Заголовок 		= Наименование;
		Иначе
			
			Если НЕ СтрНайти(Свойство.Заголовок, "Битрикс24") > 0 Тогда
				Свойство.Заголовок = Наименование;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКомпании Тогда
			Если стрНайти(Свойство.Комментарий, "Свойство компании (Битрикс24)") = 0 Тогда	
				Свойство.Комментарий = Свойство.Комментарий+" "+"Свойство компании (Битрикс24)";
			КонецЕсли;
			
		ИначеЕсли ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКонтакта Тогда
			Если стрНайти(Свойство.Комментарий, "Свойство контакта (Битрикс24)") = 0 Тогда	
				Свойство.Комментарий = Свойство.Комментарий+" "+"Свойство контакта (Битрикс24)";
			КонецЕсли;
			
		ИначеЕсли ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСделки Тогда
			Если стрНайти(Свойство.Комментарий, "Свойство сделки (Битрикс24)") = 0 Тогда	
				Свойство.Комментарий = Свойство.Комментарий+" "+"Свойство сделки (Битрикс24)";
			КонецЕсли;
			
		ИначеЕсли ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоЗаказа Тогда
			Если стрНайти(Свойство.Комментарий, "Свойство заказа (Битрикс24)") = 0 Тогда	
				Свойство.Комментарий = Свойство.Комментарий+" "+"Свойство заказа (Битрикс24)";
			КонецЕсли;
		КонецЕсли;
		
		Свойство.ЗаполнятьОбязательно 	= ТекЭлемент.Получить("mandatory") = "Y"; 
		
		Если ЭтоНовый Тогда
			
			Свойство.ЭтоДополнительноеСведение = НЕ ЕстьДопРеквизиты;	
			
#Область РазборТипаСвойства
		МассивТипов = Новый Массив;
		
		Если ПользовательскийТип = "string" Тогда
			
			КС = Новый КвалификаторыСтроки(0);
			МассивТипов.Добавить(Тип("Строка"));
			ОписаниеТиповС = Новый ОписаниеТипов(МассивТипов, , КС);
			Свойство.ТипЗначения = ОписаниеТиповС;	
			
		ИначеЕсли ПользовательскийТип = "url" Тогда
			
			КС = Новый КвалификаторыСтроки(777);
			МассивТипов.Добавить(Тип("Строка"));
			ОписаниеТиповС = Новый ОписаниеТипов(МассивТипов, , КС);
			Свойство.ТипЗначения = ОписаниеТиповС;	
			
		ИначеЕсли ПользовательскийТип = "boolean" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("Булево"));
			ОписаниеТиповБ = Новый ОписаниеТипов(МассивТипов);
			Свойство.ТипЗначения = ОписаниеТиповБ;	
			
		ИначеЕсли ПользовательскийТип = "integer" Тогда
			
			КЧ = Новый КвалификаторыЧисла(12,0);
			МассивТипов.Добавить(Тип("Число"));
			ОписаниеТиповЧ  = Новый ОписаниеТипов(МассивТипов, , КЧ);
			Свойство.ТипЗначения = ОписаниеТиповЧ;	
			
		ИначеЕсли ПользовательскийТип = "double" Тогда
			
			КЧ = Новый КвалификаторыЧисла(12,3);
			МассивТипов.Добавить(Тип("Число"));
			ОписаниеТиповЧ  = Новый ОписаниеТипов(МассивТипов, , КЧ);
			Свойство.ТипЗначения = ОписаниеТиповЧ;	
			
		ИначеЕсли ПользовательскийТип = "datetime" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя);
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТиповД = Новый ОписаниеТипов(МассивТипов, , КД);
			Свойство.ТипЗначения = ОписаниеТиповД;	
			
		ИначеЕсли ПользовательскийТип = "date" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТиповД = Новый ОписаниеТипов(МассивТипов, , КД);
			Свойство.ТипЗначения = ОписаниеТиповД;	
			
		ИначеЕсли ПользовательскийТип = "employee" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("СправочникСсылка.Пользователи"));
			ОписаниеТиповП = Новый ОписаниеТипов(МассивТипов);
			Свойство.ТипЗначения = ОписаниеТиповП;	
			
		ИначеЕсли ПользовательскийТип = "enumeration" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("СправочникСсылка.ЗначенияСвойствОбъектов"));
			ОписаниеТиповП = Новый ОписаниеТипов(МассивТипов);
			Свойство.ТипЗначения = ОписаниеТиповП;	
			Свойство.ДополнительныеЗначенияИспользуются = Истина;
		КонецЕсли;
#КонецОбласти

		КонецЕсли;	

		Попытка
			Свойство.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			Свойство.Записать();
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипОбъектаОбмена, Свойство.Ссылка, ИдЭлемента, ДополнительныйИдЭлемента);
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записано свойство объекта типа: "+ТипДляЛога+ " с наименованием: " + Наименование, Свойство.Ссылка);
			
			Если ПользовательскийТип = "enumeration" Тогда
				
#Область ЗаписьЗначенийСвойств
				ЗначенияСписка = ТекЭлемент.Получить("enum");	
				
				Если ЗначениеЗаполнено(ЗначенияСписка) Тогда
					
					Для Каждого ТекЗначениеСвойства Из ЗначенияСписка Цикл
						
						ИдЗначенияСвойства 		= Формат(ТекЗначениеСвойства.Получить("id"),"ЧГ=0");
						НаименованиеЗнСвойства	= Строка(ТекЗначениеСвойства.Получить("value"));
						
#Область ПоискСозданиеЗначенияСвойства	
						ЗначениеСвойства = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЗначенийСвойств", ИдЗначенияСвойства);	
						
						Если НЕ ЗначениеЗаполнено(ЗначениеСвойства) Тогда
							Запрос = Новый Запрос;
							Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
							Запрос.УстановитьПараметр("Свойство", Свойство.Ссылка);
							Запрос.УстановитьПараметр("Наименование", НаименованиеЗнСвойства);
							Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
							|	ВТ_НаименованияЗначенийСвойств.Объект КАК Объект
							|ИЗ
							|	ВТ_НаименованияЗначенийСвойств КАК ВТ_НаименованияЗначенийСвойств
							|ГДЕ
							|	ВТ_НаименованияЗначенийСвойств.Свойство = &Свойство
							|	И ВТ_НаименованияЗначенийСвойств.Идентификатор = &Наименование";
							
							ВыборкаЗначенийСвойств = Запрос.Выполнить().Выбрать();
							
							Пока ВыборкаЗначенийСвойств.Следующий() Цикл
								ЗначениеСвойства = ВыборкаЗначенийСвойств.Объект; 
								Прервать
							КонецЦикла;
						КонецЕсли;
						
						Если ЗначениеСвойства = Неопределено Тогда
							ЗначениеСвойства = Справочники.ЗначенияСвойствОбъектов.СоздатьЭлемент();
						Иначе
							ЗначениеСвойства = ЗначениеСвойства.ПолучитьОбъект();
						КонецЕсли;
#КонецОбласти
						
						ЗначениеСвойства.Владелец 			= ?(Свойство.ВладелецДополнительныхЗначений.Пустая(), Свойство.Ссылка,  Свойство.ВладелецДополнительныхЗначений);					
						ЗначениеСвойства.Наименование 		= НаименованиеЗнСвойства;					
						ЗначениеСвойства.ПолноеНаименование = НаименованиеЗнСвойства;	
						
						ЗначениеСвойства.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
						ЗначениеСвойства.Записать();
						РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипЗначенияСвойств, ЗначениеСвойства.Ссылка, ИдЗначенияСвойства);
						
					КонецЦикла;
					
				КонецЕсли;
#КонецОбласти
				
			КонецЕсли;	
			
			Если ЭтоНовый И Наименование <> "Внешний URL" Тогда
				
				НаборСвойств 	= Свойство.НаборСвойств;
				ТекНаборСвойств = НаборСвойств.ПолучитьОбъект();
				
				Если ЕстьДопРеквизиты Тогда
					НовоеСвойствоНабора = ТекНаборСвойств.ДополнительныеРеквизиты.Добавить();
				Иначе
					НовоеСвойствоНабора = ТекНаборСвойств.ДополнительныеСведения.Добавить();
				КонецЕсли;
				
				НовоеСвойствоНабора.Свойство = Свойство.Ссылка;
				
				ТекНаборСвойств.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
				ТекНаборСвойств.Записать();
				
				Если ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКонтакта Тогда
					
					НаборСвойств 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_КонтактныеЛицаПартнеров");
					ТекНаборСвойств = НаборСвойств.ПолучитьОбъект();
					
					Если ЕстьДопРеквизиты Тогда
						НовоеСвойствоНабора = ТекНаборСвойств.ДополнительныеРеквизиты.Добавить();
					Иначе
						НовоеСвойствоНабора = ТекНаборСвойств.ДополнительныеСведения.Добавить();
					КонецЕсли;
					
					НовоеСвойствоНабора.Свойство = Свойство.Ссылка;
					
					ТекНаборСвойств.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
					ТекНаборСвойств.Записать();
				
				КонецЕсли;	
				
			КонецЕсли;	
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи свойства объекта типа: "+ТипДляЛога+" с наименованием: " + Наименование + " возникли ошибки.", Свойство.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
	
КонецФункции

Функция РазобратьПользовательскиеПоляВТаблицу(СтруктураНастроек, ИнформацияООбъекте, МенеджерВременныхТаблиц)
	
	ЗапросПоСвойствам = Новый Запрос;
	ЗапросПоСвойствам.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросПоСвойствам.Текст = "ВЫБРАТЬ *
	|ИЗ
	|	ВТ_Свойства КАК ВТ_Свойства";
	ТаблицаСвойств = ЗапросПоСвойствам.Выполнить().Выгрузить();
	
	мСвойстваИсключения = СтруктураНастроек.ПредопределенныеСвойства.НеЗагружаемыеЗначенияСвойств;
	
	тзнЗначенийСвойствТовара = Новый ТаблицаЗначений;
	тзнЗначенийСвойствТовара.Колонки.Добавить("Свойство");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЗначениеСвойства");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ПоддерживаетсяСвойство");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЗначениеОчищено");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЭтоДополнительноеСведение");
	
	Для Каждого ТекСвойство Из ТаблицаСвойств Цикл
		
		Свойство = ТекСвойство.Объект;
		
		Если мСвойстваИсключения.Найти(Свойство) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Свойство) Тогда
			Продолжить;
		КонецЕсли;
		
		НовоеЗначениеСвойстваТовара = тзнЗначенийСвойствТовара.Добавить();
		
		НовоеЗначениеСвойстваТовара.Свойство 					= Свойство;
		НовоеЗначениеСвойстваТовара.ПоддерживаетсяСвойство 		= Истина;
		НовоеЗначениеСвойстваТовара.ЗначениеОчищено 			= Ложь;
		НовоеЗначениеСвойстваТовара.ЭтоДополнительноеСведение 	= Свойство.ЭтоДополнительноеСведение;
		
		Если НЕ ЗначениеЗаполнено(ТекСвойство.ДополнительныйИдентификатор) Тогда
			Продолжить;
		КонецЕсли;
		
		КлючПоля = ТекСвойство.ДополнительныйИдентификатор;
		ПреобразоватьКлючJson(КлючПоля);
		
		ЗначениеСвойстваСПортала = Формат(ИнформацияООбъекте.Получить(КлючПоля), "ЧГ=0");
		
		Если ЗначениеСвойстваСПортала <> Неопределено И ТипЗнч(ЗначениеСвойстваСПортала) <> Тип("Соответствие")  Тогда
			
			ТипЗначенияСвойства = Свойство.ТипЗначения;
			
			Попытка
				
				Если ТипЗначенияСвойства.СодержитТип(Тип("Строка")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = ЗначениеСвойстваСПортала;
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Булево")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Булево"), ЗначениеСвойстваСПортала);
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Число")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), ЗначениеСвойстваСПортала);
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Дата")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ЗначениеСвойстваСПортала);
					
					Если НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Неопределено И ЗначениеЗаполнено(ЗначениеСвойстваСПортала) Тогда
						Попытка
							НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Дата(ЗначениеСвойстваСПортала + " 0:00:00");
						Исключение
						КонецПопытки;
					КонецЕсли;
					
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("СправочникСсылка.Пользователи")) Тогда
					
					НайденнаяСтрока = СтруктураНастроек.ТаблицаСопоставленияПользователей.Найти(ЗначениеСвойстваСПортала, "ИдПользователя");	
					
					Если НайденнаяСтрока <> Неопределено Тогда
						НовоеЗначениеСвойстваТовара.ЗначениеСвойства = НайденнаяСтрока.Пользователь1С;
					КонецЕсли;
					
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЗначенийСвойств",  ЗначениеСвойстваСПортала);
				Иначе
					НовоеЗначениеСвойстваТовара.ПоддерживаетсяСвойство = Ложь;	
				КонецЕсли;
				
			Исключение
				НовоеЗначениеСвойстваТовара.ПоддерживаетсяСвойство = Ложь;	
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось обработать значение свойства: " + Строка(ЗначениеСвойстваСПортала) + " свойства: " + Строка(Свойство));
			КонецПопытки;
			
		Иначе
			НовоеЗначениеСвойстваТовара.ЗначениеОчищено = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат тзнЗначенийСвойствТовара;
	
КонецФункции

#КонецОбласти
		 

#Область Клиенты

Функция ПроверитьНаличиеКомпанийПередДобавлениемНовых(СтруктураНастроек, ДанныеДляПоиска)
	
	мКомпании = Новый Массив;
	
	ЭтоНашаКомпания 	= ДанныеДляПоиска.ЭтоНашаКомпания; 
	Если ЭтоНашаКомпания = "N" Тогда
		ПравилаПоискаПриЗагрузкеВ1С	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Компания", "Партнеры"	 , "ПравилаПоискаПриЗагрузкеВ1С");
	Иначе
		ПравилаПоискаПриЗагрузкеВ1С	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Компания", "Организации", "ПравилаПоискаПриЗагрузкеВ1С");
	КонецЕсли;
	
	ДеревоПравил = Неопределено;
	Если ПравилаПоискаПриЗагрузкеВ1С <> "" Тогда
		ДеревоПравил = ЗначениеИзСтрокиВнутр(ПравилаПоискаПриЗагрузкеВ1С);
	Иначе
		Возврат мКомпании; 	
	КонецЕсли;
	
	Для каждого ТекСтрока Из ДеревоПравил.Строки Цикл
		Если ТекСтрока.ТипЗаписи = 0 Тогда
			мКомпании = ПроверитьЭлементПравилаИдентификацииРекурсивно(СтруктураНастроек, ДанныеДляПоиска, ТекСтрока, "Группа И")
		КонецЕсли;	
	КонецЦикла;
	
	Возврат мКомпании;
	
КонецФункции

Функция ПроверитьНаличиеКонтактовПередДобавлениемНовых(СтруктураНастроек, ДанныеДляПоиска)
	
	мКонтакты = Новый Массив;
	
	Если НЕ ДанныеДляПоиска.ЭтоКонтактноеЛицо Тогда
		ПравилаПоискаПриЗагрузкеВ1С	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Контакт", "Партнеры", "ПравилаПоискаПриЗагрузкеВ1С");
	Иначе
		ПравилаПоискаПриЗагрузкеВ1С	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Контакт", "КонтактныеЛицаПартнеров", "ПравилаПоискаПриЗагрузкеВ1С");
	КонецЕсли;
	
	ДеревоПравил = Неопределено;
	Если ПравилаПоискаПриЗагрузкеВ1С <> "" Тогда
		ДеревоПравил = ЗначениеИзСтрокиВнутр(ПравилаПоискаПриЗагрузкеВ1С);
	Иначе
		Возврат мКонтакты; 	
	КонецЕсли;
	
	Для каждого ТекСтрока Из ДеревоПравил.Строки Цикл
		Если ТекСтрока.ТипЗаписи = 0 Тогда
			мКонтакты = ПроверитьЭлементПравилаИдентификацииРекурсивно(СтруктураНастроек, ДанныеДляПоиска, ТекСтрока, "Группа И")
		КонецЕсли;	
	КонецЦикла;
	
	Возврат мКонтакты;
	
КонецФункции

Функция ПроверитьНаличиеРеквизитовПередДобавлениемНовых(СтруктураНастроек, ДанныеДляПоиска)
	
	мКонтакты = Новый Массив;
	
	ПравилаПоискаПриЗагрузкеВ1С	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Реквизит", "Контрагенты", "ПравилаПоискаПриЗагрузкеВ1С");  //проверяем только контрагентов
	
	ДеревоПравил = Неопределено;
	Если ПравилаПоискаПриЗагрузкеВ1С <> "" Тогда
		ДеревоПравил = ЗначениеИзСтрокиВнутр(ПравилаПоискаПриЗагрузкеВ1С);
	Иначе
		Возврат мКонтакты; 	
	КонецЕсли;
	
	Для каждого ТекСтрока Из ДеревоПравил.Строки Цикл
		Если ТекСтрока.ТипЗаписи = 0 Тогда
			мКонтакты = ПроверитьЭлементПравилаИдентификацииРекурсивно(СтруктураНастроек, ДанныеДляПоиска, ТекСтрока, "Группа И")
		КонецЕсли;	
	КонецЦикла;
	
	Возврат мКонтакты;
	
КонецФункции

Функция ПроверитьНаличиеБанкСчетовПередДобавлениемНовых(СтруктураНастроек, ДанныеДляПоиска)
	
	мКонтакты = Новый Массив;

	ПравилаПоискаПриЗагрузкеВ1С	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Банковский счет", "БанковскиеСчетаКонтрагентов", "ПравилаПоискаПриЗагрузкеВ1С");  //проверяем только контрагентов
	
	ДеревоПравил = Неопределено;
	Если ПравилаПоискаПриЗагрузкеВ1С <> "" Тогда
		ДеревоПравил = ЗначениеИзСтрокиВнутр(ПравилаПоискаПриЗагрузкеВ1С);
	Иначе
		Возврат мКонтакты; 	
	КонецЕсли;
	
	Для каждого ТекСтрока Из ДеревоПравил.Строки Цикл
		Если ТекСтрока.ТипЗаписи = 0 Тогда
			мКонтакты = ПроверитьЭлементПравилаИдентификацииРекурсивно(СтруктураНастроек, ДанныеДляПоиска, ТекСтрока, "Группа И")
		КонецЕсли;	
	КонецЦикла;
	
	Возврат мКонтакты;
	
КонецФункции

Функция ПроверитьЭлементПравилаИдентификацииРекурсивно(СтруктураНастроек, ДанныеДляПоиска, СтрокаПравил, ТипГруппировки)
	
	ИдентификаторыБитрикс24 = Неопределено;
	
	Для каждого ТекСтрока Из СтрокаПравил.Строки Цикл

		Если ТекСтрока.Использование  Тогда
			
			Если ТекСтрока.ТипЗаписи = 1 Тогда
				ИдентификаторыУсловия = ПроверитьЭлементПравилаИдентификацииРекурсивно(СтруктураНастроек, ДанныеДляПоиска, ТекСтрока, ТекСтрока.ЛевоеЗначение); 
			Иначе
				ИдентификаторыУсловия = ПроверитьУсловиеПравилаИдентификации(СтруктураНастроек, ДанныеДляПоиска, ТекСтрока.ЛевоеЗначение);
			КонецЕсли;
			
			Если ИдентификаторыБитрикс24 = Неопределено Тогда
				ИдентификаторыБитрикс24 = ИдентификаторыУсловия;	
			Иначе
				
				Если ИдентификаторыУсловия <> Неопределено Тогда
			
					ВТ_ИдентификаторыБитрикс24 = Новый Массив;
					
					Если ТипГруппировки = "Группа И" Тогда
						
						Для каждого ТекЭлемент Из ИдентификаторыУсловия Цикл 
							Если ИдентификаторыБитрикс24.Найти(ТекЭлемент) <> Неопределено Тогда
								ВТ_ИдентификаторыБитрикс24.Добавить(ТекЭлемент);	
							КонецЕсли;
						КонецЦикла;
						
					Иначе
						
						ВТ_ИдентификаторыБитрикс24 = ИдентификаторыБитрикс24;
						
						Для каждого ТекЭлемент Из ИдентификаторыУсловия Цикл 
							Если ИдентификаторыБитрикс24.Найти(ТекЭлемент) = Неопределено Тогда
								ВТ_ИдентификаторыБитрикс24.Добавить(ТекЭлемент);	
							КонецЕсли;
						КонецЦикла;
						
					КонецЕсли;
					
					ИдентификаторыБитрикс24 = ВТ_ИдентификаторыБитрикс24;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ?(ИдентификаторыБитрикс24 = Неопределено, Новый Массив, ИдентификаторыБитрикс24);
	
КонецФункции

Функция ПроверитьУсловиеПравилаИдентификации(СтруктураНастроек, ДанныеДляПоиска, Алгоритм)
	
	Если Алгоритм = "Не указано" Тогда
		Результат = Неопределено
	КонецЕсли;
	
	Результат = Новый Массив;
	
	Если Алгоритм = "Наименование компании" Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДанныеДляПоиска.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Наименование", ДанныеДляПоиска.Наименование); 
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_Справочник.Объект КАК Объект
		|ИЗ
		|	ВТ_Справочник КАК ВТ_Справочник
		|ГДЕ   
		|	ВТ_Справочник.Наименование = &Наименование"; 
		//|	И ВТ_Справочник.Идентификатор ЕСТЬ NULL"; 
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Добавить(Выборка.Объект);
		КонецЦикла;
		
	ИначеЕсли Алгоритм = "Фамилия" Тогда
		
		Если ДанныеДляПоиска.Фамилия <> "" Тогда
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = ДанныеДляПоиска.МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Фамилия", "%" + ДанныеДляПоиска.Фамилия + "%"); 
			Запрос.Текст = "ВЫБРАТЬ
			|	ВТ_Справочник.Объект КАК Объект
			|ИЗ
			|	ВТ_Справочник КАК ВТ_Справочник
			|ГДЕ             
			|	ВТ_Справочник.Наименование ПОДОБНО &Фамилия"; 
			//|	И ВТ_Справочник.Идентификатор ЕСТЬ NULL";  
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Результат.Добавить(Выборка.Объект);
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли Алгоритм = "Имя" Тогда
		
		Если ДанныеДляПоиска.Имя <> "" Тогда
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = ДанныеДляПоиска.МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Имя", "%" + ДанныеДляПоиска.Имя + "%"); 
			Запрос.Текст = "ВЫБРАТЬ
			|	ВТ_Справочник.Объект КАК Объект
			|ИЗ
			|	ВТ_Справочник КАК ВТ_Справочник
			|ГДЕ 
			|	ВТ_Справочник.Наименование ПОДОБНО &Имя"; 
			//|	И ВТ_Справочник.Идентификатор ЕСТЬ NULL";  
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Результат.Добавить(Выборка.Объект);
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли Алгоритм = "Отчество" Тогда
		
		Если ДанныеДляПоиска.Отчество <> "" Тогда
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = ДанныеДляПоиска.МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Отчество", "%" + ДанныеДляПоиска.Отчество + "%"); 
			Запрос.Текст = "ВЫБРАТЬ
			|	ВТ_Справочник.Объект КАК Объект
			|ИЗ
			|	ВТ_Справочник КАК ВТ_Справочник
			|ГДЕ  
			|	ВТ_Справочник.Наименование ПОДОБНО &Отчество"; 
			//|	И ВТ_Справочник.Идентификатор ЕСТЬ NULL";  
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Результат.Добавить(Выборка.Объект);
			КонецЦикла;
			
		Иначе
			Результат = Неопределено;
		КонецЕсли;
		
	ИначеЕсли Алгоритм = "Телефон" Тогда
		
		Если ДанныеДляПоиска.Телефоны <> Неопределено Тогда
			Для каждого ТекЭлемент Из ДанныеДляПоиска.Телефоны Цикл
				
				ЗначениеКИ = Новый Массив;
				ПолныйНомерТелефона = ТекЭлемент.Получить("VALUE");  
				Если Лев(ПолныйНомерТелефона, 1) = "+" Тогда  
					ЗначениеКИ.Добавить(Прав(ПолныйНомерТелефона, СтрДлина(ПолныйНомерТелефона)-1));	
				КонецЕсли;
				
				ЗначениеКИ.Добавить(ПолныйНомерТелефона);   
				
				Запрос = Новый Запрос;
				Запрос.МенеджерВременныхТаблиц = ДанныеДляПоиска.МенеджерВременныхТаблиц;
				Запрос.УстановитьПараметр("НомераТелефонов", ЗначениеКИ); 
				Запрос.Текст = "ВЫБРАТЬ
				|	ВТ_Телефоны.Объект КАК Объект
				|ИЗ
				|	ВТ_Телефоны КАК ВТ_Телефоны
				|ГДЕ
				|	ВТ_Телефоны.НомерТелефона В (&НомераТелефонов)
				|	И ВТ_Телефоны.Представление <> """""; 
				
				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					Результат.Добавить(Выборка.Объект);
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Алгоритм = "Емейл" Тогда
		
		Если ДанныеДляПоиска.Почта <> Неопределено Тогда
			Для каждого ТекЭлемент Из ДанныеДляПоиска.Почта Цикл
				
				ЗначениеКИ = ТекЭлемент.Получить("VALUE");
				
				Запрос = Новый Запрос;
				Запрос.МенеджерВременныхТаблиц = ДанныеДляПоиска.МенеджерВременныхТаблиц;
				Запрос.УстановитьПараметр("Представление", ЗначениеКИ); 
				Запрос.Текст = "ВЫБРАТЬ
				|	ВТ_Почта.Объект КАК Объект
				|ИЗ
				|	ВТ_Почта КАК ВТ_Почта
				|ГДЕ
				|	ВТ_Почта.Представление = &Представление
				|	И ВТ_Почта.Представление <> """""; 
				
				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					Результат.Добавить(Выборка.Объект);
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Алгоритм = "ИНН" Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДанныеДляПоиска.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ИНН", ДанныеДляПоиска.ИНН); 
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_Справочник.Объект КАК Объект
		|ИЗ
		|	ВТ_Справочник КАК ВТ_Справочник
		|ГДЕ    
		|	ВТ_Справочник.ИНН = &ИНН
		|	И ВТ_Справочник.ИНН <> """""; 
		//|	И ВТ_Справочник.Идентификатор ЕСТЬ NULL";  
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Добавить(Выборка.Объект);
		КонецЦикла;
		
	ИначеЕсли Алгоритм = "КПП" Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДанныеДляПоиска.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("КПП", ДанныеДляПоиска.КПП); 
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_Справочник.Объект КАК Объект
		|ИЗ
		|	ВТ_Справочник КАК ВТ_Справочник
		|ГДЕ         
		|	ВТ_Справочник.КПП = &КПП"; 
		//|	И ВТ_Справочник.Идентификатор ЕСТЬ NULL";  
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если НЕ ЗначениеЗаполнено(Выборка.Объект) ИЛИ ДанныеДляПоиска.КПП = "" Тогда
				Результат = Неопределено;
				Прервать;
			КонецЕсли;
			
			Результат.Добавить(Выборка.Объект);
			
		КонецЦикла;
		
	ИначеЕсли Алгоритм = "ИНН+КПП" Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДанныеДляПоиска.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ИНН", ДанныеДляПоиска.ИНН); 
		Запрос.УстановитьПараметр("КПП", ДанныеДляПоиска.КПП); 
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_Справочник.Объект КАК Объект
		|ИЗ
		|	ВТ_Справочник КАК ВТ_Справочник
		|ГДЕ      
		|	ВТ_Справочник.ИНН = &ИНН
		|	И ВТ_Справочник.ИНН <> """"
		|	И ВТ_Справочник.КПП = &КПП"; 
		//|	И ВТ_Справочник.Идентификатор ЕСТЬ NULL"; 
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если НЕ ЗначениеЗаполнено(Выборка.Объект) ИЛИ ДанныеДляПоиска.ИНН = "" Тогда
				Результат = Неопределено;
				Прервать;
			КонецЕсли;
			
			Результат.Добавить(Выборка.Объект);
			
		КонецЦикла;
		
	ИначеЕсли Алгоритм = "Номер счета" Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДанныеДляПоиска.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НомерСчета", ДанныеДляПоиска.НомерСчета); 
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_Справочник.Объект КАК Объект
		|ИЗ
		|	ВТ_Справочник КАК ВТ_Справочник
		|ГДЕ
		|	ВТ_Справочник.НомерСчета = &НомерСчета"; 
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Добавить(Выборка.Объект);
		КонецЦикла;
		
	ИначеЕсли Алгоритм = "БИК" Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДанныеДляПоиска.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("БИК", ДанныеДляПоиска.БикБанка); 
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_Справочник.Объект КАК Объект
		|ИЗ
		|	ВТ_Справочник КАК ВТ_Справочник
		|ГДЕ
		|	ВТ_Справочник.БИК = &БИК"; 
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Добавить(Выборка.Объект);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции


Функция ЗагрузитьОбновитьКомпании(пСтруктураНастроек, ДополнительныеДанные) Экспорт
	
	мДанных = ДополнительныеДанные.ИнформацияОКомпаниях;
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
#Область ИдентификаторыСлужебныхСвойств	

	Если пСтруктураНастроек.ОбрабатыватьУдалениеОбъектовВ1С И СтруктураНастроек.ПредопределенныеСвойства.Свойство("ПредопределенноеСвойствоПометкаУдаленияКомпанийИКонтактов") Тогда 	
		ДопИдСвойстваПометкиУдаления = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьДопИдОбъекта(СтруктураНастроек.Портал,СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоПометкаУдаленияКомпанийИКонтактов, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКомпании); 
		СтруктураНастроек.Вставить("ДопИдПредопределенногоСвойстваПометкиУдаленияКомпании", ДопИдСвойстваПометкиУдаления);
	КонецЕсли;
	
#КонецОбласти
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
#Область ФормированиеВременныхТаблицДанных		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"				, СтруктураНастроек.ТипыОбъектовОбмена.Компания);
	Запрос.УстановитьПараметр("ТипДанныхСвойств"		, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКомпании);
	Запрос.УстановитьПараметр("ТипДанныхЗначенийСвойств", СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваКомпании);
	Запрос.УстановитьПараметр("ТипТелефона"				, Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("ТипПочты"				, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийСвойств
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗначенийСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Объект,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	Б24_К_ИдентификаторыОбъектов.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор
	|ПОМЕСТИТЬ ВТ_Свойства
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО Б24_К_ИдентификаторыОбъектов.Объект = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Партнеры.Ссылка КАК Объект,
	|	Партнеры.Наименование КАК Наименование,
	|	ВЫРАЗИТЬ(Контрагенты.Партнер.НаименованиеПолное КАК СТРОКА(100)) КАК НаименованиеПолное,
	|	ВТ_Идентификаторы.Идентификатор КАК Идентификатор,
	|	Контрагенты.ИНН КАК ИНН,
	|	Контрагенты.КПП КАК КПП
	|ПОМЕСТИТЬ ВТ_Справочник
	|ИЗ
	|	Справочник.Партнеры КАК Партнеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Идентификаторы КАК ВТ_Идентификаторы
	|		ПО Партнеры.Ссылка = ВТ_Идентификаторы.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО Контрагенты.Партнер = Партнеры.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Ссылка,
	|	Организации.Наименование,
	|	ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(100)),
	|	ВТ_Идентификаторы.Идентификатор,
	|	Организации.ИНН,
	|	Организации.КПП
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Идентификаторы КАК ВТ_Идентификаторы
	|		ПО Организации.Ссылка = ВТ_Идентификаторы.Объект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор,
	|	Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Идентификаторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартнерыКонтактнаяИнформация.Ссылка КАК Объект,
	|	ПартнерыКонтактнаяИнформация.Представление КАК Представление,
	|	ПартнерыКонтактнаяИнформация.НомерТелефона КАК НомерТелефона,
	|	ПартнерыКонтактнаяИнформация.НомерТелефонаБезКодов КАК НомерТелефонаБезКодов,
	|	ПартнерыКонтактнаяИнформация.Вид КАК Вид
	|ПОМЕСТИТЬ ВТ_Телефоны
	|ИЗ
	|	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	|ГДЕ
	|	ПартнерыКонтактнаяИнформация.Тип = &ТипТелефона
	|	И ПартнерыКонтактнаяИнформация.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_Справочник.Объект
	|			ИЗ
	|				ВТ_Справочник)
	|	И ПартнерыКонтактнаяИнформация.НомерТелефона <> """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОрганизацииКонтактнаяИнформация.Ссылка,
	|	ОрганизацииКонтактнаяИнформация.Представление,
	|	ОрганизацииКонтактнаяИнформация.НомерТелефона,
	|	ОрганизацииКонтактнаяИнформация.НомерТелефонаБезКодов,
	|	ОрганизацииКонтактнаяИнформация.Вид
	|ИЗ
	|	Справочник.Организации.КонтактнаяИнформация КАК ОрганизацииКонтактнаяИнформация
	|ГДЕ
	|	ОрганизацииКонтактнаяИнформация.Тип = &ТипТелефона
	|	И ОрганизацииКонтактнаяИнформация.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_Справочник.Объект
	|			ИЗ
	|				ВТ_Справочник)
	|	И ОрганизацииКонтактнаяИнформация.НомерТелефона <> """"
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартнерыКонтактнаяИнформация.Ссылка КАК Объект,
	|	ПартнерыКонтактнаяИнформация.Представление КАК Представление,
	|	ПартнерыКонтактнаяИнформация.АдресЭП КАК АдресЭП,
	|	ПартнерыКонтактнаяИнформация.Вид КАК Вид
	|ПОМЕСТИТЬ ВТ_Почта
	|ИЗ
	|	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	|ГДЕ
	|	ПартнерыКонтактнаяИнформация.Тип = &ТипПочты
	|	И ПартнерыКонтактнаяИнформация.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_Справочник.Объект
	|			ИЗ
	|				ВТ_Справочник)
	|	И ПартнерыКонтактнаяИнформация.Представление <> """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОрганизацииКонтактнаяИнформация.Ссылка,
	|	ОрганизацииКонтактнаяИнформация.Представление,
	|	ОрганизацииКонтактнаяИнформация.АдресЭП,
	|	ОрганизацииКонтактнаяИнформация.Вид
	|ИЗ
	|	Справочник.Организации.КонтактнаяИнформация КАК ОрганизацииКонтактнаяИнформация
	|ГДЕ
	|	ОрганизацииКонтактнаяИнформация.Тип = &ТипПочты
	|	И ОрганизацииКонтактнаяИнформация.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_Справочник.Объект
	|			ИЗ
	|				ВТ_Справочник)
	|	И ОрганизацииКонтактнаяИнформация.Представление <> """"
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект";
	
	Запрос.Выполнить();
#КонецОбласти
	
	СтруктураИспКИ = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьСтруктуруВидовКИКонтрагентов();
	
	ОбновлятьВ1СПартнеров 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Компания", "Партнеры"	, "ОбновлятьВ1С");
	ОбновлятьВ1СОрганизации 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Компания", "Организации", "ОбновлятьВ1С");

	ЗагружатьВ1СПартнеров 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Компания", "Партнеры", "ЗагружатьВ1С");
	ЗагружатьВ1СОрганизации 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Компания", "Организации", "ЗагружатьВ1С");
	
	АлгоритмПриЗагрузкеДанныхВ1СПартнеров 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Компания", "Партнеры", "АлгоритмПриЗагрузкеДанныхВ1С");
	АлгоритмПриЗагрузкеДанныхВ1СОрганизаций		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Компания", "Организации", "АлгоритмПриЗагрузкеДанныхВ1С");
	
	ПоляПартнеров 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Компания", "Партнеры");
	ПоляОрганизаций 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Компания", "Организации");
	
	ОтборыДляЗагрузкиДанныхВ1СПартнеров 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Компания", "Партнеры");
	ОтборыДляЗагрузкиДанныхВ1СОрганизации 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Компания", "Организации");
	
	
	ЗагруженныеОбъекты = Новый Массив;
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ВнешнийКод 			= ТекЭлемент.Получить("ORIGIN_ID"); 
		
		ИдЭлемента 			= Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);
		
		ЗаголовокКомпании 	= Строка(ТекЭлемент.Получить("TITLE"));
		Телефоны 			= ТекЭлемент.Получить("PHONE");
		Почта 				= ТекЭлемент.Получить("EMAIL");
		ЭтоНашаКомпания 	= ТекЭлемент.Получить("IS_MY_COMPANY"); 
		Если ЭтоНашаКомпания = "Y" Тогда
			ОбновлятьВ1С 		= ОбновлятьВ1СОрганизации;
			ЗагружатьВ1С 		= ЗагружатьВ1СОрганизации;
			АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СОрганизаций; 
			ПоляОбъекта1С 		= ПоляОрганизаций;
			ОтборыДляЗагрузки 	= ОтборыДляЗагрузкиДанныхВ1СОрганизации;
		Иначе
			ОбновлятьВ1С 		= ОбновлятьВ1СПартнеров;
			ЗагружатьВ1С 		= ЗагружатьВ1СПартнеров;
			АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СПартнеров; 
			ПоляОбъекта1С 		= ПоляПартнеров;
			ОтборыДляЗагрузки 	= ОтборыДляЗагрузкиДанныхВ1СПартнеров;
		КонецЕсли;
		
		Если ЗагружатьВ1С = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;	
		
		НужноЗагружать = ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, ТекЭлемент, ОтборыДляЗагрузки);
		Если НЕ НужноЗагружать Тогда 
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		
		ПомеченНаУдаление = Ложь;
#Область ПоискПометкиНаУдаление
		Если пСтруктураНастроек.ОбрабатыватьУдалениеОбъектовВ1С Тогда 	
			ЗначениеПометки  = Формат(ТекЭлемент.Получить(СтруктураНастроек.ДопИдПредопределенногоСвойстваПометкиУдаленияКомпании), "ЧГ=0");
			Если ЗначениеПометки = "1" Тогда
				ПомеченНаУдаление = Истина;
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	

#Область ПоискСозданиеКомпании
		КомпанияСсылка = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Справочник", ИдЭлемента);	

		Если ЗначениеЗаполнено(ВнешнийКод) И НЕ ЗначениеЗаполнено(КомпанияСсылка) Тогда
			КомпанияСсылка = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Партнеры"), ВнешнийКод);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(КомпанияСсылка) Тогда
			
			ИНН = "";
			КПП = "";
			Для Каждого РеквизитКомпанииОбщий Из ДополнительныеДанные.ИнформацияОРеквизитах Цикл
				Для Каждого РеквизитКомпании Из РеквизитКомпанииОбщий Цикл
					Если Формат(РеквизитКомпании.Получить("ENTITY_ID"),"ЧГ=0") = ИдЭлемента Тогда
						ИНН = РеквизитКомпании.Получить("RQ_INN");
						КПП = РеквизитКомпании.Получить("RQ_KPP");
						Прервать;
					КонецЕсли;	
				КонецЦикла;
			КонецЦикла;
		
			ДанныеДляПоиска = Новый Структура;
			ДанныеДляПоиска.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
			ДанныеДляПоиска.Вставить("Наименование"				, ЗаголовокКомпании);
			ДанныеДляПоиска.Вставить("ЭтоНашаКомпания"			, ЭтоНашаКомпания);
			ДанныеДляПоиска.Вставить("Телефоны"					, Телефоны);
			ДанныеДляПоиска.Вставить("Почта"					, Почта);
			ДанныеДляПоиска.Вставить("ИНН"						, ИНН);
			ДанныеДляПоиска.Вставить("КПП"						, КПП);
		
			мКлиенты = ПроверитьНаличиеКомпанийПередДобавлениемНовых(СтруктураНастроек, ДанныеДляПоиска);
			
			КоличествоНайденных = мКлиенты.Количество();
			Если КоличествоНайденных > 1 Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Найдено несколько партнеров по правилам поиска. Клиент с  ид: " + ИдЭлемента + " будет пропущен.");
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
				Продолжить;
				
			ИначеЕсли КоличествоНайденных = 1 Тогда
				КомпанияСсылка = мКлиенты[0]; 	   
			КонецЕсли;
				
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КомпанияСсылка) Тогда
			Если НЕ ОбновлятьВ1С Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что партнеры не обновляются. Партнер: " + ЗаголовокКомпании + " не будет обновлен.");
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Компания, КомпанияСсылка, ИдЭлемента);
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				
				Продолжить;
				
			Иначе
				
				Компания 	= КомпанияСсылка.ПолучитьОбъект();
				
				Если ТипЗнч(Компания) = Тип("СправочникОбъект.Организации") Тогда
					АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СОрганизаций; 
					ПоляОбъекта1С 		= ПоляОрганизаций;
					ЭтоНашаКомпания 	= "Y";
				Иначе
					АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СПартнеров; 
					ПоляОбъекта1С 		= ПоляПартнеров;
					ЭтоНашаКомпания 	= "N";
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли ПомеченНаУдаление Тогда
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Компания " + ЗаголовокКомпании + " помечена на удаление в Битрикс24. Не будет создана в 1С");
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;
			
		Иначе
			
			Если ЭтоНашаКомпания = "N" Тогда 
				
				Компания = Справочники.Партнеры.СоздатьЭлемент();
				
				Компания.ДатаРегистрации  = СтруктураНастроек.ДатаФормирования;
				Если СтруктураНастроек.ТипыКонтрагентовДляКомпаний.Количество() > 0 Тогда
					Компания.ЮрФизЛицо	= СтруктураНастроек.ТипыКонтрагентовДляКомпаний[0];
				Иначе
					Компания.ЮрФизЛицо	= Перечисления.КомпанияЧастноеЛицо.Компания;
				КонецЕсли;
				
				Компания.Клиент 	= Истина;
				
			Иначе
				Компания = Справочники.Организации.СоздатьЭлемент();
				
				Компания.ЮрФизЛицо 					= Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель;
				Компания.ЮридическоеФизическоеЛицо 	= Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
				Компания.НаименованиеСокращенное	= ЗаголовокКомпании;			

			КонецЕсли;	
			
		КонецЕсли;	
#КонецОбласти

		ЭтоНоваяКомпания	= Компания.Ссылка.Пустая() = Истина;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПоляОбъекта1С"			, ПоляОбъекта1С);
		ДополнительныеПараметры.Вставить("ИдЭлементаБ24"			, ИдЭлемента);
		ДополнительныеПараметры.Вставить("Объект1С"					, Компания);
		ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
		ДополнительныеПараметры.Вставить("ДанныеЭлементаБ24"		, ТекЭлемент);
		ДополнительныеПараметры.Вставить("ПомеченНаУдаление"		, ПомеченНаУдаление);
		ДополнительныеПараметры.Вставить("ЭтоНовыйОбъект"			, ЭтоНоваяКомпания);
		ДополнительныеПараметры.Вставить("ТаблицаСвойств"			, РазобратьПользовательскиеПоляВТаблицу(СтруктураНастроек, ТекЭлемент, МенеджерВременныхТаблиц));
		ДополнительныеПараметры.Вставить("Валюта"					, Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ВалютаПоУмолчанию());
		
		ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);

		
		ЗаполнитьДополнительныеРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);

		ЗаполнениеТелефонаИЕмейлПартнера(СтруктураНастроек, СтруктураИспКИ, Компания, Телефоны, Почта);
			
		Если ЗначениеЗаполнено(АлгоритмПриЗагрузке) Тогда
			ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Компания, ТекЭлемент, АлгоритмПриЗагрузке);
		КонецЕсли;
		
		Если ЭтоНоваяКомпания Тогда   // Для выгрузки дела при создании в 1С
			Компания.УстановитьСсылкуНового(Справочники[Компания.Метаданные().Имя].ПолучитьСсылку());         
			РегистрыСведений.Б24_К_ИдентификаторыДел.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ДелоКомпании, Компания.ПолучитьСсылкуНового(), "", ИдЭлемента);
		КонецЕсли;		
		
		Попытка
			
			Компания.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			Компания.Записать();
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Компания, Компания.Ссылка, ИдЭлемента);
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан партнер/организация: " + ЗаголовокКомпании, Компания.Ссылка);
			
			Если ЭтоНашаКомпания = "N" Тогда
				
				ЗаполнитьДополнительныеСведенияОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);
				
			КонецЕсли;
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи партнера/организации: " + ЗаголовокКомпании + " возникли ошибки.", Компания.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции

Функция ЗагрузитьОбновитьКонтакты(пСтруктураНастроек, ДополнительныеДанные, ПринудительноСоздаватьКонтрагента = Ложь) Экспорт
	
	мДанных = ДополнительныеДанные.ИнформацияОКонтактах;
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
#Область ИдентификаторыСлужебныхСвойств		

	Если СтруктураНастроек.ПредопределенныеСвойства.Свойство("ПредопределенноеСвойствоПолКонтактов") Тогда
		ДопИдСвойстваПола = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьДопИдОбъекта(СтруктураНастроек.Портал,СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоПолКонтактов, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКонтакта); 
	Иначе
		ДопИдСвойстваПола = "null";	
	КонецЕсли;	
	СтруктураНастроек.Вставить("ДопИдПредопределенногоСвойстваПолаКонтакта", ДопИдСвойстваПола);
	
	Если пСтруктураНастроек.ОбрабатыватьУдалениеОбъектовВ1С И СтруктураНастроек.ПредопределенныеСвойства.Свойство("ПредопределенноеСвойствоПометкаУдаленияКомпанийИКонтактов") Тогда 	
		ДопИдСвойстваПометкиУдаления = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьДопИдОбъекта(СтруктураНастроек.Портал, СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоПометкаУдаленияКомпанийИКонтактов, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКонтакта); 
		СтруктураНастроек.Вставить("ДопИдПредопределенногоСвойстваПометкиУдаленияКонтакта", ДопИдСвойстваПометкиУдаления);
	КонецЕсли;
	
#КонецОбласти
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
#Область ФормированиеВременныхТаблицДанных		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"				, СтруктураНастроек.ТипыОбъектовОбмена.Контакт);
	Запрос.УстановитьПараметр("ТипДанныхКомпания"		, СтруктураНастроек.ТипыОбъектовОбмена.Компания);
	Запрос.УстановитьПараметр("ТипДанныхСвойств"		, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКонтакта);
	Запрос.УстановитьПараметр("ТипДанныхЗначенийСвойств", СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваКонтакта);
	Запрос.УстановитьПараметр("ТипТелефона"				, Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("ТипПочты"				, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыКомпаний
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКомпания
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийСвойств
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗначенийСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Объект,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	Б24_К_ИдентификаторыОбъектов.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор
	|ПОМЕСТИТЬ ВТ_Свойства
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО Б24_К_ИдентификаторыОбъектов.Объект = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Партнеры.Ссылка КАК Объект,
	|	Партнеры.Наименование КАК Наименование,
	|	ВЫРАЗИТЬ(Контрагенты.Партнер.НаименованиеПолное КАК СТРОКА(100)) КАК НаименованиеПолное,
	|	ВТ_Идентификаторы.Идентификатор КАК Идентификатор,
	|	Контрагенты.ИНН КАК ИНН,
	|	Контрагенты.КПП КАК КПП
	|ПОМЕСТИТЬ ВТ_Справочник
	|ИЗ
	|	Справочник.Партнеры КАК Партнеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Идентификаторы КАК ВТ_Идентификаторы
	|		ПО Партнеры.Ссылка = ВТ_Идентификаторы.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО Контрагенты.Партнер = Партнеры.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтактныеЛицаПартнеров.Ссылка,
	|	КонтактныеЛицаПартнеров.Наименование,
	|	КонтактныеЛицаПартнеров.Наименование,
	|	ВТ_Идентификаторы.Идентификатор,
	|	"""",
	|	""""
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Идентификаторы КАК ВТ_Идентификаторы
	|		ПО КонтактныеЛицаПартнеров.Ссылка = ВТ_Идентификаторы.Объект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор,
	|	Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Идентификаторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартнерыКонтактнаяИнформация.Ссылка КАК Объект,
	|	ПартнерыКонтактнаяИнформация.Представление КАК Представление,
	|	ПартнерыКонтактнаяИнформация.НомерТелефона КАК НомерТелефона,
	|	ПартнерыКонтактнаяИнформация.НомерТелефонаБезКодов КАК НомерТелефонаБезКодов,
	|	ПартнерыКонтактнаяИнформация.Вид КАК Вид
	|ПОМЕСТИТЬ ВТ_Телефоны
	|ИЗ
	|	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	|ГДЕ
	|	ПартнерыКонтактнаяИнформация.Тип = &ТипТелефона
	|	И ПартнерыКонтактнаяИнформация.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_Справочник.Объект
	|			ИЗ
	|				ВТ_Справочник)
	|	И ПартнерыКонтактнаяИнформация.НомерТелефона <> """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка,
	|	КонтактныеЛицаПартнеровКонтактнаяИнформация.Представление,
	|	КонтактныеЛицаПартнеровКонтактнаяИнформация.НомерТелефона,
	|	КонтактныеЛицаПартнеровКонтактнаяИнформация.НомерТелефонаБезКодов,
	|	КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаПартнеровКонтактнаяИнформация
	|ГДЕ
	|	КонтактныеЛицаПартнеровКонтактнаяИнформация.Тип = &ТипТелефона
	|	И КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_Справочник.Объект
	|			ИЗ
	|				ВТ_Справочник)
	|	И КонтактныеЛицаПартнеровКонтактнаяИнформация.НомерТелефона <> """"
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартнерыКонтактнаяИнформация.Ссылка КАК Объект,
	|	ПартнерыКонтактнаяИнформация.Представление КАК Представление,
	|	ПартнерыКонтактнаяИнформация.АдресЭП КАК АдресЭП,
	|	ПартнерыКонтактнаяИнформация.Вид КАК Вид
	|ПОМЕСТИТЬ ВТ_Почта
	|ИЗ
	|	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	|ГДЕ
	|	ПартнерыКонтактнаяИнформация.Тип = &ТипПочты
	|	И ПартнерыКонтактнаяИнформация.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_Справочник.Объект
	|			ИЗ
	|				ВТ_Справочник)
	|	И ПартнерыКонтактнаяИнформация.Представление <> """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка,
	|	КонтактныеЛицаПартнеровКонтактнаяИнформация.Представление,
	|	КонтактныеЛицаПартнеровКонтактнаяИнформация.АдресЭП,
	|	КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаПартнеровКонтактнаяИнформация
	|ГДЕ
	|	КонтактныеЛицаПартнеровКонтактнаяИнформация.Тип = &ТипПочты
	|	И КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_Справочник.Объект
	|			ИЗ
	|				ВТ_Справочник)
	|	И КонтактныеЛицаПартнеровКонтактнаяИнформация.Представление <> """"
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект";	
	
	Запрос.Выполнить();
#КонецОбласти	

	
	СтруктураИспКИ = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьСтруктуруВидовКИКонтрагентов();
	
	ОбновлятьВ1СПартнеров 				= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Контакт", "Партнеры"				, "ОбновлятьВ1С");
	ОбновлятьВ1СКонтактныхЛицПартнеров 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Контакт", "КонтактныеЛицаПартнеров", "ОбновлятьВ1С");
	
	ЗагружатьВ1СПартнеров 				= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Контакт", "Партнеры", "ЗагружатьВ1С");
	ЗагружатьВ1СКонтактныхЛицПартнеров 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Контакт", "КонтактныеЛицаПартнеров", "ЗагружатьВ1С");
	
	АлгоритмПриЗагрузкеДанныхВ1СПартнеров  				= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Контакт", "Партнеры"				, "АлгоритмПриЗагрузкеДанныхВ1С");
	АлгоритмПриЗагрузкеДанныхВ1СКонтактныхЛицПартнеров	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Контакт", "КонтактныеЛицаПартнеров", "АлгоритмПриЗагрузкеДанныхВ1С");
	
	ПоляПартнеров  				= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Контакт", "Партнеры");
	ПоляКонтактныхЛицПартнеров 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Контакт", "КонтактныеЛицаПартнеров");
	
	ОтборыДляЗагрузкиДанныхВ1СПартнеров 				= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения	, "Контакт"	, "Партнеры");
	ОтборыДляЗагрузкиДанныхВ1СКонтактныхЛицПартнеров 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения	, "Контакт"	, "КонтактныеЛицаПартнеров");
	
	ЗагруженныеОбъекты = Новый Массив;
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ВнешнийКод 	= ТекЭлемент.Получить("ORIGIN_ID"); 
		
		ИдЭлемента 			= Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");
		ИдКомпании 			= Формат(ТекЭлемент.Получить("COMPANY_ID"),"ЧГ=0"); 
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);
		
		Если ЗначениеЗаполнено(ИдКомпании) И НЕ Строка(ИдКомпании) = "0" И НЕ ПринудительноСоздаватьКонтрагента Тогда
			ИдЭлементаДля1С = "#"+ИдЭлемента; 
			ЭтоКонтактноеЛицо = Истина;
		Иначе
			ИдЭлементаДля1С = ИдЭлемента; 
			ЭтоКонтактноеЛицо = Ложь;
		КонецЕсли;
		
		Если ЭтоКонтактноеЛицо Тогда
			ОбновлятьВ1С 		= ОбновлятьВ1СКонтактныхЛицПартнеров;
			ЗагружатьВ1С 		= ЗагружатьВ1СКонтактныхЛицПартнеров;
			АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СКонтактныхЛицПартнеров; 
			ПоляОбъекта1С 		= ПоляКонтактныхЛицПартнеров;
			ОтборыДляЗагрузки 	= ОтборыДляЗагрузкиДанныхВ1СКонтактныхЛицПартнеров;
		Иначе
			ОбновлятьВ1С 		= ОбновлятьВ1СПартнеров;
			ЗагружатьВ1С 		= ЗагружатьВ1СПартнеров;
			АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СПартнеров; 
			ПоляОбъекта1С 		= ПоляПартнеров;
			ОтборыДляЗагрузки 	= ОтборыДляЗагрузкиДанныхВ1СПартнеров;
		КонецЕсли;
		
		Если ЗагружатьВ1С = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;	
		
		НужноЗагружать = ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, ТекЭлемент, ОтборыДляЗагрузки);
		Если НЕ НужноЗагружать Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		
		ПомеченНаУдаление = Ложь;
#Область ПоискПометкиНаУдаление
		Если пСтруктураНастроек.ОбрабатыватьУдалениеОбъектовВ1С Тогда 	
			ЗначениеПометки  = Формат(ТекЭлемент.Получить(СтруктураНастроек.ДопИдПредопределенногоСвойстваПометкиУдаленияКонтакта), "ЧГ=0");
			Если ЗначениеПометки = "1" Тогда
				ПомеченНаУдаление = Истина;
			КонецЕсли;
		КонецЕсли;
#КонецОбласти		

		Телефоны 	= ТекЭлемент.Получить("PHONE");
		Почта 		= ТекЭлемент.Получить("EMAIL");
		Должность 	= Строка(ТекЭлемент.Получить("POST"));

		Имя 		= Строка(ТекЭлемент.Получить("NAME"));
		Фамилия 	= Строка(ТекЭлемент.Получить("LAST_NAME"));
		Отчество 	= Строка(ТекЭлемент.Получить("SECOND_NAME"));     
		ЭтоИП 		= Строка(ТекЭлемент.Получить("HONORIFIC"))= "ИП";	
		
		ДанныеФИО = Б24_К_ОбщегоНазначенияВызовСервера.ПреобразоватьФИО(Фамилия,Имя, Отчество, ЭтоИП);

		НаименованиеКонтрагента = СокрЛП(ДанныеФИО.Фамилия + " " + ДанныеФИО.Имя + " " + ДанныеФИО.Отчество); 

#Область ПоискСозданиеКонтакта

		КонтактСсылка = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Справочник", ИдЭлементаДля1С);	

		Если ЗначениеЗаполнено(ВнешнийКод) И НЕ ЗначениеЗаполнено(КонтактСсылка) Тогда
			Если НЕ ЭтоКонтактноеЛицо Тогда
				ПрефиксВнешнихКодов1С = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьОписаниеПрефиксовВнешнихКодов1С();
				ВнешнийКод = ?(Лев(ВнешнийКод,3)= ПрефиксВнешнихКодов1С.КонтактныеЛица, Прав(ВнешнийКод, СтрДлина(ВнешнийКод)-3),ВнешнийКод);	
				КонтактСсылка = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Партнеры"), ВнешнийКод);
			Иначе
				КонтактСсылка = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.КонтактныеЛицаПартнеров"), ВнешнийКод);
			КонецЕсли;
		КонецЕсли;

		Компания = Неопределено;
		Если ЗначениеЗаполнено(ИдКомпании) И НЕ Строка(ИдКомпании) = "0" И НЕ ПринудительноСоздаватьКонтрагента Тогда
			Компания = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", ИдКомпании);	
			
			Если НЕ ЗначениеЗаполнено(Компания) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "У контакта: " + НаименованиеКонтрагента + " не найдена компания по ИД "+ИдКомпании+". Такие контакты не обрабатываются.");
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				Продолжить;
			КонецЕсли;
		КонецЕсли;			
		
		Если НЕ ЗначениеЗаполнено(КонтактСсылка) Тогда

			ИНН = "";
			КПП = "";
			Для Каждого РеквизитКомпанииОбщий Из ДополнительныеДанные.ИнформацияОРеквизитах Цикл
				Для Каждого РеквизитКомпании Из РеквизитКомпанииОбщий Цикл
					Если Формат(РеквизитКомпании.Получить("ENTITY_ID"),"ЧГ=0") = ИдЭлемента Тогда
						ИНН = РеквизитКомпании.Получить("RQ_INN");
						КПП = РеквизитКомпании.Получить("RQ_KPP");
						Прервать;
					КонецЕсли;	
				КонецЦикла;
			КонецЦикла;
			
			ДанныеДляПоиска = Новый Структура;
			ДанныеДляПоиска.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
			ДанныеДляПоиска.Вставить("ЭтоКонтактноеЛицо"		, ЭтоКонтактноеЛицо);
			ДанныеДляПоиска.Вставить("Наименование"				, НаименованиеКонтрагента);
			ДанныеДляПоиска.Вставить("Фамилия"					, ДанныеФИО.Фамилия);
			ДанныеДляПоиска.Вставить("Имя"						, ДанныеФИО.Имя);
			ДанныеДляПоиска.Вставить("Отчество"					, ДанныеФИО.Отчество);
			ДанныеДляПоиска.Вставить("Телефоны"					, Телефоны);
			ДанныеДляПоиска.Вставить("Почта"					, Почта);
			ДанныеДляПоиска.Вставить("ИНН"						, ИНН);
			ДанныеДляПоиска.Вставить("КПП"						, КПП);
			ДанныеДляПоиска.Вставить("Компания"					, Компания);
			
			мКлиенты = ПроверитьНаличиеКонтактовПередДобавлениемНовых(СтруктураНастроек, ДанныеДляПоиска);
			
			КоличествоНайденных = мКлиенты.Количество();
			Если КоличествоНайденных > 1 Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Найдено несколько контактов по правилам поиска. Клиент с  ид: " + ИдЭлемента + " будет пропущен.");
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
				Продолжить;
				
			ИначеЕсли КоличествоНайденных = 1 Тогда
				КонтактСсылка = мКлиенты[0]; 	   
			КонецЕсли;
				
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КонтактСсылка) Тогда
			
			Если НЕ ОбновлятьВ1С Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что контакты не обновляются. Контакт: " + НаименованиеКонтрагента + " не будет обновлен.");
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Контакт, КонтактСсылка, ИдЭлемента);
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				
				Продолжить;
				
			Иначе
				
				Контакт 	= КонтактСсылка.ПолучитьОбъект();
				
				Если ТипЗнч(Контакт) = Тип("СправочникОбъект.КонтактныеЛицаПартнеров") Тогда
					АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СКонтактныхЛицПартнеров; 
					ПоляОбъекта1С 		= ПоляКонтактныхЛицПартнеров;
					ЭтоКонтактноеЛицо	= Истина;
				Иначе
					АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СПартнеров; 
					ПоляОбъекта1С 		= ПоляПартнеров;
					ЭтоКонтактноеЛицо	= Ложь;
				КонецЕсли;			
				
			КонецЕсли;
			
		ИначеЕсли ПомеченНаУдаление Тогда
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Контакт " + НаименованиеКонтрагента + " помечен на удаление в Битрикс24. Не будет создан в 1С");
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
			
		Иначе
			
			Если НЕ ЭтоКонтактноеЛицо Тогда
				
				Контакт = Справочники.Партнеры.СоздатьЭлемент();
				
				Контакт.ДатаРегистрации  = СтруктураНастроек.ДатаФормирования;
				Если СтруктураНастроек.ТипыКонтрагентовДляКонтактов.Количество() > 0 Тогда
					Контакт.ЮрФизЛицо	= СтруктураНастроек.ТипыКонтрагентовДляКонтактов[0];
				Иначе
					Контакт.ЮрФизЛицо	= Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо;
				КонецЕсли;
				
			Иначе
				Контакт = Справочники.КонтактныеЛицаПартнеров.СоздатьЭлемент();
				Контакт.Владелец = Компания;	
			КонецЕсли;
			
		КонецЕсли;	
#КонецОбласти

		ЭтоНовыйОбъект = Контакт.Ссылка.Пустая() = Истина;

		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПоляОбъекта1С"			, ПоляОбъекта1С);
		ДополнительныеПараметры.Вставить("ИдЭлементаБ24"			, ИдЭлемента);
		ДополнительныеПараметры.Вставить("Объект1С"					, Контакт);
		ДополнительныеПараметры.Вставить("Компания"					, Компания);
		ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
		ДополнительныеПараметры.Вставить("ДанныеЭлементаБ24"		, ТекЭлемент);
		ДополнительныеПараметры.Вставить("ПомеченНаУдаление"		, ПомеченНаУдаление);
		ДополнительныеПараметры.Вставить("ЭтоНовыйОбъект"			, ЭтоНовыйОбъект);
		ДополнительныеПараметры.Вставить("ТаблицаСвойств"			, РазобратьПользовательскиеПоляВТаблицу(СтруктураНастроек, ТекЭлемент, МенеджерВременныхТаблиц));
		ДополнительныеПараметры.Вставить("Валюта"					, Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ВалютаПоУмолчанию());
		
		ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);

		ЗаполнитьДополнительныеРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);

		ЗаполнениеТелефонаИЕмейлПартнера(СтруктураНастроек, СтруктураИспКИ, Контакт, Телефоны, Почта);
			
		Если ЗначениеЗаполнено(АлгоритмПриЗагрузке) Тогда
			ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Контакт, ТекЭлемент, АлгоритмПриЗагрузке);
		КонецЕсли;

		Если ЭтоНовыйОбъект Тогда   // Для выгрузки дела при создании в 1С
			Контакт.УстановитьСсылкуНового(Справочники[Контакт.Метаданные().Имя].ПолучитьСсылку());     
			РегистрыСведений.Б24_К_ИдентификаторыДел.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ДелоКонтакта, Контакт.ПолучитьСсылкуНового(), "", ИдЭлемента);
		КонецЕсли;		
		
		Попытка
			
			Контакт.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			Контакт.Записать();
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Контакт, Контакт.Ссылка, ИдЭлемента);
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан контрагент: " + НаименованиеКонтрагента, Контакт.Ссылка);
			
			ЗаполнитьДополнительныеСведенияОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);			
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи контрагента: " + НаименованиеКонтрагента + " возникли ошибки.", Контакт.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнениеТелефонаИЕмейлПартнера(СтруктураНастроек, СтруктураИспКИ, КомпанияКонтакт, Телефоны, Почта)
	
	ТзнВрем = КомпанияКонтакт.КонтактнаяИнформация.ВыгрузитьКолонки();
	
	Если ТипЗнч(КомпанияКонтакт) = Тип("СправочникОбъект.Партнеры") Тогда
		ТелефонРабочий 	= СтруктураИспКИ.ТелефонРабочий; 	
		ТелефонМобильный 	= СтруктураИспКИ.ТелефонМобильный; 	
		ТелефонДомашний 	= СтруктураИспКИ.ТелефонДомашний; 
		
		ПочтаРабочий 		= СтруктураИспКИ.ПочтаРабочий; 	
		ПочтаЧастная 		= СтруктураИспКИ.ПочтаЧастная; 	
	ИначеЕсли ТипЗнч(КомпанияКонтакт) = Тип("СправочникОбъект.КонтактныеЛицаПартнеров") Тогда
		ТелефонРабочий 	= СтруктураИспКИ.ТелефонРабочийКонЛица; 	
		ТелефонМобильный 	= СтруктураИспКИ.ТелефонМобильныйКонЛица; 	
		ТелефонДомашний 	= СтруктураИспКИ.ТелефонДомашнийКонЛица; 
		
		ПочтаРабочий 		= СтруктураИспКИ.ПочтаРабочийКонЛица; 	
		ПочтаЧастная 		= СтруктураИспКИ.ПочтаЧастнаяКонЛица; 	
	Иначе	
		ТелефонРабочий 	= СтруктураИспКИ.ТелефонРабочийОрг; 	
		ТелефонМобильный 	= СтруктураИспКИ.ТелефонМобильныйОрг; 	
		ТелефонДомашний 	= СтруктураИспКИ.ТелефонДомашнийОрг; 
		
		ПочтаРабочий 		= СтруктураИспКИ.ПочтаРабочийОрг; 	
		ПочтаЧастная 		= СтруктураИспКИ.ПочтаЧастнаяОрг; 	
	КонецЕсли;
	
	
	Для Каждого ТекСтрока Из КомпанияКонтакт.КонтактнаяИнформация Цикл
		
		Если ТекСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон И (ТекСтрока.Вид = ТелефонРабочий ИЛИ ТекСтрока.Вид = ТелефонМобильный ИЛИ ТекСтрока.Вид = ТелефонДомашний) Тогда
			Продолжить;	
		КонецЕсли;
		
		Если ТекСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты И (ТекСтрока.Вид = ПочтаРабочий ИЛИ ТекСтрока.Вид = ПочтаЧастная) Тогда
			Продолжить;	
		КонецЕсли;
		
		НовСтрока = ТзнВрем.Добавить();
		
		ЗаполнитьЗначенияСвойств(НовСтрока, ТекСтрока); 
		
	КонецЦикла;
	
	КомпанияКонтакт.КонтактнаяИнформация.Очистить();
	КомпанияКонтакт.КонтактнаяИнформация.Загрузить(ТзнВрем);
	
	Если Телефоны <> Неопределено Тогда
		
		Для Каждого ТекТелефон Из Телефоны Цикл
			
			НомерТелефона = ТекТелефон.Получить("VALUE");
			
			Если Не ЗначениеЗаполнено(НомерТелефона) Тогда
				Продолжить;					
			КонецЕсли;
			
			ВидТелефона = ТекТелефон.Получить("VALUE_TYPE");
			
			Если ВидТелефона = "WORK" Тогда
				
				НовыйКонтакт = КомпанияКонтакт.КонтактнаяИнформация.Добавить(); 
				
				НовыйКонтакт.Вид = ТелефонРабочий;
				НовыйКонтакт.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
				НовыйКонтакт.Представление 			= НомерТелефона; 
				НовыйКонтакт.НомерТелефона 			= НомерТелефона;
				НовыйКонтакт.НомерТелефонаБезКодов 	= НомерТелефона;
				НовыйКонтакт.ЗначенияПолей 			= ПолучитьЗначениеПолейТелефонаИПочты("Телефон", НомерТелефона)
			ИначеЕсли ВидТелефона = "MOBILE" Тогда 
				
				НовыйКонтакт = КомпанияКонтакт.КонтактнаяИнформация.Добавить(); 
				
				НовыйКонтакт.Вид = ТелефонМобильный;
				НовыйКонтакт.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
				НовыйКонтакт.Представление 			= НомерТелефона; 
				НовыйКонтакт.НомерТелефона 			= НомерТелефона;
				НовыйКонтакт.НомерТелефонаБезКодов 	= НомерТелефона;
				НовыйКонтакт.ЗначенияПолей 			= ПолучитьЗначениеПолейТелефонаИПочты("Телефон", НомерТелефона)
			ИначеЕсли ВидТелефона = "HOME" Тогда 
				
				НовыйКонтакт = КомпанияКонтакт.КонтактнаяИнформация.Добавить(); 
				
				НовыйКонтакт.Вид = ТелефонДомашний;
				НовыйКонтакт.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
				НовыйКонтакт.Представление 			= НомерТелефона; 
				НовыйКонтакт.НомерТелефона 			= НомерТелефона;
				НовыйКонтакт.НомерТелефонаБезКодов 	= НомерТелефона;
				НовыйКонтакт.ЗначенияПолей 			= ПолучитьЗначениеПолейТелефонаИПочты("Телефон", НомерТелефона)
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Почта <> Неопределено Тогда
		
		Для Каждого ТекПочта Из Почта Цикл
			
			АдресПочты = ТекПочта.Получить("VALUE");
			
			Если Не ЗначениеЗаполнено(АдресПочты) Тогда
				Продолжить;					
			КонецЕсли;
			
			ВидПочты = ТекПочта.Получить("VALUE_TYPE");
			
			Если ВидПочты = "WORK" Тогда
				
				НовыйКонтакт = КомпанияКонтакт.КонтактнаяИнформация.Добавить(); 
				
				НовыйКонтакт.Вид = ПочтаРабочий;
				НовыйКонтакт.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
				НовыйКонтакт.Представление 			= АдресПочты; 
				НовыйКонтакт.АдресЭП 				= АдресПочты;
				НовыйКонтакт.ЗначенияПолей 			= ПолучитьЗначениеПолейТелефонаИПочты("ЭПочта", АдресПочты)
			ИначеЕсли ВидПочты = "HOME" Тогда 
				
				НовыйКонтакт = КомпанияКонтакт.КонтактнаяИнформация.Добавить(); 
				
				НовыйКонтакт.Вид = ПочтаЧастная;
				НовыйКонтакт.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
				НовыйКонтакт.Представление 			= АдресПочты; 
				НовыйКонтакт.АдресЭП 				= АдресПочты;
				НовыйКонтакт.ЗначенияПолей 			= ПолучитьЗначениеПолейТелефонаИПочты("ЭПочта", АдресПочты)
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначениеПолейТелефонаИПочты(ТипКИ, ЗначениеКИ)
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.УстановитьСтроку();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("КонтактнаяИнформация");
	
	ЗаписьXML.ЗаписатьАтрибут("xmlns", XMLСтрока("http://www.v8.1c.ru/ssl/contactinfo"));
	ЗаписьXML.ЗаписатьАтрибут("xmlns:xs", XMLСтрока("http://www.w3.org/2001/XMLSchema"));
	ЗаписьXML.ЗаписатьАтрибут("xmlns:xsi", XMLСтрока("http://www.w3.org/2001/XMLSchema-instance"));
	ЗаписьXML.ЗаписатьАтрибут("Представление", XMLСтрока(ЗначениеКИ));
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("Комментарий");
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("Состав");
	
	Если ТипКИ = "Телефон" Тогда
		ЗаписьXML.ЗаписатьАтрибут("xsi:type"	, "НомерТелефона");
		ЗаписьXML.ЗаписатьАтрибут("КодСтраны"	, "");
		ЗаписьXML.ЗаписатьАтрибут("КодГорода"	, "");
		ЗаписьXML.ЗаписатьАтрибут("Номер"		, XMLСтрока(ЗначениеКИ));
		ЗаписьXML.ЗаписатьАтрибут("Добавочный"	, "");
	ИначеЕсли ТипКИ = "ЭПочта" Тогда
		ЗаписьXML.ЗаписатьАтрибут("xsi:type"	, "ЭлектроннаяПочта");
		ЗаписьXML.ЗаписатьАтрибут("Значение"	, XMLСтрока(ЗначениеКИ));
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	Возврат ЗаписьXML.Закрыть();	
	
КонецФункции


Функция ЗагрузитьОбновитьРеквизиты(пСтруктураНастроек, ДополнительныеДанные) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	мТипыКИ = Новый Массив;
	мТипыКИ.Добавить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	мТипыКИ.Добавить(Перечисления.ТипыКонтактнойИнформации.Телефон);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"				, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит);
	Запрос.УстановитьПараметр("ТипДанныхКомпаний"		, СтруктураНастроек.ТипыОбъектовОбмена.Компания);
	Запрос.УстановитьПараметр("ТипДанныхКонтактов"		, СтруктураНастроек.ТипыОбъектовОбмена.Контакт);
	Запрос.УстановитьПараметр("ТипДанныхСвойств"		, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоРеквизита);
	Запрос.УстановитьПараметр("ТипДанныхЗначенийСвойств", СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваРеквизита);
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных КАК ТипДанных
	|ПОМЕСТИТЬ ВТ_ИдентификаторыВладельца
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И (Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКомпаний
	|			ИЛИ Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКонтактов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТипДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийСвойств
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗначенийСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Объект,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	Б24_К_ИдентификаторыОбъектов.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор
	|ПОМЕСТИТЬ ВТ_Свойства
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО Б24_К_ИдентификаторыОбъектов.Объект = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Объект,
	|	Контрагенты.Наименование КАК Наименование,
	|	ВЫРАЗИТЬ(Контрагенты.НаименованиеПолное КАК СТРОКА(100)) КАК НаименованиеПолное,
	|	ВТ_Идентификаторы.Идентификатор КАК Идентификатор,
	|	Контрагенты.ИНН КАК ИНН,
	|	Контрагенты.КПП КАК КПП
	|ПОМЕСТИТЬ ВТ_Справочник
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Идентификаторы КАК ВТ_Идентификаторы
	|		ПО Контрагенты.Ссылка = ВТ_Идентификаторы.Объект
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Ссылка,
	|	Организации.Наименование,
	|	ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(100)),
	|	ВТ_Идентификаторы.Идентификатор,
	|	Организации.ИНН,
	|	Организации.КПП
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Идентификаторы КАК ВТ_Идентификаторы
	|		ПО Организации.Ссылка = ВТ_Идентификаторы.Объект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор,
	|	Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Идентификаторы";	
	
	Запрос.Выполнить();
	
	ОбновлятьВ1СКонтрагентов 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Реквизит", "Контрагенты", "ОбновлятьВ1С");
	ОбновлятьВ1СОрганизации 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Реквизит", "Организации", "ОбновлятьВ1С");

	ЗагружатьВ1СКонтрагентов 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Реквизит", "Контрагенты", "ЗагружатьВ1С");
	ЗагружатьВ1СОрганизации 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Реквизит", "Организации", "ЗагружатьВ1С");
	
	АлгоритмПриЗагрузкеДанныхВ1СКонтрагентов 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Реквизит", "Контрагенты", "АлгоритмПриЗагрузкеДанныхВ1С");
	АлгоритмПриЗагрузкеДанныхВ1СОрганизаций		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Реквизит", "Организации", "АлгоритмПриЗагрузкеДанныхВ1С");
	
	ПоляКонтрагентов 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Реквизит", "Контрагенты");
	ПоляОрганизаций 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Реквизит", "Организации");
	
	ОтборыДляЗагрузкиДанныхВ1СКонтрагентов 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Реквизит", "Контрагенты");
	ОтборыДляЗагрузкиДанныхВ1СОрганизации 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Реквизит", "Организации");
	
	
	ЗагруженныеОбъекты = Новый Массив;
	
	Для каждого ТекЭлемент Из ДополнительныеДанные.ИнформацияОРеквизитах Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		НовыйРеквизит 	= Ложь;
		
		ВнешнийКод 		= ТекЭлемент.Получить("XML_ID"); 
		
		ИдЭлемента 		= Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");
		ИдВладельца 	= Формат(ТекЭлемент.Получить("ENTITY_ID"),"ЧГ=0");
		Если ЗагруженныеОбъекты.Найти(ИдВладельца + "_" + ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдВладельца + "_" + ИдЭлемента);
		
		ТипРеквизита 	= Строка(ТекЭлемент.Получить("ENTITY_TYPE_ID"));
		Если НЕ (ТипРеквизита = "4" ИЛИ ТипРеквизита = "3") Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;
		КонецЕсли;
		
		ИНН 			= Строка(ТекЭлемент.Получить("RQ_INN")); 
		КПП 			= Строка(ТекЭлемент.Получить("RQ_KPP")); 
		
		Фамилия 		= Строка(ТекЭлемент.Получить("RQ_LAST_NAME")); 
		Имя 			= Строка(ТекЭлемент.Получить("RQ_FIRST_NAME")); 
		Отчество 		= Строка(ТекЭлемент.Получить("RQ_SECOND_NAME")); 
		ЭтоИП 			= ЗначениеЗаполнено(ТекЭлемент.Получить("RQ_INN")); 
		
		ДанныеФИО		= Б24_К_ОбщегоНазначенияВызовСервера.ПреобразоватьФИО(Фамилия, Имя, Отчество, ЭтоИП);
		
		ФИО				= СокрЛП(ДанныеФИО.Фамилия + " " + ДанныеФИО.Имя + " " + ДанныеФИО.Отчество); 
		
		Если ТипРеквизита = "3" Тогда     
			Наименование		= ФИО;
		Иначе
			Наименование 		= Строка(ТекЭлемент.Получить("RQ_COMPANY_NAME"));
			Наименование		= ?(ЗначениеЗаполнено(Наименование), Наименование, ФИО);
		КонецЕсли;
		
		ЭтоОрганизация 	= Ложь; 
		ЭтоКонтрагент 	= Ложь; 
		ЭтоКонтакт 		= Ложь; 
		
		РеквизитСсылка 		= Неопределено;
		ВладелецРеквизита 	= Неопределено;
#Область ПоискСозданиеРеквизита
		РеквизитСсылка = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Справочник", ИдЭлемента);		
		
		Если ЗначениеЗаполнено(ВнешнийКод) И НЕ ЗначениеЗаполнено(РеквизитСсылка) Тогда
			
			НовыйРеквизит = Истина;
			
			РеквизитСсылка = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Контрагенты"), ВнешнийКод);
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(РеквизитСсылка) Тогда	
			
			НовыйРеквизит = Истина;
			
			ДанныеДляПоиска = Новый Структура;
			ДанныеДляПоиска.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
			ДанныеДляПоиска.Вставить("Наименование"				, Наименование);
			ДанныеДляПоиска.Вставить("Фамилия"					, ДанныеФИО.Фамилия);
			ДанныеДляПоиска.Вставить("Имя"						, ДанныеФИО.Имя);
			ДанныеДляПоиска.Вставить("Отчество"					, ДанныеФИО.Отчество);
			ДанныеДляПоиска.Вставить("ИНН"						, ИНН);
			ДанныеДляПоиска.Вставить("КПП"						, КПП);
			
			мРеквизиты = ПроверитьНаличиеРеквизитовПередДобавлениемНовых(СтруктураНастроек, ДанныеДляПоиска);
			
			КоличествоНайденных = мРеквизиты.Количество();
			Если КоличествоНайденных > 1 Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Найдено несколько контрагентов по правилам поиска. Реквизит с  ид: " + ИдЭлемента + " будет пропущен.");
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
				Продолжить;
				
			ИначеЕсли КоличествоНайденных = 1 Тогда
				РеквизитСсылка = мРеквизиты[0]; 	   
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(РеквизитСсылка) Тогда
			
			НовыйРеквизит = Истина;
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Идентификатор", ИдВладельца);
			
			Если ТипРеквизита = "3" Тогда     
				Запрос.УстановитьПараметр("ТипДанных", СтруктураНастроек.ТипыОбъектовОбмена.Контакт);
			Иначе
				Запрос.УстановитьПараметр("ТипДанных", СтруктураНастроек.ТипыОбъектовОбмена.Компания);
			КонецЕсли;
			
			Запрос.Текст = "ВЫБРАТЬ
			|	ВТ_Идентификаторы.Объект КАК Объект
			|ИЗ
			|	ВТ_ИдентификаторыВладельца КАК ВТ_Идентификаторы
			|ГДЕ
			|	ВТ_Идентификаторы.Идентификатор = &Идентификатор
			|   И ВТ_Идентификаторы.ТипДанных = &ТипДанных";
			
			ВыполненныйЗапрос = Запрос.Выполнить();
			
			Если НЕ ВыполненныйЗапрос.Пустой() Тогда
				Выборка = ВыполненныйЗапрос.Выбрать();
				Пока Выборка.Следующий() Цикл
					ВладелецРеквизита = Выборка.Объект;
					Прервать;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РеквизитСсылка) Тогда
			
			ЭтоОрганизация 	= ТипЗнч(РеквизитСсылка) = Тип("СправочникОбъект.Организации"); 
			ЭтоКонтрагент 	= ТипЗнч(РеквизитСсылка) = Тип("СправочникОбъект.Контрагенты"); 
			ЭтоКонтакт		= ТипЗнч(РеквизитСсылка) = Тип("СправочникОбъект.КонтактныеЛицаПартнеров"); 
			
			Реквизит = РеквизитСсылка.ПолучитьОбъект();
			
		ИначеЕсли ЗначениеЗаполнено(ВладелецРеквизита) Тогда
			
			Если ТипЗнч(ВладелецРеквизита) = Тип("СправочникСсылка.Партнеры") Тогда
				
				ЭтоКонтрагент 		= Истина; 
				Реквизит 			= Справочники.Контрагенты.СоздатьЭлемент();
				Реквизит.Партнер 	= ВладелецРеквизита; 
				
			ИначеЕсли ТипЗнч(ВладелецРеквизита) = Тип("СправочникСсылка.Организации") Тогда
				
				ЭтоОрганизация 		= Истина; 
				Реквизит			= ВладелецРеквизита.ПолучитьОбъект(); 
				
			Иначе
				ЭтоКонтакт 		= Истина; 
			КонецЕсли;

		Иначе
			//Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Для заполнения реквизитов не найден Партнер: " + Наименование + ". Будет пропущен.");
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			Продолжить;
		КонецЕсли;	
#КонецОбласти

		Если ЭтоКонтакт Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Неподдерживаемый тип для реквизита: " + Наименование + ". Не записывается реквизит.");
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;
		КонецЕсли;

		ЭтоОрганизация = ТипЗнч(Реквизит) = Тип("СправочникОбъект.Организации");
		Если ЭтоОрганизация Тогда
			ОбновлятьВ1С 		= ОбновлятьВ1СОрганизации;
			ЗагружатьВ1С 		= ЗагружатьВ1СОрганизации;
			АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СОрганизаций; 
			ПоляОбъекта1С 		= ПоляОрганизаций;
			ОтборыДляЗагрузки 	= ОтборыДляЗагрузкиДанныхВ1СОрганизации;
		Иначе
			ОбновлятьВ1С 		= ОбновлятьВ1СКонтрагентов;
			ЗагружатьВ1С 		= ЗагружатьВ1СКонтрагентов;
			АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СКонтрагентов; 
			ПоляОбъекта1С 		= ПоляКонтрагентов;
			ОтборыДляЗагрузки 	= ОтборыДляЗагрузкиДанныхВ1СКонтрагентов;
		КонецЕсли;
		
		Если ЗагружатьВ1С = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;	    
		
		НужноЗагружать = ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, ТекЭлемент, ОтборыДляЗагрузки);
		Если НЕ НужноЗагружать Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		
		ПомеченНаУдаление = Ложь;
#Область ПоискПометкиНаУдаление
		Если пСтруктураНастроек.ОбрабатыватьУдалениеОбъектовВ1С Тогда 	
			ПомеченНаУдаление = ТекЭлемент.Получить("ACTIVE") = "N"
		КонецЕсли;
#КонецОбласти	
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПоляОбъекта1С"			, ПоляОбъекта1С);
		ДополнительныеПараметры.Вставить("ИдЭлементаБ24"			, ИдЭлемента);
		ДополнительныеПараметры.Вставить("Объект1С"					, Реквизит);
		ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
		ДополнительныеПараметры.Вставить("ДанныеЭлементаБ24"		, ТекЭлемент);
		ДополнительныеПараметры.Вставить("ПомеченНаУдаление"		, ПомеченНаУдаление);
		ДополнительныеПараметры.Вставить("ЭтоНовыйОбъект"			, НовыйРеквизит);
		ДополнительныеПараметры.Вставить("ОбновлятьВ1С"				, ОбновлятьВ1С);
		ДополнительныеПараметры.Вставить("Наименование"				, Наименование);
		ДополнительныеПараметры.Вставить("Валюта"					, Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ВалютаПоУмолчанию());
		
		ДополнительныеПараметры.Вставить("ТаблицаСвойств"			, РазобратьПользовательскиеПоляВТаблицу(СтруктураНастроек, ТекЭлемент, МенеджерВременныхТаблиц));
		
		ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);

		ЗаполнитьДополнительныеРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);


ЕстьАдресФ = Ложь;
ЕстьАдресЮ = Ложь;
ЕстьАдресД = Ложь;
#Область ЗаполнениеАдреса
		Если НЕ СтруктураНастроек.НеЗагружатьАдресаРеквизитов Тогда    
		
			Для каждого ТекАдресОбщий Из ДополнительныеДанные.ИнформацияОАдресах Цикл

				Для каждого ТекАдрес Из ТекАдресОбщий Цикл
					
					ИдВладельца = Формат(ТекАдрес.Получить("ENTITY_ID"),"ЧГ=0");

					Если ИдЭлемента <> ИдВладельца Тогда
						Продолжить;	
					КонецЕсли;

					ИдВладельца 	= Формат(ТекАдрес.Получить("ENTITY_ID"),"ЧГ=0");
					ТипАдреса		= ТекАдрес.Получить("TYPE_ID");
					Страна 			= Строка(ТекАдрес.Получить("COUNTRY"));
					Регион 			= Строка(ТекАдрес.Получить("PROVINCE"));
					Район 			= Строка(ТекАдрес.Получить("REGION"));
					Индекс 			= Строка(ТекАдрес.Получить("POSTAL_CODE"));
					ГородНП 		= Строка(ТекАдрес.Получить("CITY"));
					Адрес1 			= Строка(ТекАдрес.Получить("ADDRESS_1"));
					Адрес2 			= Строка(ТекАдрес.Получить("ADDRESS_2"));
					
					
					Если Строка(ТипАдреса) = "1" Тогда
						ВидКИ = ?(ЭтоОрганизация, Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации, Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
						ЕстьАдресФ = Истина;
					ИначеЕсли Строка(ТипАдреса) = "6" Тогда
						ВидКИ = ?(ЭтоОрганизация, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
						ЕстьАдресЮ = Истина;
					ИначеЕсли Строка(ТипАдреса) = "11" Тогда
						ВидКИ = ?(ЭтоОрганизация, Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации, Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента);
						ЕстьАдресД = Истина;
					Иначе
						Продолжить;
					КонецЕсли;
					
					Страна	= ?(ЗначениеЗаполнено(Страна), ВРег(Страна), Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьНазваниеСтраны()); 
					
					СтруктураДанных = Б24_К_РаботаСАдресамиСервер.ПолучитьСтруктуруСПолямиКИ(Страна, Регион, Район, Индекс, ГородНП, Адрес1, Адрес2);
					
					НайденнаяСтрока = Реквизит.КонтактнаяИнформация.Найти(ВидКИ, "Вид"); 
					
					Если НайденнаяСтрока = Неопределено Тогда
						
						НовыйКИ =  Реквизит.КонтактнаяИнформация.Добавить();
						НовыйКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес;
						НовыйКИ.Вид = ВидКИ;
						
						НовыйКИ.Страна 			= СтруктураДанных.Страна;
						НовыйКИ.Регион 			= СтруктураДанных.Регион;
						НовыйКИ.Город 			= СтруктураДанных.Город;
						НовыйКИ.Представление 	= СтруктураДанных.Представление;
						
						НовыйКИ.ЗначенияПолей 	= Б24_К_РаботаСАдресамиСервер.ПолучитьЗначениеПолейАдреса(СтруктураДанных);	
						НовыйКИ.Значение		= "";
						
					Иначе
						Если НЕ ОбновлятьВ1С И ЗначениеЗаполнено(НайденнаяСтрока.Представление) Тогда
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что контрагенты не обновляются. У контрагента : " + Реквизит.Наименование + " не будет обновлен " + ВидКИ.Наименование);
							Продолжить;
						КонецЕсли;
						
						НайденнаяСтрока.Страна 			= СтруктураДанных.Страна;
						НайденнаяСтрока.Регион 			= СтруктураДанных.Регион;
						НайденнаяСтрока.Город 			= СтруктураДанных.Город;
						
						НайденнаяСтрока.Представление 	= СтруктураДанных.Представление;
						
						НайденнаяСтрока.ЗначенияПолей 	= Б24_К_РаботаСАдресамиСервер.ПолучитьЗначениеПолейАдреса(СтруктураДанных);	
						НайденнаяСтрока.Значение		= "";
					КонецЕсли;
					
				КонецЦикла;
						
			КонецЦикла;
		КонецЕсли;
#КонецОбласти

		Если ЗначениеЗаполнено(АлгоритмПриЗагрузке) Тогда
			ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Реквизит, ТекЭлемент, АлгоритмПриЗагрузке);
		КонецЕсли;

		Попытка
			Реквизит.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			Реквизит.ДополнительныеСвойства.Вставить("Б24_К_НеВыгружатьДело", Истина);
			Реквизит.Записать();
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит, Реквизит.Ссылка, ИдЭлемента);
			
			Если ЕстьАдресФ Тогда
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ФактАдресРеквизита, Реквизит.Ссылка, ИдЭлемента);
			КонецЕсли;
			Если ЕстьАдресЮ Тогда
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ЮрАдресРеквизита, Реквизит.Ссылка, ИдЭлемента);
			КонецЕсли;
			Если ЕстьАдресД Тогда
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.АдресДоставкиРеквизита, Реквизит.Ссылка, ИдЭлемента);
			КонецЕсли;
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записаны реквизиты контрагента: " + Наименование, Реквизит.Ссылка);
			
			ЗаполнитьДополнительныеСведенияОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи реквизитов контрагента: " + Наименование + " возникли ошибки.", Реквизит.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции


Функция ЗагрузитьОбновитьБанковскиеСчетаКомпанийКонтактов(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"			, СтруктураНастроек.ТипыОбъектовОбмена.БанковскийСчетРеквизита);
	Запрос.УстановитьПараметр("ТипДанныхВладельца"	, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит);
	Запрос.УстановитьПараметр("Портал"				, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК Объект,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	Банки.Код КАК БИК,
	|	ВТ_Идентификаторы.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Справочник
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторБанков КАК Банки
	|		ПО БанковскиеСчета.Банк = Банки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Идентификаторы КАК ВТ_Идентификаторы
	|		ПО (ВТ_Идентификаторы.Объект = БанковскиеСчета.Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка,
	|	БанковскиеСчета.НомерСчета,
	|	Банки.Код,
	|	ВТ_Идентификаторы.Идентификатор
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторБанков КАК Банки
	|		ПО БанковскиеСчета.Банк = Банки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Идентификаторы КАК ВТ_Идентификаторы
	|		ПО (ВТ_Идентификаторы.Объект = БанковскиеСчета.Ссылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Идентификаторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыВладельца
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхВладельца
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Запрос.Выполнить();
	                 
	ОбновлятьВ1СБСКонтрагентов 					= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Банковский счет", "БанковскиеСчетаКонтрагентов", "ОбновлятьВ1С");
	ЗагружатьВ1С 								= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Банковский счет", "БанковскиеСчетаКонтрагентов", "ЗагружатьВ1С");   
	АлгоритмПриЗагрузкеДанныхВ1СБСКонтрагентов 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Банковский счет", "БанковскиеСчетаКонтрагентов", "АлгоритмПриЗагрузкеДанныхВ1С");
	ПоляБСКонтрагентов 							= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Банковский счет", "БанковскиеСчетаКонтрагентов");
	ОтборыДляЗагрузкиДанныхВ1СБСКонтрагентов 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Банковский счет", "БанковскиеСчетаКонтрагентов");
	
	ОбновлятьВ1СБСОрганизаций 					= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Банковский счет", "БанковскиеСчетаОрганизаций", "ОбновлятьВ1С");
	АлгоритмПриЗагрузкеДанныхВ1СБСОрганизаций 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Банковский счет", "БанковскиеСчетаОрганизаций", "АлгоритмПриЗагрузкеДанныхВ1С");
	ПоляБСОрганизаций 							= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Банковский счет", "БанковскиеСчетаОрганизаций");
	ОтборыДляЗагрузкиДанныхВ1СБСОрганизаций 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Банковский счет", "БанковскиеСчетаОрганизаций");
	
	ЗагруженныеОбъекты = Новый Массив;	
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ВнешнийКод 		= Строка(ТекЭлемент.Получить("XML_ID"));		
		
		ИдЭлемента 		= Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");     
		
		Если ЗагружатьВ1С = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;	
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);    
		
		ИдВладельца 		= Формат(ТекЭлемент.Получить("ENTITY_ID"),"ЧГ=0");
		НомерСчета			= Формат(ТекЭлемент.Получить("RQ_ACC_NUM"), "ЧГ=0");
		БикБанка 			= Формат(ТекЭлемент.Получить("RQ_BIK"), "ЧГ=0");
		
		Если Не ЗначениеЗаполнено(БикБанка) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не указан БИК в счете:" + НомерСчета + " у реквизита с кодом " + ИдВладельца);
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			Продолжить;
		КонецЕсли;
		
		ВладелецБСчета = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыВладельца", ИдВладельца);	
		Если ВладелецБСчета = Неопределено Тогда
			//Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не найден владелец банковского счета. Ид реквизита: " + ИдВладельца + ". Будет пропущен банковский счет : " + ИдЭлемента);
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ВладелецБСчета) = Тип("СправочникСсылка.Контрагенты") Тогда
			ОбновлятьВ1С 		= ОбновлятьВ1СБСКонтрагентов;
			АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СБСКонтрагентов; 
			ПоляОбъекта1С 		= ПоляБСКонтрагентов;
			ОтборыДляЗагрузки 	= ОтборыДляЗагрузкиДанныхВ1СБСКонтрагентов;
		Иначе
			ОбновлятьВ1С 		= ОбновлятьВ1СБСОрганизаций;
			АлгоритмПриЗагрузке	= АлгоритмПриЗагрузкеДанныхВ1СБСОрганизаций; 
			ПоляОбъекта1С 		= ПоляБСОрганизаций;
			ОтборыДляЗагрузки 	= ОтборыДляЗагрузкиДанныхВ1СБСОрганизаций;
		КонецЕсли;
		
		НужноЗагружать = ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, ТекЭлемент, ОтборыДляЗагрузки);
		Если НЕ НужноЗагружать Тогда
			Продолжить;	
		КонецЕсли;
		
		КодВалюты = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьКодПоВалюте(Строка(ТекЭлемент.Получить("RQ_ACC_CURRENCY")));
		Валюта = Справочники.Валюты.НайтиПоКоду(КодВалюты);
		
		ПомеченНаУдаление = Ложь;
#Область ПоискПометкиНаУдаление
		Если пСтруктураНастроек.ОбрабатыватьУдалениеОбъектовВ1С Тогда 	
			ПомеченНаУдаление = ТекЭлемент.Получить("ACTIVE") = "N"
		КонецЕсли;
#КонецОбласти	

#Область ПоискСозданиеБанкСчета

		БанковскийСчетСсылка = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Справочник", ИдЭлемента);		
		
		Если ЗначениеЗаполнено(ВнешнийКод) И НЕ ЗначениеЗаполнено(БанковскийСчетСсылка) Тогда
			БанковскийСчетСсылка = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.БанковскиеСчетаКонтрагентов"), ВнешнийКод);
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(БанковскийСчетСсылка) Тогда	
			
			ДанныеДляПоиска = Новый Структура;
			ДанныеДляПоиска.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
			ДанныеДляПоиска.Вставить("НомерСчета"				, НомерСчета);
			ДанныеДляПоиска.Вставить("БикБанка"					, БикБанка);
			
			мБанковскиеСчета = ПроверитьНаличиеБанкСчетовПередДобавлениемНовых(СтруктураНастроек, ДанныеДляПоиска);
			
			КоличествоНайденных = мБанковскиеСчета.Количество();
			Если КоличествоНайденных > 1 Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Найдено несколько банковских счетов по правилам поиска. Банковский счет с  ид: " + ИдЭлемента + " будет пропущен.");
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
				Продолжить;
				
			ИначеЕсли КоличествоНайденных = 1 Тогда
				БанковскийСчетСсылка = мБанковскиеСчета[0]; 	   
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(БанковскийСчетСсылка) Тогда
			
			Если НЕ ОбновлятьВ1С Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что банковские счета не обновляются. Банковский счет с ид: " + ИдЭлемента + " не будет обновлен.");
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.БанковскийСчетРеквизита, БанковскийСчетСсылка, ИдЭлемента);
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				
				Продолжить;
				
			Иначе
				
				БанковскийСчет = БанковскийСчетСсылка.ПолучитьОбъект();
				
			КонецЕсли;
			
		ИначеЕсли ПомеченНаУдаление Тогда
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Банковский счет с ид " + ИдЭлемента + " помечен на удаление в Битрикс24. Не будет создан в 1С");
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
			
		Иначе
			
			ВладелецБСчета = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыВладельца", ИдВладельца);	
			Если ВладелецБСчета = Неопределено Тогда
				//Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не найден владелец банковского счета. Ид реквизита: " + ИдВладельца + ". Будет пропущен банковский счет : " + ИдЭлемента);
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(ВладелецБСчета) = Тип("СправочникСсылка.Контрагенты") Тогда
				БанковскийСчет = Справочники.БанковскиеСчетаКонтрагентов.СоздатьЭлемент();
			Иначе
				БанковскийСчет = Справочники.БанковскиеСчетаОрганизаций.СоздатьЭлемент();
			КонецЕсли;
			
			БанковскийСчет.Владелец = ВладелецБСчета;
			
		КонецЕсли;			
#КонецОбласти

		Банк 	= Справочники.КлассификаторБанков.НайтиПоКоду(БикБанка);

		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПоляОбъекта1С"			, ПоляОбъекта1С);
		ДополнительныеПараметры.Вставить("ИдЭлементаБ24"			, ИдЭлемента);
		ДополнительныеПараметры.Вставить("Объект1С"					, БанковскийСчет);
		ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
		ДополнительныеПараметры.Вставить("ДанныеЭлементаБ24"		, ТекЭлемент);
		ДополнительныеПараметры.Вставить("ПомеченНаУдаление"		, ПомеченНаУдаление);
		ДополнительныеПараметры.Вставить("ЭтоНовыйОбъект"			, БанковскийСчет.Ссылка.Пустая());
		ДополнительныеПараметры.Вставить("Банк"						, Банк);
		ДополнительныеПараметры.Вставить("Валюта"					, Валюта);
		
		ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);

		Если ЗначениеЗаполнено(АлгоритмПриЗагрузке) Тогда
			ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, БанковскийСчет, ТекЭлемент, АлгоритмПриЗагрузке);
		КонецЕсли;

		Попытка
			БанковскийСчет.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			БанковскийСчет.Записать();
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.БанковскийСчетРеквизита, БанковскийСчет.Ссылка, ИдЭлемента);
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан номер счета: " + НомерСчета, БанковскийСчет.Ссылка);
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи банковского счета: " + НомерСчета + " возникли ошибки.", БанковскийСчет.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 


#Область РазделыТоваров

Функция ЗагрузитьОбновитьРазделыТоваров(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	ЕстьПользовательскоеДерево = ЗначениеЗаполнено(СтруктураНастроек.НастройкиСинхронизацииТоваров.ДеревоГрупп);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных", СтруктураНастроек.ТипыОбъектовОбмена.ГруппаТовара);
	Запрос.УстановитьПараметр("ЕстьПользовательскоеДерево", ЕстьПользовательскоеДерево);
	Запрос.УстановитьПараметр("Портал"					  , СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Запрос.Выполнить();
	
	ОбновлятьВ1СТовары	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар", "Номенклатура", "ОбновлятьВ1С");
	ЗагружатьВ1СТовары	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар", "Номенклатура", "ЗагружатьВ1С");   
	
	ЗагруженныеОбъекты = Новый Массив;
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ИдЭлемента 		= Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");
		
		Если ЗагружатьВ1СТовары = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;	
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);
		
		ВнешнийКод 		= Строка(ТекЭлемент.Получить("XML_ID")); 		
		ИдРодителя 		= Формат(ТекЭлемент.Получить("SECTION_ID"),"ЧГ=0");
		Наименование	= Строка(ТекЭлемент.Получить("NAME"));
		
#Область ПоискСозданиеРаздела	
		ГруппаНоменклатуры = Неопределено;
		ГруппаНоменклатуры = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Идентификаторы", ИдЭлемента);	
		
		Если ЗначениеЗаполнено(ВнешнийКод) И НЕ ЗначениеЗаполнено(ГруппаНоменклатуры) Тогда
			ГруппаНоменклатуры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(?(ЕстьПользовательскоеДерево, Тип("СправочникСсылка.Б24_КС_ПользовательскиеГруппыТоваров"), Тип("СправочникСсылка.Номенклатура")), ВнешнийКод);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ГруппаНоменклатуры) Тогда
			
			Если ЕстьПользовательскоеДерево Тогда
				ГруппаНоменклатуры = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.СоздатьЭлемент();
			Иначе
				ГруппаНоменклатуры = Справочники.Номенклатура.СоздатьГруппу();
			КонецЕсли;
			
		Иначе
			Если ОбновлятьВ1СТовары Тогда
				ГруппаНоменклатуры = ГруппаНоменклатуры.ПолучитьОбъект();
			Иначе
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ГруппаТовара, ГруппаНоменклатуры.Ссылка, ИдЭлемента);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что товары не обновляются. раздел номенклатуры: " + Наименование + " не будет обновлен.");
				Продолжить;  				
			КонецЕсли;
		КонецЕсли;
#КонецОбласти
		
		ГруппаНоменклатуры.Наименование	= Наименование; 
		
		РодительГруппы = Неопределено;
		
		Если ЗначениеЗаполнено(ИдРодителя) Тогда
			
			ВыборкаРодителей = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.Выбрать(Новый Структура("ТипДанных", СтруктураНастроек.ТипыОбъектовОбмена.ГруппаТовара)); 
			
			Пока ВыборкаРодителей.Следующий() Цикл
				Если ВыборкаРодителей.Идентификатор = ИдРодителя Тогда 
					РодительГруппы = ВыборкаРодителей.Объект;	
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли НЕ  ЗначениеЗаполнено(ИдРодителя) И ЕстьПользовательскоеДерево Тогда
			РодительГруппы = СтруктураНастроек.ДеревоГрупп;	
		КонецЕсли;
		
		Если РодительГруппы = Неопределено И ЗначениеЗаполнено(ИдРодителя) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не найден родитель группы номенклатуры: " + Наименование);
			Если ЕстьПользовательскоеДерево Тогда
				ГруппаНоменклатуры.Родитель = СтруктураНастроек.ДеревоГрупп;
			КонецЕсли;
		Иначе
			ГруппаНоменклатуры.Родитель = РодительГруппы;	
		КонецЕсли;
		
		Попытка
			ГруппаНоменклатуры.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			ГруппаНоменклатуры.Записать();
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ГруппаТовара, ГруппаНоменклатуры.Ссылка, ИдЭлемента);
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записана группа номенклатуры: " + Наименование, ГруппаНоменклатуры.Ссылка);
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи группы номенклатуры: " + Наименование + " возникли ошибки.", ГруппаНоменклатуры.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции

Функция ЗагрузитьОбновитьРазделыТоваров2(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	ЕстьПользовательскоеДерево 	= Ложь;  
	МенеджерВременныхТаблиц 	= Новый МенеджерВременныхТаблиц;  
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"	, СтруктураНастроек.ТипыОбъектовОбмена.ГруппаТовара2);
	Запрос.УстановитьПараметр("Портал"		, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Запрос.Выполнить();

	ЗагружатьВ1СТовары	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар2", "Номенклатура", "ЗагружатьВ1С");   
	ОбновлятьВ1СТовары	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар2", "Номенклатура", "ОбновлятьВ1С");
	
	ИмяСправочника = "Номенклатура";
	Если СтруктураНастроек.НастройкиСинхронизацииТоваров.Свойство("ИсточникИерархии") Тогда 
		Если СтруктураНастроек.НастройкиСинхронизацииТоваров.ИсточникИерархии = "Произвольная иерархия" 
			И ЗначениеЗаполнено(СтруктураНастроек.НастройкиСинхронизацииТоваров.ДеревоГрупп) Тогда
			ИмяСправочника = "Б24_КС_ПользовательскиеГруппыТоваров";                     
			ЕстьПользовательскоеДерево = Истина;
		ИначеЕсли СтруктураНастроек.НастройкиСинхронизацииТоваров.ИсточникИерархии = "Категории номенклатуры" Тогда	
			ИмяСправочника = "ВидыНоменклатуры";
		Иначе
			ИмяСправочника = "Номенклатура";
		КонецЕсли;
	КонецЕсли;
	
	ЗагруженныеОбъекты = Новый Массив;
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ИдЭлемента 		= Формат(ТекЭлемент.Получить("id"),"ЧГ=0");
		
		Если ЗагружатьВ1СТовары = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;	
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);
		
		ВнешнийКод 		= Строка(ТекЭлемент.Получить("xmlId")); 		
		ИдРодителя 		= Формат(ТекЭлемент.Получить("iblockSectionId"),"ЧГ=0");
		Наименование	= Строка(ТекЭлемент.Получить("name"));
		
#Область ПоискСозданиеРаздела	
		ГруппаНоменклатуры = Неопределено;
		ГруппаНоменклатуры = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Идентификаторы", ИдЭлемента);	
		
		Если ЗначениеЗаполнено(ВнешнийКод) И НЕ ЗначениеЗаполнено(ГруппаНоменклатуры) Тогда
			ГруппаНоменклатуры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка."+ИмяСправочника), ВнешнийКод);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ГруппаНоменклатуры) Тогда
			
			Если ЕстьПользовательскоеДерево Тогда
				ГруппаНоменклатуры = Справочники[ИмяСправочника].СоздатьЭлемент();
			Иначе
				ГруппаНоменклатуры = Справочники[ИмяСправочника].СоздатьГруппу();
			КонецЕсли;
			
		Иначе
			Если ОбновлятьВ1СТовары Тогда
				ГруппаНоменклатуры = ГруппаНоменклатуры.ПолучитьОбъект();
			Иначе
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ГруппаТовара2, ГруппаНоменклатуры.Ссылка, ИдЭлемента);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что товары не обновляются. раздел номенклатуры: " + Наименование + " не будет обновлен.");
				Продолжить;  				
			КонецЕсли;
		КонецЕсли;
#КонецОбласти
		
		ГруппаНоменклатуры.Наименование	= Наименование; 
		
		РодительГруппы = Неопределено;
		
		Если ЗначениеЗаполнено(ИдРодителя) Тогда
			
			ВыборкаРодителей = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.Выбрать(Новый Структура("ТипДанных", СтруктураНастроек.ТипыОбъектовОбмена.ГруппаТовара2)); 
			
			Пока ВыборкаРодителей.Следующий() Цикл
				Если ВыборкаРодителей.Идентификатор = ИдРодителя Тогда 
					РодительГруппы = ВыборкаРодителей.Объект;	
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли НЕ ЗначениеЗаполнено(ИдРодителя) И ЕстьПользовательскоеДерево Тогда
			РодительГруппы = СтруктураНастроек.ДеревоГрупп;	
		КонецЕсли;
		
		Если РодительГруппы = Неопределено И ЗначениеЗаполнено(ИдРодителя) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не найден родитель "+ИмяСправочника+": " + Наименование);
			Если ЕстьПользовательскоеДерево Тогда
				ГруппаНоменклатуры.Родитель = СтруктураНастроек.НастройкиСинхронизацииТоваров.ДеревоГрупп;
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(РодительГруппы) Тогда   
			Если ЕстьПользовательскоеДерево Тогда
				ГруппаНоменклатуры.Родитель = РодительГруппы;
			ИначеЕсли РодительГруппы <> Неопределено Тогда
				Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РодительГруппы, "ЭтоГруппа") = Истина Тогда
					ГруппаНоменклатуры.Родитель = РодительГруппы;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
		
		Попытка
			ГруппаНоменклатуры.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			ГруппаНоменклатуры.Записать();
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ГруппаТовара2, ГруппаНоменклатуры.Ссылка, ИдЭлемента);
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записана "+ИмяСправочника+": " + Наименование, ГруппаНоменклатуры.Ссылка);
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи "+ИмяСправочника+": " + Наименование + " возникли ошибки.", ГруппаНоменклатуры.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 


#Область ЕдиницыИзмерения

Функция ЗагрузитьОбновитьЕдиницыИзмерения(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"	, СтруктураНастроек.ТипыОбъектовОбмена.ЕдиницаИзмерения);
	Запрос.УстановитьПараметр("Портал"		, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Запрос.Выполнить();
	
	ЗагруженныеОбъекты = Новый Массив;	
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ИдЭлемента 		= Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);	
		
		КодЭлемента 	= Строка(ТекЭлемент.Получить("CODE"));    
		Наименование	= Строка(ТекЭлемент.Получить("SYMBOL_RUS"));
		НаименованиеП	= Строка(ТекЭлемент.Получить("MEASURE_TITLE"));
		НаименованиеМ	= Строка(ТекЭлемент.Получить("SYMBOL_INTL"));              
		
#Область ПоискСозданиеЕдИзм	
		ЕдиницаИзмерения = Неопределено;
		Если ЗначениеЗаполнено(КодЭлемента) Тогда
			ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(КодЭлемента,,,Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда 
			ЕдиницаИзмерения = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Идентификаторы", ИдЭлемента);	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда 
			ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.СоздатьЭлемент();
			ЕдиницаИзмерения.Владелец = Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения; 
		Иначе
			ЕдиницаИзмерения = ЕдиницаИзмерения.ПолучитьОбъект();
		КонецЕсли;
#КонецОбласти	
		
		ЕдиницаИзмерения.Код						= КодЭлемента; 
		ЕдиницаИзмерения.Наименование				= Наименование; 
		ЕдиницаИзмерения.НаименованиеПолное			= НаименованиеП; 
		ЕдиницаИзмерения.МеждународноеСокращение	= НаименованиеМ; 
		
		Попытка
			ЕдиницаИзмерения.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			ЕдиницаИзмерения.Записать();
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ЕдиницаИзмерения, ЕдиницаИзмерения.Ссылка, ИдЭлемента);
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записана единица измерения номенклатуры: " + НаименованиеП, ЕдиницаИзмерения.Ссылка);
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи единицы измерения номенклатуры: " + НаименованиеП + " возникли ошибки.", ЕдиницаИзмерения.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции

Функция ЗагрузитьОбновитьЕдиницыИзмерения2(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"	, СтруктураНастроек.ТипыОбъектовОбмена.ЕдиницаИзмерения2);
	Запрос.УстановитьПараметр("Портал"		, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Запрос.Выполнить();
	
	ЗагруженныеОбъекты = Новый Массив;	
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ИдЭлемента 		= Формат(ТекЭлемент.Получить("id"),"ЧГ=0");
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);	
		
		КодЭлемента 	= Строка(ТекЭлемент.Получить("code"));    
		Наименование	= Строка(ТекЭлемент.Получить("symbol"));
		НаименованиеП	= Строка(ТекЭлемент.Получить("measureTitle"));
		НаименованиеМ	= Строка(ТекЭлемент.Получить("symbolIntl"));              
		
#Область ПоискСозданиеЕдИзм	
		ЕдиницаИзмерения = Неопределено;
		Если ЗначениеЗаполнено(КодЭлемента) Тогда
			ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(КодЭлемента,,,Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда 
			ЕдиницаИзмерения = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Идентификаторы", ИдЭлемента);	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда 
			ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.СоздатьЭлемент(); 
			ЕдиницаИзмерения.Владелец = Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения; 
		Иначе
			ЕдиницаИзмерения = ЕдиницаИзмерения.ПолучитьОбъект();
		КонецЕсли;
#КонецОбласти	
		
		ЕдиницаИзмерения.Код						= КодЭлемента; 
		ЕдиницаИзмерения.Наименование				= Наименование; 
		ЕдиницаИзмерения.НаименованиеПолное			= НаименованиеП; 
		ЕдиницаИзмерения.МеждународноеСокращение	= НаименованиеМ; 
		
		Попытка
			ЕдиницаИзмерения.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			ЕдиницаИзмерения.Записать();
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ЕдиницаИзмерения2, ЕдиницаИзмерения.Ссылка, ИдЭлемента);
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записана единица измерения номенклатуры: " + НаименованиеП, ЕдиницаИзмерения.Ссылка);
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи единицы измерения номенклатуры: " + НаименованиеП + " возникли ошибки.", ЕдиницаИзмерения.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 


#Область СвойстваТоваров

Функция ЗагрузитьОбновитьСвойстваТоваров(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	Справочник_Номенклатура 				= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Номенклатура");
	Справочник_Номенклатура_Общие 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Номенклатура_Общие");
	Справочник_ХарактеристикиНоменклатуры 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_ХарактеристикиНоменклатуры");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"				, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара);
	Запрос.УстановитьПараметр("ТипДанныхЗначенийСвойств", СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара);
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);
	Запрос.УстановитьПараметр("Справочник_Номенклатура"				 , Справочник_Номенклатура);	
	Запрос.УстановитьПараметр("Справочник_ХарактеристикиНоменклатуры", Справочник_ХарактеристикиНоменклатуры);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Объект,
	|	ДополнительныеРеквизитыИСведения.Заголовок КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НаименованияСвойств
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|ГДЕ
	|	(ДополнительныеРеквизитыИСведения.НаборСвойств В ИЕРАРХИИ (&Справочник_Номенклатура)
	|			ИЛИ ДополнительныеРеквизитыИСведения.НаборСвойств В ИЕРАРХИИ (&Справочник_ХарактеристикиНоменклатуры))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийСвойств
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗначенийСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Запрос.Выполнить();
	
	ЗагруженныеОбъекты = Новый Массив;	
	
	ОбновлятьВ1СТовары	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар", "Номенклатура", "ОбновлятьВ1С");
	ЗагружатьВ1СТовары	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар", "Номенклатура", "ЗагружатьВ1С");  
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ЭтоСвойствоХарактеристикиНоменклатуры = Ложь;
		
		ИдЭлемента 		= Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");      
		
		Если ЗагружатьВ1СТовары = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;		
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);
		
		ОписаниеТипов 		= Неопределено;
		ТипСвойства 		= ТекЭлемент.Получить("PROPERTY_TYPE");
		ПользовательскийТип = ТекЭлемент.Получить("USER_TYPE");
#Область РазборТипаСвойства
		МассивТипов 	= Новый Массив;
		
		Если ТипСвойства = "N" И ПользовательскийТип = "" Тогда
			
			КЧ = Новый КвалификаторыЧисла(12,2);
			МассивТипов.Добавить(Тип("Число"));
			ОписаниеТипов  = Новый ОписаниеТипов(МассивТипов, , КЧ);
			
		ИначеЕсли ТипСвойства = "S" И ПользовательскийТип = "" Тогда
			
			КС = Новый КвалификаторыСтроки(100);
			МассивТипов.Добавить(Тип("Строка"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КС);
			
		ИначеЕсли ТипСвойства = "S" И ПользовательскийТип = "HTML" Тогда
			
			КС = Новый КвалификаторыСтроки(0);
			МассивТипов.Добавить(Тип("Строка"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КС);
			
		ИначеЕсли ТипСвойства = "S" И ПользовательскийТип = "DateTime" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя);
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КД);
			
		ИначеЕсли ТипСвойства = "S" И ПользовательскийТип = "Date" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КД);
			
		ИначеЕсли ТипСвойства = "S" И ПользовательскийТип = "employee" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("СправочникСсылка.Пользователи"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
			
		ИначеЕсли ТипСвойства = "L" И ПользовательскийТип = "" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("СправочникСсылка.ЗначенияСвойствОбъектов"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
			
		КонецЕсли;
		
#КонецОбласти
		
		Если ОписаниеТипов = Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;
		КонецЕсли;
		
		ВнешнийКод 	= Строка(ТекЭлемент.Получить("XML_ID"));
		Если Лев(ВнешнийКод, 2) = "H_" Тогда
			ЭтоСвойствоХарактеристикиНоменклатуры = Истина;
			ВнешнийКод	= Прав(ВнешнийКод, СтрДлина(ВнешнийКод)-2);
			
			ДлинаПрефикса 			= СтрДлина(Б24_КС_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксСвойстваХарактеристики());
			НаименованиеСБитрикс24 	= Строка(ТекЭлемент.Получить("NAME"));
			Наименование			= Прав(НаименованиеСБитрикс24, СтрДлина(НаименованиеСБитрикс24) -ДлинаПрефикса);
		Иначе	
			Наименование	= Строка(ТекЭлемент.Получить("NAME"));
		КонецЕсли;
		
		Если ТекЭлемент.Получить("MULTIPLE") = "Y" Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Свойство: " + Наименование + " не будет добавлено/обновлено. т.к. является множественным");
			Продолжить;
		КонецЕсли;
		
#Область ПоискСозданиеСвойства	
		Свойство = Неопределено;
		Свойство = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Идентификаторы", ИдЭлемента);	
		
		Если ЗначениеЗаполнено(ВнешнийКод)И НЕ ЗначениеЗаполнено(Свойство)  Тогда
			Свойство = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"), ВнешнийКод);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Свойство) Тогда
			Свойство = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_НаименованияСвойств", Наименование);	
		КонецЕсли;
		
		ЭтоНовый = Истина;
		
		Если НЕ ЗначениеЗаполнено(Свойство)Тогда
			Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.СоздатьЭлемент();
			
			Если НЕ ЭтоСвойствоХарактеристикиНоменклатуры Тогда
				Свойство.НаборСвойств = Справочник_Номенклатура_Общие;
			Иначе
				Свойство.НаборСвойств = Справочник_ХарактеристикиНоменклатуры;
			КонецЕсли;
			
		Иначе
			
			Если ОбновлятьВ1СТовары Тогда
				ЭтоНовый = Ложь;
				Свойство = Свойство.ПолучитьОбъект();
			Иначе
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара, Свойство.Ссылка, ИдЭлемента);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что товары не обновляются. Свойство: " + Наименование + " не будет обновлено");
				Продолжить;  				
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	
		
		Свойство.ПометкаУдаления 		= НЕ ТекЭлемент.Получить("ACTIVE") = "Y";	
		
		Если ЭтоНовый Тогда
			Если НЕ ЭтоСвойствоХарактеристикиНоменклатуры Тогда
				Свойство.Наименование 	= Наименование + " (Справочник ""Номенклатура"" (Общие))";
			Иначе
				Свойство.Наименование 	= Наименование + " (Справочник ""Характеристики номенклатуры""";
			КонецЕсли;
			
			Свойство.Заголовок = Наименование;
			
		Иначе
			
			Если НЕ СтрНайти(Свойство.Заголовок, "Битрикс24") > 0 Тогда
				Свойство.Заголовок = Наименование;
			КонецЕсли;
			
		КонецЕсли;
		
		Свойство.ЗаполнятьОбязательно 	= ТекЭлемент.Получить("IS_REQUIRED") = "Y"; 
		
		ТипСвойства 		= ТекЭлемент.Получить("PROPERTY_TYPE");
		ПользовательскийТип = ТекЭлемент.Получить("USER_TYPE");
		
		Если ТипСвойства = "L" И ПользовательскийТип = "" Тогда
			Свойство.ДополнительныеЗначенияИспользуются = Истина;
		КонецЕсли;
		
		Свойство.ТипЗначения = ОписаниеТипов;
		Попытка
			Свойство.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			Свойство.Записать();
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара, Свойство.Ссылка, ИдЭлемента);
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записано свойство номенклатуры: " + Наименование, Свойство.Ссылка);
			
			Если ТипСвойства = "L" И ПользовательскийТип = "" Тогда
#Область ЗаписьЗначенийСвойств
				ЗначенияСписка = ТекЭлемент.Получить("VALUES");	
				
				Если ЗначениеЗаполнено(ЗначенияСписка) Тогда
					
					Для Каждого ТекЗначениеСвойства Из ЗначенияСписка Цикл
						
						лВнешнийКодЗначенияСвойства = Строка(ТекЗначениеСвойства.Значение.Получить("XML_ID")); 		
						ИдЗначенияСвойства 		= Формат(ТекЗначениеСвойства.Значение.Получить("ID"),"ЧГ=0");
						НаименованиеЗнСвойства	= Строка(ТекЗначениеСвойства.Значение.Получить("VALUE"));
						
						ЗначениеСвойства = Неопределено;
						ЗначениеСвойства = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЗначенийСвойств", ИдЗначенияСвойства);	
						
						Если ЗначениеЗаполнено(лВнешнийКодЗначенияСвойства) И НЕ ЗначениеЗаполнено(ЗначениеСвойства) Тогда
							ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.ЗначенияСвойствОбъектов"), лВнешнийКодЗначенияСвойства);
						КонецЕсли;
						
						Если ЗначениеСвойства = Неопределено Тогда
							ЗначениеСвойства = Справочники.ЗначенияСвойствОбъектов.СоздатьЭлемент();
						Иначе
							ЗначениеСвойства = ЗначениеСвойства.ПолучитьОбъект();
						КонецЕсли;
						
						ЗначениеСвойства.Владелец 			= ?(Свойство.ВладелецДополнительныхЗначений.Пустая(), Свойство.Ссылка,  Свойство.ВладелецДополнительныхЗначений);					
						ЗначениеСвойства.Наименование 		= НаименованиеЗнСвойства;					
						ЗначениеСвойства.ПолноеНаименование = НаименованиеЗнСвойства;	
						
						ЗначениеСвойства.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
						ЗначениеСвойства.Записать();
						РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара, ЗначениеСвойства.Ссылка, ИдЗначенияСвойства);
						
					КонецЦикла;
					
				КонецЕсли;
#КонецОбласти
			КонецЕсли;	
			
			Если ЭтоНовый Тогда
				
				НаборСвойств 	= Свойство.НаборСвойств;
				ТекНаборСвойств = НаборСвойств.ПолучитьОбъект();
				
				НовоеСвойствоНабора = ТекНаборСвойств.ДополнительныеРеквизиты.Добавить();
				НовоеСвойствоНабора.Свойство = Свойство.Ссылка;
				
				ТекНаборСвойств.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
				ТекНаборСвойств.Записать();
				
			КонецЕсли;	
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи свойства товара: " + Наименование + " возникли ошибки.", Свойство.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции
 
Функция ЗагрузитьОбновитьСвойстваТоваров2(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	Справочник_Номенклатура 				= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Номенклатура");
	Справочник_Номенклатура_Общие 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Номенклатура_Общие");
	Справочник_ХарактеристикиНоменклатуры 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_ХарактеристикиНоменклатуры");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;      
	Запрос.УстановитьПараметр("Портал", СтруктураНастроек.Портал);      
	
	Запрос.УстановитьПараметр("ТипДанныхСвойствТоваров"					, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара2);
	Запрос.УстановитьПараметр("ТипДанныхЗначенийСвойствТоваров"			, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара2);  
	Запрос.УстановитьПараметр("Справочник_Номенклатура"					, Справочник_Номенклатура);
	
	Запрос.УстановитьПараметр("ТипДанныхСвойствПредложений"				, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоПредложения);
	Запрос.УстановитьПараметр("ТипДанныхЗначенийСвойствПредложений"		, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваПредложения);  
	Запрос.УстановитьПараметр("Справочник_ХарактеристикиНоменклатуры"	, Справочник_ХарактеристикиНоменклатуры);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект
	|ПОМЕСТИТЬ ВТ_ИдентификаторыСвойствТоваров
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхСвойствТоваров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор            
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийСвойствТоваров
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗначенийСвойствТоваров
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Объект,
	|	ДополнительныеРеквизитыИСведения.Заголовок КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НаименованияСвойствТоваров
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|ГДЕ
	|	ДополнительныеРеквизитыИСведения.НаборСвойств В ИЕРАРХИИ (&Справочник_Номенклатура)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|              
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект
	|ПОМЕСТИТЬ ВТ_ИдентификаторыСвойствПредложений
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхСвойствПредложений
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор            
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийСвойствПредложений
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗначенийСвойствПредложений
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Объект,
	|	ДополнительныеРеквизитыИСведения.Заголовок КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НаименованияСвойствПредложений
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|ГДЕ
	|	ДополнительныеРеквизитыИСведения.НаборСвойств В ИЕРАРХИИ (&Справочник_ХарактеристикиНоменклатуры)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Запрос.Выполнить();
	
	ЗагруженныеОбъекты = Новый Массив;	
	
	ЗагружатьВ1СТовары	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар2", "Номенклатура", "ЗагружатьВ1С");
	ОбновлятьВ1СТовары	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар2", "Номенклатура", "ОбновлятьВ1С");
	
	ЗагружатьВ1СПредложения	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Предложение", "ХарактеристикиНоменклатуры", "ЗагружатьВ1С");
	ОбновлятьВ1СПредложения	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Предложение", "ХарактеристикиНоменклатуры", "ОбновлятьВ1С");
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ИдентификаторИнфоблока = Формат(ТекЭлемент.Получить("iblockId"), "ЧГ=0");
		Если ИдентификаторИнфоблока = СтруктураНастроек.НастройкиСинхронизацииТоваров.ИдентификаторКаталогаПредложений Тогда
			ЭтоСвойствоПредложения 		= Истина;
			ТипОбъектовОбмена 			= СтруктураНастроек.ТипыОбъектовОбмена.СвойствоПредложения;	
			ТипЗначенийОбъектовОбмена 	= СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваПредложения;	
		Иначе
			ЭтоСвойствоПредложения 		= Ложь; 
			ТипОбъектовОбмена 			= СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара2;	
			ТипЗначенийОбъектовОбмена 	= СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара2;	
		КонецЕсли;
		
		ИдЭлемента = Формат(ТекЭлемент.Получить("id"),"ЧГ=0");     
		
		Если НЕ ЭтоСвойствоПредложения И ЗагружатьВ1СТовары = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		ИначеЕсли ЭтоСвойствоПредложения И ЗагружатьВ1СПредложения = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;		
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);          
		
		ЕстьСписокЗначений 	= Ложь;
		ОписаниеТипов 		= Неопределено;
#Область РазборТипаСвойства        
		ТипСвойства 		= ТекЭлемент.Получить("propertyType");
		ПользовательскийТип = ТекЭлемент.Получить("userType");
		ТипЭлементаНаФорме 	= ТекЭлемент.Получить("listType");

		МассивТипов 	= Новый Массив;
		
		Если ТипСвойства = "N" И (ПользовательскийТип = "" ИЛИ ПользовательскийТип = Неопределено) Тогда
			
			КЧ = Новый КвалификаторыЧисла(12,2);
			МассивТипов.Добавить(Тип("Число"));
			ОписаниеТипов  = Новый ОписаниеТипов(МассивТипов, , КЧ);
			
		ИначеЕсли ТипСвойства = "S" И (ПользовательскийТип = "" ИЛИ ПользовательскийТип = Неопределено) Тогда
			
			КС = Новый КвалификаторыСтроки(100);
			МассивТипов.Добавить(Тип("Строка"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КС);
			
		ИначеЕсли ТипСвойства = "S" И ПользовательскийТип = "HTML" Тогда
			
			КС = Новый КвалификаторыСтроки(0);
			МассивТипов.Добавить(Тип("Строка"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КС);
			
		ИначеЕсли ТипСвойства = "S" И ПользовательскийТип = "DateTime" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя);
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КД);
			
		ИначеЕсли ТипСвойства = "S" И ПользовательскийТип = "Date" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КД);
			
		ИначеЕсли ТипСвойства = "S" И ПользовательскийТип = "employee" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("СправочникСсылка.Пользователи"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
			
		ИначеЕсли ТипЭлементаНаФорме = "C" И ТипСвойства = "L" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("Булево"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
			
		ИначеЕсли ТипСвойства = "L" Тогда
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("СправочникСсылка.ЗначенияСвойствОбъектов"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
			ЕстьСписокЗначений = Истина;
			
		КонецЕсли;
#КонецОбласти
		
		Если ОписаниеТипов = Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;
		КонецЕсли;
		
		ВнешнийКод 		= Строка(ТекЭлемент.Получить("xmlId"));
		Наименование	= Строка(ТекЭлемент.Получить("name"));
		
		Если ТекЭлемент.Получить("multiple") = "Y" Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Свойство: " + Наименование + " не будет добавлено/обновлено. т.к. является множественным");
			Продолжить;
		КонецЕсли;
		
#Область ПоискСозданиеСвойства	
		Свойство = Неопределено;
		Свойство = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, 
		?(ЭтоСвойствоПредложения, "ВТ_ИдентификаторыСвойствПредложений", "ВТ_ИдентификаторыСвойствТоваров"), ИдЭлемента);	
		
		Если ЗначениеЗаполнено(ВнешнийКод)И НЕ ЗначениеЗаполнено(Свойство)  Тогда
			Свойство = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"), ВнешнийКод);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Свойство) Тогда
			Свойство = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц,
			?(ЭтоСвойствоПредложения, "ВТ_НаименованияСвойствПредложений", "ВТ_НаименованияСвойствТоваров"), Наименование);	
		КонецЕсли;
		
		ЭтоНовый = Истина;
		
		Если НЕ ЗначениеЗаполнено(Свойство)Тогда
			Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.СоздатьЭлемент();
			
			Если НЕ ЭтоСвойствоПредложения Тогда
				Свойство.НаборСвойств = Справочник_Номенклатура_Общие;
			Иначе
				Свойство.НаборСвойств = Справочник_ХарактеристикиНоменклатуры;
			КонецЕсли;
			
		Иначе
			
			Если (ОбновлятьВ1СТовары И НЕ ЭтоСвойствоПредложения) ИЛИ (ОбновлятьВ1СПредложения И ЭтоСвойствоПредложения)  Тогда
				ЭтоНовый = Ложь;
				Свойство = Свойство.ПолучитьОбъект();
			Иначе
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипОбъектовОбмена, Свойство.Ссылка, ИдЭлемента);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что товары не обновляются. Свойство: " + Наименование + " не будет обновлено");
				Продолжить;  				
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	
		
		Свойство.ПометкаУдаления = НЕ ТекЭлемент.Получить("active") = "Y";	
		
		Если ЭтоНовый Тогда
			Если НЕ ЭтоСвойствоПредложения Тогда
				Свойство.Наименование 	= Наименование + " (Справочник ""Номенклатура"" (Общие))";
			Иначе
				Свойство.Наименование 	= Наименование + " (Справочник ""Характеристики номенклатуры""";
			КонецЕсли;
			
			Свойство.Заголовок 		= Наименование;
			Свойство.ТипЗначения 	= ОписаниеТипов;

		Иначе
			
			Если НЕ СтрНайти(Свойство.Заголовок, "Битрикс24") > 0 Тогда
				Свойство.Заголовок = Наименование;
			КонецЕсли;
			
		КонецЕсли;
		
		Свойство.ЗаполнятьОбязательно 	= ТекЭлемент.Получить("isRequired") = "Y"; 
		
		Попытка
			Свойство.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			Свойство.Записать();
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипОбъектовОбмена, Свойство.Ссылка, ИдЭлемента);
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записано свойство: " + Наименование, Свойство.Ссылка);
			
			Если ЕстьСписокЗначений Тогда
#Область ЗаписьЗначенийСвойств     

				ЗначенияСписка = ПолучитьЗначенияСвойствТоваровПредложенийПоСвойству(СтруктураНастроек, ИдЭлемента);
				Для Каждого ТекЗначениеСвойства Из ЗначенияСписка Цикл
						
					ВнешнийКодЗначенияСвойства = Строка(ТекЗначениеСвойства.Получить("xmlId")); 		
					ИдЗначенияСвойства 			= Формат(ТекЗначениеСвойства.Получить("id"),"ЧГ=0");
					НаименованиеЗнСвойства		= Строка(ТекЗначениеСвойства.Получить("value"));
					
					ЗначениеСвойства = Неопределено;
					ЗначениеСвойства = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц,
					?(ЭтоСвойствоПредложения, "ВТ_ИдентификаторыЗначенийСвойствПредложений", "ВТ_ИдентификаторыЗначенийСвойствТоваров"), ИдЗначенияСвойства);	
					
					Если ЗначениеЗаполнено(ВнешнийКодЗначенияСвойства) И НЕ ЗначениеЗаполнено(ЗначениеСвойства) Тогда
						ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.ЗначенияСвойствОбъектов"), ВнешнийКодЗначенияСвойства);
					КонецЕсли;
					
					Если ЗначениеСвойства = Неопределено Тогда
						ЗначениеСвойства = Справочники.ЗначенияСвойствОбъектов.СоздатьЭлемент();
					Иначе
						ЗначениеСвойства = ЗначениеСвойства.ПолучитьОбъект();
					КонецЕсли;
					
					ЗначениеСвойства.Владелец 			= ?(Свойство.ВладелецДополнительныхЗначений.Пустая(), Свойство.Ссылка,  Свойство.ВладелецДополнительныхЗначений);					
					ЗначениеСвойства.Наименование 		= НаименованиеЗнСвойства;					
					ЗначениеСвойства.ПолноеНаименование = НаименованиеЗнСвойства;	
					
					ЗначениеСвойства.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
					ЗначениеСвойства.Записать();    
					
					ДанныеЗС = Новый Структура;
					ДанныеЗС.Вставить("Объект"				, ЗначениеСвойства.Ссылка);
					ДанныеЗС.Вставить("ПодчиненныйОбъект"	, ИдЭлемента);
					РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипЗначенийОбъектовОбмена, ДанныеЗС, ИдЗначенияСвойства);
					
				КонецЦикла;
					
#КонецОбласти
			КонецЕсли;	
			
			Если ЭтоНовый Тогда
				
				НаборСвойств 	= Свойство.НаборСвойств;
				ТекНаборСвойств = НаборСвойств.ПолучитьОбъект();
				
				НовоеСвойствоНабора = ТекНаборСвойств.ДополнительныеРеквизиты.Добавить();
				НовоеСвойствоНабора.Свойство = Свойство.Ссылка;
				
				ТекНаборСвойств.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
				ТекНаборСвойств.Записать();
				
			КонецЕсли;	
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи свойства товара: " + Наименование + " возникли ошибки.", Свойство.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьЗначенияСвойствТоваровПредложенийПоСвойству(СтруктураНастроек, ИдСвойства)

	Результат = Новый Массив;

	Метод 	= "/rest/catalog.productPropertyEnum.list";	
    Фильтр	= "filter[propertyId]="+ИдСвойства;
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, Метод, Фильтр); 
	Если СтруктураОтвета = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	Если result <> Неопределено Тогда    
		productPropertyEnums = result.Получить("productPropertyEnums");  
		Если productPropertyEnums <> Неопределено Тогда    
			Для каждого ТекЭлемент Из productPropertyEnums Цикл
				Результат.Добавить(ТекЭлемент);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Следующие = СтруктураОтвета.Получить("next"); 
	Пока ЗначениеЗаполнено(Следующие) Цикл
		
		Следующие = Формат(Следующие, "ЧГ=0");
	
		ТелоHTTPЗапроса = Фильтр + "&start=" + Следующие;	
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, Метод, ТелоHTTPЗапроса); 
		
		Если СтруктураОтвета = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		result = СтруктураОтвета.Получить("result");
		Если result <> Неопределено Тогда
			productPropertyEnums = result.Получить("productPropertyEnums");  
			Если productPropertyEnums <> Неопределено Тогда    
				Для каждого ТекЭлемент Из productPropertyEnums Цикл
					Результат.Добавить(ТекЭлемент);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	   Следующие = СтруктураОтвета.Получить("next");
	   
	КонецЦикла;

	Возврат Результат;

КонецФункции

#КонецОбласти 


#Область ТоварыПредложения

Функция ЗагрузитьОбновитьТовары(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
#Область ИдентификаторыСлужебныхСвойств		
	Если СтруктураНастроек.ПредопределенныеСвойства.Свойство("ПредопределенноеСвойствоВидаНоменклатуры") Тогда 
		ИдСвойстваВидаНоменклатуры 	= РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьИдОбъекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара, СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоВидаНоменклатуры); 
	Иначе
		ИдСвойстваВидаНоменклатуры = "null";	
	КонецЕсли;
	СтруктураНастроек.Вставить("ИдПредопределенногоСвойстваВидаНоменклатуры", ИдСвойстваВидаНоменклатуры);
	
	Если СтруктураНастроек.ПредопределенныеСвойства.Свойство("ПредопределенноеСвойствоАртикулНоменклатуры") Тогда
		ИдСвойстваАртикулаНоменклатуры 	= РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьИдОбъекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара, СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоАртикулНоменклатуры); 
	Иначе
		ИдСвойстваАртикулаНоменклатуры = "null";	
	КонецЕсли;
	СтруктураНастроек.Вставить("ИдПредопределенногоСвойстваАртикулаНоменклатуры", ИдСвойстваАртикулаНоменклатуры);
	
#КонецОбласти
	
	ЕстьДеревоГрупп = ЗначениеЗаполнено(СтруктураНастроек.НастройкиСинхронизацииТоваров.ДеревоГрупп);
	Если ЗначениеЗаполнено(СтруктураНастроек.НастройкиСинхронизацииТоваров.ДеревоГрупп) Тогда
		тзнПользовательскогоДереваГрупп = ПолучитьТаблицуТоваровИДереваГрупп(СтруктураНастроек);
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
#Область ФормированиеВременныхТаблицДанных		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанныхТовара"			, СтруктураНастроек.ТипыОбъектовОбмена.Товар);
	Запрос.УстановитьПараметр("ТипДанныхЕдиницИзмерения", СтруктураНастроек.ТипыОбъектовОбмена.ЕдиницаИзмерения);
	Запрос.УстановитьПараметр("ТипДанныхГрупп"			, СтруктураНастроек.ТипыОбъектовОбмена.ГруппаТовара);
	Запрос.УстановитьПараметр("ТипДанныхЗначенийСвойств", СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара);
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыТоваров
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхТовара
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЕдиницИзмерения
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЕдиницИзмерения
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыГрупп
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхГрупп
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийСвойств
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗначенийСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИдентификаторыЗначенийСвойств.Идентификатор КАК Идентификатор,
	|	ВидыНоменклатуры.Ссылка КАК Объект
	|ПОМЕСТИТЬ ВТ_ИдВидовНоменклатуры
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыЗначенийСвойств КАК ВТ_ИдентификаторыЗначенийСвойств
	|			ПО ЗначенияСвойствОбъектов.Ссылка = ВТ_ИдентификаторыЗначенийСвойств.Объект
	|		ПО ВидыНоменклатуры.Наименование = ЗначенияСвойствОбъектов.Наименование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Объект,
	|	Номенклатура.Наименование КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НаименованияТоваров
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ЭтоГруппа = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК Объект,
	|	ХарактеристикиНоменклатуры.Наименование КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НаименованияХарактеристикТоваров
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";	
	Запрос.Выполнить();
#КонецОбласти	
	
	ЗапросПоСвойствамТоваров = Новый Запрос;
	ЗапросПоСвойствамТоваров.УстановитьПараметр("ТипДанных"				, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара);
	ЗапросПоСвойствамТоваров.УстановитьПараметр("Портал"				, СтруктураНастроек.Портал);
	ЗапросПоСвойствамТоваров.УстановитьПараметр("СвойстваИсключения"	, СтруктураНастроек.ПредопределенныеСвойства.НеЗагружаемыеЗначенияСвойств);
	ЗапросПоСвойствамТоваров.УстановитьПараметр("Справочник_Номенклатура", Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Номенклатура"));	
	ЗапросПоСвойствамТоваров.Текст = "ВЫБРАТЬ
	|	ДополнительныеРеквизиты.Ссылка КАК Набор,
	|	ДополнительныеРеквизиты.Свойство КАК Свойство,
	|	ВЫБОР
	|		КОГДА ДополнительныеРеквизиты.Ссылка В ИЕРАРХИИ (&Справочник_Номенклатура)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСвойствоНоменклатуры
	|ПОМЕСТИТЬ ВТ_ВсеНаборы
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДополнительныеСведения.Ссылка,
	|	ДополнительныеСведения.Свойство,
	|	ВЫБОР
	|		КОГДА ДополнительныеСведения.Ссылка В ИЕРАРХИИ (&Справочник_Номенклатура)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ВсеНаборы.Набор) КАК Набор,
	|	ВТ_ВсеНаборы.Свойство КАК Свойство,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	ВТ_ВсеНаборы.ЭтоСвойствоНоменклатуры КАК ЭтоСвойствоНоменклатуры
	|ПОМЕСТИТЬ ВТ_СвойстваНаборы
	|ИЗ
	|	ВТ_ВсеНаборы КАК ВТ_ВсеНаборы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО ВТ_ВсеНаборы.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	НЕ ДополнительныеРеквизитыИСведения.Ссылка В (&СвойстваИсключения)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВсеНаборы.Свойство,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение,
	|	ВТ_ВсеНаборы.ЭтоСвойствоНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ВсеНаборы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СвойстваНаборы.Свойство КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	ВТ_СвойстваНаборы.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	ВТ_СвойстваНаборы.ЭтоСвойствоНоменклатуры КАК ЭтоСвойствоНоменклатуры
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СвойстваНаборы КАК ВТ_СвойстваНаборы
	|		ПО Б24_К_ИдентификаторыОбъектов.Объект = ВТ_СвойстваНаборы.Свойство
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор <> """"";

	
	тзнСвойств = ЗапросПоСвойствамТоваров.Выполнить().Выгрузить();
	
	ОбновлятьВ1СТовары 					= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар", "Номенклатура", "ОбновлятьВ1С");  
	ЗагружатьВ1СТовары					= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар", "Номенклатура", "ЗагружатьВ1С");
	АлгоритмПриЗагрузкеДанныхВ1СТоваров = Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар", "Номенклатура", "АлгоритмПриЗагрузкеДанныхВ1С");
	ПоляТоваров							= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Товар", "Номенклатура");
	ОтборыДляЗагрузкиДанныхВ1СТоваров 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Товар", "Номенклатура");
	
	ЗагруженныеОбъекты = Новый Массив;
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ИдЭлемента 		= Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");
		
		Если ЗагружатьВ1СТовары = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;			
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);
		
		НужноЗагружать = ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, ТекЭлемент, ОтборыДляЗагрузкиДанныхВ1СТоваров, Истина);
		Если НЕ НужноЗагружать Тогда
			Продолжить;	
		КонецЕсли;
		
		ИдВнешнийОбщий	= Строка(ТекЭлемент.Получить("XML_ID"));
		СтруктураИдентификаторовТовара = Б24_КС_ОбщегоНазначенияВызовСервера.ПолучитьДвеСоставныеЧастиИзСтроки(ИдВнешнийОбщий, "#");
		ИдВнешнийНоменклатуры 	= СтруктураИдентификаторовТовара.ПерваяЧасть;
		ИдВнешнийХарактеристики = СтруктураИдентификаторовТовара.ВтораяЧасть;
		
		Наименование		= Строка(ТекЭлемент.Получить("NAME"));
		НаименованиеП		= Строка(ТекЭлемент.Получить("DESCRIPTION"));
		
		Если ИдВнешнийХарактеристики = "" Тогда
			ЕстьХарактеристика 		= Ложь;
		Иначе
			ЕстьХарактеристика 		= Истина;
		КонецЕсли;
		
		Если ЕстьХарактеристика Тогда  
			
			СтруктураИдентификаторовТовара = Б24_КС_ОбщегоНазначенияВызовСервера.ПолучитьДвеСоставныеЧастиИзСтроки(Наименование, " :: ");
			
			НаименованиеНоменклатуры		= СтруктураИдентификаторовТовара.ПерваяЧасть;	
			НаименованиеХарактеристики		= СтруктураИдентификаторовТовара.ВтораяЧасть;	
			
			СтруктураИдентификаторовТовара  = Б24_КС_ОбщегоНазначенияВызовСервера.ПолучитьДвеСоставныеЧастиИзСтроки(НаименованиеП, " :: ");
			
			ПолноеНаименованиеНоменклатуры	= СтруктураИдентификаторовТовара.ПерваяЧасть;	
			ПолноеНаименованиеХарактеристики= СтруктураИдентификаторовТовара.ВтораяЧасть;	
		Иначе
			НаименованиеНоменклатуры		= Наименование;	
			ПолноеНаименованиеНоменклатуры 	= НаименованиеП;	
			
			НаименованиеХарактеристики		= "";	
			ПолноеНаименованиеХарактеристики= "";	
		КонецЕсли;
		
		ДетальнаяКартинка = ТекЭлемент.Получить("DETAIL_PICTURE");
		
		ПомеченНаУдаление = Ложь;
#Область ПоискПометкиНаУдаление
		Если пСтруктураНастроек.ОбрабатыватьУдалениеОбъектовВ1С Тогда 	
			ПомеченНаУдаление = ТекЭлемент.Получить("ACTIVE") = "N" И НЕ ЕстьХарактеристика;
		КонецЕсли;
#КонецОбласти	
		
		
#Область АнализТовара	
		
		ИнформацияОНоменклатуре = Неопределено;	
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Идентификатор"	, ИдЭлемента);
		Запрос.УстановитьПараметр("Портал"			, СтруктураНастроек.Портал);
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_Идентификаторы.Объект КАК Объект,
		|	ВТ_Идентификаторы.ПодчиненныйОбъект КАК ПодчиненныйОбъект
		|ИЗ
		|	ВТ_ИдентификаторыТоваров КАК ВТ_Идентификаторы
		|ГДЕ
		|	ВТ_Идентификаторы.Идентификатор = &Идентификатор";
		
		ВыполненныйЗапрос = Запрос.Выполнить();
		
		Если НЕ ВыполненныйЗапрос.Пустой() Тогда
			Выборка = ВыполненныйЗапрос.Выбрать();
			Пока Выборка.Следующий() Цикл
				ИнформацияОНоменклатуре = Выборка;
				Прервать;
			КонецЦикла;
		КонецЕсли;
		
		Номенклатура 	= ?(ИнформацияОНоменклатуре = Неопределено, Неопределено, ИнформацияОНоменклатуре.Объект);
		Характеристика  = ?(ИнформацияОНоменклатуре = Неопределено, Неопределено, ИнформацияОНоменклатуре.ПодчиненныйОбъект);
		
		Если ЗначениеЗаполнено(ИдВнешнийНоменклатуры) И НЕ ЗначениеЗаполнено(Номенклатура) Тогда
			Номенклатура = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Номенклатура"), ИдВнешнийНоменклатуры);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИдВнешнийХарактеристики) И НЕ ЗначениеЗаполнено(Характеристика) Тогда
			Характеристика = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.ХарактеристикиНоменклатуры"), ИдВнешнийХарактеристики);
		КонецЕсли;
		
		
		Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
			Номенклатура = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_НаименованияТоваров", Лев(НаименованиеНоменклатуры, 100));	
		КонецЕсли;
		
		Если ЕстьХарактеристика И НЕ ЗначениеЗаполнено(Характеристика) Тогда
			Характеристика = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_НаименованияХарактеристикТоваров", Лев(НаименованиеХарактеристики, 150));	
		КонецЕсли;
		
		
		ЭтоНоваяНоменклатура 	= ?(ЗначениеЗаполнено(Номенклатура), Ложь,Истина);
		ЭтоНоваяХарактеристика 	= ?(ЕстьХарактеристика И НЕ ЗначениеЗаполнено(Характеристика), Истина, Ложь);
#КонецОбласти	


#Область ФильтрПоОбновляемымТоварам
		
		Если НЕ ЭтоНоваяНоменклатура Тогда
			
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ИспользованиеХарактеристик") = Истина Тогда
				
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				
				ДанныеДляИД = Новый Структура;
				ДанныеДляИД.Вставить("Объект"				, Номенклатура);
				ДанныеДляИД.Вставить("ПодчиненныйОбъект"	, ?(Характеристика = Неопределено, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка(), Характеристика));
				
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Товар, ДанныеДляИД, ИдЭлемента);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Товары, использующие характеристики, не обновляются Из Битрикс24. Номенклатура: " + Наименование + " будет пропущена.");
				Продолжить; 
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ (ЭтоНоваяНоменклатура ИЛИ ЭтоНоваяХарактеристика) И НЕ ОбновлятьВ1СТовары Тогда
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			ДанныеДляИД = Новый Структура;
			ДанныеДляИД.Вставить("Объект"				, Номенклатура);
			ДанныеДляИД.Вставить("ПодчиненныйОбъект"	, ?(Характеристика = Неопределено, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка(), Характеристика));
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Товар, ДанныеДляИД, ИдЭлемента);
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что товары не обновляются. Номенклатура: " + Наименование + " не будет обновлена.");
			Продолжить; 
		КонецЕсли;
		
#КонецОбласти	
		
		
		ТаблицаСвойствТовара = РазобратьСвойстваТовараВТаблицу(СтруктураНастроек, МенеджерВременныхТаблиц, ТекЭлемент, тзнСвойств);
		
#Область СозданиеОбновлениеНоменклатуры	
		
		Если ЭтоНоваяНоменклатура Тогда
			
			Если ПомеченНаУдаление Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Товар: "+Наименование+" помечен на удаление. Создан  в 1С не будет. ");
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				Продолжить;	
			КонецЕсли;
	
			Номенклатура = Справочники.Номенклатура.СоздатьЭлемент();
		Иначе
			Номенклатура = Номенклатура.ПолучитьОбъект();
		КонецЕсли;

		ВидТовара	= ТекЭлемент.Получить("PROPERTY_" + СтруктураНастроек.ИдПредопределенногоСвойстваВидаНоменклатуры);
		ИдВидаТовара = ?(ВидТовара = Неопределено, Неопределено, ВидТовара.Получить("value"));
		ВидНоменклатуры = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдВидовНоменклатуры", ИдВидаТовара);
		ВидНоменклатуры = ?(ЗначениеЗаполнено(ВидНоменклатуры), ВидНоменклатуры, Номенклатура.ВидНоменклатуры);  

		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПоляОбъекта1С"			, ПоляТоваров);
		ДополнительныеПараметры.Вставить("ИдЭлементаБ24"			, ИдЭлемента);
		ДополнительныеПараметры.Вставить("Объект1С"					, Номенклатура);
		ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
		ДополнительныеПараметры.Вставить("ДанныеЭлементаБ24"		, ТекЭлемент);
		ДополнительныеПараметры.Вставить("ПомеченНаУдаление"		, ПомеченНаУдаление);
		ДополнительныеПараметры.Вставить("ЭтоНовыйОбъект"			, Номенклатура.Ссылка.Пустая() = Истина);
		ДополнительныеПараметры.Вставить("ТаблицаСвойств"			, ТаблицаСвойствТовара);
		
		ДополнительныеПараметры.Вставить("НаименованиеНоменклатуры"			, НаименованиеНоменклатуры);
		ДополнительныеПараметры.Вставить("ПолноеНаименованиеНоменклатуры"	, ПолноеНаименованиеНоменклатуры);
		ДополнительныеПараметры.Вставить("ВидНоменклатуры"			, ВидНоменклатуры);
		ДополнительныеПараметры.Вставить("Валюта"					, Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ВалютаПоУмолчанию());
		
		ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);

		ЗаполнитьДополнительныеРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);

		Если ЗначениеЗаполнено(АлгоритмПриЗагрузкеДанныхВ1СТоваров) Тогда
			ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Номенклатура, ТекЭлемент, АлгоритмПриЗагрузкеДанныхВ1СТоваров);
		КонецЕсли;
		
#КонецОбласти	
		
#Область СозданиеОбновлениеХарактеристикиНоменклатуры	
		
		Если ЕстьХарактеристика Тогда
			Если ЭтоНоваяХарактеристика Тогда
				
				Характеристика = Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();  
				
				Характеристика.Наименование 	 	= НаименованиеХарактеристики;
				Характеристика.НаименованиеПолное	= ПолноеНаименованиеХарактеристики;
				
			Иначе
				Характеристика = Характеристика.ПолучитьОбъект();
			КонецЕсли;
			
			Характеристика.ПометкаУдаления 		= ПомеченНаУдаление;
			
#Область ЗаполнениеДополнительныхРеквизитовХарактеристикНоменклатуры	
			Для Каждого ТекСвойствоТовара Из ТаблицаСвойствТовара Цикл
				
				Если ТекСвойствоТовара.ЭтоСвойствоНоменклатуры Тогда
					Продолжить;
				КонецЕсли;
				
				Если НЕ ТекСвойствоТовара.ЭтоДополнительноеСведение И ТекСвойствоТовара.ПоддерживаетсяСвойство Тогда   					
					Если ТекСвойствоТовара.ЗначениеОчищено Тогда
						Если ЭтоНоваяХарактеристика Тогда
							Продолжить;
						КонецЕсли;
						
						Для Каждого ЗаполнениеСвойствоТовара Из Характеристика.ДополнительныеРеквизиты Цикл
							Если ЗаполнениеСвойствоТовара.Свойство = ТекСвойствоТовара.Свойство Тогда
								ЗаполнениеСвойствоТовара.Значение = Неопределено;
							КонецЕсли;
						КонецЦикла;
					Иначе
						
						ЕстьРеквизит = Ложь;
						Для Каждого ЗаполнениеСвойствоТовара Из Характеристика.ДополнительныеРеквизиты Цикл
							Если ЗаполнениеСвойствоТовара.Свойство = ТекСвойствоТовара.Свойство Тогда
								ЗаполнениеСвойствоТовара.Значение = ТекСвойствоТовара.ЗначениеСвойства;
								ЕстьРеквизит = Истина
							КонецЕсли;
						КонецЦикла;
						
						Если НЕ ЕстьРеквизит Тогда
							НовыйРеквизит = Характеристика.ДополнительныеРеквизиты.Добавить();
							НовыйРеквизит.Свойство 	= ТекСвойствоТовара.Свойство; 
							НовыйРеквизит.Значение 	= ТекСвойствоТовара.ЗначениеСвойства; 
						КонецЕсли;	
						
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
#КонецОбласти	
			
		КонецЕсли;		
#КонецОбласти	


		Попытка
			Номенклатура.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			Номенклатура.Записать();
			Если ЕстьХарактеристика Тогда
				
				Если Номенклатура.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры Тогда
					Характеристика.Владелец = Номенклатура.Ссылка;
				ИначеЕсли Номенклатура.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры Тогда
					Характеристика.Владелец = Номенклатура.ВидНоменклатуры;
				ИначеЕсли Номенклатура.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры Тогда
					Характеристика.Владелец = Номенклатура.ВладелецХарактеристик;
				Иначе
				КонецЕсли;
				Характеристика.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
				Характеристика.Записать();
				
			КонецЕсли;
			
			ДанныеДляИД = Новый Структура;
			ДанныеДляИД.Вставить("Объект", Номенклатура.Ссылка);
			ДанныеДляИД.Вставить("ПодчиненныйОбъект", ?(ЕстьХарактеристика, Характеристика.Ссылка, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка()));
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Товар, ДанныеДляИД, ИдЭлемента);
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записана номенклатуры: " + Наименование, Номенклатура.Ссылка);
			
#Область ЗаполнениеПользовательскогоДереваДляТовара		
			
			Если ЕстьДеревоГрупп Тогда
				
				Родитель = ?(ЗначениеЗаполнено(Родитель), Родитель, СтруктураНастроек.ДеревоГрупп);
				
				Если НЕ ЭтоНоваяНоменклатура Тогда
					
					НайденныеРазделыСТоваром = тзнПользовательскогоДереваГрупп.НайтиСтроки(Новый Структура("Номенклатура", Номенклатура.Ссылка));
					
					Для Каждого НайденныйРаздел Из НайденныеРазделыСТоваром Цикл
						
						РазделОбъект = НайденныйРаздел.Раздел.ПолучитьОбъект(); 
						
						тзнВремТоваровРаздела = РазделОбъект.Товары.Выгрузить();
						
						РазделОбъект.Товары.Очистить();
						
						Для каждого ТоварРаздела Из тзнВремТоваровРаздела Цикл
							
							Если ТоварРаздела.Номенклатура <> Номенклатура.Ссылка Тогда
								НоваяСтрока = РазделОбъект.Товары.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, ТоварРаздела); 
							КонецЕсли;
							
						КонецЦикла;
						РазделОбъект.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
						РазделОбъект.Записать();
						
					КонецЦикла;
					РазделОбъект = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.СоздатьЭлемент();
					РазделОбъект.товары.Выгрузить()
				КонецЕсли;
				
				РодительОбъект = Родитель.ПолучитьОбъект();
				
				НовыйТоварРаздела= РодительОбъект.Товары.Добавить();
				НовыйТоварРаздела.Номенклатура = Номенклатура.Ссылка;
				
				РодительОбъект.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
				РодительОбъект.Записать();	
				
			КонецЕсли;	
#КонецОбласти	
			
#Область ЗаполнениеДополнительногоСведенияНоменклатуры	
			ЗаполнитьДополнительныеСведенияОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);
#КонецОбласти	
			
#Область ЗаполнениеДополнительногоСведенияХарактеристикиНоменклатуры	
			Если ЕстьХарактеристика Тогда
				Для Каждого ТекСвойствоТовара Из ТаблицаСвойствТовара Цикл
					
					Если ТекСвойствоТовара.ЭтоСвойствоНоменклатуры Тогда
						Продолжить;
					КонецЕсли;
					
					Если ТекСвойствоТовара.ЭтоДополнительноеСведение И ТекСвойствоТовара.ПоддерживаетсяСвойство Тогда   					
						Если ТекСвойствоТовара.ЗначениеОчищено Тогда
							Если ЭтоНоваяХарактеристика Тогда
								Продолжить;
							КонецЕсли;
							
							Выборка = РегистрыСведений.ДополнительныеСведения.Выбрать(Новый Структура("Объект", Характеристика.Ссылка));
							Пока Выборка.Следующий() Цикл
								Если Выборка.Свойство = ТекСвойствоТовара.Свойство Тогда
									Выборка.ПолучитьМенеджерЗаписи().Удалить();	
									Прервать;
								КонецЕсли;
							КонецЦикла;
							
						Иначе
							
							НовоеСведение = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
							НовоеСведение.Объект 	= Характеристика.Ссылка; 
							НовоеСведение.Свойство 	= ТекСвойствоТовара.Свойство; 
							НовоеСведение.Значение 	= ТекСвойствоТовара.ЗначениеСвойства; 
							НовоеСведение.Записать();	
							
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;			
#КонецОбласти	
			
#Область СохранениеАдресаКартинки	
			Если СтруктураНастроек.НастройкиСинхронизацииТоваров.ЗагружатьКартинкиИФайлы Тогда
				Если ЗначениеЗаполнено(ДетальнаяКартинка) Тогда
					АдресКартинки = ДетальнаяКартинка.Получить("downloadUrl");
					Если ЗначениеЗаполнено(АдресКартинки) Тогда
						
					КонецЕсли;	
				КонецЕсли;
				
			КонецЕсли;
#КонецОбласти	
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи товара: " + Наименование + " возникли ошибки.", Номенклатура.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.КартинкиИФайлы 				= СтруктураНастроек.КартинкиИФайлы; 		
	пСтруктураНастроек.Логирование.НомерСообщения 	= СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТаблицуТоваровИДереваГрупп(СтруктураНастроек)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Инфоблок", СтруктураНастроек.НастройкиСинхронизацииТоваров.ДеревоГрупп); 
	
	Запрос.Текст = 	"ВЫБРАТЬ
	|	Б24_КС_ПользовательскиеГруппыТоваровТовары.Ссылка КАК Раздел,
	|	Номенклатура1.Ссылка КАК Номенклатура,
	|	Номенклатура1.ЭтоГруппа КАК ЭтоГруппа
	|ПОМЕСТИТЬ ВТ_ПользовательскиеГруппы
	|ИЗ
	|	Справочник.Б24_КС_ПользовательскиеГруппыТоваров.Товары КАК Б24_КС_ПользовательскиеГруппыТоваровТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура1
	|		ПО Б24_КС_ПользовательскиеГруппыТоваровТовары.Номенклатура = Номенклатура1.Ссылка
	|ГДЕ
	|	Б24_КС_ПользовательскиеГруппыТоваровТовары.Ссылка В ИЕРАРХИИ(&Инфоблок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПользовательскиеГруппы.Раздел КАК Раздел,
	|	Номенклатура1.Ссылка КАК Номенклатура,
	|	Номенклатура1.ЭтоГруппа КАК ЭтоГруппа
	|ПОМЕСТИТЬ ВТ_Группы1
	|ИЗ
	|	ВТ_ПользовательскиеГруппы КАК ВТ_ПользовательскиеГруппы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура1
	|		ПО ВТ_ПользовательскиеГруппы.Номенклатура = Номенклатура1.Родитель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Группы1.Раздел КАК Раздел,
	|	Номенклатура1.Ссылка КАК Номенклатура,
	|	Номенклатура1.ЭтоГруппа КАК ЭтоГруппа
	|ПОМЕСТИТЬ ВТ_Группы2
	|ИЗ
	|	ВТ_Группы1 КАК ВТ_Группы1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура1
	|		ПО ВТ_Группы1.Номенклатура = Номенклатура1.Родитель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Группы2.Раздел КАК Раздел,
	|	Номенклатура1.Ссылка КАК Номенклатура,
	|	Номенклатура1.ЭтоГруппа КАК ЭтоГруппа
	|ПОМЕСТИТЬ ВТ_Группы3
	|ИЗ
	|	ВТ_Группы2 КАК ВТ_Группы2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура1
	|		ПО ВТ_Группы2.Номенклатура = Номенклатура1.Родитель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПользовательскиеГруппы.Раздел КАК Раздел,
	|	ВТ_ПользовательскиеГруппы.Номенклатура КАК Номенклатура,
	|	ВТ_ПользовательскиеГруппы.ЭтоГруппа КАК ЭтоГруппа
	|ПОМЕСТИТЬ ВТ_ТоварыДерева
	|ИЗ
	|	ВТ_ПользовательскиеГруппы КАК ВТ_ПользовательскиеГруппы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_Группы3.Раздел,
	|	NULL,
	|	ВТ_Группы3.ЭтоГруппа
	|ИЗ
	|	ВТ_Группы3 КАК ВТ_Группы3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_Группы2.Раздел,
	|	ВТ_Группы2.Номенклатура,
	|	ВТ_Группы2.ЭтоГруппа
	|ИЗ
	|	ВТ_Группы2 КАК ВТ_Группы2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_Группы1.Раздел,
	|	ВТ_Группы1.Номенклатура,
	|	ВТ_Группы1.ЭтоГруппа
	|ИЗ
	|	ВТ_Группы1 КАК ВТ_Группы1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ПользовательскиеГруппы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Группы1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Группы2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Группы3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ТоварыДерева.Номенклатура КАК Номенклатура,
	|	ВТ_ТоварыДерева.Раздел КАК Раздел
	|ИЗ
	|	ВТ_ТоварыДерева КАК ВТ_ТоварыДерева
	|ГДЕ
	|	ВТ_ТоварыДерева.ЭтоГруппа = ЛОЖЬ";
	
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

Функция РазобратьСвойстваТовараВТаблицу(СтруктураНастроек, МенеджерВременныхТаблиц, ИнформацияОТоваре, ТаблицаСвойств)
	
	тзнЗначенийСвойствТовара = Новый ТаблицаЗначений;
	тзнЗначенийСвойствТовара.Колонки.Добавить("Свойство");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЗначениеСвойства");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ПоддерживаетсяСвойство");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЗначениеОчищено");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЭтоДополнительноеСведение");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЭтоСвойствоНоменклатуры");
	
	Для Каждого ТекСвойство Из ТаблицаСвойств Цикл
		
		Свойство = ТекСвойство.Объект;
		
		НовоеЗначениеСвойстваТовара = тзнЗначенийСвойствТовара.Добавить();
		
		НовоеЗначениеСвойстваТовара.Свойство 					= Свойство;
		НовоеЗначениеСвойстваТовара.ЭтоСвойствоНоменклатуры		= ТекСвойство.ЭтоСвойствоНоменклатуры;
		НовоеЗначениеСвойстваТовара.ПоддерживаетсяСвойство 		= Истина;
		НовоеЗначениеСвойстваТовара.ЗначениеОчищено 			= Ложь;
		НовоеЗначениеСвойстваТовара.ЭтоДополнительноеСведение 	= Свойство.ЭтоДополнительноеСведение;
		
		ЗначениеСвойстваСПортала = ИнформацияОТоваре.Получить("PROPERTY_" + ТекСвойство.Идентификатор);
		
		Если ЗначениеСвойстваСПортала <> Неопределено Тогда
			
			ТипЗначенияСвойства = Свойство.ТипЗначения;
			
			Попытка
				
				Если ТипЗначенияСвойства.СодержитТип(Тип("Строка")) Тогда
					
					ЗначениеСвойства = ЗначениеСвойстваСПортала.Получить("value");
					
					Если ТипЗнч(ЗначениеСвойства) = Тип("Соответствие") Тогда
						НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ЗначениеСвойства.Получить("TEXT"));
					Иначе
						НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ЗначениеСвойства);
					КонецЕсли;
					
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Число")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), ЗначениеСвойстваСПортала.Получить("value"));
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Дата")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ЗначениеСвойстваСПортала.Получить("value"));
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("СправочникСсылка.Пользователи")) Тогда
					
					НайденнаяСтрока = СтруктураНастроек.ТаблицаСопоставленияПользователей.Найти(ЗначениеСвойстваСПортала.Получить("value"), "ИдПользователя");	
					
					Если НайденнаяСтрока <> Неопределено Тогда
						НовоеЗначениеСвойстваТовара.ЗначениеСвойства = НайденнаяСтрока.Пользователь1С;
					КонецЕсли;
					
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЗначенийСвойств",  ЗначениеСвойстваСПортала.Получить("value"));
				Иначе
					НовоеЗначениеСвойстваТовара.ПоддерживаетсяСвойство = Ложь;	
				КонецЕсли;
				
			Исключение
				НовоеЗначениеСвойстваТовара.ПоддерживаетсяСвойство = Ложь;	
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось обработать значение свойства: " + Строка(ЗначениеСвойстваСПортала) + " свойства: " + Строка(Свойство));
			КонецПопытки;
		Иначе
			НовоеЗначениеСвойстваТовара.ЗначениеОчищено = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат тзнЗначенийСвойствТовара;
	
КонецФункции


Функция ЗагрузитьОбновитьТоварыПредложения(пСтруктураНастроек, ДополнительныеДанные, ЭтоТовар) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
#Область ИдентификаторыСлужебныхСвойств		
	Если СтруктураНастроек.ПредопределенныеСвойства.Свойство("ПредопределенноеСвойствоВидаНоменклатуры") Тогда 
		ИдСвойстваВидаНоменклатуры 	= РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьИдОбъекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара2, СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоВидаНоменклатуры); 
	Иначе
		ИдСвойстваВидаНоменклатуры = "null";	
	КонецЕсли;
	СтруктураНастроек.Вставить("ИдПредопределенногоСвойстваВидаНоменклатуры", ИдСвойстваВидаНоменклатуры);
	
	Если СтруктураНастроек.ПредопределенныеСвойства.Свойство("ПредопределенноеСвойствоАртикулНоменклатуры") Тогда
		ИдСвойстваАртикулаНоменклатуры 	= РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьИдОбъекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара2, СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоАртикулНоменклатуры); 
	Иначе
		ИдСвойстваАртикулаНоменклатуры = "null";	
	КонецЕсли;
	СтруктураНастроек.Вставить("ИдПредопределенногоСвойстваАртикулаНоменклатуры", ИдСвойстваАртикулаНоменклатуры);
#КонецОбласти

	Если СтруктураНастроек.НастройкиСинхронизацииТоваров.ИсточникИерархии = "Произвольная иерархия" 
		И ЗначениеЗаполнено(СтруктураНастроек.НастройкиСинхронизацииТоваров.ДеревоГрупп) Тогда
		ЕстьДеревоГрупп = Истина;
		тзнПользовательскогоДереваГрупп = ПолучитьТаблицуТоваровИДереваГрупп(СтруктураНастроек);
	Иначе
		ЕстьДеревоГрупп = Ложь;	
	КонецЕсли;   
	
	Справочник_ХарактеристикиНоменклатуры 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_ХарактеристикиНоменклатуры");
	Справочник_Номенклатура 				= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Номенклатура");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
#Область ФормированиеВременныхТаблицДанных		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц; 
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);
	Запрос.УстановитьПараметр("ТипДанныхТоваров"		, СтруктураНастроек.ТипыОбъектовОбмена.Товар2);     
	Запрос.УстановитьПараметр("ТипДанныхПредложений"	, СтруктураНастроек.ТипыОбъектовОбмена.Предложение);
	Запрос.УстановитьПараметр("ТипДанныхЕдиницИзмерения", СтруктураНастроек.ТипыОбъектовОбмена.ЕдиницаИзмерения2);
	Запрос.УстановитьПараметр("ТипДанныхГрупп"			, СтруктураНастроек.ТипыОбъектовОбмена.ГруппаТовара2);
	Запрос.УстановитьПараметр("ТипДанныхЗначенийСвойств", ?(ЭтоТовар, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара2, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваПредложения));
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыТоваров
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхТоваров
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыПредложений
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхПредложений
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЕдиницИзмерения
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЕдиницИзмерения
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыГрупп
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхГрупп
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийСвойств
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗначенийСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИдентификаторыЗначенийСвойств.Идентификатор КАК Идентификатор,
	|	ВидыНоменклатуры.Ссылка КАК Объект
	|ПОМЕСТИТЬ ВТ_ИдВидыНоменклатуры
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыЗначенийСвойств КАК ВТ_ИдентификаторыЗначенийСвойств
	|			ПО ЗначенияСвойствОбъектов.Ссылка = ВТ_ИдентификаторыЗначенийСвойств.Объект
	|		ПО ВидыНоменклатуры.Наименование = ЗначенияСвойствОбъектов.Наименование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Объект,
	|	Номенклатура.Наименование КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НаименованияТоваров
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ЭтоГруппа = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК Объект,
	|	ХарактеристикиНоменклатуры.Наименование КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НаименованияХарактеристикТоваров
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";	
	Запрос.Выполнить();
#КонецОбласти	
	
	ЗапросПоСвойствам = Новый Запрос;
	ЗапросПоСвойствам.УстановитьПараметр("Портал"		, СтруктураНастроек.Портал); 
	ЗапросПоСвойствам.УстановитьПараметр("Исключения"	, СтруктураНастроек.ПредопределенныеСвойства.НеЗагружаемыеЗначенияСвойств);
	ЗапросПоСвойствам.УстановитьПараметр("ТипДанных"	, ?(ЭтоТовар, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара2, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоПредложения));
	ЗапросПоСвойствам.УстановитьПараметр("НаборСвойств"	, ?(ЭтоТовар, Справочник_Номенклатура, Справочник_ХарактеристикиНоменклатуры));
	ЗапросПоСвойствам.Текст = "ВЫБРАТЬ
	|	ДополнительныеРеквизиты.Ссылка КАК Набор,
	|	ДополнительныеРеквизиты.Свойство КАК Свойство,
	|	ВЫБОР
	|		КОГДА ДополнительныеРеквизиты.Ссылка В ИЕРАРХИИ (&НаборСвойств)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСвойствоНоменклатуры
	|ПОМЕСТИТЬ ВТ_ВсеНаборы
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДополнительныеСведения.Ссылка,
	|	ДополнительныеСведения.Свойство,
	|	ВЫБОР
	|		КОГДА ДополнительныеСведения.Ссылка В ИЕРАРХИИ (&НаборСвойств)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ВсеНаборы.Набор) КАК Набор,
	|	ВТ_ВсеНаборы.Свойство КАК Свойство,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	ВТ_ВсеНаборы.ЭтоСвойствоНоменклатуры КАК ЭтоСвойствоНоменклатуры
	|ПОМЕСТИТЬ ВТ_СвойстваНаборы
	|ИЗ
	|	ВТ_ВсеНаборы КАК ВТ_ВсеНаборы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО ВТ_ВсеНаборы.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	НЕ ДополнительныеРеквизитыИСведения.Ссылка В (&Исключения)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВсеНаборы.Свойство,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение,
	|	ВТ_ВсеНаборы.ЭтоСвойствоНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ВсеНаборы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СвойстваНаборы.Свойство КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	ВТ_СвойстваНаборы.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	ВТ_СвойстваНаборы.ЭтоСвойствоНоменклатуры КАК ЭтоСвойствоНоменклатуры
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СвойстваНаборы КАК ВТ_СвойстваНаборы
	|		ПО Б24_К_ИдентификаторыОбъектов.Объект = ВТ_СвойстваНаборы.Свойство
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор <> """"";
	тзнСвойств = ЗапросПоСвойствам.Выполнить().Выгрузить();
	
	Если ЭтоТовар Тогда  
		ОбновлятьВ1С 					= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар2", "Номенклатура", "ОбновлятьВ1С");
		ЗагружатьВ1С					= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар2", "Номенклатура", "ЗагружатьВ1С");
		АлгоритмПриЗагрузкеДанныхВ1С 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Товар2", "Номенклатура", "АлгоритмПриЗагрузкеДанныхВ1С");
		ПоляЭлемента					= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Товар2", "Номенклатура");
		ОтборыДляЗагрузкиДанныхВ1С 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Товар2", "Номенклатура");
	Иначе  
		ОбновлятьВ1С 					= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Предложение", "ХарактеристикиНоменклатуры", "ОбновлятьВ1С");
		ЗагружатьВ1С					= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Предложение", "ХарактеристикиНоменклатуры", "ЗагружатьВ1С");
		АлгоритмПриЗагрузкеДанныхВ1С 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Предложение", "ХарактеристикиНоменклатуры", "АлгоритмПриЗагрузкеДанныхВ1С");
		ПоляЭлемента					= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Предложение", "ХарактеристикиНоменклатуры");
		ОтборыДляЗагрузкиДанныхВ1С 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Предложение", "ХарактеристикиНоменклатуры");
	КонецЕсли;
	
	ЗагруженныеОбъекты = Новый Массив;

	Если ЭтоТовар Тогда
		мДанных = ДополнительныеДанные.Товары;
	Иначе
		мДанных = ДополнительныеДанные.Предложения;
	КонецЕсли; 
	
	Для каждого ЭлементДанных Из мДанных Цикл	
		
		Если ТипЗнч(ЭлементДанных) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;
		
		ТекЭлемент = ЭлементДанных.Получить(?(ЭтоТовар, "product", "offer"));
		
		ИдЭлемента 			= Формат(ТекЭлемент.Получить("id"),"ЧГ=0");    
		Наименование		= СокрЛП(ТекЭлемент.Получить("name"));  
		
		ПомеченНаУдаление 	= Ложь;
#Область ПоискПометкиНаУдаление
		Если пСтруктураНастроек.ОбрабатыватьУдалениеОбъектовВ1С Тогда 	
			ПомеченНаУдаление = ТекЭлемент.Получить("active") = "N";
		КонецЕсли;
#КонецОбласти	

		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);
		
		ИдВнешнийОбщий	= Строка(ТекЭлемент.Получить("xmlId"));
		СтруктураИдентификаторовТовара = Б24_КС_ОбщегоНазначенияВызовСервера.ПолучитьДвеСоставныеЧастиИзСтроки(ИдВнешнийОбщий, "#");
		ИдВнешнийНоменклатуры 	= СтруктураИдентификаторовТовара.ПерваяЧасть;
		ИдВнешнийХарактеристики = СтруктураИдентификаторовТовара.ВтораяЧасть;
		
		
		ВладелецПредложения 	= Неопределено; 
		ИдВладельцаПредложения 	= ""; 
#Область ПоискВладельцаПредложения
		Если НЕ ЭтоТовар Тогда   
			                             
			parentId = ТекЭлемент.Получить("parentId"); 
			Если parentId <> Неопределено Тогда
				ИдВладельцаПредложения = Формат(parentId.Получить("value"), "ЧГ=0");	
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ИдВладельцаПредложения) Тогда
				ВладелецПредложения = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыТоваров", ИдВладельцаПредложения);
			КонецЕсли;  
			
			Если НЕ ЗначениеЗаполнено(ВладелецПредложения) И НЕ ПомеченНаУдаление Тогда  
				ИнформацияОНоменклатуре = СформироватьНоменклатуруХарактеристикуПоИд(СтруктураНастроек, ИдВладельцаПредложения);
				Если ИнформацияОНоменклатуре <> Неопределено Тогда
					ВладелецПредложения = ИнформацияОНоменклатуре.Объект;	
				КонецЕсли;
			КонецЕсли;     
			
			Если НЕ ЗначениеЗаполнено(ВладелецПредложения) Тогда    
				
				Если НЕ ПомеченНаУдаление Тогда  
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось найти товар по ид:"+ИдВладельцаПредложения);
				КонецЕсли;  
				
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));  
				
				Продолжить;	
			КонецЕсли; 
			
			ДанныеВладельцаПредложения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВладелецПредложения, "СтавкаНДС, ЕдиницаИзмерения, ВидНоменклатуры");
			//Если ИдВнешнийХарактеристики = "00000000-0000-0000-0000-000000000000" И ЗначениеЗаполнено(ДанныеВладельцаПредложения.СтавкаНДС)
			//	И ЗначениеЗаполнено(ДанныеВладельцаПредложения.ЕдиницаИзмерения) Тогда     // Это простой товар, выгруженный из 1С
			//	
			//	ДанныеДляИД = Новый Структура;
			//	ДанныеДляИД.Вставить("Объект"			, ВладелецПредложения);
			//	ДанныеДляИД.Вставить("ПодчиненныйОбъект", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
			//	РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Предложение, ДанныеДляИД, ИдЭлемента);   
			//	
			//	Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			//	
			//	Продолжить;
			//	
			//КонецЕсли;	
			
			ЭтоТоварПредложение = Ложь;     
			Если ДополнительныеДанные.Товары <> Неопределено Тогда
				Для каждого ТекВладелец Из ДополнительныеДанные.Товары Цикл
					
					Если ТипЗнч(ТекВладелец) = Тип("КлючИЗначение") Тогда 
						product = ТекВладелец.Значение.Получить("product");  
					Иначе
						product = ТекВладелец.Получить("product");  
					КонецЕсли;          
					
					Если ИдВладельцаПредложения = Формат(product.Получить("id"),"ЧГ=0") Тогда    
						Если Наименование = СокрЛП(product.Получить("name")) Тогда
							
							ДанныеДляИД = Новый Структура;
							ДанныеДляИД.Вставить("Объект"			, ВладелецПредложения);
							ДанныеДляИД.Вставить("ПодчиненныйОбъект", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
							РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Предложение, ДанныеДляИД, ИдЭлемента);   
							
							Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
							
							ЭтоТоварПредложение = Истина;
							
							СтавкаНДСВладельца = Неопределено;
							НайденнаяСтавкаНДС = СтруктураНастроек.ПресетыСтавокНДС.Найти(Строка(ТекЭлемент.Получить("vatId")));
							Если НайденнаяСтавкаНДС <> Неопределено Тогда
								СтавкаНДСВладельца = НайденнаяСтавкаНДС.СтавкаНДС;	
							КонецЕсли;	
							
							ЕдиницаИзмеренияВладельца = Неопределено;
							ИдЕдИзм	= Формат(ТекЭлемент.Получить("measure"),"ЧГ=0");
							ЕдиницаИзмерения = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЕдиницИзмерения", ИдЕдИзм);
							Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
								ЕдиницаИзмеренияВладельца  = ЕдиницаИзмерения;	
							КонецЕсли;				
							
							Если ЗначениеЗаполнено(СтавкаНДСВладельца) ИЛИ ЗначениеЗаполнено(ЕдиницаИзмеренияВладельца) Тогда							
								
								ВладелецПредложенияОбъект = ВладелецПредложения.ПолучитьОбъект();  
								
								Если ЗначениеЗаполнено(СтавкаНДСВладельца) Тогда ВладелецПредложенияОбъект.СтавкаНДС = СтавкаНДСВладельца КонецЕсли; 
								Если ЗначениеЗаполнено(ЕдиницаИзмеренияВладельца) Тогда ВладелецПредложенияОбъект.ЕдиницаИзмерения = ЕдиницаИзмеренияВладельца КонецЕсли; 
								Если ЗначениеЗаполнено(ЕдиницаИзмеренияВладельца) Тогда ВладелецПредложенияОбъект.ЕдиницаДляОтчетов = ЕдиницаИзмеренияВладельца КонецЕсли; 
								
								ВладелецПредложенияОбъект.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);  
								ВладелецПредложенияОбъект.Записать();    
								
							КонецЕсли;
							
							Прервать;
							
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли; 
			
			Если ЭтоТоварПредложение Тогда Продолжить; КонецЕсли;
			
		КонецЕсли;  
#КонецОбласти		
		
		Если ЗагружатьВ1С = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;			
		
		НужноЗагружать = ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, ТекЭлемент, ОтборыДляЗагрузкиДанныхВ1С, Истина);
		Если НЕ НужноЗагружать Тогда
			Продолжить;	
		КонецЕсли;
		
		ДанныеНоменклатуры	= Неопределено;
		ВидНоменклатуры 	= Неопределено;	
		Если ЭтоТовар Тогда   
			ВидТовара	= ТекЭлемент.Получить("property" + СтруктураНастроек.ИдПредопределенногоСвойстваВидаНоменклатуры);
			ИдВидаТовара = ?(ТипЗнч(ВидТовара) <> Тип("Соответствие"), Неопределено, ВидТовара.Получить("value"));
			ВидНоменклатуры = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдВидыНоменклатуры", ИдВидаТовара);
		Иначе       
			ДанныеНоменклатуры 		= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВладелецПредложения, "Наименование, ВидНоменклатуры, ИспользованиеХарактеристик, ВладелецХарактеристик");
			ВидНоменклатуры 		= ДанныеНоменклатуры.ВидНоменклатуры; 	
			Наименование 			= ОбработатьНаименованиеПредложения(Наименование, ДанныеНоменклатуры.Наименование);
		КонецЕсли;
		
		ИдРодителя 	= Формат(ТекЭлемент.Получить("iblockSectionId"),"ЧГ=0");
		Родитель 	= ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыГрупп", ИдРодителя);     
		
		Сущность1С = Неопределено;				
#Область ПоискСущности	

      	Сущность1С = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, ?(ЭтоТовар, "ВТ_ИдентификаторыТоваров", "ВТ_ИдентификаторыПредложений"), ИдЭлемента);
		
		Если НЕ ЗначениеЗаполнено(Сущность1С) Тогда
			Если ЭтоТовар И ЗначениеЗаполнено(ИдВнешнийНоменклатуры) Тогда 
				Сущность1С = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Номенклатура"), ИдВнешнийНоменклатуры);   
			ИначеЕсли НЕ ЭтоТовар И ЗначениеЗаполнено(ИдВнешнийХарактеристики) Тогда 
				Сущность1С = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.ХарактеристикиНоменклатуры"), ИдВнешнийХарактеристики);   
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоТовар И НЕ ЗначениеЗаполнено(Сущность1С) Тогда
			Сущность1С = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, 
			?(ЭтоТовар, "ВТ_НаименованияТоваров", "ВТ_НаименованияХарактеристикТоваров"), Лев(Наименование, 100));
		КонецЕсли;
		
		ЭтоНоваяСущность1С 	=  НЕ ЗначениеЗаполнено(Сущность1С);
#КонецОбласти	


#Область ФильтрПоОбновляемымТоварам
		Если НЕ ЭтоНоваяСущность1С И НЕ ОбновлятьВ1С Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что "+?(ЭтоТовар, "товары", "предложения")
			+" не обновляются. "+?(ЭтоТовар,"Номенклатура","Характеристика")+": " + Наименование + " не будет обновлена.");
			Продолжить; 
		КонецЕсли;
#КонецОбласти	
		
		ТаблицаСвойств = РазобратьСвойстваТовараПредложенияВТаблицу(СтруктураНастроек, МенеджерВременныхТаблиц, ТекЭлемент, тзнСвойств);
		
#Область СозданиеОбновлениеСущности1С
		Если ЭтоНоваяСущность1С Тогда
			
			Если ПомеченНаУдаление Тогда   
				
				Если ЭтоТовар Тогда 
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Товар: "+Наименование
					+" помечен на удаление. Номенклатура в 1С создана не будет.");
				Иначе 
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Предложение: "+Наименование
					+" помечено на удаление. Характеристика в 1С создана не будет.");
				КонецЕсли;
					
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				
				Продолжить;	   
				
			КонецЕсли;
			
			Если ЭтоТовар Тогда 
				Сущность1С = Справочники.Номенклатура.СоздатьЭлемент();  
			Иначе    
				
				Сущность1С = Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();   
				
				Если ДанныеНоменклатуры.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать 
					ИЛИ НЕ ЗначениеЗаполнено(ДанныеНоменклатуры.ИспользованиеХарактеристик) Тогда
					Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
					Продолжить;	   
				ИначеЕсли ДанныеНоменклатуры.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры Тогда
					Сущность1С.Владелец = ВладелецПредложения;  
				ИначеЕсли ДанныеНоменклатуры.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры Тогда
					Сущность1С.Владелец = ДанныеНоменклатуры.ВидНоменклатуры;
				ИначеЕсли ДанныеНоменклатуры.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры Тогда
					Сущность1С.Владелец = ДанныеНоменклатуры.ВладелецХарактеристик;
				Иначе
					Сущность1С.Владелец = ВладелецПредложения;
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			Сущность1С = Сущность1С.ПолучитьОбъект();    
		КонецЕсли;     
		
		Если ЭтоТовар И НЕ ЗначениеЗаполнено(ВидНоменклатуры) Тогда
			ВидНоменклатуры = Сущность1С.ВидНоменклатуры;
		КонецЕсли;

		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПоляОбъекта1С"			, ПоляЭлемента);
		ДополнительныеПараметры.Вставить("ИдЭлементаБ24"			, ИдЭлемента);
		ДополнительныеПараметры.Вставить("Объект1С"					, Сущность1С);
		ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
		ДополнительныеПараметры.Вставить("ДанныеЭлементаБ24"		, ТекЭлемент);
		ДополнительныеПараметры.Вставить("ПомеченНаУдаление"		, ПомеченНаУдаление);
		ДополнительныеПараметры.Вставить("ЭтоНовыйОбъект"			, ЭтоНоваяСущность1С);
		ДополнительныеПараметры.Вставить("ТаблицаСвойств"			, ТаблицаСвойств);
		ДополнительныеПараметры.Вставить("Наименование"				, Наименование);
		ДополнительныеПараметры.Вставить("ВидНоменклатуры"			, ВидНоменклатуры);
		ДополнительныеПараметры.Вставить("Валюта"					, Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ВалютаПоУмолчанию());
		ДополнительныеПараметры.Вставить("Родитель"					, Родитель);               
		
		ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);

		ЗаполнитьДополнительныеРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);

		Если ЗначениеЗаполнено(АлгоритмПриЗагрузкеДанныхВ1С) Тогда
			ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Сущность1С, ТекЭлемент, АлгоритмПриЗагрузкеДанныхВ1С);
		КонецЕсли;
		
#КонецОбласти	

		Попытка
			Сущность1С.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			Сущность1С.Записать();
			
			Если ЭтоТовар Тогда 
				
				ДанныеДляИД = Новый Структура;
				ДанныеДляИД.Вставить("Объект"			, Сущность1С.Ссылка);
				ДанныеДляИД.Вставить("ПодчиненныйОбъект", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
				
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Товар2, ДанныеДляИД, ИдЭлемента);   
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записана номенклатура: " + Наименование, Сущность1С.Ссылка);
				
			Иначе
				
				ДанныеДляИД = Новый Структура;
				ДанныеДляИД.Вставить("Объект"			, ВладелецПредложения);
				ДанныеДляИД.Вставить("ПодчиненныйОбъект", Сущность1С.Ссылка);
				
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Предложение, ДанныеДляИД, ИдЭлемента);   
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записана характеристика: " + Наименование, Сущность1С.Ссылка);
				
			КонецЕсли;
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
#Область ЗаполнениеПользовательскогоДереваДляТовара		
			
			Если ЭтоТовар И ЕстьДеревоГрупп Тогда
				
				Родитель = ?(ЗначениеЗаполнено(Родитель), Родитель, СтруктураНастроек.НастройкиСинхронизацииТоваров.ДеревоГрупп);
				
				Если НЕ ЭтоНоваяСущность1С Тогда
					
					НайденныеРазделыСТоваром = тзнПользовательскогоДереваГрупп.НайтиСтроки(Новый Структура("Номенклатура", Сущность1С.Ссылка));
					
					Для Каждого НайденныйРаздел Из НайденныеРазделыСТоваром Цикл
						
						РазделОбъект = НайденныйРаздел.Раздел.ПолучитьОбъект(); 
						
						тзнВремТоваровРаздела = РазделОбъект.Товары.Выгрузить();
						
						РазделОбъект.Товары.Очистить();
						
						Для каждого ТоварРаздела Из тзнВремТоваровРаздела Цикл
							
							Если ТоварРаздела.Номенклатура <> Сущность1С.Ссылка Тогда
								НоваяСтрока = РазделОбъект.Товары.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, ТоварРаздела); 
							КонецЕсли;
							
						КонецЦикла;
						РазделОбъект.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
						РазделОбъект.Записать();
						
					КонецЦикла;
					РазделОбъект = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.СоздатьЭлемент();
					РазделОбъект.товары.Выгрузить()
				КонецЕсли;
				
				РодительОбъект = Родитель.ПолучитьОбъект();
				
				НовыйТоварРаздела= РодительОбъект.Товары.Добавить();
				НовыйТоварРаздела.Номенклатура = Сущность1С.Ссылка;
				
				РодительОбъект.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
				РодительОбъект.Записать();	
				
			КонецЕсли;	
#КонецОбласти	
			
			ЗаполнитьДополнительныеСведенияОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);
				
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи сущности: " + Наименование + " возникли ошибки.", Сущность1С.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.КартинкиИФайлы 				= СтруктураНастроек.КартинкиИФайлы; 		
	пСтруктураНастроек.Логирование.НомерСообщения 	= СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции

Функция РазобратьСвойстваТовараПредложенияВТаблицу(СтруктураНастроек, МенеджерВременныхТаблиц, ИнформацияОТоваре, ТаблицаСвойств)
	
	тзнЗначенийСвойствТовара = Новый ТаблицаЗначений;
	тзнЗначенийСвойствТовара.Колонки.Добавить("Свойство");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЗначениеСвойства");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ПоддерживаетсяСвойство");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЗначениеОчищено");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЭтоДополнительноеСведение");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЭтоСвойствоНоменклатуры");
	
	Для Каждого ТекСвойство Из ТаблицаСвойств Цикл
		
		Свойство = ТекСвойство.Объект;
		
		НовоеЗначениеСвойстваТовара = тзнЗначенийСвойствТовара.Добавить();
		
		НовоеЗначениеСвойстваТовара.Свойство 					= Свойство;
		НовоеЗначениеСвойстваТовара.ЭтоСвойствоНоменклатуры		= ТекСвойство.ЭтоСвойствоНоменклатуры;
		НовоеЗначениеСвойстваТовара.ПоддерживаетсяСвойство 		= Истина;
		НовоеЗначениеСвойстваТовара.ЗначениеОчищено 			= Ложь;
		НовоеЗначениеСвойстваТовара.ЭтоДополнительноеСведение 	= Свойство.ЭтоДополнительноеСведение;
		
		ЗначениеСвойстваСПортала = ИнформацияОТоваре.Получить("property" + ТекСвойство.Идентификатор);
		Если ЗначениеСвойстваСПортала <> Неопределено Тогда
			
			ТипЗначенияСвойства = Свойство.ТипЗначения;
			
			Попытка       
				
				Если ТипЗнч(ЗначениеСвойстваСПортала) = Тип("Соответствие") Тогда
					ЗначениеСвойства = ЗначениеСвойстваСПортала.Получить("value");   
				Иначе  
					ЗначениеСвойства = ЗначениеСвойстваСПортала;	
				КонецЕсли;
		
				Если ТипЗначенияСвойства.СодержитТип(Тип("Строка")) Тогда
					
					Если ТипЗнч(ЗначениеСвойства) = Тип("Соответствие") Тогда
						НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ЗначениеСвойства.Получить("TEXT"));
					Иначе
						НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ЗначениеСвойства);
					КонецЕсли;   
					
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Булево")) Тогда      
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = ЗначениеСвойства = "Y";
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЗначенийСвойств", ЗначениеСвойства);
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Число")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), ЗначениеСвойства);
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Дата")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ЗначениеСвойства);
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("СправочникСсылка.Пользователи")) Тогда
					
					НайденнаяСтрока = СтруктураНастроек.ТаблицаСопоставленияПользователей.Найти(ЗначениеСвойства, "ИдПользователя");	
					Если НайденнаяСтрока <> Неопределено Тогда
						НовоеЗначениеСвойстваТовара.ЗначениеСвойства = НайденнаяСтрока.Пользователь1С;
					КонецЕсли;     
					
				Иначе
					НовоеЗначениеСвойстваТовара.ПоддерживаетсяСвойство = Ложь;	
				КонецЕсли;
				
			Исключение
				НовоеЗначениеСвойстваТовара.ПоддерживаетсяСвойство = Ложь;	
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось обработать значение свойства: " + Строка(ЗначениеСвойства) + " свойства: " + Строка(Свойство));
			КонецПопытки;
		Иначе
			НовоеЗначениеСвойстваТовара.ЗначениеОчищено = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат тзнЗначенийСвойствТовара;
	
КонецФункции

Функция ОбработатьНаименованиеПредложения(ТекстДляОбработки, ТекстИсключение)
	
	Результат = ТекстДляОбработки;
	
	Если Прав(ТекстДляОбработки, 1) = ")" Тогда       
		
		ОбработанныйТекст = СтрЗаменить(ТекстДляОбработки, ТекстИсключение, "");	
		ОбработанныйТекст = СокрЛП(ОбработанныйТекст);
		
		Если Лев(ОбработанныйТекст, 1) = "(" Тогда   
			
			ОбработанныйТекст = Лев(ОбработанныйТекст, СтрДлина(ОбработанныйТекст)-1); 
			ОбработанныйТекст = Прав(ОбработанныйТекст, СтрДлина(ОбработанныйТекст)-1);	
			
			Результат = ОбработанныйТекст; 
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 


#Область Документы

Функция ПолучитьБазовыйМенеджерТаблицДляДокументов(СтруктураНастроек, ПоддержкаТоваров2 = Истина)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
#Область ФормированиеВременныхТаблицДанных		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;  
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);  
	Запрос.УстановитьПараметр("ТипДанныхКомпания"		, СтруктураНастроек.ТипыОбъектовОбмена.Компания);
	Запрос.УстановитьПараметр("ТипДанныхКонтакт"		, СтруктураНастроек.ТипыОбъектовОбмена.Контакт);
	Запрос.УстановитьПараметр("ТипДанныхРеквизит"		, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит);
	Запрос.УстановитьПараметр("ТипДанныхБанкСчет"		, СтруктураНастроек.ТипыОбъектовОбмена.БанковскийСчетРеквизита);
	//Запрос.УстановитьПараметр("ЭтоТовар2"				, СтруктураНастроек.ВерсияТоваров = "v.2" И ПоддержкаТоваров2);
	Запрос.УстановитьПараметр("ЭтоТовар2"				, Истина);
	Запрос.УстановитьПараметр("ТипДанныхТовар"			, СтруктураНастроек.ТипыОбъектовОбмена.Товар);
	Запрос.УстановитьПараметр("ТипДанныхТовар2"			, СтруктураНастроек.ТипыОбъектовОбмена.Товар2);
	Запрос.УстановитьПараметр("ТипДанныхПредложение"	, СтруктураНастроек.ТипыОбъектовОбмена.Предложение);	
	Запрос.УстановитьПараметр("ТипДанныхЕдИзм"			, СтруктураНастроек.ТипыОбъектовОбмена.ЕдиницаИзмерения);
	Запрос.УстановитьПараметр("ТипДанныхЕдИзм2"			, СтруктураНастроек.ТипыОбъектовОбмена.ЕдиницаИзмерения2);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыКонтактов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКонтакт
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыРеквизитов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхРеквизит
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыКомпаний
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКомпания
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЕдИзм
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	НЕ &ЭтоТовар2
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЕдИзм
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	&ЭтоТовар2
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЕдИзм2
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект
	|ПОМЕСТИТЬ ВТ_ИдентификаторыТоваров
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	НЕ &ЭтоТовар2
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхТовар
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	&ЭтоТовар2
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхТовар2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	&ЭтоТовар2
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхПредложение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыБанкСчетов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхБанкСчет
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	Запрос.Выполнить();
#КонецОбласти	
	
	Возврат МенеджерВременныхТаблиц;

КонецФункции


Функция ЗагрузитьОбновитьСчета(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
	
	Возврат Результат;
	
КонецФункции

Функция ЗагрузитьОбновитьСчета2(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
	
	Возврат Результат;
	
КонецФункции


Функция ЗагрузитьОбновитьСделки(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	ПрефиксВнешнихКодовБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();
	
  	ЗагружатьВ1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Проект", "ЗаказКлиента", "ЗагружатьВ1С");
	ОбновлятьВ1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Проект", "ЗаказКлиента", "ОбновлятьВ1С");
	АлгоритмПриЗагрузке 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Проект", "ЗаказКлиента", "АлгоритмПриЗагрузкеДанныхВ1С");
	РежимЗаписи 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Проект", "ЗаказКлиента", "РежимЗаписи");
	
	ПоляОбъекта1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Проект", "ЗаказКлиента");
	ОтборыДляЗагрузки 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Проект", "ЗаказКлиента");
	
	ПоляТЧОбъекта1С 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностейТЧ(СтруктураНастроек.НастройкаПодключения, "Проект", "ЗаказКлиента");

	ЗагруженныеОбъекты = Новый Массив;
	
	Для каждого ТекЭлемент Из мДанных.ИнформацияОСделках Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли; 
		
		
		//CATEGORY_ID = ТекЭлемент.Получить("CATEGORY_ID");
		//Если CATEGORY_ID = Неопределено Тогда
		//	Продолжить;
		//КонецЕсли;
		//
		//Если НЕ CATEGORY_ID = "2" Тогда
		//	Продолжить;
		//КонецЕсли;
		//
		ЕстьКритическиеОшибки 					= Ложь;
		ПринудительноРегистрироватьДокумент 	= Ложь;
		
		ИдБитрикс24		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("ID"),"ЧГ=0"));
		
		ТемаСделки     	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТекЭлемент.Получить("TITLE"));
		ПрефиксСделки 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТекЭлемент.Получить("UF_CRM_1718639017074"));
		
		ДатаНачала     	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"),   ТекЭлемент.Получить("DATE_CREATE"));
		ИдКомпании    	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("COMPANY_ID"),"ЧГ=0"));
		ИдКонтакта     	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("CONTACT_ID"),"ЧГ=0"));
		КодВалюты 	   	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьКодПоВалюте(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("CURRENCY_ID"),"ЧГ=0")));
		
		ИдСтатуса      	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"),   ТекЭлемент.Получить("STAGE_ID"));
		ИдНаправления  	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"),   ТекЭлемент.Получить("CATEGORY_ID"));
		ИдНаправления	= ?(ИдНаправления = "0", "", ИдНаправления);
		Закрыт         	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТекЭлемент.Получить("CLOSED")) = "Y";
		
		Если ЗагружатьВ1С = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдБитрикс24, Истина)));
			Продолжить;	
		КонецЕсли;	
		
		Если ЗагруженныеОбъекты.Найти(ИдБитрикс24) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдБитрикс24, Истина)));
			Продолжить;	
		КонецЕсли;
			ИнформацияОСтатусах = Неопределено;
#КонецОбласти	
		
#Область ПоискСозданиеСделки	
		ВнешнийКод    	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТекЭлемент.Получить("ORIGIN_ID"));
		
		Сделка = СсылкаПоИдБ24(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Проект, ПрефиксВнешнихКодовБитрикс24.Проекты + ИдБитрикс24);
		
		Если ЗначениеЗаполнено(Сделка) Тогда 
			Если НЕ (Сделка.Наименование = ТемаСделки И Сделка.Префикс = ПрефиксСделки) Тогда
				
				Сделка = Сделка.ПолучитьОбъект();
				Сделка.Наименование = ТемаСделки;
				Сделка.Префикс = ПрефиксСделки;
				Сделка.Записать();
				
				
			КонецЕсли;
			
			Продолжить;
			//ЭтоНоваяСделка = Ложь;
		Иначе 
			Сделка = Справочники.Проекты.СоздатьЭлемент();
			ЭтоНоваяСделка = Истина;
			Сделка.Наименование = ТемаСделки;  
			Сделка.Префикс 		= ПрефиксСделки;
			Сделка.Записать();
		КонецЕсли;
#КонецОбласти	

	Если ЗначениеЗаполнено(Сделка.Ссылка) Тогда
			
				МенеджерЗаписей = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
				МенеджерЗаписей.ТипДанных 					= СтруктураНастроек.ТипыОбъектовОбмена.Проект;
				МенеджерЗаписей.Портал						= СтруктураНастроек.Портал;
				МенеджерЗаписей.Объект 						= Сделка.Ссылка;
				МенеджерЗаписей.Идентификатор 				= "S_" + ИдБитрикс24; 
				МенеджерЗаписей.ДополнительныйИдентификатор = Строка(ВнешнийКод); 
				
				МенеджерЗаписей.Записать();
			//РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Сделка, Сделка.Ссылка, ИдБитрикс24,ВнешнийКод);
			
		КонецЕсли;
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции


Функция ЗагрузитьПодчиненныеОтгрузки(СтруктураНастроек, ОснованиеОбъект, ИдЗаказа, Отгрузки, Отменен)
	
	РезультатЕстьКритическиеОшибки = Ложь;
	
	Если Отгрузки.Количество() = 0 Тогда
		Возврат РезультатЕстьКритическиеОшибки;
	КонецЕсли;  
	
	ОснованиеСсылка = ОснованиеОбъект.Ссылка;   
   	ОбъектРасчетов	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОснованиеСсылка, "ОбъектРасчетов");      
	
	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Загрузка доставок для документа:" + Строка(ОснованиеСсылка));
	
	МенеджерВременныхТаблиц = ПолучитьБазовыйМенеджерТаблицДляДокументов(СтруктураНастроек);
	
	ЗагруженныеОбъекты = Новый Массив;
	Для каждого ТекЭлемент Из Отгрузки Цикл	
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ИдБитрикс24				= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("id"),"ЧГ=0"));
		ВнешнийИдентификатор	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТекЭлемент.Получить("xmlId"));
		Отгружен	 			= ТекЭлемент.Получить("deducted") = "Y";   
		Отменен	 				= ТекЭлемент.Получить("canceled") = "Y";   
		ДоставкаРазрешена		= ТекЭлемент.Получить("allowDelivery") = "Y";
		ИдСтатуса				= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("statusId"),"ЧГ=0"));
		ИдДоставки				= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("deliveryId"),"ЧГ=0"));
		ТрекНомер 				= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("trackingNumber"),"ЧГ=0"));
		
		СуммаДоставки 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ТекЭлемент.Получить("priceDelivery"),"ЧРД=.; ЧГ=0"));
		
		ЭлементыКорзины = Неопределено;        
#Область ПолучениеКорзины		
		ЭлементыКорзины = ТекЭлемент.Получить("shipmentItems");  
		Если ЭлементыКорзины = Неопределено Тогда
			ТелоHTTPЗапроса = "&filter[id]=" + ИдБитрикс24;	
			СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/sale.shipmentitem.list", "&filter[orderDeliveryId]=" + ИдБитрикс24); 
			Если СтруктураОтвета <> Неопределено Тогда
				result = СтруктураОтвета.Получить("result");
				Если result <> Неопределено Тогда
					shipmentItems = result.Получить("shipmentItems");
					Если shipmentItems <> Неопределено Тогда
						ЭлементыКорзины = shipmentItems; 
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		ЭлементыКорзины = ?(ЭлементыКорзины = Неопределено, Новый Массив, ЭлементыКорзины);
#КонецОбласти	
		
		ДатаНачала     			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"),   ТекЭлемент.Получить("dateInsert"));
		Если Не ЗначениеЗаполнено(ДатаНачала) Тогда ДатаНачала = ТекущаяДатаСеанса() КонецЕсли;
		
		ДатаОтгрузки 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"),   ТекЭлемент.Получить("deliveryDocDate"));
		Если Не ЗначениеЗаполнено(ДатаОтгрузки) Тогда ДатаОтгрузки = ДатаНачала + 7*24*60*60 КонецЕсли;
		
		ЗагружатьВ1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Отгрузка", "РеализацияТоваровУслуг", "ЗагружатьВ1С");
		ОбновлятьВ1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Отгрузка", "РеализацияТоваровУслуг", "ОбновлятьВ1С");
		АлгоритмПриЗагрузке 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Отгрузка", "РеализацияТоваровУслуг", "АлгоритмПриЗагрузкеДанныхВ1С");
		РежимЗаписи 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Отгрузка", "РеализацияТоваровУслуг", "РежимЗаписи");
		ПоляОбъекта1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Отгрузка", "РеализацияТоваровУслуг");
		ОтборыДляЗагрузки 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Отгрузка", "РеализацияТоваровУслуг");
		ПоляТЧОбъекта1С 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностейТЧ(СтруктураНастроек.НастройкаПодключения, "Отгрузка", "РеализацияТоваровУслуг");
		ЗагружатьВ1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Отгрузка", "РеализацияТоваровУслуг", "ЗагружатьВ1С");
		
#Область ФильтрацияПоЗагрузке
		
		Если ЗагруженныеОбъекты.Найти(ИдБитрикс24) <> Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдБитрикс24);
		
		Если ЗагружатьВ1С = Ложь Тогда
			Продолжить;	
		КонецЕсли;
		
		НужноЗагружать = ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, ТекЭлемент, ОтборыДляЗагрузки);
		Если НЕ НужноЗагружать Тогда
			Продолжить;	
		КонецЕсли;
		
		Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.Свойство("ИнформацияОСтатусах") Тогда	
			ИнформацияОСтатусах = СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИнформацияОСтатусах;
			Если ИнформацияОСтатусах.ИсточникСтатусовОтгрузок = "СвойствоОтгрузок" Тогда
				тзнСтатусов = ИнформацияОСтатусах.СоответствияСЗначениямиСвойствОтгрузки;	
			Иначе	
				тзнСтатусов = ИнформацияОСтатусах.СоответствияССтатусамиОтгрузки;
			КонецЕсли;
		Иначе  
			
			тзнСтатусов = Новый ТаблицаЗначений;
			тзнСтатусов.Колонки.Добавить("ИдСтатуса");
			тзнСтатусов.Колонки.Добавить("Статус");
			тзнСтатусов.Колонки.Добавить("НеЗагружать");

			ИнформацияОСтатусах = Неопределено;
		КонецЕсли;                                                        
		
		Если ИнформацияОСтатусах <> Неопределено И ТипЗнч(тзнСтатусов) = Тип("ТаблицаЗначений") Тогда
			
			ПропускаемЗаказИзЗаСтатуса = Ложь;
			
			НайденнаяСтрока = тзнСтатусов.Найти(ИдСтатуса, "ИдСтатуса");
			Если НайденнаяСтрока <> Неопределено Тогда
				ПропускаемЗаказИзЗаСтатуса = НайденнаяСтрока.НеЗагружать;	
			КонецЕсли;				
			
			Если ПропускаемЗаказИзЗаСтатуса Тогда			
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, " Отгрузка с ид:  " + Строка(ИдБитрикс24) + " будет пропущена т.к.  у нее неподходящий статус");
				Продолжить;  			
			КонецЕсли;					
		КонецЕсли;
#КонецОбласти	
		
		ЭтоНоваяОтгрузка = Ложь;
#Область ПоискСозданиеОтгрузки		
		
        Отгрузка = СсылкаПоИдБ24(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка, ИдБитрикс24);
		Если ЗначениеЗаполнено(ВнешнийИдентификатор) И НЕ ЗначениеЗаполнено(Отгрузка) Тогда
			Отгрузка = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ДокументСсылка.РеализацияТоваровУслуг"), ВнешнийИдентификатор);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Отгрузка) Тогда 
			
			Если Отменен Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, " Отгрузка с ид:  " + Строка(ИдБитрикс24) + " будет пропущен т.к.  его в 1С нет и она отменена.");
				Продолжить;  			
			КонецЕсли;					
			
			Отгрузка = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			ЭтоНоваяОтгрузка = Истина;
		Иначе
			
			Если ОбновлятьВ1С Тогда
				Отгрузка = Отгрузка.ПолучитьОбъект();
			Иначе
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что заказы и подчиненные данные не обновляются. Отгрузка: " + Строка(Отгрузка) + " не будет обновлена.");
				Продолжить;  				
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	
	
		ИнформацияОДоставках = Неопределено;
		Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.Свойство("ИнформацияОДоставках") Тогда	
			ИнформацияОДоставках = СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИнформацияОДоставках;
		КонецЕсли;   
	
		ПеревозчикПартнер = Справочники.Партнеры.ПустаяСсылка();
#Область ПоискАдресаДоставки	
		Если ИнформацияОДоставках <>Неопределено Тогда
			Если ИнформацияОДоставках.ИсточникСлужбДоставкиОтгрузок = "СпособыДоставкиОтгрузок" Тогда
				НайденнаяСтрока = ИнформацияОДоставках.СоответствияСпособовДоставкиОтгрузки.Найти(ИдДоставки, "ИдСлужбы");
				Если НайденнаяСтрока <> Неопределено Тогда
					ПеревозчикПартнер = НайденнаяСтрока.СлужбаДоставки;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	

		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПоляОбъекта1С"			, ПоляОбъекта1С);
		ДополнительныеПараметры.Вставить("ИдЭлементаБ24"			, ИдБитрикс24);
		ДополнительныеПараметры.Вставить("Объект1С"					, Отгрузка);
		ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
		ДополнительныеПараметры.Вставить("ДанныеЭлементаБ24"		, ТекЭлемент);
		ДополнительныеПараметры.Вставить("ПомеченНаУдаление"		, Отменен);
		ДополнительныеПараметры.Вставить("ЭтоНовыйОбъект"			, ЭтоНоваяОтгрузка);  
		ДополнительныеПараметры.Вставить("ИнформацияОДоставках"		, ИнформацияОДоставках);
		ДополнительныеПараметры.Вставить("ТипОснования"				, "Заказ");
		ДополнительныеПараметры.Вставить("Основание"				, ОснованиеСсылка);       
		ДополнительныеПараметры.Вставить("ОбъектРасчетов"			, ОбъектРасчетов);       
		ДополнительныеПараметры.Вставить("Валюта"					, ОснованиеОбъект.Валюта);
		ДополнительныеПараметры.Вставить("Организация"				, ОснованиеОбъект.Организация);   
		ДополнительныеПараметры.Вставить("Партнер"					, ОснованиеОбъект.Партнер);
		ДополнительныеПараметры.Вставить("Контрагент"				, ОснованиеОбъект.Контрагент);
		ДополнительныеПараметры.Вставить("Подразделение"			, ОснованиеОбъект.Подразделение);
		ДополнительныеПараметры.Вставить("ПеревозчикПартнер"		, ПеревозчикПартнер);
		ДополнительныеПараметры.Вставить("ИдДоставки"				, ИдДоставки);		
		
		ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);
		
#Область ДобавлениеДополнительныхРеквизитовДокумента	
		НайденноеСвойствоТЧ = Отгрузка.ДополнительныеРеквизиты.Найти(СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоНомераОтгрузкиБитрикс24, "Свойство");
		Если НайденноеСвойствоТЧ = Неопределено Тогда
			НовыйРеквизит = Отгрузка.ДополнительныеРеквизиты.Добавить();
			НовыйРеквизит.Значение = ИдБитрикс24;
			НовыйРеквизит.Свойство = СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоНомераОтгрузкиБитрикс24;
		Иначе
			НайденноеСвойствоТЧ.Значение = ИдБитрикс24;	
		КонецЕсли;
		
		НайденноеСвойствоТЧ = Отгрузка.ДополнительныеРеквизиты.Найти(СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоДатыОтгрузкиБитрикс24, "Свойство");
		Если НайденноеСвойствоТЧ = Неопределено Тогда
			НовыйРеквизит = Отгрузка.ДополнительныеРеквизиты.Добавить();
			НовыйРеквизит.Значение = ДатаНачала;
			НовыйРеквизит.Свойство = СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоДатыОтгрузкиБитрикс24;
		Иначе
			НайденноеСвойствоТЧ.Значение = ДатаНачала;	
		КонецЕсли;
		
		НайденноеСвойствоТЧ = Отгрузка.ДополнительныеРеквизиты.Найти(СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоТрекНомераОтгрузкиБитрикс24, "Свойство");
		Если НайденноеСвойствоТЧ = Неопределено Тогда
			НовыйРеквизит = Отгрузка.ДополнительныеРеквизиты.Добавить();
			НовыйРеквизит.Значение = ТрекНомер;
			НовыйРеквизит.Свойство = СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоТрекНомераОтгрузкиБитрикс24;
		Иначе
			НайденноеСвойствоТЧ.Значение = ТрекНомер;	
		КонецЕсли;
		
		НайденноеСвойствоТЧ = Отгрузка.ДополнительныеРеквизиты.Найти(СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоДоставкаРазрешенаОтгрузкиБитрикс24, "Свойство");
		Если НайденноеСвойствоТЧ = Неопределено Тогда
			НовыйРеквизит = Отгрузка.ДополнительныеРеквизиты.Добавить();
			НовыйРеквизит.Значение = ДоставкаРазрешена;
			НовыйРеквизит.Свойство = СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоДоставкаРазрешенаОтгрузкиБитрикс24;
		Иначе
			НайденноеСвойствоТЧ.Значение = ДоставкаРазрешена;	
		КонецЕсли;
		
#КонецОбласти		
		
#Область ДобавлениеДополнительногоРеквизитаСтатуса
		Если ИнформацияОСтатусах <> Неопределено И ТипЗнч(тзнСтатусов) = Тип("ТаблицаЗначений") Тогда
			
			Если ИнформацияОСтатусах.ИсточникСтатусовОтгрузок = "СвойствоОтгрузок" Тогда
				Если ЗначениеЗаполнено(ИнформацияОСтатусах.СвойствоОтгрузки) Тогда
					ЗначениеСвойства = Неопределено;
					
					НайденнаяСтрока = тзнСтатусов.Найти(ИдСтатуса, "ИдСтатуса");
					Если НайденнаяСтрока <> Неопределено Тогда
						ЗначениеСвойства = НайденнаяСтрока.Статус;	
					КонецЕсли;
					                                                                     
					НайденноеСвойствоТЧ = Отгрузка.ДополнительныеРеквизиты.Найти(ИнформацияОСтатусах.СвойствоОтгрузки, "Свойство");
					Если НайденноеСвойствоТЧ = Неопределено Тогда
						НовыйРеквизит = Отгрузка.ДополнительныеРеквизиты.Добавить();
						НовыйРеквизит.Значение = ЗначениеСвойства;
						НовыйРеквизит.Свойство = ИнформацияОСтатусах.СвойствоОтгрузки;
					Иначе
						НайденноеСвойствоТЧ.Значение = ЗначениеСвойства;	
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
#КонецОбласти	
		
#Область ДобавлениеДополнительногоРеквизитаДоставки
		ИнформацияОДоставках = Неопределено;
		Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.Свойство("ИнформацияОДоставках") Тогда	
			ИнформацияОДоставках = СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИнформацияОДоставках;
		КонецЕсли;                                                        

		Если ИнформацияОДоставках <>Неопределено Тогда
			
			Если ИнформацияОДоставках.ИсточникСлужбДоставкиОтгрузок = "СвойствоОтгрузок" Тогда
				Если ЗначениеЗаполнено(ИнформацияОДоставках.СвойствоОтгрузки) Тогда
					тзнДоставок = ИнформацияОДоставках.СоответствияСЗначениямиСвойствОтгрузки;
					
					ЗначениеСвойства = Неопределено;
					
					НайденнаяСтрока = тзнДоставок.Найти(ИдДоставки, "ИдСлужбы");
					Если НайденнаяСтрока <> Неопределено Тогда
						ЗначениеСвойства = НайденнаяСтрока.Служба;	
					КонецЕсли;
					                                                                     
					НайденноеСвойствоТЧ = Отгрузка.ДополнительныеРеквизиты.Найти(ИнформацияОДоставках.СвойствоОтгрузки, "Свойство");
					Если НайденноеСвойствоТЧ = Неопределено Тогда
						НовыйРеквизит = Отгрузка.ДополнительныеРеквизиты.Добавить();
						НовыйРеквизит.Значение = ЗначениеСвойства;
						НовыйРеквизит.Свойство = ИнформацияОДоставках.СвойствоОтгрузки;
					Иначе
						НайденноеСвойствоТЧ.Значение = ЗначениеСвойства;	
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
#КонецОбласти	

	ЕстьКритическиеОшибки = Ложь;
	
#Область ЗаполнениеТоваров	
		тзнТоваровАрх = Отгрузка.Товары.Выгрузить();
		Отгрузка.Товары.Очистить();
		
		тзнПозиций = Новый ТаблицаЗначений;
		тзнПозиций.Колонки.Добавить("ИдентификаторПозиции");
		тзнПозиций.Колонки.Добавить("КлючСвязи");

		Для Каждого ТоварОтгрузки Из ЭлементыКорзины Цикл	
			
			ИдПозицииОтгрузки		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТоварОтгрузки.Получить("id"),"ЧГ=0"));
			ИдПозицииЗаказаСтр		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТоварОтгрузки.Получить("basketId"),"ЧГ=0"));
			КлючСвязиОтгрузки 		= Число(Прав(ИдПозицииЗаказаСтр, 5)); 
			
			ПозицияЗаказа = ОснованиеОбъект.Товары.Найти(КлючСвязиОтгрузки, "КлючСвязи");
			
			Если ПозицияЗаказа = Неопределено Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не найдена информация позиции номенклатуры отгрузки по данным заказа. Ключ связи : " + Строка(КлючСвязиОтгрузки) + " . Возможно потерялись ключи связи в заказе Из за удаления позиций товаров в заказе или изменении ключей связи.");
				ЕстьКритическиеОшибки = Истина;
				Продолжить;	
			КонецЕсли;
			
			ПозицияОтгрузки = тзнТоваровАрх.Найти(КлючСвязиОтгрузки, "КлючСвязи");
			
			Если ПозицияЗаказа <> Неопределено Тогда 
				ИнформацияОПозицииТовара = ПозицияЗаказа;	
			Иначе
				Продолжить;
			КонецЕсли;
			
			Номенклатура				= ИнформацияОПозицииТовара.Номенклатура;
			Характеристика     			= ИнформацияОПозицииТовара.Характеристика;
			
			Количество = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ТоварОтгрузки.Получить("quantity"),"ЧРД=.; ЧГ=0"));
			
			НоваяСтрока = Отгрузка.Товары.Добавить();
			
			НайденныеСтроки = тзнТоваровАрх.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Номенклатура, Характеристика));
			Если НайденныеСтроки.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденныеСтроки[0]);      
			КонецЕсли;
			
			ДополнительныеПараметрыТЧ = Новый Структура;
			ДополнительныеПараметрыТЧ.Вставить("ПоляТЧОбъекта1С"			, ПоляТЧОбъекта1С);
			ДополнительныеПараметрыТЧ.Вставить("МетаданныеТЧ"				, Метаданные.Документы.РеализацияТоваровУслуг.ТабличныеЧасти.Товары);
			ДополнительныеПараметрыТЧ.Вставить("Объект1С"					, ОснованиеСсылка);
			ДополнительныеПараметрыТЧ.Вставить("СтрокаТЧ"					, НоваяСтрока);
			ДополнительныеПараметрыТЧ.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
			ДополнительныеПараметрыТЧ.Вставить("ДанныеЭлементаТЧБ24"		, ТоварОтгрузки);
			ДополнительныеПараметрыТЧ.Вставить("ПомеченНаУдаление"			, Отменен);
			ДополнительныеПараметрыТЧ.Вставить("ЭтоНовыйОбъект"				, ЭтоНоваяОтгрузка);
			ДополнительныеПараметрыТЧ.Вставить("Основание"					, ОснованиеСсылка);   
			ДополнительныеПараметрыТЧ.Вставить("ОснованиеОбъект"			, ОснованиеОбъект);
			ДополнительныеПараметрыТЧ.Вставить("ОбъектРасчетов"				, ОбъектРасчетов);          
			ДополнительныеПараметрыТЧ.Вставить("Номенклатура"				, Номенклатура);
			ДополнительныеПараметрыТЧ.Вставить("Характеристика"				, Характеристика);
			ДополнительныеПараметрыТЧ.Вставить("КлючСвязиПозиции"			, КлючСвязиОтгрузки);
			ДополнительныеПараметрыТЧ.Вставить("ИнформацияОПозицииТовара"	, ИнформацияОПозицииТовара);
			ДополнительныеПараметрыТЧ.Вставить("Количество"					, Количество);
			
			ЗаполнитьРеквизитыТЧОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметрыТЧ, "Товары");
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				тзнТоваровАрх.Удалить(НайденныеСтроки[0]);
			КонецЕсли;
			
			СтруктураДействий = Новый Структура;
			
			СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Отгрузка);
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
			СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
			
			ОбработкаТабличнойЧастиВызовСервера.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, Неопределено);			
			НоваяСтрокаПозиций = тзнПозиций.Добавить();
			НоваяСтрокаПозиций.ИдентификаторПозиции 	= ИдПозицииОтгрузки; 
			НоваяСтрокаПозиций.КлючСвязи 				= КлючСвязиОтгрузки;
			
		КонецЦикла;
		
#КонецОбласти	

#Область ЗаполнениеСуммыДоставки	

		Если СуммаДоставки > 0 Тогда
			                            
			НоменклатураДоставки = СтруктураНастроек.НастройкиСинхронизацииЗаказов.НоменклатураДоставка;
			СтавкаНДС 			 = НоменклатураДоставки.СтавкаНДС;
				
			НоваяЗаписьТовара = Отгрузка.Товары.Добавить();
#Область ЗаполнениеСтрокиСтарымиДанными	
			НайденныеСтроки = тзнТоваровАрх.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", НоменклатураДоставки, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка()));
			Если НайденныеСтроки.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(НоваяЗаписьТовара, НайденныеСтроки[0]);
				тзнТоваровАрх.Удалить(НайденныеСтроки[0]);
			КонецЕсли;
#КонецОбласти	
			НоваяЗаписьТовара.КлючСвязи = 9999;
			НоваяЗаписьТовара.КодСтроки = 9999;
			НоваяЗаписьТовара.ЗаказКлиента 		= ОснованиеСсылка;
			НоваяЗаписьТовара.ОбъектРасчетов    = ОснованиеОбъект.ОбъектРасчетов;
			НоваяЗаписьТовара.Номенклатура 		= НоменклатураДоставки;
			
			НоваяЗаписьТовара.Упаковка			= НоменклатураДоставки.ЕдиницаИзмерения;
			НоваяЗаписьТовара.СтавкаНДС  		= НоменклатураДоставки.СтавкаНДС;
				
			НоваяЗаписьТовара.Склад		 		= ?(ЗначениеЗаполнено(НоваяЗаписьТовара.Склад), НоваяЗаписьТовара.Склад, Отгрузка.Склад);
				
			НоваяЗаписьТовара.Количество  			= 1;
			НоваяЗаписьТовара.КоличествоУпаковок  	= 1;
			
			НоваяЗаписьТовара.Цена					= СуммаДоставки;
			
			НоваяЗаписьТовара.ПроцентАвтоматическойСкидки 	= 0;
			НоваяЗаписьТовара.СуммаАвтоматическойСкидки		= 0;
			
			НоваяЗаписьТовара.ПроцентРучнойСкидки 	= 0;
			НоваяЗаписьТовара.ПроцентРучнойСкидки 	= 0;
			
			СтруктураДействий = Новый Структура;
			
			СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Отгрузка);
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
			СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
			
			ОбработкаТабличнойЧастиВызовСервера.ОбработатьСтрокуТЧ(НоваяЗаписьТовара, СтруктураДействий, Неопределено);
			
		КонецЕсли;
			
#КонецОбласти	
		
#Область ЗаполнениеГрафикаОплаты
		Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиОплаты") Тогда
			
			Отгрузка.ЭтапыГрафикаОплаты.Очистить();
			
			Для каждого ТекЭтапыГрафика Из ОснованиеОбъект.ЭтапыГрафикаОплаты Цикл
				НовыйЭтапГрафикаОплаты = Отгрузка.ЭтапыГрафикаОплаты.Добавить();
				НовыйЭтапГрафикаОплаты.Заказ 				= ОснованиеСсылка;
				НовыйЭтапГрафикаОплаты.ОбъектРасчетов 		= ОснованиеОбъект.ОбъектРасчетов;
				НовыйЭтапГрафикаОплаты.ВариантОплаты 		= ТекЭтапыГрафика.ВариантОплаты;
				НовыйЭтапГрафикаОплаты.ДатаПлатежа 			= ТекЭтапыГрафика.ДатаПлатежа; 	
				НовыйЭтапГрафикаОплаты.Сдвиг 				= ТекЭтапыГрафика.Сдвиг;
				НовыйЭтапГрафикаОплаты.ПроцентПлатежа 		= ТекЭтапыГрафика.ПроцентПлатежа;
				НовыйЭтапГрафикаОплаты.ПроцентЗалогаЗаТару 	= ТекЭтапыГрафика.ПроцентЗалогаЗаТару;
				НовыйЭтапГрафикаОплаты.СуммаПлатежа 		= ТекЭтапыГрафика.СуммаПлатежа;
				НовыйЭтапГрафикаОплаты.СуммаВзаиморасчетов 	= ТекЭтапыГрафика.СуммаПлатежа;
			КонецЦикла;
			
		КонецЕсли;
#КонецОбласти	

		Если ЗначениеЗаполнено(АлгоритмПриЗагрузке) Тогда
			ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Отгрузка, ТекЭлемент, АлгоритмПриЗагрузке);
		КонецЕсли;
		
#Область ПроведениеДокумента
		Отгрузка.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);

		Если НЕ Отменен Тогда
			
			Если (РежимЗаписи = "Проводить отгруженные" И Отгружен)
				ИЛИ РежимЗаписи = "Проводить" Тогда
				
				Попытка
					Отгрузка.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Проведен документ: " + Строка(Отгрузка), Отгрузка.Ссылка);
				Исключение
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время проведения документа: " + Строка(Отгрузка) + " возникли ошибки.", Отгрузка.Ссылка);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
					ЕстьКритическиеОшибки = Истина;
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Попытка записи документа: " + Строка(Отгрузка), Отгрузка.Ссылка);
					
					Попытка
						Отгрузка.Записать(РежимЗаписиДокумента.Запись);
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан документ: " + Строка(Отгрузка), Отгрузка.Ссылка);
					Исключение
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи документа: " + Строка(Отгрузка) + " возникли ошибки.", Отгрузка.Ссылка);
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
						СтруктураНастроек.ВыполненоБезОшибок = Ложь;
		
					КонецПопытки;
					
				КонецПопытки;
				
			Иначе
				Попытка
					Если ЭтоНоваяОтгрузка ИЛИ НЕ Отгрузка.Проведен Тогда
						Отгрузка.Записать(РежимЗаписиДокумента.Запись); 
					Иначе
						Отгрузка.Записать(РежимЗаписиДокумента.ОтменаПроведения); 
					КонецЕсли;
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан: " + Строка(Отгрузка), Отгрузка.Ссылка);
				Исключение
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи документа: " + Строка(Отгрузка) + " возникли ошибки.", Отгрузка.Ссылка);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					ЕстьКритическиеОшибки 	 = Истина;
				
					СтруктураНастроек.ВыполненоБезОшибок = Ложь;
		
				КонецПопытки;
			КонецЕсли;
		Иначе
			
			Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.КогдаОтменен = "Помечать на удаление" Тогда
				
				Отгрузка.ПометкаУдаления = Истина;
					
				Попытка
					Отгрузка.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Помечен на удаление документ: " + Строка(Отгрузка), Отгрузка.Ссылка);
				Исключение
					
					ЕстьКритическиеОшибки = Истина;
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Ошибка при установке пометки на удаление документа: " + Строка(Отгрузка) + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), Отгрузка.Ссылка);
				КонецПопытки;
				
			ИначеЕсли СтруктураНастроек.НастройкиСинхронизацииЗаказов.КогдаОтменен = "Отменять проведение" Тогда
				
				Попытка
					Отгрузка.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Отменено проведение у документа: " + Строка(Отгрузка), Отгрузка.Ссылка);
				Исключение
					
					ЕстьКритическиеОшибки 	 = Истина;
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Ошибка при отмене проведения документа: " + Строка(Отгрузка) + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), Отгрузка.Ссылка);
				КонецПопытки;
				
				
			Иначе 
				
	     	КонецЕсли;
		КонецЕсли;
		
		РезультатЕстьКритическиеОшибки = ?(ЕстьКритическиеОшибки, ЕстьКритическиеОшибки, РезультатЕстьКритическиеОшибки);
		
#КонецОбласти		

		Если ЗначениеЗаполнено(Отгрузка.Ссылка) Тогда
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка, Отгрузка.Ссылка, ИдБитрикс24, ИдЗаказа);
			
			Для Каждого ТекПозиция Из тзнПозиций Цикл
				
				ДанныеОПозиции = Новый Структура;
				ДанныеОПозиции.Вставить("Объект"			, Отгрузка.Ссылка);
				ДанныеОПозиции.Вставить("ПодчиненныйОбъект"	, ТекПозиция.КлючСвязи);
				
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ТоварыОтгрузки, ДанныеОПозиции, ТекПозиция.ИдентификаторПозиции);

			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;  
	
	Возврат РезультатЕстьКритическиеОшибки;
	
КонецФункции

Функция ЗагрузитьПодчиненныеОплаты(СтруктураНастроек, ОснованиеОбъект, ИдЗаказа, Оплаты, Отменен)
	
	РезультатЕстьКритическиеОшибки = Ложь;
	
	Если Оплаты.Количество() = 0 Тогда
		Возврат РезультатЕстьКритическиеОшибки;
	КонецЕсли;   
	
	ОснованиеСсылка = ОснованиеОбъект.Ссылка;   
   	ОбъектРасчетов	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОснованиеСсылка, "ОбъектРасчетов");     
	
	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Загрузка оплат для документа:" + Строка(ОснованиеСсылка));
	
	ПрефиксВнешнихКодовБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();

	ТипОснования = "Заказ";
	
	ТзнНДС = ОснованиеОбъект.Товары.Выгрузить(); 
	тзнНДС.Свернуть("СтавкаНДС", "СуммаНДС, Сумма");	
	
	МенеджерВременныхТаблиц = ПолучитьБазовыйМенеджерТаблицДляДокументов(СтруктураНастроек);
	
	ЗагруженныеОбъекты = Новый Массив;
	Для каждого ТекЭлемент Из Оплаты Цикл
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ИдБитрикс24				= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("id"),"ЧГ=0"));
		ВнешнийИдентификатор 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТекЭлемент.Получить("xmlId"));
		ПлатежнаяСистема 		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("paySystemId"),"ЧГ=0"));
		Оплачен 				= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТекЭлемент.Получить("paid"))= "Y";
		
		Если ЗагруженныеОбъекты.Найти(ИдБитрикс24) <> Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдБитрикс24);
		
		ВидОплаты = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТекЭлемент.Получить("paySystemIsCash"));  // "Y" - наличные, "A" -эквайринг, "N" -безнал
		Если ВидОплаты = "Y" Тогда
			ИмяОбъекта = "ПриходныйКассовыйОрдер";
		ИначеЕсли ВидОплаты = "N" Тогда
			ИмяОбъекта = "ПоступлениеБезналичныхДенежныхСредств";
		Иначе
			ИмяОбъекта = "ОперацияПоПлатежнойКарте";
		КонецЕсли;

		ОбновлятьВ1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Оплата", ИмяОбъекта, "ОбновлятьВ1С");
		АлгоритмПриЗагрузке 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Оплата", ИмяОбъекта, "АлгоритмПриЗагрузкеДанныхВ1С");
		РежимЗаписи 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Оплата", ИмяОбъекта, "РежимЗаписи");
		ПоляОбъекта1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Оплата", ИмяОбъекта);
		ОтборыДляЗагрузки 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Оплата", ИмяОбъекта);
		ПоляТЧОбъекта1С 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностейТЧ(СтруктураНастроек.НастройкаПодключения, "Оплата", ИмяОбъекта);
		ЗагружатьВ1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Оплата", ИмяОбъекта, "ЗагружатьВ1С");
		
		Если ЗагружатьВ1С = Ложь Тогда
			Продолжить;	
		КонецЕсли;
		
		НужноЗагружать = ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, ТекЭлемент, ОтборыДляЗагрузки);
		Если НЕ НужноЗагружать Тогда
			Продолжить;	
		КонецЕсли;
		
		ИнформацияОПлатежнойСистеме = Неопределено; 

		Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.Свойство("ИнформацияОПлатежныхСистемах") Тогда
			
			Для Каждого ТекПлатежнаяСистема Из СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИнформацияОПлатежныхСистемах.СоответствияПлатежныхСистем Цикл
				
				Если ТекПлатежнаяСистема.ПлатежнаяСистема.НайтиПоЗначению(ПлатежнаяСистема)<> Неопределено Тогда
					ИнформацияОПлатежнойСистеме = ТекПлатежнаяСистема;	
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		ЭтоНоваяОплата = Ложь;
#Область ПоискСозданиеОплаты		
		
		Оплата = СсылкаПоИдБ24(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Оплата, ИдБитрикс24);
		Если ЗначениеЗаполнено(ВнешнийИдентификатор) И НЕ ЗначениеЗаполнено(Оплата) Тогда
			
			Если ВидОплаты = "Y" Тогда
				ВнешнийИдентификатор = Прав(ВнешнийИдентификатор, СтрДлина(ВнешнийИдентификатор)-СтрДлина(ПрефиксВнешнихКодовБитрикс24.ПоступлениеВКассу));	
				Оплата = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"), ВнешнийИдентификатор);
			ИначеЕсли ВидОплаты = "N" Тогда
				ВнешнийИдентификатор = Прав(ВнешнийИдентификатор, СтрДлина(ВнешнийИдентификатор)-СтрДлина(ПрефиксВнешнихКодовБитрикс24.ПоступлениеНаСчет));	
				Оплата = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств"), ВнешнийИдентификатор);
			Иначе
				ВнешнийИдентификатор = Прав(ВнешнийИдентификатор, СтрДлина(ВнешнийИдентификатор)-СтрДлина(ПрефиксВнешнихКодовБитрикс24.ОперацияПоПлатежнымКартам));	
				Оплата = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ДокументСсылка.ОперацияПоПлатежнойКарте"), ВнешнийИдентификатор);
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Оплата) Тогда 
			
			Если ВидОплаты = "Y" Тогда
				Оплата = Документы.ПриходныйКассовыйОрдер.СоздатьДокумент();
			ИначеЕсли ВидОплаты = "N" Тогда
				Оплата = Документы.ПоступлениеБезналичныхДенежныхСредств.СоздатьДокумент();
			Иначе
				Оплата = Документы.ОперацияПоПлатежнойКарте.СоздатьДокумент();
			КонецЕсли;
			
			ЭтоНоваяОплата = Истина;
		Иначе
			
			Если ОбновлятьВ1С Тогда
				Оплата = Оплата.ПолучитьОбъект();
			Иначе
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что оплаты не обновляются. Документ: " + Строка(Оплата) + " не будет обновлен.");
				Продолжить;  				
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	

		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПоляОбъекта1С"			, ПоляОбъекта1С);
		ДополнительныеПараметры.Вставить("ИдЭлементаБ24"			, ИдБитрикс24);
		ДополнительныеПараметры.Вставить("Объект1С"					, Оплата);
		ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
		ДополнительныеПараметры.Вставить("ДанныеЭлементаБ24"		, ТекЭлемент);
		ДополнительныеПараметры.Вставить("ПомеченНаУдаление"		, Отменен);
		ДополнительныеПараметры.Вставить("ЭтоНовыйОбъект"			, ЭтоНоваяОплата);
		ДополнительныеПараметры.Вставить("ИнформацияОПлатежнойСистеме", ИнформацияОПлатежнойСистеме);   
		ДополнительныеПараметры.Вставить("ТипОснования"				, ТипОснования); 
		ДополнительныеПараметры.Вставить("Основание"				, ОснованиеСсылка); 
		ДополнительныеПараметры.Вставить("ОснованиеОбъект"			, ОснованиеОбъект);          
		ДополнительныеПараметры.Вставить("ОбъектРасчетов"			, ОбъектРасчетов);          
		ДополнительныеПараметры.Вставить("Валюта"					, ОснованиеОбъект.Валюта);
		ДополнительныеПараметры.Вставить("Организация"				, ОснованиеОбъект.Организация);
		ДополнительныеПараметры.Вставить("Контрагент"				, ОснованиеОбъект.Контрагент);
		ДополнительныеПараметры.Вставить("Партнер"					, ОснованиеОбъект.Партнер);
		ДополнительныеПараметры.Вставить("Подразделение"			, ОснованиеОбъект.Подразделение);
		
		ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);
		
#Область ЗаполнениеРасшифровки	

		Оплата.РасшифровкаПлатежа.Очистить();
		
		ОплатаВсего = Оплата.СуммаДокумента;

		КоличествоСтрок = ТзнНДС.Количество();
		ИтераторСтрок 	= 0;
		
		Для Каждого ТекНДС Из ТзнНДС Цикл 
			
			ИтераторСтрок = ИтераторСтрок + 1;
			
			Если ОплатаВсего = 0 Тогда
				Продолжить;
			ИначеЕсли ОплатаВсего < ТекНДС.Сумма Тогда
				
				СуммаРасшифровка 	= ОплатаВсего;
				ТекНДС.Сумма 		= ТекНДС.Сумма - ОплатаВсего; 
				ОплатаВсего 		= 0;
				
			Иначе
				
				Если КоличествоСтрок = ИтераторСтрок Тогда
					СуммаРасшифровка 	= ОплатаВсего;
					ОплатаВсего			= 0;
				Иначе
					СуммаРасшифровка 	= ТекНДС.Сумма;
					ОплатаВсего 		= ОплатаВсего - ТекНДС.Сумма; 
				КонецЕсли;
				
				ТекНДС.Сумма = 0;
				
			КонецЕсли;
			
			Если СуммаРасшифровка = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрокаРасшифровки = Оплата.РасшифровкаПлатежа.Добавить();
			
			ДополнительныеПараметрыТЧ = Новый Структура;
			ДополнительныеПараметрыТЧ.Вставить("ПоляТЧОбъекта1С"			, ПоляТЧОбъекта1С);
			ДополнительныеПараметрыТЧ.Вставить("МетаданныеТЧ"				, Метаданные.Документы[ИмяОбъекта].ТабличныеЧасти.РасшифровкаПлатежа);
			ДополнительныеПараметрыТЧ.Вставить("Объект1С"					, Оплата);
			ДополнительныеПараметрыТЧ.Вставить("СтрокаТЧ"					, НоваяСтрокаРасшифровки);
			ДополнительныеПараметрыТЧ.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
			ДополнительныеПараметрыТЧ.Вставить("ДанныеЭлементаТЧБ24"		, ТекНДС);
			ДополнительныеПараметрыТЧ.Вставить("ПомеченНаУдаление"			, Отменен);
			ДополнительныеПараметрыТЧ.Вставить("ЭтоНовыйОбъект"				, ЭтоНоваяОплата);
			ДополнительныеПараметрыТЧ.Вставить("Номенклатура"				, Неопределено);
			ДополнительныеПараметрыТЧ.Вставить("Характеристика"				, Неопределено);
			ДополнительныеПараметрыТЧ.Вставить("ТипОснования"				, ТипОснования); 
			ДополнительныеПараметрыТЧ.Вставить("Основание"					, ОснованиеСсылка);
			ДополнительныеПараметрыТЧ.Вставить("ОснованиеОбъект"			, ОснованиеОбъект); 
			ДополнительныеПараметрыТЧ.Вставить("ОбъектРасчетов"				, ОбъектРасчетов);          
			ДополнительныеПараметрыТЧ.Вставить("СуммаРасшифровка"			, СуммаРасшифровка);
			
			ЗаполнитьРеквизитыТЧОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметрыТЧ, "РасшифровкаПлатежа");
			
		КонецЦикла;
		
#КонецОбласти		

		Если ЗначениеЗаполнено(АлгоритмПриЗагрузке) Тогда
			ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Оплата, ТекЭлемент, АлгоритмПриЗагрузке);
		КонецЕсли;
		
#Область ПроведениеДокумента
		Оплата.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
		
		Если (РежимЗаписи = "Проводить оплаченные" И Оплачен)
			ИЛИ РежимЗаписи = "Проводить" Тогда
			
			Попытка
				Оплата.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Проведен документ: " + Строка(Оплата), Оплата.Ссылка);
			Исключение
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время проведения документа: " + Строка(Оплата) + " возникли ошибки.", Оплата.Ссылка);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Попытка записи документа: " + Строка(Оплата), Оплата.Ссылка);
				
				Попытка
					Оплата.Записать(РежимЗаписиДокумента.Запись);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан документ: " + Строка(Оплата), Оплата.Ссылка);
				Исключение
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи документа: " + Строка(Оплата) + " возникли ошибки.", Оплата.Ссылка);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
					СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
				КонецПопытки;
				
			КонецПопытки;
			
		Иначе
			Попытка
				Если ЭтоНоваяОплата ИЛИ НЕ Оплата.Проведен Тогда
					Оплата.Записать(РежимЗаписиДокумента.Запись); 
				Иначе
					Оплата.Записать(РежимЗаписиДокумента.ОтменаПроведения); 
				КонецЕсли;
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан: " + Строка(Оплата), Оплата.Ссылка);
			Исключение
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи документа: " + Строка(Оплата) + " возникли ошибки.", Оплата.Ссылка);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
				СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
			КонецПопытки;
		КонецЕсли;
		
#КонецОбласти		
		
		Если ЗначениеЗаполнено(Оплата.Ссылка) Тогда
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Оплата, Оплата.Ссылка, ИдБитрикс24, ИдЗаказа);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатЕстьКритическиеОшибки;
	
КонецФункции


Функция ЗагрузитьОбновитьЗаказы(пСтруктураНастроек, мДанных) Экспорт
	
	Результат = Новый Массив; 
		
	ЕстьКритическиеОшибки = Ложь;
		
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	ПрефиксВнешнихКодовБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();
	
	ИнформацияОСтатусах = Неопределено;
	
	тзнСтатусов = Новый ТаблицаЗначений;
	тзнСтатусов.Колонки.Добавить("ИдСтатуса");
	тзнСтатусов.Колонки.Добавить("Статус");
	тзнСтатусов.Колонки.Добавить("НеЗагружать");
	
	Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.Свойство("ИнформацияОСтатусах") Тогда	
		ИнформацияОСтатусах = СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИнформацияОСтатусах;
		Если ИнформацияОСтатусах.ИсточникСтатусовЗаказов = "СостоянияЗаказов" Тогда
			тзнСтатусов = ИнформацияОСтатусах.СоответствияССостояниямиЗаказа;	
		ИначеЕсли ИнформацияОСтатусах.ИсточникСтатусовЗаказов = "СвойствоЗаказов" Тогда
			тзнСтатусов = ИнформацияОСтатусах.СоответствияСЗначениямиСвойствЗаказа;
		Иначе	
			тзнСтатусов = ИнформацияОСтатусах.СоответствияССтатусамиЗаказа;
		КонецЕсли;
	КонецЕсли;     
	
	ИнформацияОДоставках = Неопределено;
	Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.Свойство("ИнформацияОДоставках") Тогда	
		ИнформацияОДоставках = СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИнформацияОДоставках;
	КонецЕсли;                                                        
	
	МенеджерВременныхТаблиц = ПолучитьБазовыйМенеджерТаблицДляДокументов(СтруктураНастроек);
#Область ФормированиеВременныхТаблицДанных		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;    
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);
	Запрос.УстановитьПараметр("ТипДанныхСвойств"		, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоЗаказа);
	Запрос.УстановитьПараметр("ТипДанныхЗначенийСвойств", СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваЗаказа);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГрафикиОплаты.Ссылка КАК ГрафикОплаты,
	|	ГрафикиОплаты.ФормаОплаты КАК ФормаОплаты
	|ПОМЕСТИТЬ ВТ_ГрафикиОплаты
	|ИЗ
	|	Справочник.ГрафикиОплаты КАК ГрафикиОплаты
	|ГДЕ
	|	ГрафикиОплаты.ПометкаУдаления = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФормаОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.ДополнительныйИдентификатор КАК СТРОКА(100)) КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийСвойств
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗначенийСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Объект,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	Б24_К_ИдентификаторыОбъектов.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор
	|ПОМЕСТИТЬ ВТ_Свойства
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО Б24_К_ИдентификаторыОбъектов.Объект = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхСвойств
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор <> """"";
	Запрос.Выполнить();
#КонецОбласти	

	ЗагружатьВ1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Заказ", "ЗаказКлиента", "ЗагружатьВ1С");
	ОбновлятьВ1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Заказ", "ЗаказКлиента", "ОбновлятьВ1С");
	АлгоритмПриЗагрузке 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Заказ", "ЗаказКлиента", "АлгоритмПриЗагрузкеДанныхВ1С");
	РежимЗаписи 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиСущностей(СтруктураНастроек.НастройкаПодключения, "Заказ", "ЗаказКлиента", "РежимЗаписи");
	
	ПоляОбъекта1С 			= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностей(СтруктураНастроек.НастройкаПодключения, "Заказ", "ЗаказКлиента");
	ОтборыДляЗагрузки 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьОтборыДляЗагрузкиДанныхВ1С(СтруктураНастроек.НастройкаПодключения, "Заказ", "ЗаказКлиента");
	
	ПоляТЧОбъекта1С 		= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗагружаемыеПоляСущностейТЧ(СтруктураНастроек.НастройкаПодключения, "Заказ", "ЗаказКлиента");
	
	ЗагруженныеОбъекты = Новый Массив;
	
	Для каждого ТекЭлемент Из мДанных Цикл
		
		Если ТипЗнч(ТекЭлемент) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;  
		
		ЕстьКритическиеОшибки 					= Ложь;
		ПринудительноРегистрироватьДокумент 	= Ложь;
		
#Область ПолучениеЗначенийЗаказа
		ИдБитрикс24				= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("id"),"ЧГ=0"));
		ВнешнийИдентификатор	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("xmlId"),"ЧГ=0"));
		Отменен	 				= ТекЭлемент.Получить("canceled") = "Y";   
		ИдСтатуса				= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекЭлемент.Получить("statusId"),"ЧГ=0"));
		КомментарийПользователя = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТекЭлемент.Получить("userDescription"));
		
		ДатаНачала = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ТекЭлемент.Получить("dateInsert"));
		Если Не ЗначениеЗаполнено(ДатаНачала) Тогда ДатаНачала = ТекущаяДатаСеанса() КонецЕсли;
		
		Оплачен	 	= ТекЭлемент.Получить("payed") = "Y";   
		Отгружен 	= ТекЭлемент.Получить("deducted") = "Y"; 
		Закрыт		= Оплачен И Отгружен;		
		
		СимвольныйКодВалюты	= ТекЭлемент.Получить("currency");
		КодВалюты = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьКодПоВалюте(ВРег(СимвольныйКодВалюты));
		
		ИдКомпании		= "";
		ИдКонтакта		= "";
		
		Клиенты			= ТекЭлемент.Получить("clients");
		Если Клиенты <> Неопределено Тогда
			Для каждого ТекКлиент Из Клиенты Цикл
				
				Если Строка(ТекКлиент.Получить("entityTypeId")) = "3" Тогда
					ИдКонтакта		= ?(ЗначениеЗаполнено(ИдКонтакта), ИдКонтакта, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекКлиент.Получить("entityId"),"ЧГ=0")));
				Иначе
					ИдКомпании		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекКлиент.Получить("entityId"),"ЧГ=0"));
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		ИдРеквизита			= "";
		ИдНашегоРеквизита	= "";
		ИдБанкСчетаРек		= "";
		ИдНашегоБанкСчетаРек= "";
		
		Реквизиты			= ТекЭлемент.Получить("requisiteLink");
		Если Реквизиты <> Неопределено И ТипЗнч(Реквизиты) <> Тип("Массив") Тогда
			
			ИдРеквизита			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты.Получить("requisiteId"),"ЧГ=0"));
			ИдНашегоРеквизита	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты.Получить("mcRequisiteId"),"ЧГ=0"));
			ИдБанкСчетаРек		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты.Получить("bankDetailId"),"ЧГ=0"));
			ИдНашегоБанкСчетаРек= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты.Получить("mcBankDetailId"),"ЧГ=0"));
			
		ИначеЕсли Реквизиты <> Неопределено И ТипЗнч(Реквизиты) = Тип("Массив") Тогда
			Если Реквизиты.Количество() > 0 Тогда
				
				ИдРеквизита			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты[0].Получить("requisiteId"),"ЧГ=0"));
				ИдНашегоРеквизита	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты[0].Получить("mcRequisiteId"),"ЧГ=0"));
				ИдБанкСчетаРек		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты[0].Получить("bankDetailId"),"ЧГ=0"));
				ИдНашегоБанкСчетаРек= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты[0].Получить("mcBankDetailId"),"ЧГ=0"));
			
			КонецЕсли;
		КонецЕсли;                                                                                                                      
					
		Товары = Неопределено;
		Товары = ТекЭлемент.Получить("basketItems");                                                                                             
		Товары = ?(Товары = Неопределено, Новый Массив, Товары);
		                               
		СвойстваЗаказа = ТекЭлемент.Получить("propertyValues");
		СвойстваЗаказа = ?(СвойстваЗаказа = Неопределено, Новый Массив, СвойстваЗаказа);

		Оплаты			= ТекЭлемент.Получить("payments");	
		Оплаты 			= ?(Оплаты = Неопределено, Новый Массив, Оплаты);
		
		Отгрузки		= ТекЭлемент.Получить("shipments");		
		Отгрузки 		= ?(Отгрузки = Неопределено, Новый Массив, Отгрузки);
		
		СуммаДоставки = 0;       
		Для Каждого ТекОтгрузка Из Отгрузки Цикл
			СуммаДоставки = СуммаДоставки + Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ТекОтгрузка.Получить("priceDelivery"),"ЧРД=.; ЧГ=0"));
		КонецЦикла;
		
		ТрекНомер = "";       
		Для Каждого ТекОтгрузка Из Отгрузки Цикл
			ТрекНомер = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекОтгрузка.Получить("trackingNumber"),"ЧГ=0"));
			Прервать	
		КонецЦикла;
		
		ИдДоставки = "";
		Для Каждого ТекОтгрузка Из Отгрузки Цикл
			ИдДоставки = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекОтгрузка.Получить("deliveryId"),"ЧГ=0"));
			Прервать;	
		КонецЦикла;
		
#КонецОбласти
		
#Область ФильтрацияПоЗагрузке
		Если ЗагружатьВ1С = Ложь Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдБитрикс24, Истина)));
			Продолжить;	
		КонецЕсли;	
		
		Если ЗагруженныеОбъекты.Найти(ИдБитрикс24) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдБитрикс24, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдБитрикс24);

		НужноЗагружать = ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, ТекЭлемент, ОтборыДляЗагрузки);
		Если НЕ НужноЗагружать Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдБитрикс24, Истина)));
			Продолжить;	
		КонецЕсли;
		
		Если ДатаНачала < СтруктураНастроек.НастройкиСинхронизацииЗаказов.ДатаНачалаЗагрузкиЗаказов Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдБитрикс24, Истина)));
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, " Заказ с ид:  " + Строка(ИдБитрикс24) + " будет пропущен т.к. дата заказа меньше даты, с которой заказы загружаются в 1С.");
			Продолжить;  			
		КонецЕсли;	
		
		Если ИнформацияОСтатусах <> Неопределено И ТипЗнч(тзнСтатусов) = Тип("ТаблицаЗначений") Тогда
			
			ПропускаемЗаказИзЗаСтатуса = Ложь;
			
			НайденнаяСтрока = тзнСтатусов.Найти(ИдСтатуса, "ИдСтатуса");
			Если НайденнаяСтрока <> Неопределено Тогда
				ПропускаемЗаказИзЗаСтатуса = НайденнаяСтрока.НеЗагружать;	
			КонецЕсли;				
			
			Если ПропускаемЗаказИзЗаСтатуса Тогда			
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдБитрикс24, Истина)));
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, " Заказ с ид:  " + Строка(ИдБитрикс24) + " будет пропущен т.к.  у него не подходящий статус");
				Продолжить;  			
			КонецЕсли;					
		КонецЕсли;
#КонецОбласти
		
		ЭтоНовыйЗаказ = Ложь;
#Область ПоискСозданиеЗаказа		
        Заказ = СсылкаПоИдБ24(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Заказ, ПрефиксВнешнихКодовБитрикс24.Заказы + ИдБитрикс24);
		Если ЗначениеЗаполнено(ВнешнийИдентификатор) И НЕ ЗначениеЗаполнено(Заказ) Тогда
			Заказ = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ДокументСсылка.ЗаказКлиента"), ВнешнийИдентификатор);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Заказ) Тогда 
			
			Если Отменен Тогда
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдБитрикс24, Истина)));
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, " Заказ с ид:  " + Строка(ИдБитрикс24) + " будет пропущен т.к.  его в 1С нет и он отменен.");
				Продолжить;  			
			КонецЕсли;					
			
			Заказ = Документы.ЗаказКлиента.СоздатьДокумент();
			ЭтоНовыйЗаказ = Истина;
		Иначе
			
			Если ОбновлятьВ1С Тогда
				Заказ = Заказ.ПолучитьОбъект();
			Иначе
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдБитрикс24, Истина)));
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что заказы и подчиненные данные не обновляются. Заказ: " + Строка(Заказ) + " не будет обновлен.");
				Продолжить;  				
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	

		Если ЭтоНовыйЗаказ И СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИсточникНомераДокумента <> "По данным с Битрикс24" Тогда
			ПринудительноРегистрироватьДокумент = Истина;
		КонецЕсли;
		
		Если ЭтоНовыйЗаказ Тогда
			Организация = СтруктураНастроек.НастройкиСинхронизацииЗаказов.Соглашение.Организация;
		Иначе
			Организация = ?(ЗначениеЗаполнено(Заказ.Организация), Заказ.Организация, СтруктураНастроек.НастройкиСинхронизацииЗаказов.Соглашение.Организация);	
		КонецЕсли;
		
		ИнформацияОКлиенте 		= ПолучитьИнформациюОКомпанииКонтакте(СтруктураНастроек, МенеджерВременныхТаблиц, ИдКомпании, ИдКонтакта, ИдРеквизита);
		Партнер					= ИнформацияОКлиенте.Партнер;
		Контрагент				= ИнформацияОКлиенте.Контрагент;
		КонтактноеЛицо			= ИнформацияОКлиенте.КонтактноеЛицо;
		ЕстьКритическиеОшибки 	= ИнформацияОКлиенте.ЕстьКритическиеОшибки; 

		Валюта = Справочники.Валюты.НайтиПоКоду(КодВалюты);
		Если НЕ ЗначениеЗаполнено(Валюта) Тогда
			Валюта = Справочники.Валюты.НайтиПоКоду("643");
		КонецЕсли;
		
		БанкСчетКонтрагента = Справочники.БанковскиеСчетаКонтрагентов.ПустаяСсылка();
#Область ПоискБанкСчетаРеквизита	
		Если НЕ(ИдБанкСчетаРек = "" ИЛИ ИдБанкСчетаРек = "0") Тогда
			
			БанкСчетКонтрагента 	= ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыБанкСчетов", ИдБанкСчетаРек);
			
			Если Не ЗначениеЗаполнено(БанкСчетКонтрагента) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найден банковский счет реквизита. Попытка создания банковского счета с Ид: " + ИдБанкСчетаРек);
				БанкСчетКонтрагента 	= СформироватьБанковскийСчетПоИд(СтруктураНастроек, ИдБанкСчетаРек);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(БанкСчетКонтрагента) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не найден банковский счет с Ид: " + ИдБанкСчетаРек);
			КонецЕсли;
			
		КонецЕсли;
#КонецОбласти	


		АдресДоставки 				= "";
		АдресДоставкиЗначенияПолей 	= "";
#Область ПоискАдресаДоставки	
		СвойстваЗаказа = ТекЭлемент.Получить("propertyValues");
		СвойстваЗаказа = ?(СвойстваЗаказа = Неопределено, Новый Массив, СвойстваЗаказа);
		Для Каждого ТекСвойство Из СвойстваЗаказа Цикл
			
			Если ТекСвойство.Получить("name") = "Адрес доставки" Тогда
				АдресДоставки = ТекСвойство.Получить("value");
				Если ЗначениеЗаполнено(АдресДоставки) Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		лВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
		Если НЕ ЗначениеЗаполнено(АдресДоставки) Тогда
			НайденнаяСтрокаАдрес = Контрагент.КонтактнаяИнформация.Найти(лВидКонтактнойИнформации);
			Если НайденнаяСтрокаАдрес <> Неопределено Тогда
				АдресДоставки = НайденнаяСтрокаАдрес.Представление;
			КонецЕсли;
		КонецЕсли;
		
		АдресДоставкиЗначенияПолей = УправлениеКонтактнойИнформациейСлужебныйВызовСервера.КонтактнаяИнформацияПоПредставлению(АдресДоставки, лВидКонтактнойИнформации); 						
#КонецОбласти	

		ПеревозчикПартнер = Справочники.Партнеры.ПустаяСсылка();
#Область ПоискАдресаДоставки	
		Если ИнформацияОДоставках <>Неопределено Тогда
			Если ИнформацияОДоставках.ИсточникСлужбДоставкиЗаказов = "СпособыДоставкиЗаказов" Тогда
				НайденнаяСтрока = ИнформацияОДоставках.СоответствияСпособовДоставкиЗаказа.Найти(ИдДоставки, "ИдСлужбы");
				Если НайденнаяСтрока <> Неопределено Тогда
					ПеревозчикПартнер = НайденнаяСтрока.СлужбаДоставки;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	

		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПоляОбъекта1С"			, ПоляОбъекта1С);
		ДополнительныеПараметры.Вставить("ИдЭлементаБ24"			, ИдБитрикс24);
		ДополнительныеПараметры.Вставить("Объект1С"					, Заказ);
		ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
		ДополнительныеПараметры.Вставить("ДанныеЭлементаБ24"		, ТекЭлемент);
		ДополнительныеПараметры.Вставить("ПомеченНаУдаление"		, Отменен);
		ДополнительныеПараметры.Вставить("ЭтоНовыйОбъект"			, ЭтоНовыйЗаказ);
		ДополнительныеПараметры.Вставить("Валюта"					, Валюта);
		ДополнительныеПараметры.Вставить("Организация"				, Организация);
		ДополнительныеПараметры.Вставить("Соглашение"				, СтруктураНастроек.НастройкиСинхронизацииЗаказов.Соглашение);
		ДополнительныеПараметры.Вставить("Партнер"					, Партнер);
		ДополнительныеПараметры.Вставить("Контрагент"				, Контрагент);
		ДополнительныеПараметры.Вставить("КонтактноеЛицо"			, КонтактноеЛицо);
		ДополнительныеПараметры.Вставить("БанкСчетКонтрагента"		, БанкСчетКонтрагента);
		ДополнительныеПараметры.Вставить("Товары"					, Товары);
		ДополнительныеПараметры.Вставить("ТаблицаСтатусов"			, тзнСтатусов);
		ДополнительныеПараметры.Вставить("ИнформацияОСтатусах"		, ИнформацияОСтатусах);
		ДополнительныеПараметры.Вставить("ИнформацияОДоставках"		, ИнформацияОДоставках);
		
		ДополнительныеПараметры.Вставить("СуммаДоставки"			, СуммаДоставки);
		ДополнительныеПараметры.Вставить("ИдДоставки"				, ИдДоставки);
		ДополнительныеПараметры.Вставить("АдресДоставки"			, АдресДоставки);
		ДополнительныеПараметры.Вставить("АдресДоставкиЗнчПолей"	, АдресДоставкиЗначенияПолей);
		ДополнительныеПараметры.Вставить("ПеревозчикПартнер"		, ПеревозчикПартнер);
		
		ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);

		ТаблицаСвойств = ПолучитьТаблицуСвойствЗаказов(СтруктураНастроек, МенеджерВременныхТаблиц, СвойстваЗаказа);
		ЗаполнениеСвойствЗаказа(СтруктураНастроек, ТаблицаСвойств, Заказ, ЭтоНовыйЗаказ, Ложь);			
		
#Область ДобавлениеДополнительныхРеквизитовДокумента	
		НайденноеСвойствоТЧ = Заказ.ДополнительныеРеквизиты.Найти(СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоТрекНомераЗаказаБитрикс24, "Свойство");
		Если НайденноеСвойствоТЧ = Неопределено Тогда
			НовыйРеквизит = Заказ.ДополнительныеРеквизиты.Добавить();
			НовыйРеквизит.Значение = ТрекНомер;
			НовыйРеквизит.Свойство = СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоТрекНомераЗаказаБитрикс24;
		Иначе
			НайденноеСвойствоТЧ.Значение = ТрекНомер;	
		КонецЕсли;
		
		НайденноеСвойствоТЧ = Заказ.ДополнительныеРеквизиты.Найти(СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоКомментарияЗаказаБитрикс24, "Свойство");
		Если НайденноеСвойствоТЧ = Неопределено Тогда
			НовыйРеквизит = Заказ.ДополнительныеРеквизиты.Добавить();
			НовыйРеквизит.Значение = КомментарийПользователя;
			НовыйРеквизит.Свойство = СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоКомментарияЗаказаБитрикс24;
		Иначе
			НайденноеСвойствоТЧ.Значение = КомментарийПользователя;	
		КонецЕсли;
		
		НайденноеСвойствоТЧ = Заказ.ДополнительныеРеквизиты.Найти(СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоНомераЗаказаБитрикс24, "Свойство");
		Если НайденноеСвойствоТЧ = Неопределено Тогда
			НовыйРеквизит = Заказ.ДополнительныеРеквизиты.Добавить();
			НовыйРеквизит.Значение = ИдБитрикс24;
			НовыйРеквизит.Свойство = СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоНомераЗаказаБитрикс24;
		Иначе
			НайденноеСвойствоТЧ.Значение = ИдБитрикс24;	
		КонецЕсли;
		
		НайденноеСвойствоТЧ = Заказ.ДополнительныеРеквизиты.Найти(СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоДатыЗаказаБитрикс24, "Свойство");
		Если НайденноеСвойствоТЧ = Неопределено Тогда
			НовыйРеквизит = Заказ.ДополнительныеРеквизиты.Добавить();
			НовыйРеквизит.Значение = ДатаНачала;
			НовыйРеквизит.Свойство = СтруктураНастроек.ПредопределенныеСвойства.ПредопределенноеСвойствоДатыЗаказаБитрикс24;
		Иначе
			НайденноеСвойствоТЧ.Значение = ДатаНачала;	
		КонецЕсли;
		
#КонецОбласти		
		
#Область ДобавлениеДополнительногоРеквизитаСтатуса
		Если ИнформацияОСтатусах <> Неопределено И ТипЗнч(тзнСтатусов) = Тип("ТаблицаЗначений") Тогда
			
			Если ИнформацияОСтатусах.ИсточникСтатусовЗаказов = "СвойствоЗаказов" Тогда
				Если ЗначениеЗаполнено(ИнформацияОСтатусах.СвойствоЗаказа) Тогда
					ЗначениеСвойства = Неопределено;
					
					НайденнаяСтрока = тзнСтатусов.Найти(ИдСтатуса, "ИдСтатуса");
					Если НайденнаяСтрока <> Неопределено Тогда
						ЗначениеСвойства = НайденнаяСтрока.Статус;	
					КонецЕсли;
					                                                                     
					НайденноеСвойствоТЧ = Заказ.ДополнительныеРеквизиты.Найти(ИнформацияОСтатусах.СвойствоЗаказа, "Свойство");
					Если НайденноеСвойствоТЧ = Неопределено Тогда
						НовыйРеквизит = Заказ.ДополнительныеРеквизиты.Добавить();
						НовыйРеквизит.Значение = ЗначениеСвойства;
						НовыйРеквизит.Свойство = ИнформацияОСтатусах.СвойствоЗаказа;
					Иначе
						НайденноеСвойствоТЧ.Значение = ЗначениеСвойства;	
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
#КонецОбласти	

#Область ДобавлениеСлужбыДоставки
		Если ИнформацияОДоставках <>Неопределено Тогда
			Если ИнформацияОДоставках.ИсточникСлужбДоставкиЗаказов = "СвойствоЗаказов" Тогда
				Если ЗначениеЗаполнено(ИнформацияОДоставках.СоответствияСЗначениямиСвойствЗаказа) Тогда
					ЗначениеСвойства = Неопределено;
					НайденнаяСтрока = ИнформацияОДоставках.СоответствияСЗначениямиСвойствЗаказа.Найти(ИдДоставки, "ИдСлужбы");
					Если НайденнаяСтрока <> Неопределено Тогда
						ЗначениеСвойства = НайденнаяСтрока.Служба;	
					КонецЕсли;
					
					НайденноеСвойствоТЧ = Заказ.ДополнительныеРеквизиты.Найти(ИнформацияОДоставках.СвойствоЗаказа, "Свойство");
					Если НайденноеСвойствоТЧ = Неопределено Тогда
						НовыйРеквизит = Заказ.ДополнительныеРеквизиты.Добавить();
						НовыйРеквизит.Значение = ЗначениеСвойства;
						НовыйРеквизит.Свойство = ИнформацияОДоставках.СвойствоЗаказа;
					Иначе
						НайденноеСвойствоТЧ.Значение = ЗначениеСвойства;	
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	                                                                                              

		
#Область ЗаполнениеТоваров	
		тзнТоваровАрх = Заказ.Товары.Выгрузить();

		Заказ.Товары.Очистить();	
		
		ИдДоставки = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьИдОбъекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Товар, СтруктураНастроек.НастройкиСинхронизацииЗаказов.НоменклатураДоставка, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка()); 
		
		тзнСвойстваПозиций = Новый ТаблицаЗначений;
		тзнСвойстваПозиций.Колонки.Добавить("ИдентификаторПозиции");
		тзнСвойстваПозиций.Колонки.Добавить("КлючСвязи");
		тзнСвойстваПозиций.Колонки.Добавить("НаименованиеСвойства");
		тзнСвойстваПозиций.Колонки.Добавить("КодСвойства");
		тзнСвойстваПозиций.Колонки.Добавить("ИдентификаторСвойства");
		тзнСвойстваПозиций.Колонки.Добавить("Значение");
		тзнСвойстваПозиций.Колонки.Добавить("Номенклатура");
		тзнСвойстваПозиций.Колонки.Добавить("Характеристика");
		
		Для Каждого ТоварЗаказа Из Товары Цикл
			
			ИдПозиции			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТоварЗаказа.Получить("id"),"ЧГ=0"));
			КлючСвязиПозиции 	= Число(Прав(ИдПозиции, 5)); 
			
			ИдТовара 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТоварЗаказа.Получить("productId"),"ЧГ=0"));
			ВнешнийКодПозиции	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(ТоварЗаказа.Получить("productXmlId")));
			
			НаименованиеТовара	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(ТоварЗаказа.Получить("name")));			
			
			СвойстваПозиции		= ТоварЗаказа.Получить("properties");
			
			Если ИдДоставки = ИдТовара Тогда  //для УНФ храним доставку в реквизите
				Продолжить;
			КонецЕсли;
			
			Номенклатура	= Справочники.Номенклатура.ПустаяСсылка();	
			Характеристика  = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
#Область ПоискТовара	

			Если ЗначениеЗаполнено(ВнешнийКодПозиции) Тогда
				
				СтруктураИдентификаторовТовара = Б24_КС_ОбщегоНазначенияВызовСервера.ПолучитьДвеСоставныеЧастиИзСтроки(ВнешнийКодПозиции, "#");
				ИдВнешнийНоменклатуры 	= СтруктураИдентификаторовТовара.ПерваяЧасть;
				ИдВнешнийХарактеристики = СтруктураИдентификаторовТовара.ВтораяЧасть;
						
				Если ЗначениеЗаполнено(ИдВнешнийНоменклатуры) Тогда
					Номенклатура = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Номенклатура"), ИдВнешнийНоменклатуры);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ИдВнешнийХарактеристики) Тогда
					Характеристика = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.ХарактеристикиНоменклатуры"), ИдВнешнийХарактеристики);
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ИдТовара) И НЕ ЗначениеЗаполнено(Номенклатура) Тогда
				
				ИнформацияОНоменклатуре = Неопределено;	
				
				Запрос = Новый Запрос;
				Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
				Запрос.УстановитьПараметр("Идентификатор", ИдТовара);
				
				Запрос.Текст = "ВЫБРАТЬ
				|	ВТ_Идентификаторы.Объект КАК Объект,
				|	ВТ_Идентификаторы.ПодчиненныйОбъект КАК ПодчиненныйОбъект
				|ИЗ
				|	ВТ_ИдентификаторыТоваров КАК ВТ_Идентификаторы
				|ГДЕ
				|	ВТ_Идентификаторы.Идентификатор = &Идентификатор";
				
				ВыполненныйЗапрос = Запрос.Выполнить();
				
				Если НЕ ВыполненныйЗапрос.Пустой() Тогда
					Выборка = ВыполненныйЗапрос.Выбрать();
					Пока Выборка.Следующий() Цикл
						ИнформацияОНоменклатуре = Выборка;
						Прервать;
					КонецЦикла;
				КонецЕсли;
				
				Если ИнформацияОНоменклатуре = Неопределено Тогда
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найден товар по ид. Попытка создания товара с Ид: " + ИдТовара);
					ИнформацияОНоменклатуре = СформироватьНоменклатуруХарактеристикуПоИд(СтруктураНастроек, ИдТовара);
					
					Если ИнформацияОНоменклатуре = Неопределено Тогда
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не найдена информация о номенклатуре с Ид: " + ИдТовара);
						ЕстьКритическиеОшибки = Истина;
					Иначе
						Номенклатура   = ИнформацияОНоменклатуре.Объект;	
						Характеристика = ИнформацияОНоменклатуре.ПодчиненныйОбъект;	
					КонецЕсли;
					
				Иначе
					Номенклатура   = ИнформацияОНоменклатуре.Объект;	
					Характеристика = ИнформацияОНоменклатуре.ПодчиненныйОбъект;	
				КонецЕсли; 
			КонецЕсли; 		
			
			
			Если НЕ ЗначениеЗаполнено(ВнешнийКодПозиции) И НЕ ЗначениеЗаполнено(ИдТовара) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "В заказе был указан не товар, а строка. Товар: " + НаименованиеТовара + " в сделку: " + Строка(Заказ) + " не будет указан.");
			КонецЕсли; 		
#КонецОбласти	
			Номенклатура = ?(ЗначениеЗаполнено(Номенклатура), Номенклатура, Справочники.Номенклатура.ПустаяСсылка());			
			
			НоваяСтрока = Заказ.Товары.Добавить();
			
#Область ЗаполнениеСтрокиСтарымиДанными	
			НайденныеСтроки = тзнТоваровАрх.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Номенклатура, Характеристика));
			Если НайденныеСтроки.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденныеСтроки[0]);
			КонецЕсли;
#КонецОбласти	
			
			ДополнительныеПараметрыТЧ = Новый Структура;
			ДополнительныеПараметрыТЧ.Вставить("ПоляТЧОбъекта1С"			, ПоляТЧОбъекта1С);
			ДополнительныеПараметрыТЧ.Вставить("МетаданныеТЧ"				, Метаданные.Документы.ЗаказКлиента.ТабличныеЧасти.Товары);
			ДополнительныеПараметрыТЧ.Вставить("Объект1С"					, Заказ);
			ДополнительныеПараметрыТЧ.Вставить("СтрокаТЧ"					, НоваяСтрока);
			ДополнительныеПараметрыТЧ.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
			ДополнительныеПараметрыТЧ.Вставить("ДанныеЭлементаТЧБ24"		, ТоварЗаказа);
			ДополнительныеПараметрыТЧ.Вставить("ПомеченНаУдаление"			, Отменен);
			ДополнительныеПараметрыТЧ.Вставить("ЭтоНовыйОбъект"				, ЭтоНовыйЗаказ);
			ДополнительныеПараметрыТЧ.Вставить("Номенклатура"				, Номенклатура);
			ДополнительныеПараметрыТЧ.Вставить("Характеристика"				, Характеристика);
			ДополнительныеПараметрыТЧ.Вставить("КлючСвязиПозиции"			, КлючСвязиПозиции);
			ДополнительныеПараметрыТЧ.Вставить("ТоварыАрх"					, тзнТоваровАрх);
			
			ЗаполнитьРеквизитыТЧОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметрыТЧ, "Товары");
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				тзнТоваровАрх.Удалить(НайденныеСтроки[0]);
			КонецЕсли;
			
			СтруктураДействий = Новый Структура;
			
			СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Заказ);
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
			СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
			
			ОбработкаТабличнойЧастиВызовСервера.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, Неопределено);
			
#Область ЗаполнениеСвойствПозицийЗаказов	
			Если ТипЗнч(СвойстваПозиции) = Тип("Массив") Тогда
				
				Если СвойстваПозиции.Количество() = 0 Тогда
					НовоеСвойствоПозиции = тзнСвойстваПозиций.Добавить();
					НовоеСвойствоПозиции.ИдентификаторПозиции 	= ИдПозиции; 
					НовоеСвойствоПозиции.КлючСвязи 				= КлючСвязиПозиции;
					НовоеСвойствоПозиции.Номенклатура 			= Номенклатура; 
					НовоеСвойствоПозиции.Характеристика 		= Характеристика; 
				Иначе
					
					Для Каждого ТекСвойствоПозиции Из СвойстваПозиции Цикл
						
						ИдСвойстваПозицииПозиции 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекСвойствоПозиции.Получить("id"),"ЧГ=0"));
						ЗначениеСвойстваПозицииПозиции 		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекСвойствоПозиции.Получить("value"),"ЧРД=.; ЧГ=0"));
						НаименованиеСвойстваПозицииПозиции 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТекСвойствоПозиции.Получить("name"));
						КодСвойстваПозицииПозиции 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТекСвойствоПозиции.Получить("code"));
						
						НовоеСвойствоПозиции = тзнСвойстваПозиций.Добавить();
						НовоеСвойствоПозиции.ИдентификаторПозиции 	= ИдПозиции; 
						НовоеСвойствоПозиции.КлючСвязи 				= КлючСвязиПозиции; 
						НовоеСвойствоПозиции.Номенклатура 			= Номенклатура; 
						НовоеСвойствоПозиции.Характеристика 		= Характеристика; 
						
						НовоеСвойствоПозиции.ИдентификаторСвойства 	= ИдСвойстваПозицииПозиции; 
						НовоеСвойствоПозиции.НаименованиеСвойства 	= НаименованиеСвойстваПозицииПозиции; 
						НовоеСвойствоПозиции.КодСвойства 			= КодСвойстваПозицииПозиции; 
						НовоеСвойствоПозиции.Значение 				= ЗначениеСвойстваПозицииПозиции; 
						
					КонецЦикла;
					
				КонецЕсли;
			
			Иначе
				НовоеСвойствоПозиции = тзнСвойстваПозиций.Добавить();
				НовоеСвойствоПозиции.ИдентификаторПозиции 	= ИдПозиции; 
				НовоеСвойствоПозиции.КлючСвязи 				= КлючСвязиПозиции;
				НовоеСвойствоПозиции.Номенклатура 			= Номенклатура; 
				НовоеСвойствоПозиции.Характеристика 		= Характеристика; 
			КонецЕсли;
#КонецОбласти				
			
		КонецЦикла;
		

#Область ЗаполнениеСуммыДоставки	
		СуммаДоставки = 0;       
		Если Отгрузки <> Неопределено Тогда
			Для Каждого ТекОтгрузка Из Отгрузки Цикл
				СуммаДоставки = СуммаДоставки + Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ТекОтгрузка.Получить("priceDelivery"),"ЧРД=.; ЧГ=0"));
			КонецЦикла;
		КонецЕсли;
		
		Если СуммаДоставки > 0 Тогда
			
			НоменклатураДоставки = СтруктураНастроек.НастройкиСинхронизацииЗаказов.НоменклатураДоставка;
			
			НоваяЗаписьТовара = Заказ.Товары.Добавить();
#Область ЗаполнениеСтрокиСтарымиДанными	
			НайденныеСтроки = тзнТоваровАрх.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", НоменклатураДоставки, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка()));
			Если НайденныеСтроки.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(НоваяЗаписьТовара, НайденныеСтроки[0]);
				тзнТоваровАрх.Удалить(НайденныеСтроки[0]);
			КонецЕсли;
#КонецОбласти	
			НоваяЗаписьТовара.КлючСвязи = 9999;
			НоваяЗаписьТовара.КодСтроки = 9999;
			
			НоваяЗаписьТовара.Номенклатура 		= НоменклатураДоставки;
			НоваяЗаписьТовара.Характеристика 	= Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
				
			НоваяЗаписьТовара.Упаковка			= НоменклатураДоставки.ЕдиницаИзмерения;
			НоваяЗаписьТовара.СтавкаНДС  		= НоменклатураДоставки.СтавкаНДС;
				
			НоваяЗаписьТовара.Склад		 		= ?(ЗначениеЗаполнено(НоваяЗаписьТовара.Склад), НоваяЗаписьТовара.Склад, Заказ.Склад);
				
			НоваяЗаписьТовара.ВариантОбеспечения= ?(ЗначениеЗаполнено(НоваяЗаписьТовара.ВариантОбеспечения), НоваяЗаписьТовара.ВариантОбеспечения, Перечисления.ВариантыОбеспечения.НеТребуется);
				
			Если НоваяЗаписьТовара.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга ИЛИ НЕ ЗначениеЗаполнено(НоваяЗаписьТовара.Номенклатура) Тогда
				НоваяЗаписьТовара.Содержание = НоменклатураДоставки.НаименованиеПолное;
			КонецЕсли;
				
			НоваяЗаписьТовара.Количество  			= 1;
			НоваяЗаписьТовара.КоличествоУпаковок  	= 1;
			
			НоваяЗаписьТовара.Цена					= СуммаДоставки;
			
			НоваяЗаписьТовара.ПроцентАвтоматическойСкидки 	= 0;
			НоваяЗаписьТовара.СуммаАвтоматическойСкидки		= 0;
			
			НоваяЗаписьТовара.ПроцентРучнойСкидки 	= 0;
			НоваяЗаписьТовара.ПроцентРучнойСкидки 	= 0;
			
			СтруктураДействий = Новый Структура;
			
			СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Заказ);
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
			СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
			
			ОбработкаТабличнойЧастиВызовСервера.ОбработатьСтрокуТЧ(НоваяЗаписьТовара, СтруктураДействий, Неопределено);
			
		КонецЕсли;
#КонецОбласти	
		
#КонецОбласти		
		

#Область ПолучениеИнформацииОСостоянииЗаказаДоПроведения
		ИнформацияОСостоянииЗаказа = Новый Структура;
		Если ЗначениеЗаполнено(Заказ.Ссылка) Тогда
			Если ИнформацияОСтатусах <> Неопределено Тогда			
				Если ИнформацияОСтатусах.ИсточникСтатусовЗаказов = "СостоянияЗаказов" Тогда
					
					Запрос = Новый Запрос;
					Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
					Запрос.УстановитьПараметр("Заказ", Заказ.Ссылка);
					Запрос.Текст = "ВЫБРАТЬ *
					|ИЗ
					|	РегистрСведений.СостоянияЗаказовКлиентов КАК СостоянияЗаказовКлиентов
					|ГДЕ
					|	СостоянияЗаказовКлиентов.Заказ = &Заказ";
					
					тзнСЗ = Запрос.Выполнить().Выгрузить();;
					Если тзнСЗ.Количество()> 0 Тогда
						ИнформацияОСостоянииЗаказа = тзнСЗ[0];	
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	
		
#Область ЗаполнениеГрафикаОплаты
		Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиОплаты") Тогда
			
			//Заказ.ГрафикОплаты = Заказ.Соглашение.ГрафикОплаты;
			//
			//Заказ.ЭтапыГрафикаОплаты.Очистить();
			//ВзаиморасчетыСервер.ПроверитьЗаполнитьЭтапыГрафикаОплаты(Заказ);
			
		КонецЕсли;
#КонецОбласти	
		
		Если ЗначениеЗаполнено(АлгоритмПриЗагрузке) Тогда
			ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Заказ, ТекЭлемент, АлгоритмПриЗагрузке);
		КонецЕсли;
		
#Область ПроведениеДокумента
		ОтменаПроведенияСОшибкой = Ложь;
		Заказ.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);

		Если НЕ Отменен ИЛИ СтруктураНастроек.НастройкиСинхронизацииЗаказов.КогдаОтменен = "" Тогда
			
			Если (РежимЗаписи = "Проводить закрытые" И Закрыт)
				ИЛИ РежимЗаписи = "Проводить" Тогда
				
				Попытка
					Заказ.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Проведен документ: " + Строка(Заказ), Заказ.Ссылка);
				Исключение
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время проведения документа: " + Строка(Заказ) + " возникли ошибки.", Заказ.Ссылка);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
					ЕстьКритическиеОшибки = Истина;
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Попытка записи документа: " + Строка(Заказ), Заказ.Ссылка);
					
					Попытка
						Заказ.Записать(РежимЗаписиДокумента.Запись);
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан документ: " + Строка(Заказ), Заказ.Ссылка);
					Исключение
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи документа: " + Строка(Заказ) + " возникли ошибки.", Заказ.Ссылка);
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
						СтруктураНастроек.ВыполненоБезОшибок = Ложь;
		
					КонецПопытки;
					
				КонецПопытки;
				
			Иначе
				
				Попытка
					Если ЭтоНовыйЗаказ Тогда
						Заказ.Записать(РежимЗаписиДокумента.Запись); 
					ИначеЕсли Заказ.Проведен Тогда
						Заказ.Записать(РежимЗаписиДокумента.Проведение); 
					Иначе
						Заказ.Записать(РежимЗаписиДокумента.ОтменаПроведения); 
					КонецЕсли;
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан: " + Строка(Заказ), Заказ.Ссылка);
				Исключение
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи документа: " + Строка(Заказ) + " возникли ошибки.", Заказ.Ссылка);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					ОтменаПроведенияСОшибкой = Истина;
					ЕстьКритическиеОшибки 	 = Истина;
				
					СтруктураНастроек.ВыполненоБезОшибок = Ложь;
		
				КонецПопытки;
				
			КонецЕсли;
		Иначе
			
			Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.КогдаОтменен = "Помечать на удаление" Тогда

				Заказ.ПометкаУдаления = Истина;
				
				Попытка
					Заказ.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Помечен на удаление документ: " + Строка(Заказ), Заказ.Ссылка);
				Исключение
					
					ЕстьКритическиеОшибки = Истина;
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Ошибка при установке пометки на удаление документа: " + Строка(Заказ) + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), Заказ.Ссылка);
				КонецПопытки;
				
			ИначеЕсли СтруктураНастроек.НастройкиСинхронизацииЗаказов.КогдаОтменен = "Отменять проведение" Тогда
				
				Попытка
					Заказ.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Отменено проведение у документа: " + Строка(Заказ), Заказ.Ссылка);
				Исключение
					
					ЕстьКритическиеОшибки 	 = Истина;
					ОтменаПроведенияСОшибкой = Истина;
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Ошибка при отмене проведения документа: " + Строка(Заказ) + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), Заказ.Ссылка);
				КонецПопытки;
				
			Иначе 
				
	     	КонецЕсли;
		КонецЕсли;
		
#КонецОбласти		

#Область ДобавлениеСостоянияЗаказа
		Если ЗначениеЗаполнено(Заказ.Ссылка) Тогда
			Если ИнформацияОСтатусах <> Неопределено Тогда
				
				Если ИнформацияОСтатусах.ИсточникСтатусовЗаказов = "СостоянияЗаказов" Тогда
					
					тзнСтатусов = ИнформацияОСтатусах.СоответствияССостояниямиЗаказа;
					
					НайденныеСтроки = тзнСтатусов.НайтиСтроки(Новый Структура("ИдСтатуса", ИдСтатуса));
					
					Если НайденныеСтроки.Количество() > 0 Тогда
						НайденнаяСтрока = НайденныеСтроки[0];	
					Иначе
						НайденнаяСтрока = Неопределено;	
					КонецЕсли;
					
					Если НайденнаяСтрока <> Неопределено Тогда
						
						НаборЗаписей = РегистрыСведений.СостоянияЗаказовКлиентов.СоздатьНаборЗаписей();
						НаборЗаписей.Отбор.Заказ.Установить(Заказ.Ссылка);
						НоваяЗапись = НаборЗаписей.Добавить();
						
						ЗаполнитьЗначенияСвойств(НоваяЗапись, ИнформацияОСостоянииЗаказа); 
						
						НоваяЗапись.Заказ 		= Заказ.Ссылка;
						НоваяЗапись.ДатаСобытия = ТекущаяДатаСеанса();
						НоваяЗапись.Состояние 	= НайденнаяСтрока.Статус;
						
						НаборЗаписей.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
						НаборЗаписей.Записать(Истина);
					
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	

		Если ЗначениеЗаполнено(Заказ.Ссылка) Тогда
			
			Для каждого ТекСвойствоПозицииЗаказа Из тзнСвойстваПозиций Цикл
				
				НоваяЗаписьСвойстваПозиции = РегистрыСведений.Б24_КС_СвойстваПозицийЗаказов.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(НоваяЗаписьСвойстваПозиции, ТекСвойствоПозицииЗаказа); 
				НоваяЗаписьСвойстваПозиции.Объект = Заказ.Ссылка;
				НоваяЗаписьСвойстваПозиции.Записать(Истина);
				
			КонецЦикла;
			
			ОбновитьДополнительнуюИнформациюОЗаказе(Заказ.Ссылка, ТекЭлемент);
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Заказ, Заказ.Ссылка, ИдБитрикс24);
			
			Если ПринудительноРегистрироватьДокумент Тогда
				
				//Б24_КС_РегистрацияИзмененийВызовСервера.ПроверитьИДобавитьОбъектВТаблицуИзменений(СтруктураНастроек.НастройкаПодключения, СтруктураНастроек.ТипыОбъектовОбмена.Заказ, Заказ.Ссылка);	
				//
				//СоответствияТипов = Новый Соответствие;
				//СоответствияТипов.Вставить(СтруктураНастроек.ТипыОбъектовОбмена.Заказ, Истина);
				//
				//ПараметрыЗапроса = Новый Структура;
				//ПараметрыЗапроса.Вставить("НастройкаПодключения", СтруктураНастроек.НастройкаПодключения);
				//ПараметрыЗапроса.Вставить("СоответствияТипов"	, СоответствияТипов);
				//ПараметрыЗапроса.Вставить("НастройкаПодключения", СтруктураНастроек.НастройкаПодключения);
				//ПараметрыЗапроса.Вставить("ПолнаяВыгрузка"		, СтруктураНастроек.ПолнаяВыгрузка);
				//ПараметрыЗапроса.Вставить("ПолнаяЗагрузка"		, СтруктураНастроек.ПолнаяЗагрузка);
				//ПараметрыЗапроса.Вставить("ВидСинхронизации"	, СтруктураНастроек.ВидСинхронизации);
				//	
				//Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КС_ОбщегоНазначенияВызовСервера.ЗапуститьВыполнениеВыгрузкиОпределенныхТиповВФоне", "Обновление информации на портале", ПараметрыЗапроса, 2); 
				
			КонецЕсли;
			
			тзнСвойстваПозиций.Свернуть("ИдентификаторПозиции, КлючСвязи");
			Для Каждого ТекСвойствоПозицииЗаказа Из тзнСвойстваПозиций Цикл
				
				ДанныеОПозиции = Новый Структура;
				ДанныеОПозиции.Вставить("Объект"			, Заказ.Ссылка);
				ДанныеОПозиции.Вставить("ПодчиненныйОбъект"	, ТекСвойствоПозицииЗаказа.КлючСвязи);
				
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ТоварыЗаказа, ДанныеОПозиции, ТекСвойствоПозицииЗаказа.ИдентификаторПозиции);

			КонецЦикла;
			
			Если НЕ ОтменаПроведенияСОшибкой Тогда
				ЗаполнениеСвойствЗаказа(СтруктураНастроек, ТаблицаСвойств, Заказ.Ссылка, ЭтоНовыйЗаказ, Истина);			
			КонецЕсли;
			
			ЕстьКритическиеОшибкиОплат = ЗагрузитьПодчиненныеОплаты(СтруктураНастроек, Заказ, ИдБитрикс24, Оплаты, Отменен);
			ЕстьКритическиеОшибки = ?(ЕстьКритическиеОшибки, ЕстьКритическиеОшибки, ЕстьКритическиеОшибкиОплат);
			
			ЕстьКритическиеОшибкиОтгрузок = ЗагрузитьПодчиненныеОтгрузки(СтруктураНастроек, Заказ, ИдБитрикс24, Отгрузки, Отменен);
			ЕстьКритическиеОшибки = ?(ЕстьКритическиеОшибки, ЕстьКритическиеОшибки, ЕстьКритическиеОшибкиОтгрузок);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдБитрикс24, НЕ ЕстьКритическиеОшибки)));
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции

Процедура ОбновитьДополнительнуюИнформациюОЗаказе(Заказ, ИнформацияОЗаказе) Экспорт
	
	НоваяЗаписьИнфоЗаказа = РегистрыСведений.Б24_КС_ИнформацияОЗаказах.СоздатьМенеджерЗаписи();
	НоваяЗаписьИнфоЗаказа.Заказ = Заказ;
	НоваяЗаписьИнфоЗаказа.ДополнительныеДанные = Новый ХранилищеЗначения(ИнформацияОЗаказе);
	НоваяЗаписьИнфоЗаказа.Записать();
	
КонецПроцедуры

Функция ПолучитьТаблицуСвойствЗаказов(СтруктураНастроек, МенеджерВременныхТаблиц, СвойстваЗаказа)
	
	тзнЗначенийСвойствТовара = Новый ТаблицаЗначений;
	тзнЗначенийСвойствТовара.Колонки.Добавить("Свойство");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЗначениеСвойства");
	тзнЗначенийСвойствТовара.Колонки.Добавить("ЭтоДополнительноеСведение");
	
	
	мСвойстваИсключения = СтруктураНастроек.ПредопределенныеСвойства.НеЗагружаемыеЗначенияСвойств;
	
	Для Каждого ТекСвойствоЗаказа Из СвойстваЗаказа Цикл
		
		ИдСвойстваЗаказа			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекСвойствоЗаказа.Получить("orderPropsId"),"ЧГ=0"));
		ЗначениеСвойстваСПортала	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекСвойствоЗаказа.Получить("value"),"ЧРД=.; ЧГ=0"));
		
		СвойствоЗаказа = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Свойства", ИдСвойстваЗаказа);
		
		Если ЗначениеЗаполнено(СвойствоЗаказа) Тогда
			
			Если мСвойстваИсключения.Найти(СвойствоЗаказа) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			НовоеЗначениеСвойстваТовара = тзнЗначенийСвойствТовара.Добавить();
			НовоеЗначениеСвойстваТовара.Свойство 				 	= СвойствоЗаказа;
			НовоеЗначениеСвойстваТовара.ЭтоДополнительноеСведение 	= СвойствоЗаказа.ЭтоДополнительноеСведение; 
			
			ТипЗначенияСвойства = СвойствоЗаказа.ТипЗначения;
			
			Попытка
					
				Если ТипЗначенияСвойства.СодержитТип(Тип("Строка")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = ЗначениеСвойстваСПортала;
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Булево")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Булево"), ЗначениеСвойстваСПортала);
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Число")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), ЗначениеСвойстваСПортала);
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Дата")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ЗначениеСвойстваСПортала);
				// Используется в других конфигурациях 
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("СправочникСсылка.Пользователи")) Тогда
					
					НайденнаяСтрока = СтруктураНастроек.ТаблицаСопоставленияПользователей.Найти( ЗначениеСвойстваСПортала, "ИдПользователя");	
					
					Если НайденнаяСтрока <> Неопределено Тогда
						НовоеЗначениеСвойстваТовара.ЗначениеСвойства = НайденнаяСтрока.Пользователь1С;
					КонецЕсли;
					
				ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
					НовоеЗначениеСвойстваТовара.ЗначениеСвойства = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЗначенийСвойств",  ЗначениеСвойстваСПортала);
				Иначе
					НовоеЗначениеСвойстваТовара.ПоддерживаетсяСвойство = Ложь;	
				КонецЕсли;
				
			Исключение
				НовоеЗначениеСвойстваТовара.ПоддерживаетсяСвойство = Ложь;	
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось обработать значение свойства: " + Строка(ЗначениеСвойстваСПортала) + " свойства: " + Строка(СвойствоЗаказа));
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат тзнЗначенийСвойствТовара;
	
КонецФункции

Процедура ЗаполнениеСвойствЗаказа(СтруктураНастроек, ТаблицаСвойств, Объект, ЭтоНовыйОбъект, ЭтоДополнительноеСведение)
	
	Для Каждого ТекСвойство Из ТаблицаСвойств Цикл
		
		Если ТекСвойство.ЭтоДополнительноеСведение = ЭтоДополнительноеСведение Тогда
			
			Если ЭтоДополнительноеСведение Тогда
				
				НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Объект.Установить(Объект);
				НаборЗаписей.Прочитать();
				
				Для Каждого ТекСвойство Из ТаблицаСвойств Цикл
					
					ЕстьСвойство = Ложь;
					Для Каждого ТекСтрока Из НаборЗаписей Цикл
						Если ТекСтрока.Свойство = ТекСвойство.Свойство Тогда
							ЕстьСвойство = Истина;
							ТекСтрока.Значение = ТекСвойство.ЗначениеСвойства;	
						КонецЕсли;
					КонецЦикла;
					
					Если ЕстьСвойство = Ложь Тогда
						НоваяЗапись = НаборЗаписей.Добавить();
						НоваяЗапись.Объект = Объект;
						НоваяЗапись.Свойство = ТекСвойство.Свойство;
						НоваяЗапись.Значение = ТекСвойство.ЗначениеСвойства;
					КонецЕсли;
					
				КонецЦикла;
				
				НаборЗаписей.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
				НаборЗаписей.Записать();
				
			Иначе
				
				Для Каждого ТекСвойство Из ТаблицаСвойств Цикл
					
					Если НЕ ЗначениеЗаполнено(ТекСвойство.ЗначениеСвойства)Тогда
						Если ЭтоНовыйОбъект Тогда
							Продолжить;
						КонецЕсли;
						
						Для Каждого ЗаполнениеСвойствоТовара Из Объект.ДополнительныеРеквизиты Цикл
							Если ЗаполнениеСвойствоТовара.Свойство = ТекСвойство.Свойство Тогда
								ЗаполнениеСвойствоТовара.Значение = Неопределено;
							КонецЕсли;
						КонецЦикла;
					Иначе
						
						ЕстьРеквизит = Ложь;
						Для Каждого ЗаполнениеСвойствоТовара Из Объект.ДополнительныеРеквизиты Цикл
							Если ЗаполнениеСвойствоТовара.Свойство = ТекСвойство.Свойство Тогда
								ЗаполнениеСвойствоТовара.Значение = ТекСвойство.ЗначениеСвойства;
								Если ТипЗнч(ТекСвойство.ЗначениеСвойства) = Тип("Строка") Тогда
									ЗаполнениеСвойствоТовара.ТекстоваяСтрока = ТекСвойство.ЗначениеСвойства;
								КонецЕсли;
								ЕстьРеквизит = Истина
							КонецЕсли;
						КонецЦикла;
						
						Если НЕ ЕстьРеквизит Тогда
							НовыйРеквизит = Объект.ДополнительныеРеквизиты.Добавить();
							НовыйРеквизит.Свойство 	= ТекСвойство.Свойство; 
							НовыйРеквизит.Значение 	= ТекСвойство.ЗначениеСвойства; 
							Если ТипЗнч(ТекСвойство.ЗначениеСвойства) = Тип("Строка") Тогда
								НовыйРеквизит.ТекстоваяСтрока = ТекСвойство.ЗначениеСвойства;
							КонецЕсли;
						КонецЕсли;	
						
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры



#Область ОбщиеПроцедурыИФункции

Функция ПолучитьДоговор(СтруктураНастроек, Организация, Контрагент, ВалютаРасчетов)
	
	Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	
	Если ТипЗнч(Контрагент) <> Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат Договор;	
	КонецЕсли;
	
	НаименованиеДоговора = "Договор WEB - Битрикс24";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Организация"			, Организация);
	Запрос.УстановитьПараметр("Контрагент"			, Контрагент);
	Запрос.УстановитьПараметр("Партнер"				, Контрагент.Партнер);
	Запрос.УстановитьПараметр("ВалютаРасчетов"		, ВалютаРасчетов);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	|	ДоговорыКонтрагентов.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Организация = &Организация
	|	И ДоговорыКонтрагентов.Контрагент = &Контрагент
	|	И ДоговорыКонтрагентов.Партнер = &Партнер
	|	И ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаРасчетов
	|	И ДоговорыКонтрагентов.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем)";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		тзнДоговоров = ВыполненныйЗапрос.Выгрузить();
		
		Если тзнДоговоров.Количество() = 1 Тогда 
			Договор = тзнДоговоров[0].Ссылка;
		Иначе
			Для каждого ТекДоговор Из тзнДоговоров Цикл	
				Если ТекДоговор.Наименование = НаименованиеДоговора Тогда
					Договор = ТекДоговор.Ссылка;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Договор) Тогда
		НовыйДоговор = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
		НовыйДоговор.Наименование 			= НаименованиеДоговора;
		НовыйДоговор.Партнер	 			= Контрагент.Партнер;
		НовыйДоговор.Контрагент	 			= Контрагент;
		НовыйДоговор.Организация  			= Организация;
		НовыйДоговор.ТипДоговора            = Перечисления.ТипыДоговоров.СПокупателем;
		НовыйДоговор.Статус					= Перечисления.СтатусыДоговоровКонтрагентов.Действует;
		
		НовыйДоговор.ВалютаВзаиморасчетов	= ВалютаРасчетов;
		НовыйДоговор.ПорядокРасчетов		= Перечисления.ПорядокРасчетов.ПоЗаказамНакладным;
		
		//НовыйДоговор.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
		НовыйДоговор.Записать();
		Договор = НовыйДоговор.Ссылка;
	КонецЕсли;
	
	Возврат Договор;
	
КонецФункции

Функция СформироватьПростуюСтруктуруСИд(Ключ, Значение)
	
	Результат = Новый Структура("Ид_" + Ключ, Значение);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьДатуИзСтрокиJSON(ДатаСтрокой)
	
	Если НЕ ЗначениеЗаполнено(ДатаСтрокой) Тогда
		Возврат "";
	КонецЕсли;
	
	Результат = Неопределено;
	
	Попытка
		
		лГоды 		= Число(Лев(ДатаСтрокой, 4));
		лМесяцы 	= Число(Сред(ДатаСтрокой,6,2));
		лДни 		= Число(Сред(ДатаСтрокой,9,2));
		лЧасы 		= Число(Сред(ДатаСтрокой,12,2));
		лМинуты		= Число(Сред(ДатаСтрокой,15,2));
		лСекунды 	= Число(Сред(ДатаСтрокой,18,2));
		
		Результат = Дата(лГоды, лМесяцы, лДни, лЧасы, лМинуты, лСекунды);
	Исключение
		Результат = Неопределено;
	КонецПопытки;
	
	Возврат Результат;	
	
КонецФункции

Функция ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, НазваниеВременнойТаблицы, ИдЭлемента)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Идентификатор", ИдЭлемента);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_Идентификаторы.Объект КАК Объект
	|ИЗ
	|	"+НазваниеВременнойТаблицы+" КАК ВТ_Идентификаторы
	|ГДЕ
	|	ВТ_Идентификаторы.Идентификатор = &Идентификатор";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат = Выборка.Объект;
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция ПолучитьИнформациюОКомпанииКонтакте(СтруктураНастроек, МенеджерВременныхТаблиц, ИдКомпании, ИдКонтакта, ИдРеквизита)
	
	Результат = Новый Структура;
	Результат.Вставить("Партнер"				, Справочники.Партнеры.ПустаяСсылка());
	Результат.Вставить("Контрагент"				, Справочники.Контрагенты.ПустаяСсылка());
	Результат.Вставить("КонтактноеЛицо"			, Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка());
	Результат.Вставить("ЕстьКритическиеОшибки"	, Ложь);
	
	Контакт			= "";
	Компания		= "";
	ЕстьКомпания	= Ложь;
	Если НЕ(ИдКомпании = "" ИЛИ ИдКомпании = "0") Тогда
		
		ЕстьКомпания = Истина;
		
		Компания 	= ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", ИдКомпании);	
		Если НЕ ЗначениеЗаполнено(Компания) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найдена компания по ид. Попытка создания компании с Ид: " + ИдКомпании);
			Компания = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Компания", ИдКомпании);
		КонецЕсли;
					
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Компания) Тогда
		Результат.Партнер = Компания;
	КонецЕсли;
	
	Если НЕ(ИдКонтакта = "" ИЛИ ИдКонтакта = "0") Тогда
		
		Если ЕстьКомпания Тогда
			Контакт 	= ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", "#"+ИдКонтакта);
		Иначе
			Контакт 	= ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", ИдКонтакта);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Контакт) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найден контакт по ид. Попытка создания контакта с Ид: " + ИдКонтакта);
			
			Если ЕстьКомпания Тогда
				Контакт = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Контакт", ИдКонтакта);
			Иначе
				Контакт = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "КомпанияКонтакт", ИдКонтакта);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контакт) Тогда
		Если ТипЗнч(Контакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
			Результат.КонтактноеЛицо = Контакт;
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.Партнеры") Тогда
			Если ЗначениеЗаполнено(Компания) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Партнерами являются и компания и контакт. Установлен будет Из компании");
			Иначе
				Результат.Партнер = Контакт;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Результат.Партнер) Тогда
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не найдена компания/контакт с Ид: " + ИдКомпании+"/"+ИдКонтакта);
		Результат.ЕстьКритическиеОшибки = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдРеквизита) И ИдРеквизита <> "0" Тогда
		
		Результат.Контрагент 	= ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыРеквизитов", ИдРеквизита);
		ИдДляЛога	= ИдРеквизита;
		
		Если НЕ ЗначениеЗаполнено(Результат.Контрагент) Тогда
			
			ИнформацияОКонтрагенте 	=  РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит, ИдРеквизита);
			
			Если ИнформацияОКонтрагенте <> Неопределено Тогда
				Результат.Контрагент = ИнформацияОКонтрагенте.Объект; 	
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Результат.Контрагент) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не найден реквизит с Ид: " + ИдДляЛога);
				Результат.ЕстьКритическиеОшибки = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Результат.Партнер) Тогда
		Результат.Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("Партнер", Результат.Партнер); 	
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьЗначениеИзМассиваСоответствий(МассивСоответствий, КолонкаПоиска, ЗначениеКолонкиПоиска, КолонкаЗначения)
	
	Результат = "";
	
	Для Каждого ТекЭлемент Из МассивСоответствий Цикл
		
		Если ТекЭлемент = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Формат(ТекЭлемент.Получить(КолонкаПоиска), "ЧГ=0") = ЗначениеКолонкиПоиска Тогда
			Результат = ТекЭлемент.Получить(КолонкаЗначения);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ПреобразоватьКлючJson(КлючJson)

	КлючJson = СтрЗаменить(КлючJson, "UF_CRM_SMART_INVOICE_", "ufCrm_SMART_INVOICE_");
	КлючJson = СтрЗаменить(КлючJson, "ufCrmSmartInvoice", "ufCrm_SMART_INVOICE_");

КонецПроцедуры

Функция СсылкаПоИдБ24(Портал, ТипДанных, ИдБитрикс24, НуженТолькоОбъект = Истина)
	
	Результат = Неопределено;
	
	ДанныеПоИд = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(Портал, ТипДанных, ИдБитрикс24);  
	Если ДанныеПоИд <> Неопределено Тогда
		Если НуженТолькоОбъект Тогда
			Результат = ДанныеПоИд.Объект;
		Иначе
			Результат = ДанныеПоИд;
		КонецЕсли;
	КонецЕсли;
			
	Возврат Результат;

КонецФункции 

#КонецОбласти


#Область ЗагрузкаДанныхПоИд

Функция СформироватьДанныеДляДругогоМодуля(НастройкаПодключения, Операция, Данные) Экспорт
	
	Результат = Неопределено;
	
	СтруктураНастроек = Б24_КС_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Ложь, 2, Истина);
	Если СтруктураНастроек = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Операция = "Компания" Тогда
		СтруктураНастроек.Операция = "Компании";
		Результат = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Компания", Данные);
	ИначеЕсли Операция = "Контакт" Тогда
		СтруктураНастроек.Операция = "Контакты";
		Результат = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Контакт", Данные);
	ИначеЕсли Операция = "ТоварыСделки" Тогда
		СтруктураНастроек.Операция = "Проекты";
#Область ТоварыСделки	
		ИдПозицииДокумента	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Данные.Получить("ID"),"ЧГ=0")); 
		ИдТовара			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Данные.Получить("PRODUCT_ID"),"ЧГ=0"));
		НаименованиеТовара	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(Данные.Получить("PRODUCT_NAME")));
		
		КодЕдИзм			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Данные.Получить("MEASURE_CODE"),"ЧГ=0"));
		
		Количество				= Данные.Получить("QUANTITY");
		ЦенаСоСкидкойСНалогом	= Данные.Получить("PRICE");
		ЦенаБезСкидкиБезНалога	= Данные.Получить("PRICE_NETTO");
		ЦенаБезСкидкиСНалогом	= Данные.Получить("PRICE_BRUTTO");
		СуммаВключаетНДС		= Данные.Получить("TAX_INCLUDED") = "Y";       
		
		НалогВключен		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(Данные.Получить("TAX_INCLUDED")))= "Y";
		Скидка				= Данные.Получить("DISCOUNT_SUM");
		ПроцентСкидки		= Данные.Получить("DISCOUNT_RATE");
		ПроцентСкидки		=?(ПроцентСкидки=0 И Скидка<>0, 100, ПроцентСкидки);
		
		СтавкаНалога		= Данные.Получить("TAX_RATE");
		Если СтавкаНалога 	= Неопределено Тогда СтавкаНалога = "Без НДС" КонецЕсли;
		
		Если НалогВключен = Истина Тогда   // баг есть
			Цена = ?(ЦенаБезСкидкиСНалогом = 0 И Скидка<>0, Скидка/ПроцентСкидки*100 ,ЦенаБезСкидкиСНалогом); // есть баг с скидкой
		Иначе
			Цена = ?(ЦенаБезСкидкиБезНалога = 0 И Скидка<>0, Скидка/ПроцентСкидки*100 ,ЦенаБезСкидкиБезНалога); // есть баг с скидкой
		КонецЕсли;
		
		ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
#Область ПоискЕдиницыИзмерения	
		Если ЗначениеЗаполнено(КодЕдИзм) Тогда
			ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(КодЕдИзм);;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найдена единица измерения по ид. Попытка создания единицы измерения с Ид: " + КодЕдИзм);
			ЕдиницаИзмерения = СформироватьЕдиницуИзмеренияПоИд(СтруктураНастроек, КодЕдИзм);
		КонецЕсли;
#КонецОбласти	
		
		Номенклатура	= Справочники.Номенклатура.ПустаяСсылка();	
		Характеристика  = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
#Область ПоискТовара	
		Если ЗначениеЗаполнено(ИдТовара) Тогда
			
			Если СтруктураНастроек.ВерсияТоваров = "v.2" Тогда     
				ИнформацияОНоменклатуре = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Предложение, ИдТовара);
				Если ИнформацияОНоменклатуре = Неопределено Тогда
					ИнформацияОНоменклатуре = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Товар2, ИдТовара);
				КонецЕсли;
			Иначе 
				ИнформацияОНоменклатуре = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Товар, ИдТовара);
			КонецЕсли;
			
			Если ИнформацияОНоменклатуре = Неопределено Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найден товар по ид. Попытка создания товара с Ид: " + ИдТовара);
				ИнформацияОНоменклатуре = СформироватьНоменклатуруХарактеристикуПоИд(СтруктураНастроек, ИдТовара);
				
				Если ИнформацияОНоменклатуре = Неопределено Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не найдена информация о номенклатуре с Ид: " + ИдТовара);
				Иначе
					Номенклатура   = ИнформацияОНоменклатуре.Объект;	
					Характеристика = ИнформацияОНоменклатуре.ПодчиненныйОбъект;	
				КонецЕсли;
				
			Иначе
				Номенклатура   = ИнформацияОНоменклатуре.Объект;	
				Характеристика = ИнформацияОНоменклатуре.ПодчиненныйОбъект;	
			КонецЕсли; 
		Иначе
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "В сделке был указан не товар, а строка. Товар: " + НаименованиеТовара + " не будет указан.");
		КонецЕсли; 			
#КонецОбласти	
		Номенклатура = ?(ЗначениеЗаполнено(Номенклатура), Номенклатура, Справочники.Номенклатура.ПустаяСсылка());
		
		Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда ЕдиницаИзмерения = Номенклатура.ЕдиницаИзмерения КонецЕсли;
		
		СтавкаНДС = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтавкуНДСПоСтавке(СтавкаНалога);
		Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда СтавкаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "СтавкаНДС") КонецЕсли;
		
		ТипНоменклатурыТовар = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ТипНоменклатуры")= Перечисления.ТипыНоменклатуры.Товар;
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("Б24_К_ИдентификаторПозиции"	, ИдПозицииДокумента);
		СтруктураДанных.Вставить("Номенклатура"					, XMLСтрока(Номенклатура));
		СтруктураДанных.Вставить("Характеристика"				, XMLСтрока(Характеристика));
		СтруктураДанных.Вставить("ТипНоменклатурыТовар"			, ТипНоменклатурыТовар);
		СтруктураДанных.Вставить("Содержание"					, НаименованиеТовара);
		СтруктураДанных.Вставить("ЕдиницаИзмерения"				, XMLСтрока(ЕдиницаИзмерения));
		СтруктураДанных.Вставить("Количество"					, Количество);
		СтруктураДанных.Вставить("СтавкаНДС"					, XMLСтрока(СтавкаНДС)); 
		СтруктураДанных.Вставить("СуммаВключаетНДС"				, СуммаВключаетНДС);           
		СтруктураДанных.Вставить("Цена"							, Цена);
		
		СтруктураДанных.Вставить("ПроцентРучнойСкидки"			, ПроцентСкидки);
		СтруктураДанных.Вставить("СуммаРучнойСкидки"			, СтруктураДанных.Цена*СтруктураДанных.ПроцентРучнойСкидки*СтруктураДанных.Количество/100);
		
			
		СтруктураДанных.Вставить("ДатаОтгрузки"					, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса());
		
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		
		ЗаписатьJSON(ЗаписьJSON, СтруктураДанных);
		Результат = ЗаписьJSON.Закрыть();
#КонецОбласти	 
	ИначеЕсли Операция = "ТоварыСмартПроцесса" Тогда
		СтруктураНастроек.Операция = "СмартПроцессы";
#Область ТоварыСмартПроцесса
		ИдПозицииДокумента	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Данные.Получить("id"),"ЧГ=0")); 
		ИдТовара			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Данные.Получить("productId"),"ЧГ=0"));
		НаименованиеТовара	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(Данные.Получить("productName")));
		
		КодЕдИзм			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Данные.Получить("measureCode"),"ЧГ=0"));
		
		Количество				= Данные.Получить("quantity");
		ЦенаСоСкидкойСНалогом	= Данные.Получить("price");
		ЦенаБезСкидкиБезНалога	= Данные.Получить("priceNetto");
		ЦенаБезСкидкиСНалогом	= Данные.Получить("priceBrutto");
		СуммаВключаетНДС		= Данные.Получить("taxIncluded") = "Y";     
		
		НалогВключен		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(Данные.Получить("taxIncluded")))= "Y";
		Скидка				= Данные.Получить("discountSum");
		ПроцентСкидки		= Данные.Получить("discountRate");
		ПроцентСкидки		=?(ПроцентСкидки=0 И Скидка<>0, 100, ПроцентСкидки);
		
		СтавкаНалога		= Данные.Получить("taxRate");
		Если СтавкаНалога 	= Неопределено Тогда СтавкаНалога = "Без НДС" КонецЕсли;
		
		Если НалогВключен = Истина Тогда   // баг есть
			Цена = ?(ЦенаБезСкидкиСНалогом = 0 И Скидка<>0, Скидка/ПроцентСкидки*100 ,ЦенаБезСкидкиСНалогом); 
		Иначе
			Цена = ?(ЦенаБезСкидкиБезНалога = 0 И Скидка<>0, Скидка/ПроцентСкидки*100 ,ЦенаБезСкидкиБезНалога); 
		КонецЕсли;
		
		ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
#Область ПоискЕдиницыИзмерения	
		Если ЗначениеЗаполнено(КодЕдИзм) Тогда
			ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(КодЕдИзм);;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найдена единица измерения по ид. Попытка создания единицы измерения с Ид: " + КодЕдИзм);
			ЕдиницаИзмерения = СформироватьЕдиницуИзмеренияПоИд(СтруктураНастроек, КодЕдИзм);
		КонецЕсли;
#КонецОбласти	
		
		Номенклатура	= Справочники.Номенклатура.ПустаяСсылка();	
		Характеристика  = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
#Область ПоискТовара	
		Если ЗначениеЗаполнено(ИдТовара) Тогда
			
			Если СтруктураНастроек.ВерсияТоваров = "v.2" Тогда
				ИнформацияОНоменклатуре = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Предложение, ИдТовара);
				Если ИнформацияОНоменклатуре = Неопределено Тогда
					ИнформацияОНоменклатуре = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Товар2, ИдТовара);
				КонецЕсли;
			Иначе 
				ИнформацияОНоменклатуре = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Товар, ИдТовара);
			КонецЕсли;
			
			Если ИнформацияОНоменклатуре = Неопределено Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найден товар по ид. Попытка создания товара с Ид: " + ИдТовара);
				ИнформацияОНоменклатуре = СформироватьНоменклатуруХарактеристикуПоИд(СтруктураНастроек, ИдТовара);
				
				Если ИнформацияОНоменклатуре = Неопределено Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не найдена информация о товаре с Ид: " + ИдТовара);
				Иначе
					Номенклатура   = ИнформацияОНоменклатуре.Объект;	
					Характеристика = ИнформацияОНоменклатуре.ПодчиненныйОбъект;	
				КонецЕсли;
				
			Иначе
				Номенклатура   = ИнформацияОНоменклатуре.Объект;	
				Характеристика = ИнформацияОНоменклатуре.ПодчиненныйОбъект;	
			КонецЕсли; 
		Иначе
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Была указана строка, а не товар. Товар: " + НаименованиеТовара + " не будет указан.");
		КонецЕсли; 			
#КонецОбласти	
		Номенклатура = ?(ЗначениеЗаполнено(Номенклатура), Номенклатура, Справочники.Номенклатура.ПустаяСсылка());
		
		Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда ЕдиницаИзмерения = Номенклатура.ЕдиницаИзмерения КонецЕсли;
		
		СтавкаНДС = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтавкуНДСПоСтавке(СтавкаНалога);     
		Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда СтавкаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "СтавкаНДС") КонецЕсли;

		ТипНоменклатурыТовар = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ТипНоменклатуры")= Перечисления.ТипыНоменклатуры.Товар;
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("Б24_К_ИдентификаторПозиции"	, ИдПозицииДокумента);
		СтруктураДанных.Вставить("Номенклатура"					, XMLСтрока(Номенклатура));
		СтруктураДанных.Вставить("Характеристика"				, XMLСтрока(Характеристика));
		СтруктураДанных.Вставить("ТипНоменклатурыТовар"			, ТипНоменклатурыТовар);
		СтруктураДанных.Вставить("Содержание"					, НаименованиеТовара);
		СтруктураДанных.Вставить("ЕдиницаИзмерения"				, XMLСтрока(ЕдиницаИзмерения));
		СтруктураДанных.Вставить("Количество"					, Количество);
		СтруктураДанных.Вставить("СтавкаНДС"					, XMLСтрока(СтавкаНДС)); 
		СтруктураДанных.Вставить("СуммаВключаетНДС"				, СуммаВключаетНДС);           
		СтруктураДанных.Вставить("Цена"							, Цена);
		
		СтруктураДанных.Вставить("ПроцентРучнойСкидки"			, ПроцентСкидки);
		СтруктураДанных.Вставить("СуммаРучнойСкидки"			, СтруктураДанных.Цена*СтруктураДанных.ПроцентРучнойСкидки*СтруктураДанных.Количество/100);
			
		СтруктураДанных.Вставить("ДатаОтгрузки"					, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса());
		
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		
		ЗаписатьJSON(ЗаписьJSON, СтруктураДанных);
		Результат = ЗаписьJSON.Закрыть();

#КонецОбласти
	КонецЕсли;						
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьКомпаниюКонтактПоИд(пСтруктураНастроек, ТипВладельца, Ид) Экспорт 
	
	Результат = Справочники.Контрагенты.ПустаяСсылка();
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Клиент"	, Новый Массив);
	СтруктураДанных.Вставить("Реквизит"	, Новый Массив);
	СтруктураДанных.Вставить("Адреса"	, Новый Массив);
	СтруктураДанных.Вставить("БанкСчета", Новый Массив);
	
	Если ТипВладельца = "Компания" Тогда
		МетодКлиента = "crm.company.get?id=" + Ид; 	
		МетодРеквизита = "crm.requisite.list?filter[ENTITY_ID]=" + Ид + "&filter[ENTITY_TYPE_ID]=4" + "&start=-1"; 	
	Иначе
		МетодКлиента = "crm.contact.get?id=" + Ид; 
		МетодРеквизита = "crm.requisite.list?filter[ENTITY_ID]=" + Ид + "&filter[ENTITY_TYPE_ID]=3" + "&start=-1"; 	
	КонецЕсли;
	
	ТелоHTTPЗапроса = "";
	ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[K]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодКлиента);
	ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[R]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодРеквизита);
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(пСтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");                                             
	Если result <> Неопределено Тогда                                                         
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда 	
			
			ДанныеКлиента 		= result2.Получить("K"); 
			ДанныеРеквизита 	= result2.Получить("R");
			
			Если ДанныеКлиента <> Неопределено Тогда
				СтруктураДанных.Клиент.Добавить(ДанныеКлиента);	
			КонецЕсли;
			
			Если ДанныеРеквизита <> Неопределено Тогда
				СтруктураДанных.Реквизит.Добавить(ДанныеРеквизита);	
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("ИнформацияОРеквизитах", СтруктураДанных.Реквизит);
	
	Если ТипВладельца = "Компания" Тогда
		ДополнительныеДанные.Вставить("ИнформацияОКомпаниях", СтруктураДанных.Клиент);
		ЗагрузитьОбновитьКомпании(пСтруктураНастроек, ДополнительныеДанные);
		
		ИнформацияОКлиенте = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(пСтруктураНастроек.Портал, пСтруктураНастроек.ТипыОбъектовОбмена.Компания, Ид);
	ИначеЕсли ТипВладельца = "Контакт" Тогда
		ДополнительныеДанные.Вставить("ИнформацияОКонтактах", СтруктураДанных.Клиент);
		ЗагрузитьОбновитьКонтакты(пСтруктураНастроек, ДополнительныеДанные);	
		
		ИнформацияОКлиенте = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(пСтруктураНастроек.Портал, пСтруктураНастроек.ТипыОбъектовОбмена.Контакт, Ид);
	Иначе
		ДополнительныеДанные.Вставить("ИнформацияОКонтактах", СтруктураДанных.Клиент);
		ЗагрузитьОбновитьКонтакты(пСтруктураНастроек, ДополнительныеДанные, Истина);	
		
		ИнформацияОКлиенте = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(пСтруктураНастроек.Портал, пСтруктураНастроек.ТипыОбъектовОбмена.Контакт, Ид);
	КонецЕсли;
	
	Если ИнформацияОКлиенте = Неопределено Тогда
		Возврат Результат;	
	Иначе
		Результат = ИнформацияОКлиенте.Объект; 	
	КонецЕсли;
	
	// Предполагаем, что запросов будет менее 51   т.е. меньше 25 реквизитов(на каждый 2 запроса) и у каждого меньше 50 банк. счетов.
	КоличествоЗапросов = 0;
	ТелоHTTPЗапроса = "";
	
	Для каждого ТекРеквизит Из ДанныеРеквизита Цикл 

		Если КоличествоЗапросов = 50 Тогда
			Прервать;
		КонецЕсли;
		
		ИдРеквизита 	= Формат(ТекРеквизит.Получить("ID"), "ЧГ=0");  
		
		МетодАдреса 	= "crm.address.list?filter[ENTITY_ID]=" + ИдРеквизита + "&filter[ENTITY_TYPE_ID]=8"; 	
		МетодБанкСчета 	= "crm.requisite.bankdetail.list?filter[ENTITY_ID]=" + ИдРеквизита; 	
		
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[A]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодАдреса);
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[B]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодБанкСчета);

		КоличествоЗапросов = КоличествоЗапросов + 1;

	КонецЦикла;
	
	Если ТелоHTTPЗапроса <> "" Тогда
	
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(пСтруктураНастроек, ТелоHTTPЗапроса);
		
		result = СтруктураОтвета.Получить("result");                                             
		Если result <> Неопределено Тогда                                                         
			result2 = result.Получить("result");
			Если result2 <> Неопределено Тогда 			
				
				ДанныеАдресов 		= result2.Получить("A"); 
				ДанныеБанкСчетов 	= result2.Получить("B"); 
				
				Если ДанныеАдресов <> Неопределено Тогда
					СтруктураДанных.Адреса.Добавить(ДанныеАдресов);	
				КонецЕсли;
				
				Если ДанныеБанкСчетов <> Неопределено Тогда
					СтруктураДанных.БанкСчета = ДанныеБанкСчетов;	
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("ИнформацияОРеквизитах"	, ДанныеРеквизита);
	ДополнительныеДанные.Вставить("ИнформацияОАдресах"		, СтруктураДанных.Адреса);
	
	ЗагрузитьОбновитьРеквизиты(пСтруктураНастроек, ДополнительныеДанные); 
	
	ЗагрузитьОбновитьБанковскиеСчетаКомпанийКонтактов(пСтруктураНастроек, СтруктураДанных.БанкСчета);
	
	Возврат Результат;	

КонецФункции

Функция СформироватьБанковскийСчетПоИд(пСтруктураНастроек, Ид) 
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("БанкСчета", Новый Массив);
	
	МетодБанкСчета 	= "crm.requisite.bankdetail.get?id=" + Ид; 	

	ТелоHTTPЗапроса = "";
	ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодБанкСчета);
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(пСтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат Справочники.БанковскиеСчетаКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
	Result = СтруктураОтвета.Получить("result");                                             
	Если result <> Неопределено Тогда                                                         
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда 			
			Если result2.Количество() > 0 Тогда
				СтруктураДанных.БанкСчета.добавить(result2[0]);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЗагрузитьОбновитьБанковскиеСчетаКомпанийКонтактов(пСтруктураНастроек, СтруктураДанных.БанкСчета);
	ИнформацияОБанкСчете = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(пСтруктураНастроек.Портал, пСтруктураНастроек.ТипыОбъектовОбмена.БанковскийСчетРеквизита, Ид);
	
	Если ИнформацияОБанкСчете = Неопределено Тогда
		Возврат Справочники.БанковскиеСчетаКонтрагентов.ПустаяСсылка();	
	Иначе
		Возврат ИнформацияОБанкСчете.Объект; 	
	КонецЕсли;
	
КонецФункции

Функция СформироватьНоменклатуруХарактеристикуПоИд(пСтруктураНастроек, Ид) Экспорт
	
	Результат = Неопределено;         
	
	ДополнительныеДанные = Новый Структура;      
	ДополнительныеДанные.Вставить("Товары", Новый Массив);
	ДополнительныеДанные.Вставить("Предложения", Новый Массив);

	Если пСтруктураНастроек.ВерсияТоваров = "v.2" Тогда
		
		ЭтоТовар = Истина;
		
		ТелоHTTPЗапроса = "&id=" + Ид;	
	
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(пСтруктураНастроек, "/rest/catalog.product.get", ТелоHTTPЗапроса); 
		Если СтруктураОтвета <> Неопределено Тогда
			result = СтруктураОтвета.Получить("result"); 
			Если result <> Неопределено Тогда     
				product = result.Получить("product"); 
				Если product <> Неопределено Тогда        
					Данные = Новый Соответствие;
					Если product.Получить("parentId") = Неопределено Тогда        
						Данные.Вставить("product", product);
						ДополнительныеДанные.Товары.Добавить(Данные);
					Иначе      
						ЭтоТовар = Ложь;  
						Данные.Вставить("offer", product);
						ДополнительныеДанные.Предложения.Добавить(Данные);   
						ДополнительныеДанные.Товары = Б24_КС_ОбщегоНазначенияВызовСервера.ПолучитьВладельцевПредложенийЧерезGET(пСтруктураНастроек, ДополнительныеДанные.Предложения); 
					КонецЕсли;    
					
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;                     
		
		ЗагрузитьОбновитьТоварыПредложения(пСтруктураНастроек, ДополнительныеДанные, ЭтоТовар); 
		
		Если ЭтоТовар Тогда
			ИнформацияОТоваре = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(пСтруктураНастроек.Портал, пСтруктураНастроек.ТипыОбъектовОбмена.Товар2, Ид);
		Иначе
			ИнформацияОТоваре = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(пСтруктураНастроек.Портал, пСтруктураНастроек.ТипыОбъектовОбмена.Предложение, Ид);
		КонецЕсли;
			
		Если ИнформацияОТоваре = Неопределено Тогда
			Возврат Результат;	
		Иначе
			
			Результат = Новый Структура;
			Результат.Вставить("Объект"				, ИнформацияОТоваре.Объект);	
			Результат.Вставить("ПодчиненныйОбъект"	, ИнформацияОТоваре.ПодчиненныйОбъект);	
			Возврат Результат;
			
		КонецЕсли;  
		
	Иначе
		
		мДанных = Новый Массив;
		
		МетодТовара 	= "crm.product.get?id=" + Ид; 	

		ТелоHTTPЗапроса = "";
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодТовара);
		
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(пСтруктураНастроек, ТелоHTTPЗапроса);
		
		Если СтруктураОтвета = Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
		
		Result = СтруктураОтвета.Получить("result");                                             
		Если result <> Неопределено Тогда                                                         
			result2 = result.Получить("result");
			Если result2 <> Неопределено Тогда 			
				Если result2.Количество() > 0 Тогда
					мДанных.Добавить(result2[0]);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ЗагрузитьОбновитьТовары(пСтруктураНастроек, мДанных);
		ИнформацияОТоваре = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(пСтруктураНастроек.Портал, пСтруктураНастроек.ТипыОбъектовОбмена.Товар, Ид);
		
		Если ИнформацияОТоваре = Неопределено Тогда
			Возврат Результат;	
		Иначе
			
			Результат = Новый Структура;
			Результат.Вставить("Объект"				, ИнформацияОТоваре.Объект);	
			Результат.Вставить("ПодчиненныйОбъект"	, ИнформацияОТоваре.ПодчиненныйОбъект);	
			Возврат Результат;
			
		КонецЕсли;  
		
	КонецЕсли;	
	
КонецФункции

Функция СформироватьЕдиницуИзмеренияПоИд(пСтруктураНастроек, Ид) 
	
	Результат = Неопределено;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ЕдиницыИзмерения", Новый Массив);
	
	МетодБанкСчета 	= "crm.measure.get?id=" + Ид; 	
	
	
	ТелоHTTPЗапроса = "";
	ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодБанкСчета);
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(пСтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Result = СтруктураОтвета.Получить("result");                                             
	Если result <> Неопределено Тогда                                                         
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда 			
			Если result2.Количество() > 0 Тогда
				СтруктураДанных.ЕдиницыИзмерения.Добавить(result2[0]);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЗагрузитьОбновитьТовары(пСтруктураНастроек, СтруктураДанных.ЕдиницыИзмерения);
	ИнформацияОЕдиницеИзмерения = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(пСтруктураНастроек.Портал, пСтруктураНастроек.ТипыОбъектовОбмена.ЕдиницаИзмерения, Ид);
	
	Если ИнформацияОЕдиницеИзмерения = Неопределено Тогда
		Возврат Результат;	
	Иначе
		
		Возврат ИнформацияОЕдиницеИзмерения.Объект;
		
	КонецЕсли;
	
КонецФункции

Функция СформироватьСделкуПоИд(пСтруктураНастроек, Ид) Экспорт 
	
	Результат = Документы.ЗаказКлиента.ПустаяСсылка();
	
	ПрефиксВнешнихКодовБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();	
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ИнформацияОСделках"			, Новый Массив);
	СтруктураДанных.Вставить("ИнформацияОРеквизитахСделок"	, Новый Массив);
	СтруктураДанных.Вставить("ИнформацияОТоварахСделок"		, Новый Массив);
	
	МетодСделки				= "crm.deal.get?id=" + Ид; 	
	МетодРеквизитовСделки 	= "crm.requisite.link.get?entityId=" + Ид + "&entityTypeId=2"; 	
	МетодТоваровСделки 		= "crm.deal.productrows.get?id=" + Ид; 	
	
	мЗапросы = Новый Массив;
	мЗапросы.Добавить(МетодСделки);
	мЗапросы.Добавить(МетодРеквизитовСделки);
	мЗапросы.Добавить(МетодТоваровСделки);
	
	ТелоHTTPЗапроса = "";
	ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[S]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодСделки);
	ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[RS]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодРеквизитовСделки);
	ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[TS]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодТоваровСделки);
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(пСтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");                                             
	Если result <> Неопределено Тогда                                                         
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда 		
			
			ДанныеСделок 			= result2.Получить("S"); 
			ДанныеРеквизитовСделок 	= result2.Получить("RS"); 
			ДанныеТоваровСделок 	= result2.Получить("TS"); 
			
			Если ДанныеСделок <> Неопределено Тогда
				СтруктураДанных.ИнформацияОСделках.Добавить(ДанныеСделок);	
			КонецЕсли;
			
			Если ДанныеРеквизитовСделок <> Неопределено Тогда
				СтруктураДанных.ИнформацияОРеквизитахСделок.Добавить(ДанныеРеквизитовСделок);	
			КонецЕсли;
			
			Если ДанныеТоваровСделок <> Неопределено Тогда
				СтруктураДанных.ИнформацияОТоварахСделок = ДанныеТоваровСделок;	
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ЗагрузитьОбновитьСделки(пСтруктураНастроек, СтруктураДанных);
	
	ИнформацияОСделке = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(пСтруктураНастроек.Портал, пСтруктураНастроек.ТипыОбъектовОбмена.Проект, ПрефиксВнешнихКодовБитрикс24.Проекты + Ид);
	
	Если ИнформацияОСделке = Неопределено Тогда
		Возврат Результат;	
	Иначе
		Возврат ИнформацияОСделке.Объект; 	
	КонецЕсли;

КонецФункции

#КонецОбласти


#Область ЗаполнениеПоКонструктору

Функция ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, ДанныеЭлементаБ24, ОтборыДляЗагрузки, ЭтоТовар = Ложь)
	
	Для каждого ТекОтбор Из ОтборыДляЗагрузки Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекОтбор.ВидСравнения) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипПоля 			= ТекОтбор.ТипПоля;
		ЗначениеДляОтбора 	= "";     
		
		КлючЗапроса = ТекОтбор.КлючЗапроса;
		ПреобразоватьКлючJson(КлючЗапроса);
		
		ЗначениеЭлементаБ24 = ДанныеЭлементаБ24.Получить(КлючЗапроса);
#Область ПолучениеЗначенияОтбора			
 		Если ЗначениеЭлементаБ24 <> Неопределено Тогда

			Если ЭтоТовар Тогда
			
				Если ТипЗнч(ЗначениеЭлементаБ24) = Тип("Соответствие") Тогда
					ЗначениеСвойства = ЗначениеЭлементаБ24.Получить("value");
				Иначе
					ЗначениеСвойства = ЗначениеЭлементаБ24; 	
				КонецЕсли;
				
				Если ТипПоля = "S_" ИЛИ ТипПоля = "S_HTML" Тогда  
					
					Если ТипЗнч(ЗначениеСвойства) = Тип("Соответствие") Тогда
						ЗначениеДляОтбора = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ЗначениеСвойства.Получить("TEXT"));
					Иначе
						ЗначениеДляОтбора = СокрЛП(Формат(ЗначениеСвойства, "ЧГ=0"));
					КонецЕсли;
					
				ИначеЕсли ТипПоля = "N_" Тогда  
					ЗначениеДляОтбора = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), ЗначениеСвойства);
				ИначеЕсли ТипПоля = "S_DateTime" ИЛИ ТипПоля = "S_Date"Тогда 
					ЗначениеДляОтбора = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ЗначениеСвойства);
				ИначеЕсли ТипПоля = "S_employee" Тогда  			
					ЗначениеДляОтбора = СокрЛП(Формат(ЗначениеСвойства, "ЧГ=0"));
				ИначеЕсли ТипПоля = "L_" Тогда  
					ЗначениеДляОтбора = СокрЛП(Формат(ЗначениеСвойства, "ЧГ=0"));
				Иначе
					ЗначениеДляОтбора = ЗначениеСвойства;
				КонецЕсли;
			
			Иначе
				
				Если ТипПоля = "boolean" Тогда  	
					ЗначениеДляОтбора =  Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Булево"), ЗначениеЭлементаБ24);
				ИначеЕсли ТипПоля = "string" ИЛИ ТипПоля = "url" Тогда  
					ЗначениеДляОтбора = СокрЛП(Формат(ЗначениеЭлементаБ24, "ЧГ=0"));
				ИначеЕсли ТипПоля = "integer" ИЛИ ТипПоля = "double" Тогда  	
					ЗначениеДляОтбора = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), ЗначениеЭлементаБ24);
				ИначеЕсли ТипПоля = "date" ИЛИ ТипПоля = "datetime" Тогда  
					ЗначениеДляОтбора = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ЗначениеЭлементаБ24);
				ИначеЕсли ТипПоля = "employee" ИЛИ ТипПоля = "S_employee" Тогда  
					ЗначениеДляОтбора = СокрЛП(Формат(ЗначениеЭлементаБ24, "ЧГ=0"));
				ИначеЕсли ТипПоля = "enumeration" ИЛИ ТипПоля = "L_" Тогда  
					ЗначениеДляОтбора = СокрЛП(Формат(ЗначениеЭлементаБ24, "ЧГ=0"));
				Иначе
					ЗначениеДляОтбора = СокрЛП(Формат(ЗначениеЭлементаБ24, "ЧГ=0"));
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
#КонецОбласти					

		Попытка

			Если ТекОтбор.ВидСравнения = "Равно" Тогда
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					Если НЕ ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено Тогда	
						Возврат Ложь;	
					КонецЕсли;
				Иначе
					Если ТекОтбор.Значение <> ЗначениеДляОтбора Тогда	
						Возврат Ложь;	
					КонецЕсли;
				КонецЕсли;
					
			ИначеЕсли ТекОтбор.ВидСравнения = "Не равно" Тогда
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					Если ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено Тогда	
						Возврат Ложь;	
					КонецЕсли;
				Иначе
					Если ТекОтбор.Значение = ЗначениеДляОтбора Тогда	
						Возврат Ложь;	
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли ТекОтбор.ВидСравнения = "Больше" Тогда
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					Если ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено Тогда	
						Возврат Ложь;	
					КонецЕсли;
				Иначе
					Если ТекОтбор.Значение > ЗначениеДляОтбора Тогда	
						Возврат Ложь;	
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ТекОтбор.ВидСравнения = "Меньше" Тогда
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					Если ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено Тогда	
						Возврат Ложь;	
					КонецЕсли;
				Иначе
					Если ТекОтбор.Значение < ЗначениеДляОтбора Тогда	
						Возврат Ложь;	
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли ТекОтбор.ВидСравнения = "Содержит" Тогда
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					
					ЕстьВхождение = Ложь;
					Для каждого ТекЭлемент Из ТекОтбор.Значение Цикл
							
						ЕстьВхождение = СтрНайти(ЗначениеДляОтбора, ТекЭлемент.Значение) > 0;	
						Если ЕстьВхождение Тогда
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
					
					Если НЕ ЕстьВхождение Тогда	
						Возврат Ложь;	
					КонецЕсли;
					
				Иначе
					Если СтрНайти(ЗначениеДляОтбора, ТекОтбор.Значение) = 0 Тогда  
						Возврат Ложь;	
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли ТекОтбор.ВидСравнения = "В списке" Тогда
				
				ЕстьВхождение = Ложь;
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					
					
					ЕстьВхождение = ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено;
							
					Если НЕ ЕстьВхождение Тогда
						Возврат Ложь;	
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли ТекОтбор.ВидСравнения = "Не в списке" Тогда
				
				ЕстьВхождение = Ложь;
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					
					
					ЕстьВхождение = НЕ ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено;
							
					Если НЕ ЕстьВхождение Тогда
						Возврат Ложь;	
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		Исключение
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось сравнить данные для фильтра по загрузке данных в 1С");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат Ложь;	
		КонецПопытки;
		
	КонецЦикла;
		
	Возврат Истина;	
	
КонецФункции

Процедура ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры)
	
	ПоляОбъекта1С 			= ДополнительныеПараметры.ПоляОбъекта1С;
	Объект1С 				= ДополнительныеПараметры.Объект1С;
	
	МетаданныеОбъекта 	= Объект1С.Метаданные();
	РеквизитыОбъекта 	= МетаданныеОбъекта.Реквизиты;
	
	Для каждого ТекПоле Из ПоляОбъекта1С Цикл
		
		Если ТекПоле.ИсточникДанных	= "" ИЛИ ТекПоле.ИсточникДанных = "Не изменять" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекПоле.ТипРеквизита = "Свойство" ИЛИ ТекПоле.ТипРеквизита = "Сведение" Тогда
			Продолжить;
		КонецЕсли;
		
		ПолученноеЗначение = ПолучитьЗначениеРеквизитаОбъекта1СПоКонструктору(СтруктураНастроек, ТекПоле, ДополнительныеПараметры);
		
		Если ПолученноеЗначение = "Битрикс принудительно пропуск" Тогда
			
		ИначеЕсли ПолученноеЗначение <> Неопределено И ТекПоле.ТипРеквизита = "" Тогда
			
			МетаОписаниеРеквизита = РеквизитыОбъекта.Найти(ТекПоле.Реквизит1С);
			
			Если МетаОписаниеРеквизита <> Неопределено Тогда
				
				Если МетаОписаниеРеквизита.Тип.СодержитТип(ТипЗнч(ПолученноеЗначение)) Тогда
					Объект1С[ТекПоле.Реквизит1С] = ПолученноеЗначение;
				Иначе
					
					НовоеЗначениеРеквизита = ПолученноеЗначение;
					
					ТипыРеквизита = МетаОписаниеРеквизита.Тип.Типы(); 
					Для каждого ТекТип Из ТипыРеквизита Цикл
						
						НайденныйМетаОбъект = Метаданные.НайтиПоТипу(ТекТип);
						Если НайденныйМетаОбъект <> Неопределено Тогда
							
							Если Метаданные.Справочники.Содержит(НайденныйМетаОбъект) Тогда
								
								НайденноеЗначениеПоНаименованию = Справочники[НайденныйМетаОбъект.Имя].НайтиПоНаименованию(Строка(ПолученноеЗначение));
								Если ЗначениеЗаполнено(НайденноеЗначениеПоНаименованию) Тогда
									НовоеЗначениеРеквизита = НайденноеЗначениеПоНаименованию;
									Прервать;
								КонецЕсли;
								
							ИначеЕсли Метаданные.Перечисления.Содержит(НайденныйМетаОбъект) Тогда
								
								Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления[НайденныйМетаОбъект.Имя].ЗначенияПеречисления Цикл
								    Если ЗначениеПеречисления.Синоним = Строка(ПолученноеЗначение) Тогда
								        НовоеЗначениеРеквизита = Перечисления[НайденныйМетаОбъект.Имя][ЗначениеПеречисления.Имя];
								        Прервать;
								    КонецЕсли;
								КонецЦикла;	
								
							КонецЕсли;	
						Иначе
							НовоеЗначениеРеквизита = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(ТекТип, ПолученноеЗначение); 	
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
					
					Объект1С[ТекПоле.Реквизит1С] = НовоеЗначениеРеквизита;
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			Объект1С[ТекПоле.Реквизит1С] = ПолученноеЗначение;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДополнительныеРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры)
	
	ПоляОбъекта1С 			= ДополнительныеПараметры.ПоляОбъекта1С;
	Объект1С 				= ДополнительныеПараметры.Объект1С;
	ЭтоНовыйОбъект 			= ДополнительныеПараметры.ЭтоНовыйОбъект;
	ТаблицаСвойств 			= ДополнительныеПараметры.ТаблицаСвойств;
	
	мНеизменяемыеСвойства = Новый Массив;
	Для каждого ТекЭлемент Из ДополнительныеПараметры.ПоляОбъекта1С Цикл   
		Если ТекЭлемент.ИсточникДанных = "Не изменять" Тогда    
			мНеизменяемыеСвойства.Добавить(ТекЭлемент.СсылкаНаСвойство);	 
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекСвойство Из ТаблицаСвойств Цикл
		
		Если СтруктураНастроек.ПредопределенныеСвойства.НеЗагружаемыеЗначенияСвойств.Найти(ТекСвойство.Свойство) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если мНеизменяемыеСвойства.Найти(ТекСвойство.Свойство) <> Неопределено Тогда	
			Продолжить;
		КонецЕсли;   
		
		Если НЕ ТекСвойство.ЭтоДополнительноеСведение И ТекСвойство.ПоддерживаетсяСвойство Тогда   					
		
			ЗначениеОчищено	= ТекСвойство.ЗначениеОчищено;
			ЗначениеСвойства = ТекСвойство.ЗначениеСвойства; 
			Для каждого ТекПоле Из ПоляОбъекта1С Цикл
				Если ТекПоле.СсылкаНаСвойство = ТекСвойство.Свойство Тогда
					Если ТекПоле.ИсточникДанных <> "Из пользовательского поля" Тогда
						ЗначениеОчищено = Ложь;
						ЗначениеСвойства = ПолучитьЗначениеРеквизитаОбъекта1СПоКонструктору(СтруктураНастроек, ТекПоле, ДополнительныеПараметры);   
					КонецЕсли;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ЗначениеОчищено ИЛИ НЕ ЗначениеЗаполнено(ЗначениеСвойства)Тогда
				Если ЭтоНовыйОбъект Тогда
					Продолжить;
				КонецЕсли;
				
				Для Каждого ЗаполнениеСвойствоТовара Из Объект1С.ДополнительныеРеквизиты Цикл
					Если ЗаполнениеСвойствоТовара.Свойство = ТекСвойство.Свойство Тогда
						ЗаполнениеСвойствоТовара.Значение 		 = Неопределено;
						ЗаполнениеСвойствоТовара.ТекстоваяСтрока = "";
					КонецЕсли;
				КонецЦикла;
			Иначе
				
				ЕстьРеквизит = Ложь;
				Для Каждого ЗаполнениеСвойствоТовара Из Объект1С.ДополнительныеРеквизиты Цикл
					
					Если ЗаполнениеСвойствоТовара.Свойство = ТекСвойство.Свойство Тогда
						
						ЗаполнениеСвойствоТовара.Значение = ЗначениеСвойства;
						Если ТипЗнч(ЗаполнениеСвойствоТовара.Значение) = Тип("Строка") Тогда
							ЗаполнениеСвойствоТовара.ТекстоваяСтрока = ЗаполнениеСвойствоТовара.Значение;
						КонецЕсли;
						
						ЕстьРеквизит = Истина
						
					КонецЕсли;
					
				КонецЦикла;
				
				Если НЕ ЕстьРеквизит Тогда
					НовыйРеквизит = Объект1С.ДополнительныеРеквизиты.Добавить();
					НовыйРеквизит.Свойство 	= ТекСвойство.Свойство; 
					НовыйРеквизит.Значение 	= ЗначениеСвойства; 
					Если ТипЗнч(НовыйРеквизит.Значение) = Тип("Строка") Тогда
						НовыйРеквизит.ТекстоваяСтрока = НовыйРеквизит.Значение;
					КонецЕсли;
				КонецЕсли;	
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДополнительныеСведенияОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры)
	
	ПоляОбъекта1С 			= ДополнительныеПараметры.ПоляОбъекта1С;
	Объект1С 				= ДополнительныеПараметры.Объект1С.Ссылка;
	ЭтоНовыйОбъект 			= ДополнительныеПараметры.ЭтоНовыйОбъект;
	ТаблицаСвойств 			= ДополнительныеПараметры.ТаблицаСвойств;
	
	мНеизменяемыеСвойства = Новый Массив;
	Для каждого ТекЭлемент Из ДополнительныеПараметры.ПоляОбъекта1С Цикл   
		Если ТекЭлемент.ИсточникДанных = "Не изменять" Тогда    
			мНеизменяемыеСвойства.Добавить(ТекЭлемент.СсылкаНаСвойство);	 
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект1С);
	НаборЗаписей.Прочитать();
	
	Для Каждого ТекСвойство Из ТаблицаСвойств Цикл
		
		Если мНеизменяемыеСвойства.Найти(ТекСвойство.Свойство) <> Неопределено Тогда	
			Продолжить;
		КонецЕсли;   
		
		Если ТекСвойство.ЭтоДополнительноеСведение И ТекСвойство.ПоддерживаетсяСвойство Тогда   					
			
			ЗначениеСвойства = ТекСвойство.ЗначениеСвойства;
			Для каждого ТекПоле Из ПоляОбъекта1С Цикл
				Если ТекПоле.СсылкаНаСвойство = ТекСвойство.Свойство Тогда
					Если ТекПоле.ИсточникДанных <> "Из пользовательского поля" Тогда
						ЗначениеСвойства = ПолучитьЗначениеРеквизитаОбъекта1СПоКонструктору(СтруктураНастроек, ТекПоле, ДополнительныеПараметры);
					КонецЕсли;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			ЕстьСвойство = Ложь;
			Для Каждого ТекСтрока Из НаборЗаписей Цикл
				Если ТекСтрока.Свойство = ТекСвойство.Свойство Тогда
					ЕстьСвойство = Истина;
					ТекСтрока.Значение = ЗначениеСвойства;	
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьСвойство = Ложь Тогда
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Объект = Объект1С;
				НоваяЗапись.Свойство = ТекСвойство.Свойство;
				НоваяЗапись.Значение = ЗначениеСвойства;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
	НаборЗаписей.Записать();
	
КонецПроцедуры


Процедура ЗаполнитьРеквизитыТЧОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры, ИмяТЧ)
	
	ПоляТЧОбъекта1С 		= ДополнительныеПараметры.ПоляТЧОбъекта1С;
	Объект1С 				= ДополнительныеПараметры.Объект1С;
	СтрокаТЧ 				= ДополнительныеПараметры.СтрокаТЧ;
	МетаданныеТЧ            = ДополнительныеПараметры.МетаданныеТЧ;
	
	РеквизитыТЧОбъекта 	= МетаданныеТЧ.Реквизиты;
	
	Для каждого ТекПоле Из ПоляТЧОбъекта1С Цикл
		
		Если ТекПоле.ИсточникДанных	= "" ИЛИ ТекПоле.ИсточникДанных = "Не изменять" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекПоле.ИмяТабЧасти <> ИмяТЧ Тогда
			Продолжить;
		КонецЕсли;
		
		ПолученноеЗначение = ПолучитьЗначениеРеквизитаТЧОбъекта1СПоКонструктору(СтруктураНастроек, ТекПоле, ДополнительныеПараметры);
		Если ПолученноеЗначение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		МетаОписаниеРеквизита = РеквизитыТЧОбъекта.Найти(ТекПоле.Реквизит1С);
		Если МетаОписаниеРеквизита = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если МетаОписаниеРеквизита.Тип.СодержитТип(ТипЗнч(ПолученноеЗначение)) Тогда
			СтрокаТЧ[ТекПоле.Реквизит1С] = ПолученноеЗначение;
		Иначе
			
			НовоеЗначениеРеквизита = ПолученноеЗначение;
			
			ТипыРеквизита = МетаОписаниеРеквизита.Тип.Типы(); 
			Для каждого ТекТип Из ТипыРеквизита Цикл
				
				НайденныйМетаОбъект = Метаданные.НайтиПоТипу(ТекТип);
				Если НайденныйМетаОбъект <> Неопределено Тогда
					
					Если Метаданные.Справочники.Содержит(НайденныйМетаОбъект) Тогда
						
						НайденноеЗначениеПоНаименованию = Справочники[НайденныйМетаОбъект.Имя].НайтиПоНаименованию(Строка(ПолученноеЗначение));
						Если ЗначениеЗаполнено(НайденноеЗначениеПоНаименованию) Тогда
							НовоеЗначениеРеквизита = НайденноеЗначениеПоНаименованию;
							Прервать;
						КонецЕсли;
						
					КонецЕсли;	
					
				КонецЕсли;
				
			КонецЦикла;
			
			СтрокаТЧ[ТекПоле.Реквизит1С] = НовоеЗначениеРеквизита;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


Функция ПолучитьЗначениеРеквизитаОбъекта1СПоКонструктору(СтруктураНастроек, ОписаниеПоля, ДополнительныеПараметры)
	
	Результат = Неопределено;   // при неопределено реквизит не очищается, а пропускается значение  . также работает параметр "Битрикс принудительно пропуск"
	
	Объект1С 				= ДополнительныеПараметры.Объект1С;
	МенеджерВременныхТаблиц = ДополнительныеПараметры.МенеджерВременныхТаблиц;
	
	ИдЭлементаБ24 			= ДополнительныеПараметры.ИдЭлементаБ24;
	ДанныеЭлементаБ24 		= ДополнительныеПараметры.ДанныеЭлементаБ24;
	ПомеченНаУдаление 		= ДополнительныеПараметры.ПомеченНаУдаление;
	
	Если НЕ (ЗначениеЗаполнено(ОписаниеПоля.Значение) ИЛИ ОписаниеПоля.ИсточникДанных = "Из основания") Тогда
		Возврат Результат;
	КонецЕсли;
	
	ЗначениеКлюча = Неопределено;
	
	Попытка
		
		Если ОписаниеПоля.ИсточникДанных = "Фиксированное значение" Тогда
			
			Результат = ОписаниеПоля.Значение;	     
			
		ИначеЕсли ОписаниеПоля.ИсточникДанных = "Ключ JSON" Тогда
			
			Результат = ДанныеЭлементаБ24.Получить(ОписаниеПоля.Значение);
			
		ИначеЕсли ОписаниеПоля.ИсточникДанных = "Из основания" Тогда
			                          
			Основание = ДополнительныеПараметры.Основание;
			
			РеквизитЗаказа = ОписаниеПоля.Реквизит1С;
			РеквизитЗаказа = ?(РеквизитЗаказа = "ВалютаВзаиморасчетов"		, "Валюта"						, РеквизитЗаказа);
			РеквизитЗаказа = ?(РеквизитЗаказа = "ВалютаВзаиморасчетов"		, "Валюта"						, РеквизитЗаказа);
			РеквизитЗаказа = ?(РеквизитЗаказа = "БанковскийСчетОрганизации"	, "БанковскийСчет"				, РеквизитЗаказа);
			РеквизитЗаказа = ?(РеквизитЗаказа = "Подразделение"				, "Подразделение"				, РеквизитЗаказа);
			РеквизитЗаказа = ?(РеквизитЗаказа = "СтруктурнаяЕдиница"		, "Склад"						, РеквизитЗаказа);
			
			Если Основание.Метаданные().Реквизиты.Найти(РеквизитЗаказа) <> Неопределено Тогда
				Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, РеквизитЗаказа); 
			Иначе	
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось заполнить реквизит: " + РеквизитЗаказа + " из заказа:" + Строка(Основание));
			КонецЕсли;
			
		ИначеЕсли ОписаниеПоля.ИсточникДанных = "Предопределенный алгоритм" Тогда
			
#Область ОбщиеАлгоритмы				
			Если ОписаниеПоля.Значение = "Удален на портале" Тогда
				
				Результат = ПомеченНаУдаление;	
				
			ИначеЕсли ОписаниеПоля.Значение = "Ответственный" Тогда
				
				Результат = "Битрикс принудительно пропуск";
				
				Если ОписаниеПоля.ВидСущности = "Заказ" ИЛИ ОписаниеПоля.ВидСущности = "Оплата" ИЛИ ОписаниеПоля.ВидСущности = "Отгрузка" Тогда
					
					ИдСотрудника 	= ДанныеЭлементаБ24.Получить("responsibleId");
					Если ЗначениеЗаполнено(ИдСотрудника) Тогда
						
						ИдСотрудника 	= Формат(ИдСотрудника,"ЧГ=0");
						
						НайденнаяСтрока = СтруктураНастроек.ТаблицаСопоставленияПользователей.Найти(ИдСотрудника, "ИдПользователя");
						Если НайденнаяСтрока <> Неопределено Тогда
							Результат = НайденнаяСтрока.Пользователь1С; 					
						КонецЕсли;
						
					КонецЕсли;
					
					Если НЕ ЗначениеЗаполнено(Результат) Тогда
						Результат = СтруктураНастроек.НастройкиСинхронизацииЗаказов.Ответственный;	
					КонецЕсли;
					
				Иначе
					
					ИдСотрудника 	= ДанныеЭлементаБ24.Получить("ASSIGNED_BY_ID");
					Если НЕ ЗначениеЗаполнено(ИдСотрудника) Тогда
						ИдСотрудника 	= ДанныеЭлементаБ24.Получить("RESPONSIBLE_ID");
					КонецЕсли;
					Если НЕ ЗначениеЗаполнено(ИдСотрудника) Тогда
						ИдСотрудника 	= ДанныеЭлементаБ24.Получить("assignedById");
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ИдСотрудника) Тогда
						
						ИдСотрудника 	= Формат(ИдСотрудника,"ЧГ=0");
						
						НайденнаяСтрока = СтруктураНастроек.ТаблицаСопоставленияПользователей.Найти(ИдСотрудника, "ИдПользователя");
						Если НайденнаяСтрока <> Неопределено Тогда
							Результат = НайденнаяСтрока.Пользователь1С; 					
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли ОписаниеПоля.Значение = "Валюта" Тогда
				
				Результат = ДополнительныеПараметры.Валюта;	
				
			ИначеЕсли ОписаниеПоля.Значение = "Вид цен" Тогда
				
				Если СтруктураНастроек.ВерсияТоваров = "v.2" Тогда   
					Если СтруктураНастроек.НастройкиСинхронизацииТоваров.Свойство("Прайсы") 
						И СтруктураНастроек.НастройкиСинхронизацииТоваров.Прайсы.Количество()>0 Тогда
						Результат = СтруктураНастроек.НастройкиСинхронизацииТоваров.Прайсы[0].Значение;	
					КонецЕсли;
				Иначе        
					СтруктураНастроек.НастройкиСинхронизацииТоваров.Свойство("ТипЦены", Результат);
				КонецЕсли;
				
			КонецЕсли;	
#КонецОбласти	
			
			Если ОписаниеПоля.ВидСущности = "Компания" Тогда
#Область Компании	
				Если ОписаниеПоля.Значение = "Наименование" Тогда
					
					Результат = Строка(ДанныеЭлементаБ24.Получить("TITLE"));	     
					
				ИначеЕсли ОписаниеПоля.Значение = "Публичное наименование компании" Тогда
					
					Результат = ?(ЗначениеЗаполнено(Объект1С.НаименованиеПолное), Объект1С.НаименованиеПолное, Строка(ДанныеЭлементаБ24.Получить("TITLE")))	
					
				ИначеЕсли ОписаниеПоля.Значение = "Сокращенное юр. наименование компании" Тогда
					
					Результат = ?(ЗначениеЗаполнено(Объект1С.НаименованиеСокращенное), Объект1С.НаименованиеСокращенное, Строка(ДанныеЭлементаБ24.Получить("TITLE")))	
					
				ИначеЕсли ОписаниеПоля.Значение = "Это покупатель" Тогда
				
					Результат = "Битрикс принудительно пропуск";
					
					ТипКомпании		= ДанныеЭлементаБ24.Получить("COMPANY_TYPE");
					НайденнаяСтрока = СтруктураНастроек.ПресетыТиповКомпаний.Найти(ТипКомпании);
					Если НайденнаяСтрока <> Неопределено Тогда
						Результат = НайденнаяСтрока.Клиент;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Это поставщик" Тогда
				
					Результат = "Битрикс принудительно пропуск";
					
					ТипКомпании		= ДанныеЭлементаБ24.Получить("COMPANY_TYPE");
					НайденнаяСтрока = СтруктураНастроек.ПресетыТиповКомпаний.Найти(ТипКомпании);
					Если НайденнаяСтрока <> Неопределено Тогда
						Результат = НайденнаяСтрока.Поставщик;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Это прочие отношения" Тогда
				
					Результат = "Битрикс принудительно пропуск";
					
					ТипКомпании		= ДанныеЭлементаБ24.Получить("COMPANY_TYPE");
					НайденнаяСтрока = СтруктураНастроек.ПресетыТиповКомпаний.Найти(ТипКомпании);
					Если НайденнаяСтрока <> Неопределено Тогда
						Результат = НайденнаяСтрока.ПрочиеОтношения;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Это конкурент" Тогда
				
					Результат = "Битрикс принудительно пропуск";
					
					ТипКомпании		= ДанныеЭлементаБ24.Получить("COMPANY_TYPE");
					НайденнаяСтрока = СтруктураНастроек.ПресетыТиповКомпаний.Найти(ТипКомпании);
					Если НайденнаяСтрока <> Неопределено Тогда
						Результат = НайденнаяСтрока.Конкурент;	
					КонецЕсли;
					
				КонецЕсли;		
#КонецОбласти				
			ИначеЕсли ОписаниеПоля.ВидСущности = "Контакт" Тогда					
#Область Контакт 
				Если ОписаниеПоля.Значение = "Пол" Тогда
				
					Результат = "Битрикс принудительно пропуск";
				
					Пол = Неопределено;
					ИдПола  = ДанныеЭлементаБ24.Получить(СтруктураНастроек.ДопИдПредопределенногоСвойстваПолаКонтакта);
					Если ЗначениеЗаполнено(ИдПола) Тогда
						ЗнСвПола = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЗначенийСвойств", ИдПола);
						Если ЗначениеЗаполнено(ЗнСвПола) Тогда
							Если ЗнСвПола.Наименование = "Мужской" Тогда
								Результат = Перечисления.ПолФизическогоЛица.Мужской;	
							Иначе
								Результат = Перечисления.ПолФизическогоЛица.Женский;	
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;			
					
				ИначеЕсли ОписаниеПоля.Значение = "Владелец контактного лица" Тогда
					
					Результат 	= ДополнительныеПараметры.Компания;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Дата рождения" Тогда
					
					Результат 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"),   ДанныеЭлементаБ24.Получить("BIRTHDATE"));
					
				ИначеЕсли ОписаниеПоля.Значение = "Наименование" Тогда
					
					Имя 		= Строка(ДанныеЭлементаБ24.Получить("NAME"));
					Фамилия 	= Строка(ДанныеЭлементаБ24.Получить("LAST_NAME"));
					Отчество 	= Строка(ДанныеЭлементаБ24.Получить("SECOND_NAME"));   
					ЭтоИП 		= Строка(ДанныеЭлементаБ24.Получить("HONORIFIC"))= "ИП";	
					ДанныеФИО	= Б24_К_ОбщегоНазначенияВызовСервера.ПреобразоватьФИО(Фамилия, Имя, Отчество, ЭтоИП); 
					
					Результат 	= СокрЛП(ДанныеФИО.Фамилия + " " + ДанныеФИО.Имя + " " + ДанныеФИО.Отчество); 
					
				ИначеЕсли ОписаниеПоля.Значение = "Полное наименование контакта" Тогда
					
					Имя 		= Строка(ДанныеЭлементаБ24.Получить("NAME"));
					Фамилия 	= Строка(ДанныеЭлементаБ24.Получить("LAST_NAME"));
					Отчество 	= Строка(ДанныеЭлементаБ24.Получить("SECOND_NAME"));
					
					ДанныеФИО	= Б24_К_ОбщегоНазначенияВызовСервера.ПреобразоватьФИО(Фамилия, Имя, Отчество); 
					
					НаименованиеКонтрагента = СокрЛП(ДанныеФИО.Фамилия + " " + ДанныеФИО.Имя + " " + ДанныеФИО.Отчество); 
					
					Результат = ?(ЗначениеЗаполнено(Объект1С.НаименованиеПолное), Объект1С.НаименованиеПолное, НаименованиеКонтрагента);	
					
				ИначеЕсли ОписаниеПоля.Значение = "Это поставщик" Тогда
				
					Результат = "Битрикс принудительно пропуск";
					
					ТипКонтакта = Строка(ДанныеЭлементаБ24.Получить("TYPE_ID"));
					НайденнаяСтрока = СтруктураНастроек.ПресетыТиповКонтактов.Найти(ТипКонтакта);
					Если НайденнаяСтрока <> Неопределено Тогда
						Результат = НайденнаяСтрока.Поставщик;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Это покупатель" Тогда
				
					Результат = "Битрикс принудительно пропуск";
					
					ТипКонтакта = Строка(ДанныеЭлементаБ24.Получить("TYPE_ID"));
					НайденнаяСтрока = СтруктураНастроек.ПресетыТиповКонтактов.Найти(ТипКонтакта);
					Если НайденнаяСтрока <> Неопределено Тогда
						Результат = НайденнаяСтрока.Клиент;	
					КонецЕсли;
			
				ИначеЕсли ОписаниеПоля.Значение = "Это прочие отношения" Тогда
				
					Результат = "Битрикс принудительно пропуск";
					
					ТипКонтакта = Строка(ДанныеЭлементаБ24.Получить("TYPE_ID"));
					НайденнаяСтрока = СтруктураНастроек.ПресетыТиповКонтактов.Найти(ТипКонтакта);
					Если НайденнаяСтрока <> Неопределено Тогда
						Результат = НайденнаяСтрока.ПрочиеОтношения;	
					КонецЕсли;
			
				ИначеЕсли ОписаниеПоля.Значение = "Это конкурент" Тогда
				
					Результат = "Битрикс принудительно пропуск";
					
					НайденнаяСтрока = СтруктураНастроек.ПресетыТиповКонтактов.Найти(ТипКонтакта);
					Если НайденнаяСтрока <> Неопределено Тогда
						Результат = НайденнаяСтрока.Конкурент;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Должность" Тогда
				
					Результат = Строка(ДанныеЭлементаБ24.Получить("POST"));
					
				КонецЕсли;					
#КонецОбласти			
			ИначеЕсли ОписаниеПоля.ВидСущности = "Реквизит" Тогда
#Область Реквизиты	
				Если ОписаниеПоля.Значение = "Юр/Физлицо" Тогда
	
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						
						НайденнаяСтрока = СтруктураНастроек.ПресетыШаблоновРеквизитов.Найти(ДанныеЭлементаБ24.Получить("PRESET_ID"), "ИдШаблона");
						Если НайденнаяСтрока <> Неопределено Тогда
							Результат = НайденнаяСтрока.ТипКонтрагента; 	
						Иначе
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось определить шаблон Битрикс24 для реквизита: " + ДополнительныеПараметры.Наименование);
						КонецЕсли;
					Иначе
						Результат = Объект1С.ЮрФизЛицо;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Юридическое физическое лицо" Тогда
	
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						
						НайденнаяСтрока = СтруктураНастроек.ПресетыШаблоновРеквизитов.Найти(ДанныеЭлементаБ24.Получить("PRESET_ID"), "ИдШаблона");
						Если НайденнаяСтрока <> Неопределено Тогда
							Если НайденнаяСтрока.ТипКонтрагента = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
								Результат = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;	
							Иначе
								Результат = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;	
							КонецЕсли;
						Иначе
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось определить шаблон Битрикс24 для реквизита: " + ДополнительныеПараметры.Наименование);
						КонецЕсли;
						
					Иначе
						Результат = Объект1С.ЮридическоеФизическоеЛицо;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Наименование" Тогда
					
					Наименование 	= "";
					
					Фамилия 		= Строка(ДанныеЭлементаБ24.Получить("RQ_LAST_NAME")); 
					Имя 			= Строка(ДанныеЭлементаБ24.Получить("RQ_FIRST_NAME")); 
					Отчество 		= Строка(ДанныеЭлементаБ24.Получить("RQ_SECOND_NAME")); 
					ФИО				= Фамилия + " " + Имя + " " + Отчество; 
					
					ТипРеквизита 	= Строка(ДанныеЭлементаБ24.Получить("ENTITY_TYPE_ID"));
					
					Если ТипРеквизита = "3" Тогда     
						Наименование		= ФИО;
						Наименование		= ?(ЗначениеЗаполнено(Наименование), Наименование, Строка(ДанныеЭлементаБ24.Получить("RQ_NAME")));
					Иначе
						Наименование 		= Строка(ДанныеЭлементаБ24.Получить("RQ_COMPANY_NAME"));
						Наименование		= ?(ЗначениеЗаполнено(Наименование), Наименование, ФИО);
						Наименование		= ?(ЗначениеЗаполнено(Наименование), Наименование, Строка(ДанныеЭлементаБ24.Получить("RQ_COMPANY_FULL_NAME")));
					КонецЕсли;
					
					Результат = Наименование;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Наименование полное" Тогда
					
					Если ДополнительныеПараметры.ОбновлятьВ1С ИЛИ ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
							
						НаименованиеПолное = "";
						
						Фамилия 		= Строка(ДанныеЭлементаБ24.Получить("RQ_LAST_NAME")); 
						Имя 			= Строка(ДанныеЭлементаБ24.Получить("RQ_FIRST_NAME")); 
						Отчество 		= Строка(ДанныеЭлементаБ24.Получить("RQ_SECOND_NAME")); 
						ФИО				= Фамилия + " " + Имя + " " + Отчество; 
						
						ТипРеквизита 	= Строка(ДанныеЭлементаБ24.Получить("ENTITY_TYPE_ID"));
						
						Если ТипРеквизита = "3" Тогда     
							Наименование		= ФИО;
							Наименование		= ?(ЗначениеЗаполнено(Наименование), Наименование, Строка(ДанныеЭлементаБ24.Получить("RQ_NAME")));
							НаименованиеПолное	= Наименование;
						Иначе
							Наименование 		= Строка(ДанныеЭлементаБ24.Получить("RQ_COMPANY_NAME"));
							Наименование		= ?(ЗначениеЗаполнено(Наименование), Наименование, ФИО);
							Наименование		= ?(ЗначениеЗаполнено(Наименование), Наименование, Строка(ДанныеЭлементаБ24.Получить("RQ_COMPANY_FULL_NAME")));
							НаименованиеПолное 	= Строка(ДанныеЭлементаБ24.Получить("RQ_COMPANY_FULL_NAME"));
							НаименованиеПолное	= ?(ЗначениеЗаполнено(НаименованиеПолное), НаименованиеПолное, Наименование);
						КонецЕсли;
						
						Результат = НаименованиеПолное;	
					Иначе
						Результат = Объект1С.НаименованиеПолное;

					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Свидетельство с.н" Тогда
					
					Если ДополнительныеПараметры.ОбновлятьВ1С ИЛИ ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						СвидетельствоСерияНомер	= Строка(ДанныеЭлементаБ24.Получить("RQ_IDENT_DOC_SER")) + " " + Строка(ДанныеЭлементаБ24.Получить("RQ_IDENT_DOC_NUM"));
						Результат = СвидетельствоСерияНомер;	
					Иначе
						Результат = Объект1С.СвидетельствоСерияНомер;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Свидетельство дата выдачи" Тогда
					
					Если ДополнительныеПараметры.ОбновлятьВ1С ИЛИ ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						СвидетельствоДатаВыдачи = ПолучитьДатуИзСтрокиJSON(Строка(ДанныеЭлементаБ24.Получить("RQ_IDENT_DOC_DATE")));
						Результат = СвидетельствоДатаВыдачи;	
					Иначе
						Результат = Объект1С.СвидетельствоДатаВыдачи;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "ИНН" Тогда
					
					Если ДополнительныеПараметры.ОбновлятьВ1С ИЛИ ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						ИНН = Строка(ДанныеЭлементаБ24.Получить("RQ_INN"));
						Результат = ИНН;	
					Иначе
						Результат = Объект1С.ИНН;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "КПП" Тогда
					
					Если ДополнительныеПараметры.ОбновлятьВ1С ИЛИ ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						КПП	= Строка(ДанныеЭлементаБ24.Получить("RQ_KPP"));
						Результат = КПП;	
					Иначе
						Результат = Объект1С.КПП;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Код по ОКПО" Тогда
					
					Если ДополнительныеПараметры.ОбновлятьВ1С ИЛИ ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						КодПоОКПО = Строка(ДанныеЭлементаБ24.Получить("RQ_OKPO"));
						Результат = КодПоОКПО;	
					Иначе
						Результат = Объект1С.КодПоОКПО;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "ОГРН" И ОписаниеПоля.ИмяОбъекта = "Организации"Тогда
					
					Если ДополнительныеПараметры.ОбновлятьВ1С ИЛИ ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						ОГРН = Строка(ДанныеЭлементаБ24.Получить("RQ_OGRN"));
						ОГРН = ?(ЗначениеЗаполнено(ОГРН), ОГРН, Строка(ДанныеЭлементаБ24.Получить("RQ_OGRNIP")));
						Результат = ОГРН;	
					Иначе
						Результат = Объект1С.ОГРН;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "ОГРН" И ОписаниеПоля.ИмяОбъекта = "Контрагенты"Тогда
					
					Если ДополнительныеПараметры.ОбновлятьВ1С ИЛИ ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						ОГРН = Строка(ДанныеЭлементаБ24.Получить("RQ_OGRN"));
						ОГРН = ?(ЗначениеЗаполнено(ОГРН), ОГРН, Строка(ДанныеЭлементаБ24.Получить("RQ_OGRNIP")));
						Результат = ОГРН;	
					Иначе
						Результат = Объект1С.РегистрационныйНомер;
					КонецЕсли;
					
				КонецЕсли;					
#КонецОбласти				
			ИначеЕсли ОписаниеПоля.ВидСущности = "Банковский счет" Тогда
#Область БанкСчета	
				Если ОписаниеПоля.Значение = "Наименование" Тогда
	
		        	Результат = Строка(ДанныеЭлементаБ24.Получить("NAME"));	     
	
				ИначеЕсли ОписаниеПоля.Значение = "Номер счета" Тогда
	
		        	Результат = Строка(ДанныеЭлементаБ24.Получить("RQ_ACC_NUM"));	     
					
				ИначеЕсли ОписаниеПоля.Значение = "Ручное изменение реквизитов банка" Тогда
					
					Результат = НЕ ЗначениеЗаполнено(ДополнительныеПараметры.Банк);
					
				ИначеЕсли ОписаниеПоля.Значение = "СВИФТ банка" Тогда
				
					Результат = "Битрикс принудительно пропуск";
				
					Если НЕ ЗначениеЗаполнено(ДополнительныеПараметры.Банк) Тогда
						Результат = Строка(ДанныеЭлементаБ24.Получить("RQ_SWIFT"));
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Наименование банка" Тогда
				
					Результат = "Битрикс принудительно пропуск";
				
					Если НЕ ЗначениеЗаполнено(ДополнительныеПараметры.Банк) Тогда
						Результат = Строка(ДанныеЭлементаБ24.Получить("RQ_BANK_NAME"));
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Адрес банка" Тогда
				
					Результат = "Битрикс принудительно пропуск";
				
					Если НЕ ЗначениеЗаполнено(ДополнительныеПараметры.Банк) Тогда
						Результат = Строка(ДанныеЭлементаБ24.Получить("RQ_BANK_ADDR"));
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "БИК банка" Тогда
				
					Результат = "Битрикс принудительно пропуск";
				
					Если НЕ ЗначениеЗаполнено(ДополнительныеПараметры.Банк) Тогда
						Результат = Строка(ДанныеЭлементаБ24.Получить("RQ_BIK"));
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Корр cчет банка" Тогда
				
					Результат = "Битрикс принудительно пропуск";
				
					Если НЕ ЗначениеЗаполнено(ДополнительныеПараметры.Банк) Тогда
						Результат = Строка(ДанныеЭлементаБ24.Получить("RQ_COR_ACC_NUM"));
					КонецЕсли;
				
				ИначеЕсли ОписаниеПоля.Значение = "Банк" Тогда
					
					Результат 	= ДополнительныеПараметры.Банк;
	
				КонецЕсли;
					
#КонецОбласти				
			ИначеЕсли ОписаниеПоля.ВидСущности = "Товар" Тогда
#Область Товар
				Если ОписаниеПоля.Значение = "Наименование" Тогда
					
					Результат = ДополнительныеПараметры.НаименованиеНоменклатуры;
					
				ИначеЕсли ОписаниеПоля.Значение = "Полное наименование" Тогда
					
					Результат = ?(ЗначениеЗаполнено(Объект1С.НаименованиеПолное), Объект1С.НаименованиеПолное, ДополнительныеПараметры.НаименованиеНоменклатуры);
					
				ИначеЕсли ОписаниеПоля.Значение = "Группа" Тогда
					
					ИдРодителя 		= Формат(ДанныеЭлементаБ24.Получить("SECTION_ID"),"ЧГ=0");
					Если ЗначениеЗаполнено(ИдРодителя) Тогда
						
						Родитель = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыГрупп", ИдРодителя);
						Если НЕ ЗначениеЗаполнено(СтруктураНастроек.НастройкиСинхронизацииТоваров.ДеревоГрупп) Тогда
							Результат = Родитель;
						Иначе
							Результат = "Битрикс принудительно пропуск";
						КонецЕсли;	
						
					Иначе
						
						Результат = "Битрикс принудительно пропуск";
						
					КонецЕсли;				
					
				ИначеЕсли ОписаниеПоля.Значение = "Артикул" Тогда
					
					АртикулДанные   = ДанныеЭлементаБ24.Получить("PROPERTY_" + СтруктураНастроек.ИдПредопределенногоСвойстваАртикулаНоменклатуры);
					Результат = ?(АртикулДанные = Неопределено, Неопределено, АртикулДанные.Получить("value"));
					
				ИначеЕсли ОписаниеПоля.Значение = "Ставка НДС" Тогда
					
					СтавкаНДС = Строка(ДанныеЭлементаБ24.Получить("VAT_ID"));
	
					НайденнаяСтавкаНДС = СтруктураНастроек.ПресетыСтавокНДС.Найти(СтавкаНДС);
					Если НайденнаяСтавкаНДС <> Неопределено Тогда
						Результат = НайденнаяСтавкаНДС.СтавкаНДС;	
					Иначе
						Результат = ДополнительныеПараметры.ВидНоменклатуры.СтавкаНДС;	
					КонецЕсли;		
					
				ИначеЕсли ОписаниеПоля.Значение = "Вид номенклатуры" Тогда
					
					Результат = ДополнительныеПараметры.ВидНоменклатуры;
					
				ИначеЕсли ОписаниеПоля.Значение = "Тип номенклатуры" Тогда
					
					Результат = ДополнительныеПараметры.ВидНоменклатуры.ТипНоменклатуры;
					
				ИначеЕсли ОписаниеПоля.Значение = "Качество" Тогда
					
					Результат = ?(ЗначениеЗаполнено(Объект1С.Качество), Объект1С.Качество, Перечисления.ГрадацииКачества.Новый);
					
				ИначеЕсли ОписаниеПоля.Значение = "Использование характеристик" Тогда
					
					Результат = ДополнительныеПараметры.ВидНоменклатуры.ИспользованиеХарактеристик;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Использовать упаковки" Тогда
					
					Результат = ДополнительныеПараметры.ВидНоменклатуры.ИспользоватьУпаковки;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Набор упаковок" Тогда
					
					Результат = ДополнительныеПараметры.ВидНоменклатуры.НаборУпаковок;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Вариант оформления продажи" Тогда
					
					Результат = ДополнительныеПараметры.ВидНоменклатуры.ВариантОформленияПродажи;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Группа доступа" Тогда
					
					Результат = ?(ЗначениеЗаполнено(Объект1С.ГруппаДоступа), Объект1С.ГруппаДоступа, ДополнительныеПараметры.ВидНоменклатуры.ГруппаДоступа); 	
					
				ИначеЕсли ОписаниеПоля.Значение = "Единица измерения" Тогда
					
					ИдЕдИзм	= Формат(ДанныеЭлементаБ24.Получить("MEASURE"),"ЧГ=0");
					ЕдиницаИзмерения = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЕдиницИзмерения", ИдЕдИзм);
					Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
						Результат  = ЕдиницаИзмерения;	
					Иначе
						Результат  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительныеПараметры.ВидНоменклатуры, "ЕдиницаИзмерения");
					КонецЕсли;				
					
			КонецЕсли;		
#КонецОбласти		
			ИначеЕсли ОписаниеПоля.ВидСущности = "Товар2" Тогда
#Область Товар2
				Если ОписаниеПоля.Значение = "Наименование" Тогда
					
					Результат = ДополнительныеПараметры.Наименование;
					
				ИначеЕсли ОписаниеПоля.Значение = "Полное наименование" Тогда
					
					НаименованиеПолное = ДанныеЭлементаБ24.Получить("previewText");	
					Результат = ?(ЗначениеЗаполнено(Объект1С.НаименованиеПолное), Объект1С.НаименованиеПолное, НаименованиеПолное);
					
				ИначеЕсли ОписаниеПоля.Значение = "Описание" Тогда
					
					Описание = ДанныеЭлементаБ24.Получить("detailText");	
					Результат = ?(ЗначениеЗаполнено(Объект1С.Описание), Объект1С.Описание, Описание);
					
				ИначеЕсли ОписаниеПоля.Значение = "Группа" Тогда
					
					Если СтруктураНастроек.НастройкиСинхронизацииТоваров.ИсточникИерархии = "Иерархия номенклатуры" И ЗначениеЗаполнено(ДополнительныеПараметры.Родитель) Тогда
						Результат = ДополнительныеПараметры.Родитель;
					Иначе
						Результат = "Битрикс принудительно пропуск";
					КонецЕсли;								
				ИначеЕсли ОписаниеПоля.Значение = "Артикул" Тогда
					
					АртикулДанные   = ДанныеЭлементаБ24.Получить("property" + СтруктураНастроек.ИдПредопределенногоСвойстваАртикулаНоменклатуры);
					Результат = ?(АртикулДанные = Неопределено, Неопределено, АртикулДанные.Получить("value"));
					
				ИначеЕсли ОписаниеПоля.Значение = "Ставка НДС" Тогда
					
					СтавкаНДС = Строка(ДанныеЭлементаБ24.Получить("vatId"));
	
					НайденнаяСтавкаНДС = СтруктураНастроек.ПресетыСтавокНДС.Найти(СтавкаНДС);
					Если НайденнаяСтавкаНДС <> Неопределено Тогда
						Результат = НайденнаяСтавкаНДС.СтавкаНДС;	
					Иначе
						Результат = ДополнительныеПараметры.ВидНоменклатуры.СтавкаНДС;	
					КонецЕсли;		
					
				ИначеЕсли ОписаниеПоля.Значение = "Единица измерения" Тогда
					
					ИдЕдИзм	= Формат(ДанныеЭлементаБ24.Получить("measure"),"ЧГ=0");
					ЕдиницаИзмерения = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЕдиницИзмерения", ИдЕдИзм);
					Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
						Результат  = ЕдиницаИзмерения;	
					Иначе
						Результат  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительныеПараметры.ВидНоменклатуры, "ЕдиницаИзмерения");
					КонецЕсли;				
					
					Если НЕ ЗначениеЗаполнено(Результат) Тогда
						Результат = "Битрикс принудительно пропуск";
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Вид номенклатуры" Тогда
					
					Результат = ДополнительныеПараметры.ВидНоменклатуры;
					
				ИначеЕсли ОписаниеПоля.Значение = "Тип номенклатуры" Тогда
					
					Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительныеПараметры.ВидНоменклатуры, "ТипНоменклатуры");
					
				ИначеЕсли ОписаниеПоля.Значение = "Качество" Тогда
					
					Результат = ?(ЗначениеЗаполнено(Объект1С.Качество), Объект1С.Качество, Перечисления.ГрадацииКачества.Новый);
					
				ИначеЕсли ОписаниеПоля.Значение = "Использование характеристик" Тогда
					
					Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительныеПараметры.ВидНоменклатуры, "ИспользованиеХарактеристик");	
					
				ИначеЕсли ОписаниеПоля.Значение = "Использовать упаковки" Тогда
					
					Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительныеПараметры.ВидНоменклатуры, "ИспользоватьУпаковки");	
					
				ИначеЕсли ОписаниеПоля.Значение = "Набор упаковок" Тогда
					
					Результат = ДополнительныеПараметры.ВидНоменклатуры.НаборУпаковок;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Вариант оформления продажи" Тогда
					
					Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительныеПараметры.ВидНоменклатуры, "ВариантОформленияПродажи");	
					
				ИначеЕсли ОписаниеПоля.Значение = "Группа доступа" Тогда
					
					Результат = ?(ЗначениеЗаполнено(Объект1С.ГруппаДоступа), Объект1С.ГруппаДоступа
					, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительныеПараметры.ВидНоменклатуры, "ГруппаДоступа")); 	
					
				КонецЕсли;	
#КонецОбласти				
			ИначеЕсли ОписаниеПоля.ВидСущности = "Предложение" Тогда
#Область Предложение
				Если ОписаниеПоля.Значение = "Наименование" Тогда
					
					Результат = ДополнительныеПараметры.Наименование;
					
				ИначеЕсли ОписаниеПоля.Значение = "Наименование для печати" Тогда       
					
					НаименованиеДляПечати = ДанныеЭлементаБ24.Получить("detailText");	
					Результат = ?(ЗначениеЗаполнено(НаименованиеДляПечати), НаименованиеДляПечати, Объект1С.НаименованиеПолное);
					
				ИначеЕсли ОписаниеПоля.Значение = "Артикул" Тогда
					
					АртикулДанные   = ДанныеЭлементаБ24.Получить("property" + СтруктураНастроек.ИдПредопределенногоСвойстваАртикулаНоменклатуры);
					Результат = ?(АртикулДанные = Неопределено, Неопределено, АртикулДанные.Получить("value"));
					
				КонецЕсли;		
#КонецОбласти				
			ИначеЕсли ОписаниеПоля.ВидСущности = "Проект" Тогда
#Область Сделка
				Если  ОписаниеПоля.Значение = "Дата документа" Тогда
					
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						
						Если СтруктураНастроек.НастройкиСинхронизацииСделок.ИсточникДатыДокумента = "По данным с Битрикс24" Тогда
							Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ДанныеЭлементаБ24.Получить("DATE_CREATE"));	
						Иначе
							Результат = ТекущаяДатаСеанса();	
						КонецЕсли;					
						
					Иначе
						Результат = Объект1С.Дата;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Номер документа" Тогда
					
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						
						Если СтруктураНастроек.НастройкиСинхронизацииСделок.ИсточникДатыДокумента = "По данным с Битрикс24" Тогда
							Результат = ИдЭлементаБ24;
						Иначе
							
							Объект1С.Организация		= ДополнительныеПараметры.Организация;
							Объект1С.Подразделение 		= СтруктураНастроек.НастройкиСинхронизацииСделок.Подразделение;
							Объект1С.Соглашение 		= СтруктураНастроек.НастройкиСинхронизацииСделок.Соглашение;
	
							Объект1С.УстановитьНовыйНомер();
							
							Результат = Объект1С.Номер;	
						КонецЕсли;					
						
					Иначе
						Результат = Объект1С.Номер;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Подразделение" Тогда
					
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						Результат = СтруктураНастроек.НастройкиСинхронизацииСделок.Подразделение;
					Иначе
						Результат = Объект1С.Подразделение;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Организация" Тогда
					
					Результат = ДополнительныеПараметры.Организация;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Соглашение" Тогда
					
					Если ЗначениеЗаполнено(Объект1С.Соглашение) Тогда
						Результат = Объект1С.Соглашение;	
					Иначе
						Результат = ДополнительныеПараметры.Соглашение;	  
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Партнер" Тогда
					
					Результат = ДополнительныеПараметры.Партнер;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Контрагент" Тогда
					
					Результат = ДополнительныеПараметры.Контрагент;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Контактное лицо" Тогда
					
					Результат = ДополнительныеПараметры.КонтактноеЛицо;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Договор" Тогда
				
					Результат = "Битрикс принудительно пропуск";
				
					Если ЗначениеЗаполнено(ДополнительныеПараметры.Контрагент) И ЗначениеЗаполнено(ДополнительныеПараметры.Организация) И НЕ ЗначениеЗаполнено(Объект1С.Договор) Тогда
						Если ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами") И ДополнительныеПараметры.Соглашение.ИспользуютсяДоговорыКонтрагентов Тогда
							Результат	= ПолучитьДоговор(СтруктураНастроек, ДополнительныеПараметры.Организация, ДополнительныеПараметры.Контрагент, ДополнительныеПараметры.Валюта);
						КонецЕсли;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Банковский счет контрагента" Тогда
					
					БанкСчет = Справочники.БанковскиеСчетаКонтрагентов.ПустаяСсылка();
					
					ИдБанкСчетаРек = Формат(ПолучитьЗначениеИзМассиваСоответствий(ДополнительныеПараметры.ИнформацияОРеквизитах, "ENTITY_ID", ИдЭлементаБ24, "BANK_DETAIL_ID"),"ЧГ=0");
					Если НЕ(ИдБанкСчетаРек = "" ИЛИ ИдБанкСчетаРек = "0") Тогда
						
						БанкСчет 	= ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыБанкСчетов", ИдБанкСчетаРек);
						
						Если Не ЗначениеЗаполнено(БанкСчет) Тогда
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найден банковский счет реквизита. Попытка создания банковского счета с Ид: " + ИдБанкСчетаРек);
							БанкСчет 	= СформироватьБанковскийСчетПоИд(СтруктураНастроек, ИдБанкСчетаРек);
						КонецЕсли;
						
						Если НЕ ЗначениеЗаполнено(БанкСчет) Тогда
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не найден банковский счет с Ид: " + ИдБанкСчетаРек);
						КонецЕсли;
				
					КонецЕсли;
			
					Результат = БанкСчет;
					
				ИначеЕсли ОписаниеПоля.Значение = "Банковский счет" Тогда
					
					ФормаОплаты = ДополнительныеПараметры.Соглашение.ФормаОплаты;
					ФормаОплаты = ?(ЗначениеЗаполнено(Объект1С.ФормаОплаты), Объект1С.ФормаОплаты, ФормаОплаты);
					
					ВремСтруктураДанных = Новый Структура;
					ВремСтруктураДанных.Вставить("Организация"		, ДополнительныеПараметры.Организация);
					ВремСтруктураДанных.Вставить("ФормаОплаты"		, ФормаОплаты);
					ВремСтруктураДанных.Вставить("БанковскийСчет"	, Объект1С.БанковскийСчет);
					ВремСтруктураДанных.Вставить("Валюта"			, ДополнительныеПараметры.Валюта);
					ВремСтруктураДанных.Вставить("НаправлениеДеятельности", Объект1С.НаправлениеДеятельности);
					
					Результат = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(ВремСтруктураДанных);			
							
				ИначеЕсли ОписаниеПоля.Значение = "Способ доставки" Тогда
					
					Результат = ?(ЗначениеЗаполнено(Объект1С.СпособДоставки), Объект1С.СпособДоставки, Перечисления.СпособыДоставки.Самовывоз);
					
				ИначеЕсли ОписаниеПоля.Значение = "Склад" Тогда
					
					Склад = Объект1С.Склад; 	
					Если СтруктураНастроек.ВерсияТоваров = "v.2" Тогда   
						Если СтруктураНастроек.НастройкиСинхронизацииТоваров.Свойство("Склады") И СтруктураНастроек.НастройкиСинхронизацииТоваров.Склады.Количество()>0 Тогда
							Склад = СтруктураНастроек.НастройкиСинхронизацииТоваров.Склады[0].Значение;
						КонецЕсли;
					Иначе  
						Склад = СтруктураНастроек.НастройкиСинхронизацииТоваров.Склад;
					КонецЕсли;
					
					Результат = Склад;
					
				ИначеЕсли ОписаниеПоля.Значение = "Приоритет" Тогда
					
					Приоритет = Справочники.Приоритеты.НайтиПоНаименованию("Средний");
					Результат = ?(ЗначениеЗаполнено(Объект1С.Приоритет), Объект1С.Приоритет, Приоритет);
					
				ИначеЕсли ОписаниеПоля.Значение = "Дата завершения" Тогда
					
					ДатаЗавершения	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"),   ДанныеЭлементаБ24.Получить("CLOSEDATE"));
					Если НЕ ЗначениеЗаполнено(ДатаЗавершения) Тогда
						ДатаЗавершения = ТекущаяДатаСеанса();
					КонецЕсли;
					
					Результат = ДатаЗавершения;
					
				ИначеЕсли ОписаниеПоля.Значение = "Сумма включает НДС" Тогда
					
					Результат = ПолучитьЗначениеИзМассиваСоответствий(ДополнительныеПараметры.Товары, "OWNER_ID", ИдЭлементаБ24, "TAX_INCLUDED")= "Y"
					
				ИначеЕсли ОписаниеПоля.Значение = "Налогообложение НДС" Тогда
					
					НалогообложениеНДС = ЗначениеНастроекПовтИсп.НалогообложениеНДС(ДополнительныеПараметры.Организация, Объект1С.Склад, Объект1С.Договор, Объект1С.НаправлениеДеятельности, Объект1С.Дата);			
					НалогообложениеНДС = ?(ЗначениеЗаполнено(НалогообложениеНДС), НалогообложениеНДС,  Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС);
					Результат = ?(ЗначениеЗаполнено(Объект1С.НалогообложениеНДС), Объект1С.НалогообложениеНДС, НалогообложениеНДС);
					
				ИначеЕсли ОписаниеПоля.Значение = "Сумма документа" Тогда
					
					Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ДанныеЭлементаБ24.Получить("OPPORTUNITY"),"ЧГ=0"));
					
				ИначеЕсли ОписаниеПоля.Значение = "Хозяйственная операция" Тогда
					
					ХозяйственнаяОперация = ДополнительныеПараметры.Соглашение.ХозяйственнаяОперация;
					ХозяйственнаяОперация = ?(ЗначениеЗаполнено(ХозяйственнаяОперация), ХозяйственнаяОперация,  Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
					Результат = ?(ЗначениеЗаполнено(Объект1С.ХозяйственнаяОперация), Объект1С.ХозяйственнаяОперация, ХозяйственнаяОперация);
					
				ИначеЕсли ОписаниеПоля.Значение = "Форма оплаты" Тогда
					
					//УсловияПродаж 	= ПродажиСервер.ПолучитьУсловияПродаж(ДополнительныеПараметры.Соглашение);
					//Результат		= УсловияПродаж.ФормаОплаты;
						
				ИначеЕсли ОписаниеПоля.Значение = "График оплаты" Тогда
				
					Результат = "Битрикс принудительно пропуск";
				
					Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиОплаты") Тогда
						//
						//УсловияПродаж 	= ПродажиСервер.ПолучитьУсловияПродаж(ДополнительныеПараметры.Соглашение);
						//ГрафикОплаты 	= УсловияПродаж.ГрафикОплаты;

						//Результат = ГрафикОплаты;
						
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Статус" Тогда
					
					СтатусЗаказа = Объект1С.Статус;	
					
					Если СтруктураНастроек.НастройкиСинхронизацииСделок.Свойство("ИнформацияОСтатусах") Тогда	
						
						СтатусЗаказа = Перечисления.СтатусыЗаказовКлиентов.НеСогласован;
						
						ИнформацияОСтатусах = СтруктураНастроек.НастройкиСинхронизацииСделок.ИнформацияОСтатусах;
						тзнСтатусов			= ИнформацияОСтатусах.СтатусыНаправленийСтатусов;
						
						Если ИнформацияОСтатусах <> Неопределено И ТипЗнч(тзнСтатусов) = Тип("ТаблицаЗначений") Тогда
							Если ИнформацияОСтатусах.ИсточникСтатусов1С = "СтатусыЗаказов" Тогда
								
								ИдСтатуса 		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ДанныеЭлементаБ24.Получить("STAGE_ID"));
								ИдНаправления  	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ДанныеЭлементаБ24.Получить("CATEGORY_ID"));
								ИдНаправления	= ?(ИдНаправления = "0", "", ИдНаправления);
								
								НайденныеСтроки = тзнСтатусов.НайтиСтроки(Новый Структура("ИдСтатуса, ИдНаправления", ИдСтатуса, ИдНаправления));
								Если НайденныеСтроки.Количество() >0 Тогда
									НайденнаяСтрока = НайденныеСтроки[0];	
								Иначе
									НайденнаяСтрока = Неопределено;	
								КонецЕсли;
									
								Если НайденнаяСтрока <> Неопределено Тогда
									СтатусЗаказа = НайденнаяСтрока.Статус;	

								Иначе                                                     
									СтатусЗаказа = ?(ЗначениеЗаполнено(Объект1С.Статус), Объект1С.Статус, СтатусЗаказа);	
								КонецЕсли;
							Иначе
								СтатусЗаказа = ?(ЗначениеЗаполнено(Объект1С.Статус), Объект1С.Статус, СтатусЗаказа);	
							КонецЕсли;
						Иначе
							СтатусЗаказа = ?(ЗначениеЗаполнено(Объект1С.Статус), Объект1С.Статус, СтатусЗаказа);	
						КонецЕсли;
						
					КонецЕсли;

					Результат = СтатусЗаказа;
					
				ИначеЕсли ОписаниеПоля.Значение = "Комментарий" Тогда
					
					Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ДанныеЭлементаБ24.Получить("COMMENTS"));
				
				КонецЕсли;						
#КонецОбласти			
			ИначеЕсли ОписаниеПоля.ВидСущности = "Заказ" Тогда
#Область Заказ	
				Если ОписаниеПоля.Значение = "Дата документа" Тогда
	
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						
						Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИсточникДатыДокумента = "По данным с Битрикс24" Тогда
							
							Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ДанныеЭлементаБ24.Получить("dateInsert"));
							Если Не ЗначениеЗаполнено(Результат) Тогда Результат = ТекущаяДатаСеанса() КонецЕсли;
						
						Иначе
							Результат = ТекущаяДатаСеанса();	
						КонецЕсли;					
						
					Иначе
						Результат = Объект1С.Дата;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Номер документа" Тогда
					
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						
						Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИсточникДатыДокумента = "По данным с Битрикс24" Тогда
							Результат = ИдЭлементаБ24;
						Иначе
							
							Объект1С.Организация		= ДополнительныеПараметры.Организация;
							Объект1С.Подразделение 		= СтруктураНастроек.НастройкиСинхронизацииЗаказов.Подразделение;
							Объект1С.Соглашение 		= ДополнительныеПараметры.Соглашение;
	
							Объект1С.УстановитьНовыйНомер();
							
							Результат = Объект1С.Номер;	
						КонецЕсли;					
						
					Иначе
						Результат = Объект1С.Номер;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Организация" Тогда
					
					Результат = ДополнительныеПараметры.Организация;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Подразделение" Тогда
					
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						Результат = СтруктураНастроек.НастройкиСинхронизацииЗаказов.Подразделение;
					Иначе
						Результат = Объект1С.Подразделение;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Соглашение" Тогда
					
					Результат = ДополнительныеПараметры.Соглашение;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Партнер" Тогда
					
					Результат = ДополнительныеПараметры.Партнер;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Контрагент" Тогда
					
					Результат = ДополнительныеПараметры.Контрагент;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Контактное лицо" Тогда
					
					Результат = ДополнительныеПараметры.КонтактноеЛицо;	
					
				ИначеЕсли ОписаниеПоля.Значение = "Договор" Тогда
				
					Результат = "Битрикс принудительно пропуск";
				
					Если ЗначениеЗаполнено(ДополнительныеПараметры.Контрагент) И ЗначениеЗаполнено(ДополнительныеПараметры.Организация) И НЕ ЗначениеЗаполнено(Объект1С.Договор) Тогда
						Если ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами") И ДополнительныеПараметры.Соглашение.ИспользуютсяДоговорыКонтрагентов Тогда
							Результат	= ПолучитьДоговор(СтруктураНастроек, ДополнительныеПараметры.Организация, ДополнительныеПараметры.Контрагент, ДополнительныеПараметры.Валюта);
						КонецЕсли;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Банковский счет контрагента" Тогда
					
					Результат = ДополнительныеПараметры.БанкСчетКонтрагента;
					
				ИначеЕсли ОписаниеПоля.Значение = "Банковский счет" Тогда
					
					ФормаОплаты = ДополнительныеПараметры.Соглашение.ФормаОплаты;
					ФормаОплаты = ?(ЗначениеЗаполнено(Объект1С.ФормаОплаты), Объект1С.ФормаОплаты, ФормаОплаты);
					
					ВремСтруктураДанных = Новый Структура;
					ВремСтруктураДанных.Вставить("Организация"		, ДополнительныеПараметры.Организация);
					ВремСтруктураДанных.Вставить("ФормаОплаты"		, ФормаОплаты);
					ВремСтруктураДанных.Вставить("БанковскийСчет"	, Объект1С.БанковскийСчет);
					ВремСтруктураДанных.Вставить("Валюта"			, ДополнительныеПараметры.Валюта);
					ВремСтруктураДанных.Вставить("НаправлениеДеятельности", Объект1С.НаправлениеДеятельности);
					
					Результат = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(ВремСтруктураДанных);			
					
				ИначеЕсли ОписаниеПоля.Значение = "Комментарий" Тогда
					
					Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ДанныеЭлементаБ24.Получить("comments"));
					
				ИначеЕсли ОписаниеПоля.Значение = "Способ доставки" Тогда
					
					СпособДоставки = Перечисления.СпособыДоставки.Самовывоз;					

					Если ДополнительныеПараметры.ИнформацияОДоставках <>Неопределено Тогда
						Если ДополнительныеПараметры.ИнформацияОДоставках.ИсточникСлужбДоставкиЗаказов = "СпособыДоставкиЗаказов" Тогда
							НайденнаяСтрока = ДополнительныеПараметры.ИнформацияОДоставках.СоответствияСпособовДоставкиЗаказа.Найти(ДополнительныеПараметры.ИдДоставки, "ИдСлужбы");
							Если НайденнаяСтрока <> Неопределено Тогда
								СпособДоставки 	= НайденнаяСтрока.СпособДоставки;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					Результат = СпособДоставки;
					
				ИначеЕсли ОписаниеПоля.Значение = "Перевозчик партнер" Тогда
					
					Результат = ДополнительныеПараметры.ПеревозчикПартнер;
					
				ИначеЕсли ОписаниеПоля.Значение = "Адрес доставки" Тогда
					
					Результат = ДополнительныеПараметры.АдресДоставки;
					
				ИначеЕсли ОписаниеПоля.Значение = "Адрес доставки значения полей" Тогда
					
					Результат = ДополнительныеПараметры.АдресДоставкиЗнчПолей;
					
				ИначеЕсли ОписаниеПоля.Значение = "Адрес доставки перевозчика" И ЗначениеЗаполнено(ДополнительныеПараметры.ПеревозчикПартнер) Тогда
					
					АдресДоставкиПеревозчика = Неопределено;
					Если ДополнительныеПараметры.ИнформацияОДоставках <>Неопределено Тогда
						Если ДополнительныеПараметры.ИнформацияОДоставках.ИсточникСлужбДоставкиЗаказов = "СпособыДоставкиЗаказов" Тогда
							НайденнаяСтрока = ДополнительныеПараметры.ИнформацияОДоставках.СоответствияСпособовДоставкиЗаказа.Найти(ДополнительныеПараметры.ИдДоставки, "ИдСлужбы");
							Если НайденнаяСтрока <> Неопределено Тогда
								
								НайденнаяСтрокаАдрес = ДополнительныеПараметры.ПеревозчикПартнер.КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.АдресПартнера);
								Если НайденнаяСтрокаАдрес <> Неопределено Тогда
									АдресДоставкиПеревозчика = НайденнаяСтрокаАдрес.Представление;
								КонецЕсли;
								
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
								
					Результат = АдресДоставкиПеревозчика;
					
				ИначеЕсли ОписаниеПоля.Значение = "Адрес доставки перевозчика значения полей" Тогда
					
					АдресДоставкиПеревозчикаЗначенияПолей = Неопределено;
					Если ДополнительныеПараметры.ИнформацияОДоставках <>Неопределено И ЗначениеЗаполнено(ДополнительныеПараметры.ПеревозчикПартнер) Тогда
						Если ДополнительныеПараметры.ИнформацияОДоставках.ИсточникСлужбДоставкиЗаказов = "СпособыДоставкиЗаказов" Тогда
							НайденнаяСтрока = ДополнительныеПараметры.ИнформацияОДоставках.СоответствияСпособовДоставкиЗаказа.Найти(ДополнительныеПараметры.ИдДоставки, "ИдСлужбы");
							Если НайденнаяСтрока <> Неопределено Тогда
								
								НайденнаяСтрокаАдрес = ДополнительныеПараметры.ПеревозчикПартнер.КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.АдресПартнера);
								Если НайденнаяСтрокаАдрес <> Неопределено Тогда
									АдресДоставкиПеревозчикаЗначенияПолей = УправлениеКонтактнойИнформациейСлужебныйВызовСервера.КонтактнаяИнформацияПоПредставлению(НайденнаяСтрокаАдрес.Представление, Справочники.ВидыКонтактнойИнформации.АдресПартнера); 						
								КонецЕсли;
								
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					Результат = АдресДоставкиПеревозчикаЗначенияПолей;
				
				ИначеЕсли ОписаниеПоля.Значение = "Склад" Тогда
					
					Склад = Объект1С.Склад; 	
					Если СтруктураНастроек.ВерсияТоваров = "v.2" Тогда   
						Если СтруктураНастроек.НастройкиСинхронизацииТоваров.Свойство("Склады") И СтруктураНастроек.НастройкиСинхронизацииТоваров.Склады.Количество()>0 Тогда
							Склад = СтруктураНастроек.НастройкиСинхронизацииТоваров.Склады[0].Значение;
						КонецЕсли;
					Иначе  
						Склад = СтруктураНастроек.НастройкиСинхронизацииТоваров.Склад;
					КонецЕсли;
					
					Результат = Склад;
					
				ИначеЕсли ОписаниеПоля.Значение = "Приоритет" Тогда
					
					//Приоритет = Справочники.Приоритеты.НайтиПоНаименованию("Средний");
					//Результат = ?(ЗначениеЗаполнено(Объект1С.Приоритет), Объект1С.Приоритет, Приоритет);
					
				ИначеЕсли ОписаниеПоля.Значение = "Дата завершения" Тогда
					
					Если ЗначениеЗаполнено(Объект1С.ДатаОтгрузки) Тогда
						Результат = Объект1С.ДатаОтгрузки;
					Иначе
						
						ДатаНачала = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"),   ДанныеЭлементаБ24.Получить("dateInsert"));
						Если Не ЗначениеЗаполнено(ДатаНачала) Тогда ДатаНачала = ТекущаяДатаСеанса() КонецЕсли;
						
						Результат = ДатаНачала + 7*24*60*60;     
						
						Если Результат < Объект1С.Дата Тогда Результат = Объект1С.Дата КонецЕсли; 
						
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Сумма включает НДС" Тогда
					
					ЦенаВклНДС = Истина;		
					Если ДополнительныеПараметры.Товары.Количество() > 0 Тогда 
						ЦенаВклНДС = ДополнительныеПараметры.Товары[0].Получить("vatIncluded") = "Y";	
					КонецЕсли;	
					
					Результат = ЦенаВклНДС;
					
				ИначеЕсли ОписаниеПоля.Значение = "Налогообложение НДС" Тогда
					
					//НалогообложениеНДС = ЗначениеНастроекПовтИсп.НалогообложениеНДС(ДополнительныеПараметры.Организация, Объект1С.Склад, Объект1С.Договор, Объект1С.НаправлениеДеятельности, Объект1С.Дата);			
					//НалогообложениеНДС = ?(ЗначениеЗаполнено(НалогообложениеНДС), НалогообложениеНДС,  Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС);
					//Результат = ?(ЗначениеЗаполнено(Объект1С.НалогообложениеНДС), Объект1С.НалогообложениеНДС, НалогообложениеНДС);
					
				ИначеЕсли ОписаниеПоля.Значение = "Сумма документа" Тогда
					
					Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ДанныеЭлементаБ24.Получить("price"),"ЧРД=.; ЧГ=0"));
					
				ИначеЕсли ОписаниеПоля.Значение = "Хозяйственная операция" Тогда
					
					ХозяйственнаяОперация = ДополнительныеПараметры.Соглашение.ХозяйственнаяОперация;
					ХозяйственнаяОперация = ?(ЗначениеЗаполнено(ХозяйственнаяОперация), ХозяйственнаяОперация,  Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
					Результат = ?(ЗначениеЗаполнено(Объект1С.ХозяйственнаяОперация), Объект1С.ХозяйственнаяОперация, ХозяйственнаяОперация);
					
				ИначеЕсли ОписаниеПоля.Значение = "Форма оплаты" Тогда
					
					//УсловияПродаж 	= ПродажиСервер.ПолучитьУсловияПродаж(ДополнительныеПараметры.Соглашение);
					//Результат		= УсловияПродаж.ФормаОплаты;
					
				ИначеЕсли ОписаниеПоля.Значение = "График оплаты" Тогда
								
					Результат = "Битрикс принудительно пропуск";
				
					Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиОплаты") Тогда
						//
						//УсловияПродаж 	= ПродажиСервер.ПолучитьУсловияПродаж(ДополнительныеПараметры.Соглашение);
						//ГрафикОплаты 	= УсловияПродаж.ГрафикОплаты;

						//Результат = ГрафикОплаты;
						
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Статус" Тогда
					
					СтатусЗаказа = Перечисления.СтатусыЗаказовКлиентов.НеСогласован;
					
					ИдСтатуса 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеЭлементаБ24.Получить("statusId"),"ЧГ=0"));
					
					Отгружен 	= ДанныеЭлементаБ24.Получить("deducted") = "Y"; 
					Оплачен	 	= ДанныеЭлементаБ24.Получить("payed") = "Y";   
					Закрыт		= Оплачен И Отгружен;
					
					ИнформацияОСтатусах = ДополнительныеПараметры.ИнформацияОСтатусах;
					Если ИнформацияОСтатусах <> Неопределено И ТипЗнч(ДополнительныеПараметры.ТаблицаСтатусов) = Тип("ТаблицаЗначений") Тогда			
						Если ИнформацияОСтатусах.ИсточникСтатусовЗаказов = "СтатусыЗаказов" Тогда
							
							НайденнаяСтрока = ДополнительныеПараметры.ТаблицаСтатусов.Найти(ИдСтатуса, "ИдСтатуса");
							Если НайденнаяСтрока <> Неопределено Тогда
								СтатусЗаказа = ?(ЗначениеЗаполнено(НайденнаяСтрока.Статус),НайденнаяСтрока.Статус, СтатусЗаказа);	
							КонецЕсли;
						Иначе
							СтатусЗаказа = ?(ЗначениеЗаполнено(Объект1С.Статус), Объект1С.Статус, СтатусЗаказа);	
						КонецЕсли;
					Иначе
						СтатусЗаказа = ?(ЗначениеЗаполнено(Объект1С.Статус), Объект1С.Статус, СтатусЗаказа);	
					КонецЕсли;
					
					Если Закрыт И НЕ ЗначениеЗаполнено(СтатусЗаказа) Тогда
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Документ закрыт, но не найдено состояние заказа");
					КонецЕсли;				
					
					Результат = СтатусЗаказа;
					
				КонецЕсли;				                                  
#КонецОбласти			
			ИначеЕсли ОписаниеПоля.ВидСущности = "Оплата" Тогда
#Область Оплата
				Если ОписаниеПоля.Значение = "Дата документа" Тогда
	
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						
						Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИсточникДатыДокумента = "По данным с Битрикс24" Тогда
							
							Результат  	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"),   ДанныеЭлементаБ24.Получить("dateBill"));
							Если Не ЗначениеЗаполнено(Результат) Тогда Результат = ТекущаяДатаСеанса() КонецЕсли;
						
						Иначе
							Результат = ТекущаяДатаСеанса();	
						КонецЕсли;					
						
					Иначе
						Результат = Объект1С.Дата;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Номер документа" Тогда
					
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						
						Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИсточникДатыДокумента = "По данным с Битрикс24" Тогда
							
							ИдБитрикс24		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеЭлементаБ24.Получить("id"),"ЧГ=0"));
							НомерОплатыБ24 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеЭлементаБ24.Получить("accountNumber"),"ЧГ=0"));
							НомерОплатыБ24	= ?(ЗначениеЗаполнено(НомерОплатыБ24), НомерОплатыБ24, ИдБитрикс24);
							
							Результат = НомерОплатыБ24;
							
						Иначе
							
							Объект1С.Организация = ДополнительныеПараметры.Организация;
							
							Если ТипЗнч(Объект1С) = Тип("ДокументОбъект.ПриходныйКассовыйОрдер") Тогда
								Объект1С.Подразделение = ДополнительныеПараметры.Подразделение;
							КонецЕсли;
							
							Объект1С.УстановитьНовыйНомер();
							
							Результат = Объект1С.Номер;	
						КонецЕсли;					
						
					Иначе
						Результат = Объект1С.Номер;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Заказ" Тогда
					
					Результат = ДополнительныеПараметры.Основание; 
					
				ИначеЕсли ОписаниеПоля.Значение = "Сумма документа" Тогда
					
					Результат 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ДанныеЭлементаБ24.Получить("sum"),"ЧРД=.; ЧГ=0"));
					
				ИначеЕсли ОписаниеПоля.Значение = "Статья ДДС" Тогда
					
					Результат = ?(ЗначениеЗаполнено(Объект1С.СтатьяДвиженияДенежныхСредств), Объект1С.СтатьяДвиженияДенежныхСредств, СтруктураНастроек.НастройкиСинхронизацииЗаказов.СтатьяДДС); 	
					
				ИначеЕсли ОписаниеПоля.Значение = "Касса" Тогда
					
					Если ДополнительныеПараметры.ИнформацияОПлатежнойСистеме <> Неопределено Тогда
						Результат = ДополнительныеПараметры.ИнформацияОПлатежнойСистеме.Касса; 
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Банковский счет организации" Тогда
					
					Если ДополнительныеПараметры.ИнформацияОПлатежнойСистеме <> Неопределено Тогда
						Результат = ДополнительныеПараметры.ИнформацияОПлатежнойСистеме.РасчетныйСчет; 
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Эквайринговый терминал" Тогда
					
					Если ДополнительныеПараметры.ИнформацияОПлатежнойСистеме <> Неопределено Тогда
						Результат = ДополнительныеПараметры.ИнформацияОПлатежнойСистеме.Терминал; 
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Договор эквайринга" Тогда
					
					Если ДополнительныеПараметры.ИнформацияОПлатежнойСистеме <> Неопределено Тогда
						Результат = ДополнительныеПараметры.ИнформацияОПлатежнойСистеме.ВидПлатежнойКарты; 
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Объект расчетов" Тогда
					
					Результат = ДополнительныеПараметры.ОбъектРасчетов;
					
				КонецЕсли;
#КонецОбласти			
			ИначеЕсли ОписаниеПоля.ВидСущности = "Отгрузка" Тогда
#Область Отгрузка
				Если ОписаниеПоля.Значение = "Дата документа" Тогда
	
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						
						Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИсточникДатыДокумента = "По данным с Битрикс24" Тогда
							
							Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"),   ДанныеЭлементаБ24.Получить("dateInsert"));
							Если Не ЗначениеЗаполнено(Результат) Тогда Результат = ТекущаяДатаСеанса() КонецЕсли;
						
						Иначе
							Результат = ТекущаяДатаСеанса();	
						КонецЕсли;					
						
					Иначе
						Результат = Объект1С.Дата;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Номер документа" Тогда
					
					Если ДополнительныеПараметры.ЭтоНовыйОбъект Тогда
						
						Если СтруктураНастроек.НастройкиСинхронизацииЗаказов.ИсточникДатыДокумента = "По данным с Битрикс24" Тогда
							
							ИдБитрикс24			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеЭлементаБ24.Получить("id"),"ЧГ=0"));
							НомерОтгрузкиБ24 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеЭлементаБ24.Получить("accountNumber"),"ЧГ=0"));
							НомерОтгрузкиБ24	= ?(ЗначениеЗаполнено(НомерОтгрузкиБ24), НомерОтгрузкиБ24, ИдБитрикс24);
							
							Результат = НомерОтгрузкиБ24;
							
						Иначе
							
							Объект1С.Организация 	= ДополнительныеПараметры.Организация;
							Объект1С.Подразделение 	= ДополнительныеПараметры.Подразделение;
							
							Объект1С.УстановитьНовыйНомер();
							
							Результат = Объект1С.Номер;	
							
						КонецЕсли;					
						
					Иначе
						Результат = Объект1С.Номер;	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Заказ" Тогда
					
					Результат = ДополнительныеПараметры.Основание; 
					
				ИначеЕсли ОписаниеПоля.Значение = "Статус" Тогда
				
					Результат = ?(ЗначениеЗаполнено(Объект1С.Статус), Объект1С.Статус, Перечисления.СтатусыРеализацийТоваровУслуг.КПредоплате);  
					
				ИначеЕсли ОписаниеПоля.Значение = "Вариант оформления продажи" Тогда
				
					Результат = ?(ЗначениеЗаполнено(Объект1С.ВариантОформленияПродажи), Объект1С.ВариантОформленияПродажи, Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг);  
					
				ИначеЕсли ОписаниеПоля.Значение = "Способ доставки" Тогда
					
					СпособДоставки = Перечисления.СпособыДоставки.Самовывоз;

					Если ДополнительныеПараметры.ИнформацияОДоставках <>Неопределено Тогда
						Если ДополнительныеПараметры.ИнформацияОДоставках.ИсточникСлужбДоставкиОтгрузок = "СпособыДоставкиОтгрузок" Тогда
							НайденнаяСтрока = ДополнительныеПараметры.ИнформацияОДоставках.СоответствияСпособовДоставкиОтгрузки.Найти(ДополнительныеПараметры.ИдДоставки, "ИдСлужбы");
							Если НайденнаяСтрока <> Неопределено Тогда
								СпособДоставки 	= НайденнаяСтрока.СпособДоставки;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					Результат = СпособДоставки;
					
				ИначеЕсли ОписаниеПоля.Значение = "Перевозчик партнер" Тогда
					
					Результат = ДополнительныеПараметры.ПеревозчикПартнер; 
					
				ИначеЕсли ОписаниеПоля.Значение = "Адрес доставки перевозчика" И ЗначениеЗаполнено(ДополнительныеПараметры.ПеревозчикПартнер) Тогда
					
					АдресДоставкиПеревозчика = Неопределено;
					Если ДополнительныеПараметры.ИнформацияОДоставках <>Неопределено Тогда
						Если ДополнительныеПараметры.ИнформацияОДоставках.ИсточникСлужбДоставкиОтгрузок = "СпособыДоставкиОтгрузок" Тогда
							НайденнаяСтрока = ДополнительныеПараметры.ИнформацияОДоставках.СоответствияСпособовДоставкиОтгрузки.Найти(ДополнительныеПараметры.ИдДоставки, "ИдСлужбы");
							Если НайденнаяСтрока <> Неопределено Тогда
								
								НайденнаяСтрокаАдрес = ДополнительныеПараметры.ПеревозчикПартнер.КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.АдресПартнера);
								Если НайденнаяСтрокаАдрес <> Неопределено Тогда
									АдресДоставкиПеревозчика = НайденнаяСтрокаАдрес.Представление;
								КонецЕсли;
								
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
								
					Результат = АдресДоставкиПеревозчика;
					
				ИначеЕсли ОписаниеПоля.Значение = "Адрес доставки перевозчика значения полей" Тогда
					
					АдресДоставкиПеревозчикаЗначенияПолей = Неопределено;
					Если ДополнительныеПараметры.ИнформацияОДоставках <>Неопределено И ЗначениеЗаполнено(ДополнительныеПараметры.ПеревозчикПартнер) Тогда
						Если ДополнительныеПараметры.ИнформацияОДоставках.ИсточникСлужбДоставкиОтгрузок = "СпособыДоставкиОтгрузок" Тогда
							НайденнаяСтрока = ДополнительныеПараметры.ИнформацияОДоставках.СоответствияСпособовДоставкиОтгрузки.Найти(ДополнительныеПараметры.ИдДоставки, "ИдСлужбы");
							Если НайденнаяСтрока <> Неопределено Тогда
								
								НайденнаяСтрокаАдрес = ДополнительныеПараметры.ПеревозчикПартнер.КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.АдресПартнера);
								Если НайденнаяСтрокаАдрес <> Неопределено Тогда
									АдресДоставкиПеревозчикаЗначенияПолей = УправлениеКонтактнойИнформациейСлужебныйВызовСервера.КонтактнаяИнформацияПоПредставлению(НайденнаяСтрокаАдрес.Представление, Справочники.ВидыКонтактнойИнформации.АдресПартнера); 						
								КонецЕсли;
								
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					Результат = АдресДоставкиПеревозчикаЗначенияПолей;
					
				КонецЕсли;
#КонецОбласти				
			КонецЕсли;	

		ИначеЕсли ОписаниеПоля.ИсточникДанных = "Из пользовательского поля" Тогда
			
			ЗначениеСвойства 	= ДанныеЭлементаБ24.Получить(ОписаниеПоля.Значение);
			ЗначениеРеквизита 	= ЗначениеСвойства;
			
			Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
				
				Если ОписаниеПоля.ВидСущности = "Компания" Тогда
					ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКомпании;
				ИначеЕсли ОписаниеПоля.ВидСущности = "Контакт" Тогда
					ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКонтакта;
				ИначеЕсли ОписаниеПоля.ВидСущности = "Товар" Тогда
					ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара;
				ИначеЕсли ОписаниеПоля.ВидСущности = "Счет" Тогда
					ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСчета;
				ИначеЕсли ОписаниеПоля.ВидСущности = "Проект" Тогда
					ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСделки;
				Иначе
					ТипДанных = Неопределено
				КонецЕсли;
				
				Если ТипДанных <> Неопределено Тогда
					
					ДанныеСвойства = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоДопИдБ24Объекта(СтруктураНастроек.Портал, ТипДанных, ОписаниеПоля.Значение); 	
					Если ДанныеСвойства <> Неопределено Тогда
						Свойство = ДанныеСвойства.Объект;
						Если ЗначениеЗаполнено(Свойство) Тогда
							
							ТипЗначенияСвойства = Свойство.ТипЗначения;
							
							Если ТипЗначенияСвойства.СодержитТип(Тип("Строка")) Тогда
								ЗначениеРеквизита = ЗначениеСвойства;
							ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Булево")) Тогда
								ЗначениеРеквизита = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Булево"), ЗначениеСвойства);
							ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Число")) Тогда
								ЗначениеРеквизита = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), ЗначениеСвойства);
							ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("Дата")) Тогда
								ЗначениеРеквизита = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ЗначениеСвойства);
							ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("СправочникСсылка.Пользователи")) Тогда
								
								НайденнаяСтрока = СтруктураНастроек.ТаблицаСопоставленияПользователей.Найти(ЗначениеСвойства, "ИдПользователя");	
								
								Если НайденнаяСтрока <> Неопределено Тогда
									ЗначениеРеквизита = НайденнаяСтрока.Пользователь1С;
								КонецЕсли;
								
							ИначеЕсли ТипЗначенияСвойства.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
								ЗначениеРеквизита = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЗначенийСвойств",  ЗначениеСвойства);
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Результат = ЗначениеРеквизита;	
			
		ИначеЕсли ОписаниеПоля.ИсточникДанных = "<Свой алгоритм>" Тогда
			
			Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
			Параметры.СтруктураНастроек = СтруктураНастроек;
			Параметры.Объект1С 			= Объект1С;
			Параметры.ОбъектПортала 	= ДанныеЭлементаБ24;
			
			Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(ОписаниеПоля.Значение, Параметры, "Функция");		
			
		ИначеЕсли ОписаниеПоля.ИсточникДанных = "Из смарт-процесса" Тогда
			
			Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СмартПроцессы") Тогда
				
				ЗначениеСмартПроцесса = ДанныеЭлементаБ24.Получить("PARENT_ID_" + ОписаниеПоля.Значение);
				Если ЗначениеСмартПроцесса = Неопределено Тогда
					ЗначениеСмартПроцесса = ДанныеЭлементаБ24.Получить("parentId" + ОписаниеПоля.Значение);
					ЗначениеКлюча = Формат(ЗначениеСмартПроцесса, "ЧГ=0");
				Иначе
					ЗначениеКлюча = Формат(ЗначениеСмартПроцесса, "ЧГ=0");
				КонецЕсли;
				

				МодульЗагрузкаСервер = ОбщегоНазначения.ОбщийМодуль("Б24_СМ_ЗагрузкаСервер");
				Результат = МодульЗагрузкаСервер.ПолучитьЭлементСмартПроцессаПоИдДляДругихПодсистем(СтруктураНастроек, ОписаниеПоля.Значение, ЗначениеКлюча);
			КонецЕсли;
			
		КонецЕсли;
			
	Исключение                                                                                                                              
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, ОписаниеПоля.ВидСущности + "/" + ОписаниеПоля.Значение + "/" + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьЗначениеРеквизитаТЧОбъекта1СПоКонструктору(СтруктураНастроек, ОписаниеПоля, ДополнительныеПараметры)
	
	Результат = Неопределено;
	
	Объект1С 				= ДополнительныеПараметры.Объект1С;
	СтрокаТЧ 				= ДополнительныеПараметры.СтрокаТЧ;
	МенеджерВременныхТаблиц = ДополнительныеПараметры.МенеджерВременныхТаблиц;
	
	ДанныеЭлементаТЧБ24 	= ДополнительныеПараметры.ДанныеЭлементаТЧБ24;
	ПомеченНаУдаление 		= ДополнительныеПараметры.ПомеченНаУдаление;
	ЭтоНовыйОбъект 			= ДополнительныеПараметры.ЭтоНовыйОбъект;
	
	Номенклатура 			= ДополнительныеПараметры.Номенклатура;
	Характеристика 			= ДополнительныеПараметры.Характеристика;
	
	Если НЕ ЗначениеЗаполнено(ОписаниеПоля.Значение) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ЗначениеКлюча = Неопределено;
	
	Попытка
		
		Если ОписаниеПоля.ИсточникДанных = "Фиксированное значение" Тогда
			
			Результат = ОписаниеПоля.Значение;	     
			
		ИначеЕсли ОписаниеПоля.ИсточникДанных = "Ключ JSON" Тогда
			
			Результат = ДанныеЭлементаТЧБ24.Получить(ОписаниеПоля.Значение);
			
		ИначеЕсли ОписаниеПоля.ИсточникДанных = "Предопределенный алгоритм" Тогда

			Если ОписаниеПоля.ВидСущности = "Проект" Тогда
#Область Сделка
				Если ОписаниеПоля.Значение = "Номенклатура" Тогда
					
					Результат = Номенклатура;
					
				ИначеЕсли ОписаниеПоля.Значение = "Характеристика" Тогда
					
					Результат = Характеристика;
					
					
				ИначеЕсли ОписаниеПоля.Значение = "Упаковка" Тогда
					
					ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
					
					КодЕдИзм = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеЭлементаТЧБ24.Получить("MEASURE_CODE"),"ЧГ=0"));
					Если ЗначениеЗаполнено(КодЕдИзм) Тогда
						ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(КодЕдИзм);;
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найдена единица измерения по ид. Попытка создания единицы измерения с Ид: " + КодЕдИзм);
						ЕдиницаИзмерения = СформироватьЕдиницуИзмеренияПоИд(СтруктураНастроек, КодЕдИзм);
					КонецЕсли;
					
					Результат = ЕдиницаИзмерения; 
					
				ИначеЕсли ОписаниеПоля.Значение = "Ставка НДС" Тогда
					
					СтавкаНалога		= ДанныеЭлементаТЧБ24.Получить("TAX_RATE");
					Если СтавкаНалога 	= Неопределено Тогда СтавкаНалога = "Без НДС" КонецЕсли;
					
					СтавкаНДС = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтавкуНДСПоСтавке(СтавкаНалога);     
					Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда СтавкаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "СтавкаНДС") КонецЕсли;
					
					Результат = СтавкаНДС;				
					
				ИначеЕсли ОписаниеПоля.Значение = "Дата отгрузки" Тогда
					
					НайденныеСтроки = ДополнительныеПараметры.ТоварыАрх.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Номенклатура, Характеристика));
					Если НайденныеСтроки.Количество() > 0 Тогда
						Результат = ?(ЗначениеЗаполнено(НайденныеСтроки[0].ДатаОтгрузки), НайденныеСтроки[0].ДатаОтгрузки, Объект1С.ДатаОтгрузки);
					Иначе
						Результат = Объект1С.ДатаОтгрузки;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Склад" Тогда
					
					НайденныеСтроки = ДополнительныеПараметры.ТоварыАрх.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Номенклатура, Характеристика));
					Если НайденныеСтроки.Количество() > 0 Тогда
						Результат = ?(ЗначениеЗаполнено(НайденныеСтроки[0].Склад), НайденныеСтроки[0].Склад, Объект1С.Склад);
					Иначе
						Результат = Объект1С.Склад;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Вариант обеспечения" Тогда
					
					НайденныеСтроки = ДополнительныеПараметры.ТоварыАрх.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Номенклатура, Характеристика));
					Если НайденныеСтроки.Количество() > 0 Тогда
						Результат = ?(ЗначениеЗаполнено(НайденныеСтроки[0].ВариантОбеспечения), НайденныеСтроки[0].ВариантОбеспечения, Перечисления.ВариантыОбеспечения.НеТребуется);
					Иначе
						Если ЗначениеЗаполнено(ДополнительныеПараметры.СтрокаТЧ.ВариантОбеспечения) Тогда
							Результат = ДополнительныеПараметры.СтрокаТЧ.ВариантОбеспечения;
						Иначе
							Результат = Перечисления.ВариантыОбеспечения.НеТребуется;
						КонецЕсли;
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Содержание" Тогда
					
					Результат = Неопределено;
					
					Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ТипНоменклатуры") = Перечисления.ТипыНоменклатуры.Услуга 
						ИЛИ НЕ ЗначениеЗаполнено(Номенклатура) Тогда
						
						Содержание = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(ДанныеЭлементаТЧБ24.Получить("PRODUCT_NAME")));
						
						НайденныеСтроки = ДополнительныеПараметры.ТоварыАрх.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Номенклатура, Характеристика));
						Если НайденныеСтроки.Количество() > 0 Тогда
							Результат = ?(ЗначениеЗаполнено(НайденныеСтроки[0].Содержание), НайденныеСтроки[0].Содержание, Содержание);
						Иначе
							Результат = Содержание;
						КонецЕсли;
						
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Количество" Тогда
					
					Результат = ДанныеЭлементаТЧБ24.Получить("QUANTITY");
					
				ИначеЕсли ОписаниеПоля.Значение = "Цена" Тогда
						
					ЦенаБезСкидкиБезНалога	= ДанныеЭлементаТЧБ24.Получить("PRICE_NETTO");
					ЦенаБезСкидкиСНалогом	= ДанныеЭлементаТЧБ24.Получить("PRICE_BRUTTO");
					НалогВключен			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(ДанныеЭлементаТЧБ24.Получить("TAX_INCLUDED")))= "Y";
					
					Если НалогВключен = Истина Тогда
						ЦенаИтого = ЦенаБезСкидкиСНалогом;
					Иначе
						ЦенаИтого = ЦенаБезСкидкиБезНалога;
					КонецЕсли;
					
					Результат = ЦенаИтого;
						
				ИначеЕсли ОписаниеПоля.Значение = "Процент ручной скидки" Тогда
					
					Результат = ДанныеЭлементаТЧБ24.Получить("DISCOUNT_RATE");
					
			КонецЕсли;				
#КонецОбласти				
			ИначеЕсли ОписаниеПоля.ВидСущности = "Заказ" Тогда
#Область Заказ
				Если ОписаниеПоля.Значение = "Ключ связи" Тогда   
					
					Результат = ДополнительныеПараметры.КлючСвязиПозиции;
					
				ИначеЕсли ОписаниеПоля.Значение = "Код строки" Тогда
					
					Результат = ДополнительныеПараметры.КлючСвязиПозиции;
					
				ИначеЕсли ОписаниеПоля.Значение = "Номенклатура" Тогда
					
					Результат = Номенклатура;
					
				ИначеЕсли ОписаниеПоля.Значение = "Характеристика" Тогда
					
					Результат = Характеристика;
					
				ИначеЕсли ОписаниеПоля.Значение = "Упаковка" Тогда
					
					КодЕдИзм			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеЭлементаТЧБ24.Получить("measureCode"),"ЧГ=0"));
					НаименованиеЕдИзм	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеЭлементаТЧБ24.Получить("measureName"),"ЧГ=0"));				
					
					ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
					Если ЗначениеЗаполнено(КодЕдИзм) Тогда
						ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(КодЕдИзм);;
					КонецЕсли;
					Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) И ЗначениеЗаполнено(НаименованиеЕдИзм) Тогда
						ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоНаименованию(НаименованиеЕдИзм);;
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найдена единица измерения по ид. Попытка создания единицы измерения с Ид: " + КодЕдИзм);
						ЕдиницаИзмерения = СформироватьЕдиницуИзмеренияПоИд(СтруктураНастроек, КодЕдИзм);
					КонецЕсли;
					
					Результат = ЕдиницаИзмерения;	
					
					
				ИначеЕсли ОписаниеПоля.Значение = "Ставка НДС" Тогда
					
					СтавкаНалога		= ДанныеЭлементаТЧБ24.Получить("vatRate");      
					Если СтавкаНалога 	= Неопределено Тогда 
						СтавкаНалога = "Без НДС"   
					Иначе
						СтавкаНалога	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(СтавкаНалога,"ЧРД=.; ЧГ=0"))*100;
					КонецЕсли;
					
					СтавкаНДС = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтавкуНДСПоСтавке(СтавкаНалога);   
					Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "СтавкаНДС") КонецЕсли;
					
					Результат = СтавкаНДС;									
					
				ИначеЕсли ОписаниеПоля.Значение = "Склад" Тогда
					
					Результат = ?(ЗначениеЗаполнено(СтрокаТЧ.Склад), СтрокаТЧ.Склад, Объект1С.Склад)
					
				ИначеЕсли ОписаниеПоля.Значение = "Вариант обеспечения" Тогда
					
					Результат = ?(ЗначениеЗаполнено(СтрокаТЧ.ВариантОбеспечения), СтрокаТЧ.ВариантОбеспечения, Перечисления.ВариантыОбеспечения.НеТребуется)
					
				ИначеЕсли ОписаниеПоля.Значение = "Содержание" Тогда
					
					Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Содержание) 
						И (ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ТипНоменклатуры") = Перечисления.ТипыНоменклатуры.Услуга ИЛИ НЕ ЗначениеЗаполнено(Номенклатура)) Тогда
						
						Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(ДанныеЭлементаТЧБ24.Получить("name")));
	
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Количество" Тогда
					
					Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ДанныеЭлементаТЧБ24.Получить("quantity"),"ЧРД=.; ЧГ=0"));
					
				ИначеЕсли ОписаниеПоля.Значение = "Цена" Тогда
					
					Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ДанныеЭлементаТЧБ24.Получить("price"),"ЧРД=.; ЧГ=0"));
					
				ИначеЕсли ОписаниеПоля.Значение = "Дата отгрузки" Тогда
					
					Результат = Объект1С.ДатаОтгрузки;
					
				КонецЕсли;
#КонецОбласти				
			ИначеЕсли ОписаниеПоля.ВидСущности = "Оплата" Тогда
#Область Оплата
				Если ОписаниеПоля.Значение = "Заказ" Тогда
	
					Результат = ДополнительныеПараметры.Основание;
		
				ИначеЕсли ОписаниеПоля.Значение = "Партнер" Тогда
					
					Результат = ДополнительныеПараметры.ОснованиеОбъект.Партнер;
					
				ИначеЕсли ОписаниеПоля.Значение = "Валюта взаиморасчетов" Тогда
					
					Результат = ДополнительныеПараметры.ОснованиеОбъект.Валюта;
					
				ИначеЕсли ОписаниеПоля.Значение = "Статья ДДС" Тогда
					
					Результат	 = Объект1С.СтатьяДвиженияДенежныхСредств;
					
				ИначеЕсли ОписаниеПоля.Значение = "Сумма платежа" Тогда
					
					Результат = ДополнительныеПараметры.СуммаРасшифровка;
					
				ИначеЕсли ОписаниеПоля.Значение = "Ставка НДС" Тогда
					
					Результат = ДанныеЭлементаТЧБ24.СтавкаНДС;
					
				ИначеЕсли ОписаниеПоля.Значение = "Сумма НДС" Тогда
					
					Результат = 0;
					
					Если ДанныеЭлементаТЧБ24.Сумма > 0 Тогда
						Результат = Окр(ДанныеЭлементаТЧБ24.СуммаНДС*ДополнительныеПараметры.СуммаРасшифровка/ДанныеЭлементаТЧБ24.Сумма,2);
					КонецЕсли;
					
				ИначеЕсли ОписаниеПоля.Значение = "Подразделение" Тогда
					
					Результат = ДополнительныеПараметры.ОснованиеОбъект.Подразделение;
					
				ИначеЕсли ОписаниеПоля.Значение = "Организация" Тогда
					
					Результат = ДополнительныеПараметры.ОснованиеОбъект.Организация;
					
				ИначеЕсли ОписаниеПоля.Значение = "НаправлениеДеятельности" Тогда
					
					Результат = ДополнительныеПараметры.ОснованиеОбъект.НаправлениеДеятельности;
					
				ИначеЕсли ОписаниеПоля.Значение = "Объект расчетов" Тогда
					
					Результат = ДополнительныеПараметры.ОбъектРасчетов;
					
				ИначеЕсли ОписаниеПоля.Значение = "Основание платежа" Тогда
					
					Результат = ДополнительныеПараметры.Основание; 

				КонецЕсли;
#КонецОбласти				
			ИначеЕсли ОписаниеПоля.ВидСущности = "Отгрузка" Тогда
#Область Отгрузка
				Если ОписаниеПоля.Значение = "Ключ связи" Тогда
	
					Результат = ДополнительныеПараметры.КлючСвязиПозиции;
					
				ИначеЕсли ОписаниеПоля.Значение = "Код строки" Тогда
					
					Результат = ДополнительныеПараметры.КлючСвязиПозиции;
					
				ИначеЕсли ОписаниеПоля.Значение = "Заказ" Тогда
	
					Результат = ДополнительныеПараметры.Основание;
					
				ИначеЕсли ОписаниеПоля.Значение = "Объект расчетов" Тогда
	
					Результат = ДополнительныеПараметры.ОбъектРасчетов;
					
				ИначеЕсли ОписаниеПоля.Значение = "Номенклатура" Тогда
					
					Результат = Номенклатура;
					
				ИначеЕсли ОписаниеПоля.Значение = "Характеристика" Тогда
					
					Результат = Характеристика;
					
				ИначеЕсли ОписаниеПоля.Значение = "Количество" Тогда
					
					Результат = ДополнительныеПараметры.Количество;
					
				ИначеЕсли ОписаниеПоля.Значение = "Упаковка" Тогда
					
					Результат = ДополнительныеПараметры.ИнформацияОПозицииТовара.Упаковка;
					
				ИначеЕсли ОписаниеПоля.Значение = "Ставка НДС" Тогда
					
					Результат = ДополнительныеПараметры.ИнформацияОПозицииТовара.СтавкаНДС;
					
				ИначеЕсли ОписаниеПоля.Значение = "Склад" Тогда
					
					Результат = ДополнительныеПараметры.ИнформацияОПозицииТовара.Склад;
					
				ИначеЕсли ОписаниеПоля.Значение = "Цена" Тогда
					
					Результат = ДополнительныеПараметры.ИнформацияОПозицииТовара.Цена;
					
				ИначеЕсли ОписаниеПоля.Значение = "Процент автоматической скидки" Тогда
					
					Результат = ДополнительныеПараметры.ИнформацияОПозицииТовара.ПроцентАвтоматическойСкидки;
					
				ИначеЕсли ОписаниеПоля.Значение = "Сумма автоматической скидки" Тогда
					
					Результат = (ДополнительныеПараметры.ИнформацияОПозицииТовара.СуммаАвтоматическойСкидки/ДополнительныеПараметры.ИнформацияОПозицииТовара.Количество)*ДополнительныеПараметры.Количество;
					
				ИначеЕсли ОписаниеПоля.Значение = "Процент ручной скидки" Тогда
					
					Результат = ДополнительныеПараметры.ИнформацияОПозицииТовара.ПроцентРучнойСкидки;
					
				ИначеЕсли ОписаниеПоля.Значение = "Сумма ручной скидки" Тогда
					
					Результат = (ДополнительныеПараметры.ИнформацияОПозицииТовара.СуммаРучнойСкидки/ДополнительныеПараметры.ИнформацияОПозицииТовара.Количество)*ДополнительныеПараметры.Количество;
					
				ИначеЕсли ОписаниеПоля.Значение = "Сумма" Тогда
					
					Цена = ДополнительныеПараметры.ИнформацияОПозицииТовара.Цена;
					
					Результат = Цена*ДополнительныеПараметры.Количество;	
					
				КонецЕсли;
#КонецОбласти
			КонецЕсли;
		
		ИначеЕсли ОписаниеПоля.ИсточникДанных = "<Свой алгоритм>" Тогда
			
			Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
			Параметры.СтруктураНастроек			= СтруктураНастроек;
			Параметры.Объект1С 					= Объект1С;
			Параметры.СтрокаТаблицыОбъекта1С 	= СтрокаТЧ;
			Параметры.ОбъектПортала 			= ДанныеЭлементаТЧБ24;
			
			Результат = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(ОписаниеПоля.Значение, Параметры, "Функция");		
			
		КонецЕсли;
	Исключение                                                                                                                              
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, ОписаниеПоля.ВидСущности + "/" + ОписаниеПоля.Значение + "/" + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	
	Возврат Результат;
	
КонецФункции


Процедура ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Объект1С, ОбъектПортала, АлгоритмКлюча)
	
	Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
	Параметры.СтруктураНастроек = СтруктураНастроек;
	Параметры.Объект1С 			= Объект1С;
	Параметры.ОбъектПортала 	= ОбъектПортала;

	Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(АлгоритмКлюча, Параметры, "Процедура");		
		
КонецПроцедуры

#КонецОбласти
