
#Область ПрограммныйИнтерфейс

// Осуществляет переход по ссылке в отдельное окно или в браузер
//
// Параметры:
//  WebСсылка		 - Строка - Адрес ресурса
//  ЗаголовокФормы	 - Строка - Устанавливаемый заголовок формы браузера
//  Объект			 - Ссылка - Владелец формы
//  Окно			 - РежимОткрытияОкнаФормы - Тип окна владельца
//
Процедура ПерейтиПоWebСсылке(WebСсылка, ЗаголовокФормы = "", Объект = Неопределено, Окно = Неопределено) Экспорт
		
#Если ВебКлиент Тогда
	ПерейтиПоНавигационнойСсылке(WebСсылка); 	
#Иначе	
	
	СпособОткрытияОбъектовБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "ИспользуемыйБраузер");
	
	Если СпособОткрытияОбъектовБитрикс24 = "Браузер ОС по умолчанию" ИЛИ СпособОткрытияОбъектовБитрикс24 = Неопределено Тогда
		ПерейтиПоНавигационнойСсылке(WebСсылка); 
	Иначе	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Адрес"			, WebСсылка);
		ПараметрыФормы.Вставить("ЗаголовокФормы", ЗаголовокФормы);
		ПараметрыФормы.Вставить("Объект"		, Объект);
		
		ОткрытьФорму("ОбщаяФорма.Б24_К_ФормаHTML", ПараметрыФормы,,Истина,Окно,,, РежимОткрытияОкнаФормы.Независимый);
		
	КонецЕсли;
#КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область ОповещениеПользователейОбОшибках

Процедура ПроверитьНаличиеОшибокИОповестить() Экспорт
	
	мОшибкиСинхронизации = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьМассивОшибокСообщений(); 
	
	Для каждого ТекСообщение Из мОшибкиСинхронизации Цикл
		
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтотОбъект, ТекСообщение);
		
		КлючУникальности = ТекСообщение.ВремяЗапуска + "/" + ТекСообщение.Операция + "/" + ТекСообщение.НомерСообщения;
		ПоказатьОповещениеПользователя(ТекСообщение.ТипСообщенияСтрокой, Оповещение, ТекСообщение.Сообщение, БиблиотекаКартинок.Б24_К_ЗначокБитрикс, СтатусОповещенияПользователя.Важное, КлючУникальности);	
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат) Экспорт
	
	ПараметрыОкна = Новый Структура();
	ПараметрыОкна.Вставить("ДанныеЗаписиОбОшибке", Результат);
	
	ОткрытьФорму("РегистрСведений.Б24_К_ЖурналОшибок.ФормаЗаписи",ПараметрыОкна);
	
КонецПроцедуры

#КонецОбласти


Процедура ПроверитьНаОбновление() Экспорт
	
	Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ЭтоПолноправныйПользователь() Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.РольДоступнаНаКлиенте("Б24_К_Администратор") 
		И НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.РольДоступнаНаКлиенте("ПолныеПрава") Тогда
		Возврат;
	КонецЕсли;
	
	Пользователь 			 = Строка(Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьТекущегоПользователя());
	ТекущаяДатаСеанса 		 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса();
	ДатаПоследнегоОповещения = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ПроверкаВерсииМодуляСинхронизацииБитрикс24","ДатаПоследнегоОповещения", Пользователь);
	
	Если ДатаПоследнегоОповещения <> Неопределено Тогда
		Если ДатаПоследнегоОповещения > ТекущаяДатаСеанса - 60*60*24*7 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СлужебныеДанныеДляПроверкиНаОбновление = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСлужебныеДанныеДляПроверкиНаОбновление();
	ДанныеОМодуле	= СлужебныеДанныеДляПроверкиНаОбновление.ДанныеОМодуле;
	СлужебныеДанные = СлужебныеДанныеДляПроверкиНаОбновление.СлужебныеДанные;
	
	ИнформацияОМодулях = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьИнформациюОМодулях(ДанныеОМодуле);
	
	Если ИнформацияОМодулях = Неопределено Тогда
		Возврат;
	КонецЕсли;
	    
	ПоследняяПроверяемаяВерсияМодуля 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ПроверкаВерсииМодуляСинхронизацииБитрикс24","ВерсияМодуляПоследняя", Пользователь);
	КоличествоОповещений 				= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ПроверкаВерсииМодуляСинхронизацииБитрикс24","КоличествоОповещений", Пользователь);
	КоличествоОповещений 	 			= ?(КоличествоОповещений = Неопределено, 0, КоличествоОповещений);

	
	Для каждого ТекМодуль Из ИнформацияОМодулях Цикл
		
		ПоследняяВерсияМодуляНаСайте = ТекМодуль.Получить("VersionModule");
		ПоследняяВерсияМодуляНаСайте = СокрЛП(СтрЗаменить(ПоследняяВерсияМодуляНаСайте, "BETA","")); 
		
		Если ПоследняяВерсияМодуляНаСайте = ПоследняяПроверяемаяВерсияМодуля И КоличествоОповещений > 2 Тогда
			
			Результат = Новый Структура;
			Результат.Вставить("ВерсияМодуляПоследняя"	 	, ПоследняяВерсияМодуляНаСайте);
			Результат.Вставить("ДатаПоследнегоОповещения"	, ТекущаяДатаСеанса);
			Результат.Вставить("КоличествоОповещений"		, КоличествоОповещений);
			
			Возврат;
			
		ИначеЕсли ПоследняяВерсияМодуляНаСайте <> ПоследняяПроверяемаяВерсияМодуля Тогда
			КоличествоОповещений = 0;	
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СлужебныеДанные.ВерсияМодуляСинхронизации, ПоследняяВерсияМодуляНаСайте) < 0 Тогда
			
			КоличествоОповещений = КоличествоОповещений + 1 ;	
			
			ПараметрыНовойФормы = Новый Структура;
			ОткрытьФорму("Обработка.Б24_К_Администрирование.Форма.ПроверитьНаОбновления", ПараметрыНовойФормы,,,);
		Иначе
			КоличествоОповещений = 0;	
		КонецЕсли;
	  
	  Прервать; // проверяется только последняя версия
	  
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ВерсияМодуляПоследняя"	 	, ПоследняяВерсияМодуляНаСайте);
	Результат.Вставить("ДатаПоследнегоОповещения"	, ТекущаяДатаСеанса);
	Результат.Вставить("КоличествоОповещений"		, КоличествоОповещений);
	
	Б24_К_ОбщегоНазначенияВызовСервера.УстановитьЗначениеВХранилищаНастроек("ПроверкаВерсииМодуляСинхронизацииБитрикс24", Результат, Строка(Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьТекущегоПользователя()));
	
КонецПроцедуры

Функция ПолучитьСтруктуруГлобальныхПеременных() Экспорт
	

	НастройкиСеанса = Новый Структура;  
	
	Если ТипЗнч(Б24_К_НастройкиСеанса) = Тип("Структура") И Б24_К_НастройкиСеанса.Свойство("НастройкиГорячегоЗапуска") Тогда  
		НастройкиСеанса.Вставить("НастройкиГорячегоЗапуска", Б24_К_НастройкиСеанса.НастройкиГорячегоЗапуска);
	Иначе
		НастройкиГорячегоЗапуска = Новый Структура;
		НастройкиГорячегоЗапуска.Вставить("ИдПользователяБитрикс24"	, "");
		НастройкиГорячегоЗапуска.Вставить("Браузер"					, "");
		НастройкиГорячегоЗапуска.Вставить("Портал"					, "");
		НастройкиСеанса.Вставить("НастройкиГорячегоЗапуска", НастройкиГорячегоЗапуска);
	КонецЕсли;
	
	Если ТипЗнч(Б24_К_НастройкиСеанса) = Тип("Структура") И Б24_К_НастройкиСеанса.Свойство("НастройкиТелефонии") Тогда  
		НастройкиСеанса.Вставить("НастройкиТелефонии", Б24_К_НастройкиСеанса.НастройкиТелефонии);
	Иначе
		НастройкиТелефонии = Новый Структура;	
		НастройкиТелефонии.Вставить("МессенджерУстановлен"	, Ложь);
		НастройкиСеанса.Вставить("НастройкиТелефонии"	, НастройкиТелефонии);
	КонецЕсли;
	
	Возврат НастройкиСеанса;
	
КонецФункции
 
Функция ПолучитьСтруктуруДанныхСеанса() Экспорт
	
	ДанныеСеансов = Новый Структура;                        
	ДанныеСеансов.Вставить("НастройкаПодключения"		, Неопределено);
	ДанныеСеансов.Вставить("СессияБитрикс24"			, "");
	ДанныеСеансов.Вставить("ИдентификаторВладельца"		, "");
	ДанныеСеансов.Вставить("ТипВладельца"				, "");
	ДанныеСеансов.Вставить("НоваяСущность"				, Ложь);
	ДанныеСеансов.Вставить("ДополнительныеДанные"		, Неопределено);
	ДанныеСеансов.Вставить("ВводитсяНаОсновании"		, Ложь);
	ДанныеСеансов.Вставить("АдресНавигационнойСсылки"	, "");
	
	Возврат ДанныеСеансов;
	
КонецФункции
