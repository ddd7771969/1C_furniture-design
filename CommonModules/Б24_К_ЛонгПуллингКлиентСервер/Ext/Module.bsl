
Функция ПолучитьНазваниеФоновогоЗаданияСоединенияСБитрикс24(НастройкаПодключения) Экспорт
	Возврат "(Битрикс24.1С:Комплексная) Подключение к Битрикс24:" + Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьРеквизитОбъекта(НастройкаПодключения, "ИдентификаторПодключения");
КонецФункции

Процедура ЗапускДлительногоСоединенияКПорталуОбщий(НастройкаПодключения, ИдентификаторПользователяБитрикс24 = "", ДатаЗапускаФоновогоЗадания = Неопределено)  Экспорт
	
	Если НЕ ЗначениеЗаполнено(НастройкаПодключения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указано по какой настройке производится загрузка в режиме реального времени.");
		Возврат;
	КонецЕсли;
	
	Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НастройкаПодключения, "ПометкаУдаления") = Истина Тогда
		Б24_К_ЛонгПуллингВызовСервера.УстановитьНастройкуЛонгПуллинг(НастройкаПодключения, Ложь);
		Возврат;	
	КонецЕсли;
	
	ЗапускДлительногоСоединенияКПорталу(НастройкаПодключения, ИдентификаторПользователяБитрикс24, ДатаЗапускаФоновогоЗадания);
	
КонецПроцедуры

Процедура ЗапускДлительногоСоединенияКПорталу(НастройкаПодключения, ИдентификаторПользователяБитрикс24, ДатаЗапускаФоновогоЗадания)  
			
	ДлительноеСоединениеПользователемНеПрервано = Б24_К_ЛонгПуллингВызовСервера.ПолучитьНастройкуЛонгПуллинг(НастройкаПодключения);	
	Если ДлительноеСоединениеПользователемНеПрервано <> Истина Тогда
		Возврат;	
	КонецЕсли;
			
	СтруктураНастроек = Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);
	
	Если СтруктураНастроек = Неопределено Тогда
		Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(15);
		ЗапускДлительногоСоединенияКПорталу(НастройкаПодключения, ИдентификаторПользователяБитрикс24, ДатаЗапускаФоновогоЗадания);
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Подключение канала к Битрикс24";	
	
	НастройкиПодключения = ПолучитьИдКаналаИАдресПодключенияДляРеалТаймЗагрузкиСПортала(СтруктураНастроек);
	
	Если НастройкиПодключения = Ложь Тогда
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось подключиться по Long polling.");
		Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(15);
		ЗапускДлительногоСоединенияКПорталу(НастройкаПодключения, ИдентификаторПользователяБитрикс24, ДатаЗапускаФоновогоЗадания);
		Возврат;
	КонецЕсли;
	
	РазобранныйАдресХостинга = Новый Структура;
	
	Б24_К_RestApiВызовСервера.РазобратьАдресСайта(НастройкиПодключения.АдресПодключения, РазобранныйАдресХостинга);	
	
#Область ОбработкаДлительногоСоединения
	ДанныеДляПродолженияМониторинга = Новый Структура;
	ДанныеДляПродолженияМониторинга.Вставить("mid"	, "");	
	ДанныеДляПродолженияМониторинга.Вставить("tag"	, "");	
	ДанныеДляПродолженияМониторинга.Вставить("time"	, "");	
	
	
	ДатаВыгрузкиМетки = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса()-45;
	
	ЕстьОшибки = Ложь;
	
	Пока Истина Цикл        
		
		Пока ЕстьОшибки = Ложь Цикл
			
			Если НЕ ЗначениеЗаполнено(ИдентификаторПользователяБитрикс24) Тогда
				ДлительноеСоединениеПользователемНеПрервано = Б24_К_ЛонгПуллингВызовСервера.ПолучитьНастройкуЛонгПуллинг(НастройкаПодключения);	
				Если ДлительноеСоединениеПользователемНеПрервано <> Истина Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Прервано пользователем.");
					Возврат;	
				КонецЕсли;
			КонецЕсли;
			
			Если ДатаЗапускаФоновогоЗадания <> Неопределено Тогда
				
				Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса()- ДатаЗапускаФоновогоЗадания > 60*60*12 Тогда    // от утечек памяти
					
					ПараметрыВыгрузки = Новый Структура;
					ПараметрыВыгрузки.Вставить("НастройкаПодключения", НастройкаПодключения);
					Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_К_ЛонгПуллингВызовСервера.ЗапуститьСоединениеСБитрикс24ВФоне", ПолучитьНазваниеФоновогоЗаданияСоединенияСБитрикс24(НастройкаПодключения), ПараметрыВыгрузки, 0, Истина);  
				
					Возврат;
				КонецЕсли;
				
			КонецЕсли;
			
			Если НастройкиПодключения.ДатаПрерывания <= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса() Тогда
				
				СтруктураНастроек = Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);
				Если СтруктураНастроек = Неопределено Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(15);
					ЗапускДлительногоСоединенияКПорталу(НастройкаПодключения, ИдентификаторПользователяБитрикс24, ДатаЗапускаФоновогоЗадания);
					Возврат;
				КонецЕсли;
				
				НастройкиПодключения = ПолучитьИдКаналаИАдресПодключенияДляРеалТаймЗагрузкиСПортала(СтруктураНастроек);	
				Если НастройкиПодключения = Ложь Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось получить данные для подключения к порталу для реал тайм загрузки. Возможно не настроен Long polling.");
					Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(5);	
					ЗапускДлительногоСоединенияКПорталу(НастройкаПодключения, ИдентификаторПользователяБитрикс24, ДатаЗапускаФоновогоЗадания);	
					Возврат;									
				КонецЕсли;	
			КонецЕсли;
			
			ТекущуюДатуСеанса = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса();
			
			Если ДатаВыгрузкиМетки + 40 < Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса() Тогда
				
				ДатаВыгрузкиМетки = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса();
				
				ПараметрыЗапроса = Новый Структура;
				ПараметрыЗапроса.Вставить("НастройкаПодключения", НастройкаПодключения);
				Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_К_ЛонгПуллингВызовСервера.ВыгрузитьМеткуВремениВФоне", "Выгрузка метки времени для p&p", ПараметрыЗапроса, 3);
				
			КонецЕсли;
			
			Если НастройкиПодключения.Версия < 4 Тогда	
				ПараметрыДляПродолженияМониторинга = ?(ЗначениеЗаполнено(ДанныеДляПродолженияМониторинга.tag), "&tag=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ДанныеДляПродолженияМониторинга.tag), "") + ?(ЗначениеЗаполнено(ДанныеДляПродолженияМониторинга.time), "&time=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ДанныеДляПродолженияМониторинга.time), "");
			Иначе
				ПараметрыДляПродолженияМониторинга = ?(ЗначениеЗаполнено(ДанныеДляПродолженияМониторинга.mid), "&mid=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ДанныеДляПродолженияМониторинга.mid), "");
			КонецЕсли;
			
			ОтветСтрокой = Б24_К_RestApiВызовСервера.ПолучитьДанныеLongPulling(СтруктураНастроек, НастройкиПодключения, РазобранныйАдресХостинга, ПараметрыДляПродолженияМониторинга);	
			
			Если ОтветСтрокой <> Неопределено Тогда
				
				Если ОтветСтрокой <> "" Тогда
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Обработка данных");
					
					ОтветСтрокой = СтрЗаменить(ОтветСтрокой, "#!NGINXNMS!#", ""); 
					ОтветСтрокой = СтрЗаменить(ОтветСтрокой, "#!NGINXNME!#", "");
					
					Попытка
						
						СтруктураОтвета = Б24_К_RestApiВызовСервера.ПрочитатьJSONНаСервере(ОтветСтрокой, Истина);
						
						ДанныеДляПродолженияМониторинга.mid  = СтруктураОтвета.Получить("mid");
						ДанныеДляПродолженияМониторинга.tag  = СтруктураОтвета.Получить("tag");
						ДанныеДляПродолженияМониторинга.time = СтруктураОтвета.Получить("time");
						
						ТелоСообщения = СтруктураОтвета.Получить("text");
						Если ТелоСообщения = Неопределено Тогда
							Продолжить;
						КонецЕсли;   						
						
						КомандаСообщения = ТелоСообщения.Получить("command");
						Если КомандаСообщения = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						ПередаваемыеПараметры = ТелоСообщения.Получить("params");
						Если ПередаваемыеПараметры = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						Если КомандаСообщения = "channel_expire" Тогда
							ЕстьОшибки = Истина;
						Иначе
							ОбработатьСообщениеСПортала(СтруктураНастроек, НастройкаПодключения, КомандаСообщения, ПередаваемыеПараметры, ТелоСообщения);	
						КонецЕсли;
								
					Исключение           
						Если СтрНайти(ОтветСтрокой, "<b>Warning</b>") = 0 Тогда
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось разобрать JSON");
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Пришедший JSON: " + Б24_К_RestApiВызовСервера.РаскодироватьСтрокуСервер(ОтветСтрокой));
						КонецЕсли;
						ЕстьОшибки = Истина;
						
					КонецПопытки;
					
				КонецЕсли;
				
			Иначе
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось подключиться к серверу Long Pulling для загрузки данных в режиме в реального времени.");
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Повторное подключение...");
				
				ЕстьОшибки = Истина;
				
			КонецЕсли;
		
		КонецЦикла;
		
		Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(5);
		
				
		СтруктураНастроек = Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);
		Если СтруктураНастроек = Неопределено Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(15);
			ЗапускДлительногоСоединенияКПорталу(НастройкаПодключения, ИдентификаторПользователяБитрикс24, ДатаЗапускаФоновогоЗадания);
			Возврат;
		КонецЕсли;
		
		НастройкиПодключения = ПолучитьИдКаналаИАдресПодключенияДляРеалТаймЗагрузкиСПортала(СтруктураНастроек);
		Если НастройкиПодключения = Ложь Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось получить данные для подключения к порталу для реал тайм загрузки. Возможно не настроен Long polling.");
			Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(15);
			ЗапускДлительногоСоединенияКПорталу(НастройкаПодключения, ИдентификаторПользователяБитрикс24, ДатаЗапускаФоновогоЗадания);
			Возврат;
		КонецЕсли;
		РазобранныйАдресХостинга = Новый Структура;
		Б24_К_RestApiВызовСервера.РазобратьАдресСайта(НастройкиПодключения.АдресПодключения, РазобранныйАдресХостинга);	
		
		ЕстьОшибки = Ложь;
	
	КонецЦикла;
#КонецОбласти
	
КонецПроцедуры


Процедура ОбработатьСообщениеСПортала(СтруктураНастроек, НастройкаПодключения, КомандаСообщения, ПередаваемыеПараметры, ТелоСообщения) Экспорт
	
	ИменаИспользуемыхРеглЗаданий = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьИменаИспользуемыхРеглЗаданий();
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	Если КомандаСообщения = "closePPSession1C" Тогда                  
		
		Портал 						= ПередаваемыеПараметры.Получить("B24");
		ИдентификаторПодключения 	= Формат(ПередаваемыеПараметры.Получить("idAdress"), "ЧГ=0");
		
		Если СтруктураНастроек.Портал = Портал И СтруктураНастроек.ИдентификаторПодключения = ИдентификаторПодключения Тогда 
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - закрытие сеанса");
			
			Б24_К_ЛонгПуллингВызовСервера.УстановитьНастройкуЛонгПуллинг(НастройкаПодключения, Ложь);
			Возврат;
			
		КонецЕсли;
		
	ИначеЕсли КомандаСообщения = "event_offline" Тогда
		
		ПараметрыСобытия = ТелоСообщения.Получить("params");
		Если ТипЗнч(ПараметрыСобытия) <> Тип("Массив") Тогда
			Возврат;
		КонецЕсли;
		
		ЕстьДанныеДляЗагрузкиСмартПроцесса 	= Ложь;
		ЕстьДанныеДляЗагрузкиСинхронизации 	= Ложь;
		ЕстьДанныеДляЗагрузкиСУ 			= Ложь;
		Для каждого ТекПараметр Из ПараметрыСобытия Цикл
			Если Формат(ТекПараметр.Получить("connector_id"), "ЧГ=0") = СтруктураНастроек.ИдентификаторПодключения Тогда
				ЕстьДанныеДляЗагрузкиСинхронизации = Истина;
			КонецЕсли;
			Если Формат(ТекПараметр.Получить("connector_id"), "ЧГ=0") = СтруктураНастроек.ИдентификаторПодключения + Б24_СМ_ОбщегоНазначенияВызовСервера.ПолучитьПостфиксИдКоннектора() Тогда
				ЕстьДанныеДляЗагрузкиСмартПроцесса = Истина;
			КонецЕсли;   
			Если Формат(ТекПараметр.Получить("connector_id"), "ЧГ=0") = СтруктураНастроек.ИдентификаторПодключения + Б24_СУ_ОбщегоНазначенияВызовСервера.ПолучитьПостфиксИдКоннектора() Тогда
				ЕстьДанныеДляЗагрузкиСУ = Истина;
			КонецЕсли;   
		КонецЦикла;      
		
		Если ЕстьДанныеДляЗагрузкиСинхронизации Тогда
			
			Если НЕ Б24_К_ЛонгПуллингВызовСервера.РеалТаймДоступенДляСинхронизации(НастройкаПодключения) Тогда
				Возврат;
			КонецЕсли;
							
			Если НЕ Б24_К_СлужебныйВызовСервера.МодульСинхронизацииДоступен(НастройкаПодключения) Тогда
				Возврат;
			КонецЕсли; 				
			
			ИмяРасписания = "Импорт данных из Битрикс24";
			Если НЕ Б24_К_ОбщегоНазначенияВызовСервера.СейчасВыполняетсяЗадание(ИмяРасписания) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - новые данные на портале");
				Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КС_ОбщегоНазначенияВызовСервера.ЗапуститьВыполнениеЗагрузкиВФоне", ИмяРасписания, ПараметрыЗапроса, 3);	
				Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(2);    //чтобы успело запуститься фоновое задание
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЕстьДанныеДляЗагрузкиСмартПроцесса Тогда
			
			Если НЕ Б24_К_ЛонгПуллингВызовСервера.РеалТаймДоступенДляСмартПроцесса(НастройкаПодключения) Тогда
				Возврат;
			КонецЕсли;
							
			Если НЕ Б24_К_СлужебныйВызовСервера.МодульСмартПроцессовДоступен(НастройкаПодключения) Тогда           
				Возврат;
			КонецЕсли; 				
			
			ИмяРасписания = "Импорт данных СП из Битрикс24";
			Если НЕ Б24_К_ОбщегоНазначенияВызовСервера.СейчасВыполняетсяЗадание(ИмяРасписания) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - новые данные на портале");
				Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_СМ_ОбщегоНазначенияВызовСервера.ЗапуститьВыполнениеЗагрузкиВФоне", ИмяРасписания, ПараметрыЗапроса, 3);
				Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(2);    //чтобы успело запуститься фоновое задание
			КонецЕсли; 
			
		КонецЕсли;  
		
		Если ЕстьДанныеДляЗагрузкиСУ Тогда
			
			Если НЕ Б24_К_СлужебныйВызовСервера.МодульСкладскойУчетДоступен(НастройкаПодключения) Тогда           
				Возврат;
			КонецЕсли; 				
			
			ИмяРасписания = "Импорт данных СУ из Битрикс24";
			Если НЕ Б24_К_ОбщегоНазначенияВызовСервера.СейчасВыполняетсяЗадание(ИмяРасписания) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - новые данные на портале");  
				
				ПараметрыЗапроса = Б24_СУ_ОбщегоНазначенияВызовСервера.ПараметрыВыполненияОбменаВФоне(НастройкаПодключения, Новый Массив, Ложь, Истина);
				Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_СУ_ОбщегоНазначенияВызовСервера.ВыполнитьОбменВФоне", ИмяРасписания, ПараметрыЗапроса, 3, Истина);								
				
				Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(2);    //чтобы успело запуститься фоновое задание
			КонецЕсли; 
			
		КонецЕсли;  
		
	Иначе
						
		ИдМодуля = ТелоСообщения.Получить("module_id");
		Если НЕ ИдМодуля = "1ctotal" Тогда
			Возврат;
		КонецЕсли;
		
		Портал 						= ПередаваемыеПараметры.Получить("B24");
		ИдентификаторПодключения 	= Формат(ПередаваемыеПараметры.Получить("idAdress"), "ЧГ=0");
		
		Если СтруктураНастроек.Портал = Портал И СтруктураНастроек.ИдентификаторПодключения = ИдентификаторПодключения Тогда 
			
			ПараметрыЗапроса.Вставить("Портал"	, Портал);
			ПараметрыЗапроса.Вставить("ИдСессии", ПередаваемыеПараметры.Получить("session"));
			
			Если КомандаСообщения = "ImportEntity" Тогда
								
				Если НЕ Б24_К_СлужебныйВызовСервера.МодульРаботыИзОдногоОкнаДоступен(НастройкаПодключения) Тогда
					Возврат;
				КонецЕсли; 				
		
				Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("РаботаИзОдногоОкна") Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - создание сущности из интеграции сервисов");
					
					ПараметрыЗапроса.Вставить("ИдВладельца"				, Формат(ПередаваемыеПараметры.Получить("IdEntity"), "ЧГ=0"));
					ПараметрыЗапроса.Вставить("ТипВладельца"			, Формат(ПередаваемыеПараметры.Получить("TypeEntity"), "ЧГ=0"));
					ПараметрыЗапроса.Вставить("ИдПолучателя"			, Формат(ПередаваемыеПараметры.Получить("IdRecipient"), "ЧГ=0"));
					ПараметрыЗапроса.Вставить("ТипПолучателя"			, Формат(ПередаваемыеПараметры.Получить("TypeRecipient"), "ЧГ=0"));
					ПараметрыЗапроса.Вставить("ТипДокумента"			, Формат(ПередаваемыеПараметры.Получить("TypeDocument"), "ЧГ=0"));
					ПараметрыЗапроса.Вставить("ИдПользователя"			, Формат(ПередаваемыеПараметры.Получить("idUser"), "ЧГ=0"));
					ПараметрыЗапроса.Вставить("Адрес"					, ПередаваемыеПараметры.Получить("url"));
					ПараметрыЗапроса.Вставить("Браузер"					, ПередаваемыеПараметры.Получить("userAgent"));
					
					Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КБ_РаботаИзОдногоОкнаВызовСервера.ImportEntity", "Импорт компании/контакта", ПараметрыЗапроса, 1, Истина);
				КонецЕсли;
				
			ИначеЕсли КомандаСообщения = "getAnalytics" Тогда
								
				Если НЕ Б24_К_СлужебныйВызовСервера.МодульРаботыИзОдногоОкнаДоступен(НастройкаПодключения) Тогда
					Возврат;
				КонецЕсли; 				
		
				Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("РаботаИзОдногоОкна") Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - открытие отчета из Битрикс24");
					
					ПараметрыЗапроса.Вставить("ИдПользователя"			, Формат(ПередаваемыеПараметры.Получить("idUser"), "ЧГ=0"));
					ПараметрыЗапроса.Вставить("Адрес"					, ПередаваемыеПараметры.Получить("url"));
					ПараметрыЗапроса.Вставить("Браузер"					, ПередаваемыеПараметры.Получить("userAgent"));
					
					Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КБ_РаботаИзОдногоОкнаВызовСервера.getAnalytics", "Формирование аналитики", ПараметрыЗапроса, 1, Истина);
				КонецЕсли;
				
			ИначеЕсли КомандаСообщения = "ImportSession" Тогда
								
				Если НЕ Б24_К_СлужебныйВызовСервера.МодульРаботыИзОдногоОкнаДоступен(НастройкаПодключения) Тогда
					Возврат;
				КонецЕсли; 				
		
				Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("РаботаИзОдногоОкна") Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - открытие сущности из интеграции сервисов");
					
					ПараметрыЗапроса.Вставить("ИдПользователя"			, Формат(ПередаваемыеПараметры.Получить("idUser"), "ЧГ=0"));
					ПараметрыЗапроса.Вставить("Адрес"					, ПередаваемыеПараметры.Получить("url"));
					ПараметрыЗапроса.Вставить("Браузер"					, ПередаваемыеПараметры.Получить("userAgent"));
					
					Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КБ_РаботаИзОдногоОкнаВызовСервера.ImportSession", "Импорт сессии портала", ПараметрыЗапроса, 1, Истина);
				КонецЕсли;
				
			ИначеЕсли КомандаСообщения = "ListOfDocuments" Тогда
						
				Если НЕ Б24_К_СлужебныйВызовСервера.МодульРеестраПечатныхФормДоступен(НастройкаПодключения) Тогда
					Возврат;
				КонецЕсли; 				
				
				Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("РеестрПечатныхФорм") Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - получение списка документов для реестра");
					
					ПараметрыЗапроса.Вставить("ИдВладельца"				, ПередаваемыеПараметры.Получить("IdEntity"));
					ПараметрыЗапроса.Вставить("ТипВладельца"			, ПередаваемыеПараметры.Получить("TypeEntity"));
					
					Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КБ_РеестрПечатныхФормВызовСервера.ListOfDocuments", "Получение списка печатных форм", ПараметрыЗапроса, 1, Истина);
				КонецЕсли;
				
			ИначеЕсли КомандаСообщения = "GetDocuments" Тогда
				
				Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("РеестрПечатныхФорм") Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - выгрузка печатных форм для Реестра");
					
					ПараметрыЗапроса.Вставить("ИдВладельца"				, ПередаваемыеПараметры.Получить("idEntity"));
					ПараметрыЗапроса.Вставить("ТипВладельца"			, ПередаваемыеПараметры.Получить("typeEntity"));
					ПараметрыЗапроса.Вставить("ПечФормы"				, ПередаваемыеПараметры.Получить("docs"));
					
					Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КБ_РеестрПечатныхФормВызовСервера.GetDocuments", "Выгрузка документов реестра печатных форм", ПараметрыЗапроса, 1, Истина);
				КонецЕсли;
				
			ИначеЕсли КомандаСообщения = "getSpark1c" Тогда
						
				Если НЕ Б24_К_СлужебныйВызовСервера.МодульСПАРКДоступен(НастройкаПодключения) Тогда
					Возврат;
				КонецЕсли; 				
				
				Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СПАРК") Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - получение справки СПАРК");
					
					ПараметрыЗапроса.Вставить("ИдВладельца"				, ПередаваемыеПараметры.Получить("idEntity"));
					ПараметрыЗапроса.Вставить("ТипВладельца"			, ПередаваемыеПараметры.Получить("typeEntity"));
					ПараметрыЗапроса.Вставить("ИНН"						, Формат(ПередаваемыеПараметры.Получить("inn"), "ЧГ=0"));
					
					Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КБ_1СПАРКВызовСервера.getSpark1c", "Выгрузка справки СПАРК", ПараметрыЗапроса, 1, Истина);
				КонецЕсли;
				
			ИначеЕсли КомандаСообщения = "robotAction" Тогда
						
				Если НЕ Б24_К_СлужебныйВызовСервера.МодульАвтоматизацииДоступен(НастройкаПодключения) Тогда
					Возврат;
				КонецЕсли; 				
				
				Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("Автоматизация") Тогда
					
					Если НЕ Б24_К_ОбщегоНазначенияВызовСервера.СейчасВыполняетсяЗадание(ИменаИспользуемыхРеглЗаданий.РоботыБ24) Тогда
						
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - Анализ роботов");
						
						Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КА_РоботыИТриггерыВызовСервера.robotAction", ИменаИспользуемыхРеглЗаданий.РоботыБ24, ПараметрыЗапроса, 5);
					Иначе
						
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Команда пуш сервера - анализ роботов уже выполняется");
						
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли КомандаСообщения = "bitrixSeach" Тогда
						
				Если НЕ Б24_К_СлужебныйВызовСервера.МодульБитриксПоискДоступен(НастройкаПодключения) Тогда
					Возврат;
				КонецЕсли; 				
				
				ПараметрыЗапроса = Новый Структура;
				ПараметрыЗапроса.Вставить("НастройкаПодключения"	, НастройкаПодключения);
				ПараметрыЗапроса.Вставить("Портал"					, ПередаваемыеПараметры.Получить("B24"));
				ПараметрыЗапроса.Вставить("ИдСессии"				, ПередаваемыеПараметры.Получить("session"));
				ПараметрыЗапроса.Вставить("ТипКлиента"				, ПередаваемыеПараметры.Получить("entityTypeId"));
				ПараметрыЗапроса.Вставить("СтрокаПоиска"			, ПередаваемыеПараметры.Получить("searchString"));
				
				Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КБ_БитриксПоискВызовСервера.bitrixSeach", ИменаИспользуемыхРеглЗаданий.БитриксПоискЛист, ПараметрыЗапроса,, Истина);
				
			ИначеЕсли КомандаСообщения = "uploadClient" Тогда
				
				ПараметрыЗапроса = Новый Структура;
				ПараметрыЗапроса.Вставить("НастройкаПодключения"	, НастройкаПодключения);
				ПараметрыЗапроса.Вставить("Портал"					, ПередаваемыеПараметры.Получить("B24"));
				ПараметрыЗапроса.Вставить("ИдСессии"				, ПередаваемыеПараметры.Получить("session"));
				ПараметрыЗапроса.Вставить("ТипКлиента"				, ПередаваемыеПараметры.Получить("entityTypeId"));
				ПараметрыЗапроса.Вставить("ИдСтрокиКлиента"			, ПередаваемыеПараметры.Получить("idSearchString"));
				
				Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КБ_БитриксПоискВызовСервера.uploadClient", ИменаИспользуемыхРеглЗаданий.БитриксПоискЗагрузка, ПараметрыЗапроса,, Истина);
					
			ИначеЕсли КомандаСообщения = "productSearch" Тогда

				Если НЕ Б24_К_СлужебныйВызовСервера.МодульСкладскойУчетДоступен(НастройкаПодключения) Тогда
					Возврат;
				КонецЕсли;
				
				ПараметрыЗапроса = Новый Структура;
				ПараметрыЗапроса.Вставить("НастройкаПодключения"	, НастройкаПодключения);
				ПараметрыЗапроса.Вставить("Портал"					, ПередаваемыеПараметры.Получить("B24"));
				ПараметрыЗапроса.Вставить("ИдСессии"				, ПередаваемыеПараметры.Получить("session"));
				ПараметрыЗапроса.Вставить("СтрокаПоиска"			, ПередаваемыеПараметры.Получить("searchString"));
				
				Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_СУ_ПодборНоменклатурыСервер.productSearch", ИменаИспользуемыхРеглЗаданий.ПодборТовараЛист, ПараметрыЗапроса,, Истина);
				
			ИначеЕсли КомандаСообщения = "uploadProduct" Тогда
				
				ПараметрыЗапроса = Новый Структура;
				ПараметрыЗапроса.Вставить("НастройкаПодключения"	, НастройкаПодключения);
				ПараметрыЗапроса.Вставить("Портал"					, ПередаваемыеПараметры.Получить("B24"));
				ПараметрыЗапроса.Вставить("ИдСессии"				, ПередаваемыеПараметры.Получить("session"));
				ПараметрыЗапроса.Вставить("ИдСтрокиТовара"			, ПередаваемыеПараметры.Получить("idSearchString"));
				
				Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_СУ_ПодборНоменклатурыСервер.uploadProduct", ИменаИспользуемыхРеглЗаданий.ПодборТовараЗагрузка, ПараметрыЗапроса,, Истина);
				
			ИначеЕсли КомандаСообщения = "updateProduct" Тогда
				
				ПараметрыЗапроса = Новый Структура;
				ПараметрыЗапроса.Вставить("НастройкаПодключения"	, НастройкаПодключения);
				ПараметрыЗапроса.Вставить("Портал"					, ПередаваемыеПараметры.Получить("B24"));
				ПараметрыЗапроса.Вставить("ИдСессии"				, ПередаваемыеПараметры.Получить("session"));
				ПараметрыЗапроса.Вставить("ИдСтрокиТовара"			, ПередаваемыеПараметры.Получить("idSearchString"));
				
				Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_СУ_ПодборНоменклатурыСервер.updateProduct", ИменаИспользуемыхРеглЗаданий.ПодборТовараЗагрузка, ПараметрыЗапроса,, Истина);
				
				
			ИначеЕсли КомандаСообщения = "sliderClosed" Тогда 
				
			ИначеЕсли КомандаСообщения = "ping" Тогда
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
				
КонецПроцедуры


Функция ПолучитьИдКаналаИАдресПодключенияДляРеалТаймЗагрузкиСПортала(СтруктураНастроек) Экспорт
	
	Если СтруктураНастроек = Неопределено Тогда
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка,"Не удалось получить настройки. Подробности в логе синхронизации.");
		Возврат Ложь;
	КонецЕсли;   
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/pull.application.config.get", ""); 
	Если СтруктураОтвета = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	Если result = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	channels = result.Получить("channels");
	Если channels = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	shared = channels.Получить("shared");
	Если shared = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	private = channels.Получить("private");
	Если private = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	server = result.Получить("server");
	Если server = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	                                      
	Версия 				= Число(server.Получить("version"));
	АдресПодключения 	= server.Получить("long_polling");
	АдресПодключенияС 	= server.Получить("long_pooling_secure");
	ИдКанала 			= shared.Получить("id");
	
	time = СтруктураОтвета.Получить("time");
	Если time = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	date_start = time.Получить("date_start");
	Если date_start = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДатаПушСервера 		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), date_start);
	ТекущаяДатаСеанса 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса();
	
	ВремяОкончанияПриватный = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), private.Получить("end"));   	
	ВремяОкончанияПриватный	= ?(ВремяОкончанияПриватный = Неопределено, ТекущаяДатаСеанса, ВремяОкончанияПриватный);
	
	ВремяОкончанияОбщий 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), shared.Получить("end"));
	ВремяОкончанияОбщий		= ?(ВремяОкончанияОбщий = Неопределено, ТекущаяДатаСеанса, ВремяОкончанияОбщий);
	
	Если ВремяОкончанияПриватный < ВремяОкончанияОбщий Тогда
		Смещение = ВремяОкончанияПриватный - ДатаПушСервера;
	Иначе
		Смещение = ВремяОкончанияОбщий - ДатаПушСервера;
	КонецЕсли;
	
	ДатаПрерывания = ТекущаяДатаСеанса + Смещение;
	
	Если НЕ ЗначениеЗаполнено(АдресПодключения) ИЛИ НЕ ЗначениеЗаполнено(ИдКанала) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(АдресПодключенияС) Тогда
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка,"Не настроен https у сервера Push&Pull.");
		Возврат Ложь;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("АдресПодключения"	, АдресПодключенияС);	
	Результат.Вставить("ИдКанала"			, ИдКанала);	
	Результат.Вставить("Версия"				, Версия);	
	Результат.Вставить("ДатаПрерывания"		, ДатаПрерывания);	
	Результат.Вставить("clientId"			, result.Получить("clientId"));	
	
	Возврат Результат;
	
КонецФункции

