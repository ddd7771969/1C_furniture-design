Процедура ПолучитьПодключение() Экспорт
	
	ПараметрыСеанса.ЗагрузкаИзБитрикс = Истина;
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(Константы.НастройкаПодключенияКБитриксПоУмолчанию.Получить());  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Вставить("КоличествоПовторенийПриОшибках", 2);
	СтруктураНастроек.Операция = СтруктураНастроек.ТипыОперацийСинхронизации.Проекты;
	ЗагрузитьВсеДанныеСПортала(СтруктураНастроек, "crm.deal.list", "select[]=ID","filter[CATEGORY_ID]=2", "filter[ID]"); 
	
	ПараметрыСеанса.ЗагрузкаИзБитрикс = Ложь;
	
КонецПроцедуры // ПолучитьПодключение()

Процедура ЗагрузитьВсеДанныеСПортала(СтруктураНастроек, Метод, Фильтр = "" ,ОбязательныйОтбор = "", ОтборПоПараметру = "") Экспорт
		
		МетодПолностью 			= "/rest/" + Метод;
		ПараметрыHTTPЗапроса 	= ?(ЗначениеЗаполнено(Фильтр), "&" + Фильтр, "" ) + ?(ЗначениеЗаполнено(ОбязательныйОтбор), "&" + ОбязательныйОтбор, "" );
		
		СтруктураОтвета 	 		= Неопределено;
		КоличествоВыполнений 		= 0;
		СтруктураНастроек.Логирование.Измерение6 	= "0";
		
		
		Пока СтруктураОтвета = Неопределено И (КоличествоВыполнений < СтруктураНастроек.КоличествоПовторенийПриОшибках ИЛИ КоличествоВыполнений = 0) Цикл
			
			Если КоличествоВыполнений > 0 Тогда
				СтруктураНастроек.Логирование.Измерение5 = "Повторная загрузка данных с портала с типом: " + Строка(СтруктураНастроек.Операция);
			Иначе
				СтруктураНастроек.Логирование.Измерение5 = "Загрузка данных с портала с типом: " + Строка(СтруктураНастроек.Операция);
			КонецЕсли;
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Начало загрузки данных с портала с типом: " + Строка(СтруктураНастроек.Операция));
			
			СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, МетодПолностью, ПараметрыHTTPЗапроса); 
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Завершение загрузки данных с портала с типом: " + Строка(СтруктураНастроек.Операция));
			
			КоличествоВыполнений = КоличествоВыполнений + 1;
			
		КонецЦикла;
		
		Если СтруктураОтвета = Неопределено Тогда
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
			Возврат;
		КонецЕсли;
		
		СтруктураНастроек.Логирование.Измерение5 = "Обработка данных с типом: " + Строка(СтруктураНастроек.Операция) + " в 1С";
 
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Начало обработки данных в 1С, пришедших  с портала с типом: " + Строка(СтруктураНастроек.Операция));
		ОбработатьДанныеСПортала(СтруктураНастроек, СтруктураОтвета);
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Завершена обработка данных в 1С, пришедших  с портала с типом: " + Строка(СтруктураНастроек.Операция));
		
		Следующие = СтруктураОтвета.Получить("next");
		
		Пока ЗначениеЗаполнено(Следующие) Цикл
			
			Следующие = Формат(Следующие, "ЧГ=0");
			
			СтруктураНастроек.Логирование.Измерение6 = Строка(Следующие);
			
			КоличествоВыполнений 	= 0;
			СтруктураОтвета 	 	= Неопределено;

			ТелоHTTPЗапроса = ПараметрыHTTPЗапроса + "&start="+Следующие;	
			
			Пока СтруктураОтвета = Неопределено И КоличествоВыполнений < СтруктураНастроек.КоличествоПовторенийПриОшибках Цикл
				
				Если КоличествоВыполнений > 0 Тогда
					СтруктураНастроек.Логирование.Измерение5 = "Повторная загрузка данных с портала с типом: " + Строка(СтруктураНастроек.Операция);
				Иначе
					СтруктураНастроек.Логирование.Измерение5 = "Загрузка данных с портала с типом: " + Строка(СтруктураНастроек.Операция);
				КонецЕсли;
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Начало загрузки данных с портала с типом: " + Строка(СтруктураНастроек.Операция));
				
				СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, МетодПолностью, ТелоHTTPЗапроса); 
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Завершение загрузки данных с портала с типом: " + Строка(СтруктураНастроек.Операция));
				
				КоличествоВыполнений = КоличествоВыполнений + 1;
				
			КонецЦикла;
			
			Если СтруктураОтвета = Неопределено Тогда
				СтруктураНастроек.ВыполненоБезОшибок = Ложь;
				Возврат;
			КонецЕсли;
			
			СтруктураНастроек.Логирование.Измерение5 = "Обработка данных с типом: " + Строка(СтруктураНастроек.Операция) + " в 1С";
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Начало обработки данных в 1С, пришедших  с портала с типом: " + Строка(СтруктураНастроек.Операция));
			ОбработатьДанныеСПортала(СтруктураНастроек, СтруктураОтвета);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Завершена обработка данных в 1С, пришедших  с портала с типом: " + Строка(СтруктураНастроек.Операция));
			
			Следующие = СтруктураОтвета.Получить("next");
			СтруктураОтвета = Неопределено;
		КонецЦикла;
	
	СтруктураНастроек.Логирование.Измерение5 = "";
	СтруктураНастроек.Логирование.Измерение6 = "";
	
КонецПроцедуры

Процедура ОбработатьДанныеСПортала(СтруктураНастроек, ДанныеПортала)
	
	Если ДанныеПортала = Неопределено Тогда
		Возврат;
	КонецЕсли;   
	
	ТипыОперацийСинхронизации = СтруктураНастроек.ТипыОперацийСинхронизации;
	Если СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Заказы Тогда
		ДополнительныйКлюч = "orders";
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваЗаказов Тогда
		ДополнительныйКлюч = "properties";
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваКомпаний ИЛИ  СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваКонтактов 
		ИЛИ СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваСчетов ИЛИ  СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваСделок Тогда
		ДополнительныйКлюч = "fields";
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Счета И СтруктураНастроек.ВерсияСчетов = "v.2" Тогда	
		ДополнительныйКлюч = "items";       
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.ЕдиницыИзмерения И СтруктураНастроек.ВерсияТоваров = "v.2" Тогда	
		ДополнительныйКлюч = "measures";  
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваТоваров И СтруктураНастроек.ВерсияТоваров = "v.2" Тогда	
		ДополнительныйКлюч = "productProperties"; 
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваПредложений И СтруктураНастроек.ВерсияТоваров = "v.2" Тогда	
		ДополнительныйКлюч = "productProperties";
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.ГруппыТоваров И СтруктураНастроек.ВерсияТоваров = "v.2" Тогда	
		ДополнительныйКлюч = "sections";    
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Товары И СтруктураНастроек.ВерсияТоваров = "v.2" Тогда	
		ДополнительныйКлюч = "products"; 
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Предложения Тогда	
		ДополнительныйКлюч = "offers"; 
	Иначе
		ДополнительныйКлюч = Неопределено;
	КонецЕсли;  
	
	мДанных = Новый Массив;
	
	Если ТипЗнч(ДанныеПортала) = Тип("Соответствие") Тогда
		
		result = ДанныеПортала.Получить("result");
		Если result <> Неопределено Тогда
			
			Если ТипЗнч(result)=Тип("Массив") Тогда
				мДанных = result;	
			Иначе
				
				Если ДополнительныйКлюч = Неопределено Тогда
				
					result2 = result.Получить("result");
					Если result2 <> Неопределено Тогда
						
						Для каждого ТекЭлемент Из result2 Цикл
							Для каждого ТекЭлемент2 Из ТекЭлемент Цикл
								мДанных.Добавить(ТекЭлемент2);
							КонецЦикла;
						КонецЦикла;
						
					КонецЕсли;
					
				Иначе                 
					
					result2 = result.Получить(ДополнительныйКлюч);
					Если result2 <> Неопределено Тогда
						
						Для каждого ТекЭлемент Из result2 Цикл
							мДанных.Добавить(ТекЭлемент);
						КонецЦикла;
						
					Иначе
						
						result2 = result.Получить("result");
						Для каждого ТекЭлемент Из result2 Цикл
							
							result3 = ТекЭлемент.Получить(ДополнительныйКлюч);
							Если result3 <> Неопределено Тогда
								Для каждого ТекЭлемент Из result3 Цикл
									мДанных.Добавить(ТекЭлемент);
								КонецЦикла;
							КонецЕсли
							
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;            
		
	Иначе
		Для каждого ТекЭлемент Из ДанныеПортала Цикл
			ВремСоотв = Новый Соответствие;
			ВремСоотв.Вставить("ID", Формат(ТекЭлемент, "ЧГ=0")); 
			мДанных.Добавить(ВремСоотв);   
		КонецЦикла;
	КонецЕсли;
	
	Если мДанных.количество() > 0 Тогда
		
		РезультатОбработки 	= Новый Массив;
		СобытияОперации 	= Новый Массив;
		
		ВидыСобытий = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьВидыСобытийЭлементовБ24();
		
		ТипыОперацийСинхронизации = СтруктураНастроек.ТипыОперацийСинхронизации;
		
		Если СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Компании Тогда
			
			ДополнительныеДанные = Новый Структура;
			ДополнительныеДанные.Вставить("ИнформацияОКомпаниях", ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "crm.company.get", мДанных));
			ДополнительныеДанные.Вставить("ИнформацияОРеквизитах", ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "crm.requisite.list", мДанных, "start=-1&filter[ENTITY_TYPE_ID]=4&filter[ENTITY_ID]"));
			
			РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьКомпании(СтруктураНастроек, ДополнительныеДанные);
			
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеКомпании));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеКомпании));
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Контакты Тогда
			
			ДополнительныеДанные = Новый Структура;
			ДополнительныеДанные.Вставить("ИнформацияОКонтактах", ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "crm.contact.get", мДанных));
			ДополнительныеДанные.Вставить("ИнформацияОРеквизитах", ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "crm.requisite.list", мДанных, "start=-1&filter[ENTITY_TYPE_ID]=3&filter[ENTITY_ID]"));
			
			РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьКонтакты(СтруктураНастроек, ДополнительныеДанные);
			
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеКонтакта));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеКонтакта));
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Реквизиты Тогда
			
			ДополнительныеДанные = Новый Структура;
			ДополнительныеДанные.Вставить("ИнформацияОРеквизитах"	, мДанных);
			ДополнительныеДанные.Вставить("ИнформацияОАдресах"		, ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "crm.address.list", мДанных, "filter[ENTITY_TYPE_ID]=8&filter[ENTITY_ID]"));
			
			РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьРеквизиты(СтруктураНастроек, ДополнительныеДанные);
			
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеРеквизита));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеРеквизита));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеАдреса));
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.БанковскиеСчета Тогда
			
			РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьБанковскиеСчетаКомпанийКонтактов(СтруктураНастроек, мДанных);	
			
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеБанкСчета));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеБанкСчета));
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.ГруппыТоваров Тогда
			
			Если СтруктураНастроек.ВерсияТоваров = "v.2" Тогда     
				РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьРазделыТоваров2(СтруктураНастроек, мДанных);	
				
			Иначе
				РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьРазделыТоваров(СтруктураНастроек, мДанных);	
				
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеРаздела));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеРаздела));
			КонецЕсли; 
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.ЕдиницыИзмерения Тогда
			
			Если СтруктураНастроек.ВерсияТоваров = "v.2" Тогда     
				РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьЕдиницыИзмерения2(СтруктураНастроек, мДанных);	
				
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеЕдИзм2));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеЕдИзм2));  
			Иначе	
				РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьЕдиницыИзмерения(СтруктураНастроек, мДанных);	
				
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеЕдИзм));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеЕдИзм));  
			КонецЕсли;
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваТоваров Тогда
			
			Если СтруктураНастроек.ВерсияТоваров = "v.2" Тогда     
				РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьСвойстваТоваров2(СтруктураНастроек, мДанных);	
				
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеСвойстваТовараПредложения));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСвойстваТовараПредложения));
			Иначе
				РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьСвойстваТоваров(СтруктураНастроек, мДанных);	
				
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеСвойстваТовара));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСвойстваТовара));
			КонецЕсли;
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваПредложений Тогда
			
			РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьСвойстваТоваров2(СтруктураНастроек, мДанных);	
				
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеСвойстваТовараПредложения));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСвойстваТовараПредложения));
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Товары Тогда
			
			Если СтруктураНастроек.ВерсияТоваров = "v.2" Тогда         
				
				ДополнительныеДанные = Новый Структура;
				ДополнительныеДанные.Вставить("Товары"		, ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "catalog.product.get", мДанных)); 	
				ДополнительныеДанные.Вставить("Предложения"	, Неопределено);
				
				РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьТоварыПредложения(СтруктураНастроек, ДополнительныеДанные, Истина);	
				
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеТовара2));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеТовара2)); 
				
			Иначе
				ДополнительныеДанные = ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "crm.product.get", мДанных);
				РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьТовары(СтруктураНастроек, ДополнительныеДанные);
				СтруктураНастроек.КартинкиИФайлы.Очистить();     
				
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеТовара));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеТовара));
			КонецЕсли; 
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Предложения Тогда
			
			ДополнительныеДанные = Новый Структура;
			ДополнительныеДанные.Вставить("Предложения"	, ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "catalog.product.offer.get", мДанных)); 	
			ДополнительныеДанные.Вставить("Товары"		, ПолучитьВладельцевПредложенийЧерезGET(СтруктураНастроек, ДополнительныеДанные.Предложения));
			
			РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьТоварыПредложения(СтруктураНастроек, ДополнительныеДанные, Ложь);	
			
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеТовара2));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеТовара2));
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Счета Тогда
			
			Если СтруктураНастроек.ВерсияСчетов = "v.2" Тогда

				ИнформацияОСчетах = ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "crm.item.get", мДанных, "entityTypeId=31&id");
				Для каждого ТекЭлемент Из ИнформацияОСчетах Цикл
					
					item = ТекЭлемент.Получить("item");
					Если item <> Неопределено Тогда
						
						ИдЭлемента = Формат(item.Получить("id"), "ЧГ=0");
						
						ТелоЗапросаТЧ = "&filter[%3DownerType]=SI&filter[%3DownerId]="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдЭлемента);  
						
						ДанныеЭлементаТЧСмартПроцесса = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/crm.item.productrow.list", "id", ТелоЗапросаТЧ, "productRows");
						
						ТекЭлемент.Вставить("productrow", ДанныеЭлементаТЧСмартПроцесса);
						
					КонецЕсли;
					
				КонецЦикла;				
				
				РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьСчета2(СтруктураНастроек, ИнформацияОСчетах);
				
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеСмартПроцесса));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСмартПроцесса));
				
			Иначе
				
				ДополнительныеДанные = Новый Структура;
				ДополнительныеДанные.Вставить("ИнформацияОСчетах"			, ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "crm.invoice.get", мДанных));
				ДополнительныеДанные.Вставить("ИнформацияОРеквизитахСчетов"	, ПолучитьПодробныеДанныеРеквизитовСчетовЧерезGET(СтруктураНастроек, "crm.requisite.link.get", "5", мДанных));
				РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьСчета(СтруктураНастроек, ДополнительныеДанные);
				
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеСчета));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСчета));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСтатусаСчета));
				
			КонецЕсли;
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Проекты Тогда
			
			ДополнительныеДанные = Новый Структура;
			ДополнительныеДанные.Вставить("ИнформацияОСделках"			, ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "crm.deal.get", мДанных));
			стПредопределенныеСвойства = Новый Структура("ПредопределенноеСвойствоОрганизацияСделки",СоздатьПолучитьСвойствоОрганизацияБитрикс24(СтруктураНастроек.НастройкаПодключения));
			СтруктураНастроек.Вставить("ПредопределенныеСвойства", стПредопределенныеСвойства);
			СтруктураНастроек.Вставить("ОбрабатыватьУдалениеОбъектовВ1С", Ложь);
			РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьСделки(СтруктураНастроек, ДополнительныеДанные);
			
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеСделки));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСделки));
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваКомпаний Тогда
			
			ДополнительныеДанные 	= ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "userfieldconfig.get", мДанных, "moduleId=crm&id");
			РезультатОбработки 	= Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьПользовательскиеПоляОбъектов(СтруктураНастроек, ДополнительныеДанные, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКомпании);
			
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеСвойстваКомпаний));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСвойстваКомпаний));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеЗначенийСвойствКомпаний));
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваКонтактов Тогда
			
			ДополнительныеДанные 	= ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "userfieldconfig.get", мДанных, "moduleId=crm&id");
			РезультатОбработки 	= Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьПользовательскиеПоляОбъектов(СтруктураНастроек, ДополнительныеДанные, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКонтакта);
			
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеСвойстваКонтактов));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСвойстваКонтактов));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеЗначенийСвойствКонтактов));
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваСделок Тогда
			
			ДополнительныеДанные 	= ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "userfieldconfig.get", мДанных, "moduleId=crm&id");
			РезультатОбработки 	= Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьПользовательскиеПоляОбъектов(СтруктураНастроек, ДополнительныеДанные, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСделки);
			
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеСвойстваСделок));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСвойстваСделок));
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеЗначенийСвойствСделок));
			
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.СвойстваСчетов Тогда
			
			Если СтруктураНастроек.ВерсияСчетов = "v.2" Тогда
				
				ДополнительныеДанные 	= ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "userfieldconfig.get", мДанных, "moduleId=crm&id");
				РезультатОбработки 	= Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьПользовательскиеПоляОбъектов(СтруктураНастроек, ДополнительныеДанные, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСчета2);
				
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеСвойстваСмартПроцесса));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСвойстваСмартПроцесса));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеЗначенийСвойствСмартПроцесса));
				
			Иначе
				
				ДополнительныеДанные 	= ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, "userfieldconfig.get", мДанных, "moduleId=crm&id");
				РезультатОбработки 	= Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьПользовательскиеПоляОбъектов(СтруктураНастроек, ДополнительныеДанные, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСчета);
				
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеСвойстваСчетов));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеСвойстваСчетов));
				СобытияОперации.Добавить(ВРег(ВидыСобытий.ОбновлениеЗначенийСвойствСчетов));
				
			КонецЕсли;	
		
		ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Заказы Тогда
			
			ДополнительныеДанные =  ПолучитьПодробныеДанныеЗаказовЧерезGET(СтруктураНастроек, "sale.order.get", мДанных);
			РезультатОбработки = Б24_КС_ЗагрузкаСервер.ЗагрузитьОбновитьЗаказы(СтруктураНастроек, ДополнительныеДанные);
			
			СобытияОперации.Добавить(ВРег(ВидыСобытий.ДобавлениеЗаказа));

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры   

Функция СоздатьПолучитьСвойствоОрганизацияБитрикс24(НастройкаПодключения) 

	УстановитьПривилегированныйРежим(Истина);
	
	ТипыДанныхДляОбменаСПорталом = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();
	
	НаименованиеСвойства 	= "Организация для Битрикс24";
	//Документ_ЗаказКлиента 	= ПолучитьНаборСвойств("Документ_ЗаказКлиента"); 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаименованиеСвойства", НаименованиеСвойства);
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|ГДЕ
	|	ДополнительныеРеквизитыИСведения.Заголовок = &НаименованиеСвойства";
	ВыполненныйЗапрос = Запрос.Выполнить();	
	
	Если ВыполненныйЗапрос.Пустой() Тогда
		
		СвойствоОбъект 	= ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.СоздатьЭлемент();
		СвойствоОбъект.Комментарий 	= НСтр("ru = 'Создано автоматически при синхронизации с Битрикс24'");
		СвойствоОбъект.Наименование = НаименованиеСвойства + " (Заказ клиента)";
		СвойствоОбъект.Заголовок 	= НаименованиеСвойства;
		СвойствоОбъект.ЗаголовокФормыВыбораЗначения = НаименованиеСвойства;
		СвойствоОбъект.ЗаголовокФормыЗначения 		= НаименованиеСвойства;
		СвойствоОбъект.НаборСвойств = Неопределено;
		
		
		лТип = Тип("СправочникСсылка.ЗначенияСвойствОбъектов");
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(лТип);
		
		СвойствоОбъект.ТипЗначения 	= Новый ОписаниеТипов(МассивТипов);
		СвойствоОбъект.ЭтоДополнительноеСведение = Истина;
		СвойствоОбъект.Доступен 	= Ложь;
		СвойствоОбъект.Виден 		= Ложь;
		СвойствоОбъект.ДополнительныеЗначенияИспользуются 	= Истина;
		СвойствоОбъект.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
		СвойствоОбъект.Записать();
		СвойствоСсылка = СвойствоОбъект.Ссылка; 
		
		//лНабор = Документ_ЗаказКлиента.ПолучитьОбъект();
		//лНабор.ДополнительныеСведения.Добавить().Свойство = СвойствоСсылка;
		//лНабор.Записать();
		//
		ДобавитьСвойствоВТаблицуИзменений(СвойствоСсылка, ТипыДанныхДляОбменаСПорталом.СвойствоСделки, Истина, ТипыДанныхДляОбменаСПорталом.ЗначениеСвойстваСделки);
		//ДобавитьСвойствоВТаблицуИзменений(СвойствоСсылка, ТипыДанныхДляОбменаСПорталом.СвойствоЗаказа, Истина, ТипыДанныхДляОбменаСПорталом.ЗначениеСвойстваЗаказа);
		
		СоздаватьНовыеЗначения = Истина;
		
	Иначе
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СвойствоСсылка = Выборка.Ссылка; 
		КонецЦикла;
			
		Если НЕ ЗначениеЗаполнено(РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьИдОбъекта(НастройкаПодключения.Портал, ТипыДанныхДляОбменаСПорталом.СвойствоСделки, СвойствоСсылка)) Тогда
			ДобавитьСвойствоВТаблицуИзменений(СвойствоСсылка, ТипыДанныхДляОбменаСПорталом.СвойствоСделки, Истина, ТипыДанныхДляОбменаСПорталом.ЗначениеСвойстваСделки);
		КонецЕсли;	
		//Если НЕ ЗначениеЗаполнено(РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьИдОбъекта(НастройкаПодключения.Портал, ТипыДанныхДляОбменаСПорталом.СвойствоЗаказа, СвойствоСсылка)) Тогда
		//	ДобавитьСвойствоВТаблицуИзменений(СвойствоСсылка, ТипыДанныхДляОбменаСПорталом.СвойствоЗаказа, Истина, ТипыДанныхДляОбменаСПорталом.ЗначениеСвойстваЗаказа);
		//КонецЕсли;	
			
	КонецЕсли;
	
	
	Если ЗначениеЗаполнено(СвойствоСсылка) Тогда
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Владелец", СвойствоСсылка);
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЗначенияСвойствОбъектов.Наименование КАК Наименование,
		               |	ЗначенияСвойствОбъектов.Ссылка КАК Ссылка
		               |ПОМЕСТИТЬ ВТ_ЗначенияСвойств
		               |ИЗ
		               |	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		               |ГДЕ
		               |	ЗначенияСвойствОбъектов.Владелец = &Владелец
		               |	И ЗначенияСвойствОбъектов.ЭтоГруппа = ЛОЖЬ
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	Наименование
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	Организации.Наименование КАК Наименование,
		               |	Организации.НаименованиеПолное КАК НаименованиеПолное
		               |ИЗ
		               |	Справочник.Организации КАК Организации
		               |ГДЕ
		               |	НЕ Организации.Наименование В
		               |				(ВЫБРАТЬ
		               |					ВТ_ЗначенияСвойств.Наименование
		               |				ИЗ
		               |					ВТ_ЗначенияСвойств)";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НовоеЗначение = Справочники.ЗначенияСвойствОбъектов.СоздатьЭлемент();
			НовоеЗначение.Владелец 				= СвойствоСсылка;
			НовоеЗначение.Наименование 			= Выборка.Наименование;
			НовоеЗначение.ПолноеНаименование 	= Выборка.НаименованиеПолное;
			НовоеЗначение.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			НовоеЗначение.Записать();
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_ЗначенияСвойств.Наименование КАК Наименование,
		|	ВТ_ЗначенияСвойств.Ссылка КАК Ссылка
		|ИЗ
		|	ВТ_ЗначенияСвойств КАК ВТ_ЗначенияСвойств
		|ГДЕ
		|	НЕ ВТ_ЗначенияСвойств.Наименование В
		|				(ВЫБРАТЬ
		|					Организации.Наименование
		|				ИЗ
		|					Справочник.Организации КАК Организации)";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ТекЗначение = Выборка.Ссылка.ПолучитьОбъект();
			ТекЗначение.ПометкаУдаления = Истина;
			ТекЗначение.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			ТекЗначение.Записать();
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СвойствоСсылка;
	
КонецФункции

Процедура ДобавитьСвойствоВТаблицуИзменений(Свойство, ТипСвойства, НадоВыгружатьЗначения = Ложь, ТипЗначенийСвойства = Неопределено)
	
	СтруктураОтборка = Новый Структура;
	СтруктураОтборка.Вставить("НастройкаПодключения"	, Неопределено);
	СтруктураОтборка.Вставить("ТипДанных"				, Неопределено);
	СтруктураОтборка.Вставить("Объект"					, Свойство);
	СтруктураОтборка.Вставить("ПодчиненныйОбъект"		, Неопределено);
	
	
	мНастройкиСинхронизации = Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьМассивНастроекСинхронизации();
	Для Каждого НастройкаПодключения Из мНастройкиСинхронизации Цикл
		
		СтруктураОтборка.НастройкаПодключения 	= НастройкаПодключения;
		СтруктураОтборка.ТипДанных 				= ТипСвойства;
		
		Если РегистрыСведений.Б24_КС_ТаблицаИзменений.Получить(СтруктураОтборка).ВремяЗаписиВМиллисекундах = 0 Тогда 
				НоваяЗапись = РегистрыСведений.Б24_КС_ТаблицаИзменений.СоздатьМенеджерЗаписи();
				НоваяЗапись.НастройкаПодключения		= СтруктураОтборка.НастройкаПодключения;
				НоваяЗапись.ТипДанных 					= СтруктураОтборка.ТипДанных;
				НоваяЗапись.Объект 						= Свойство; 
				НоваяЗапись.ВремяЗаписиВМиллисекундах	= 100;
				НоваяЗапись.Записать();
		КонецЕсли;
		
		Если НадоВыгружатьЗначения Тогда
			
			СтруктураОтборка.ТипДанных 	= ТипЗначенийСвойства;
			
			Если РегистрыСведений.Б24_КС_ТаблицаИзменений.Получить(СтруктураОтборка).ВремяЗаписиВМиллисекундах = 0 Тогда 
					НоваяЗапись = РегистрыСведений.Б24_КС_ТаблицаИзменений.СоздатьМенеджерЗаписи();
					НоваяЗапись.НастройкаПодключения		= СтруктураОтборка.НастройкаПодключения;
					НоваяЗапись.ТипДанных 					= СтруктураОтборка.ТипДанных;
					НоваяЗапись.Объект 						= Свойство; 
					НоваяЗапись.ВремяЗаписиВМиллисекундах	= 100;
					НоваяЗапись.Записать();
				КонецЕсли;
				
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьПодробныеДанныеЧерезGET(СтруктураНастроек, Метод, мДанных, НазваниеПараметра= "id")
	
	РезультатОбработки = Новый Массив; 
	
	ТелоHTTPЗапроса= "";
	
	Для каждого ТекЭлемент Из мДанных Цикл
		
		ИдЭлемента = Формат(ТекЭлемент.Получить("ID"), "ЧГ=0");
		ИдЭлемента = ?(ЗначениеЗаполнено(ИдЭлемента), ИдЭлемента, Формат(ТекЭлемент.Получить("id"), "ЧГ=0"));
			
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Метод + "?"+НазваниеПараметра+"=" + ИдЭлемента);
	КонецЦикла;
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат РезультатОбработки;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	Если result <> Неопределено Тогда
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда
			РезультатОбработки = result2;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатОбработки;			
	
КонецФункции

Функция ПолучитьПодробныеДанныеРеквизитовСчетовЧерезGET(СтруктураНастроек, Метод, ТипСущности, мДанных)
	
	РезультатОбработки = Новый Массив; 
	
	ТелоHTTPЗапроса= "";
	
	Для каждого ТекЭлемент Из мДанных Цикл
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Метод + "?entityId=" + ТекЭлемент.Получить("ID") + "&entityTypeId=" + ТипСущности);
	КонецЦикла;
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат РезультатОбработки;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	Если result <> Неопределено Тогда
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда
			
			Для каждого ТекЭлемент2 Из result2 Цикл
				
				Если ТипЗнч(ТекЭлемент2)= Тип("Соответствие") Тогда
					РезультатОбработки.Добавить(ТекЭлемент2);
				Иначе
					РезультатОбработки.Добавить(ТекЭлемент2.Значение);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатОбработки;			
	
КонецФункции

Функция ПолучитьПодробныеДанныеРеквизитовСделокЧерезGET(СтруктураНастроек, Метод, ТипСущности, мДанных)
	
	РезультатОбработки = Новый Массив; 
	
	ТелоHTTPЗапроса= "";
	
	Для каждого ТекЭлемент Из мДанных Цикл
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Метод + "?entityId=" + ТекЭлемент.Получить("ID") + "&entityTypeId=" + ТипСущности);
	КонецЦикла;
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат РезультатОбработки;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	Если result <> Неопределено Тогда
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда
			Для каждого ТекЭлемент2 Из result2 Цикл
				
				Если ТипЗнч(ТекЭлемент2)= Тип("Соответствие") Тогда
					РезультатОбработки.Добавить(ТекЭлемент2);
				Иначе
					РезультатОбработки.Добавить(ТекЭлемент2.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатОбработки;			
	
КонецФункции

Функция ПолучитьПодробныеДанныеТоваровСделокЧерезGET(СтруктураНастроек, Метод, мДанных)
	
	РезультатОбработки = Новый Массив; 
	
	ТелоHTTPЗапроса= "";
	
	Для каждого ТекЭлемент Из мДанных Цикл        
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Метод + "?ID=" + Формат(ТекЭлемент.Получить("ID"), "ЧГ=0"));
	КонецЦикла;
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат РезультатОбработки;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	Если result <> Неопределено Тогда
		result2 = result.Получить("result");
		
		Если ТипЗнч(result2) = Тип("Массив") Тогда
			Для каждого ТекЭлемент Из result2 Цикл
				Если ТипЗнч(ТекЭлемент) = Тип("Массив") Тогда
					Для каждого ТекЭлемент2 Из ТекЭлемент Цикл
						РезультатОбработки.Добавить(ТекЭлемент2);
					КонецЦикла;
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатОбработки;			
	
КонецФункции

Функция ПолучитьПодробныеДанныеЗаказовЧерезGET(СтруктураНастроек, Метод, мДанных)
	
	РезультатОбработки = Новый Массив; 
	
	ТелоHTTPЗапроса= "";
	
	Для каждого ТекДанные Из мДанных Цикл
		
		Если ТипЗнч(ТекДанные) = Тип("Соответствие") Тогда
			
			ТекЗаказ = ТекДанные;
			
			ИдЭлемента = Формат(ТекЗаказ.Получить("ID"), "ЧГ=0");
			ИдЭлемента = ?(ЗначениеЗаполнено(ИдЭлемента), ИдЭлемента, Формат(ТекЗаказ.Получить("id"), "ЧГ=0"));
					
			ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Метод + "?id=" + ИдЭлемента);
			
		Иначе
			
			ДанныеЗаказов = ТекДанные.Значение;
			
			Для каждого ТекЗаказ Из ДанныеЗаказов Цикл
			
				ИдЭлемента = Формат(ТекЗаказ.Получить("ID"), "ЧГ=0");
				ИдЭлемента = ?(ЗначениеЗаполнено(ИдЭлемента), ИдЭлемента, Формат(ТекЗаказ.Получить("id"), "ЧГ=0"));
					
				ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Метод + "?id=" + ИдЭлемента);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат РезультатОбработки;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	Если result <> Неопределено Тогда
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда
			
			Если ТипЗнч(result2) = Тип("Массив") Тогда
				Для каждого ТекЭлемент Из result2 Цикл
					Если ТипЗнч(ТекЭлемент) = Тип("Соответствие") Тогда
						РезультатОбработки.Добавить(ТекЭлемент.Получить("order"));
					КонецЕсли;	
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатОбработки;			
	
КонецФункции

Функция ПолучитьВладельцевПредложенийЧерезGET(СтруктураНастроек, ДанныеПредложений) Экспорт
	
	РезультатОбработки = Новый Массив; 
	
	ТелоHTTPЗапроса	= "";
	Для каждого ЭлементДанных Из ДанныеПредложений Цикл
		
		Если ТипЗнч(ЭлементДанных) = Тип("КлючИЗначение") Тогда
			offer = ЭлементДанных.Значение.Получить("offer");	
		Иначе
			offer = ЭлементДанных.Получить("offer"); 
		КонецЕсли;
		
		ИдТовара = "";
		parentId = offer.Получить("parentId"); 
		Если parentId <> Неопределено Тогда
			ИдТовара = Формат(parentId.Получить("value"), "ЧГ=0");	
		КонецЕсли;       
		
		Если ЗначениеЗаполнено(ИдТовара) Тогда
			ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("catalog.product.get?id=" + ИдТовара);
		КонецЕсли;	
			
	КонецЦикла;
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат РезультатОбработки;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	Если result <> Неопределено Тогда
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда
			РезультатОбработки = result2;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатОбработки;			
	
КонецФункции   

Процедура СинхронизацияСделокиСБитрикс(Сделка) Экспорт

	Если омВыгрузкаCRMНаСевереПовтор.ЗапретВыгрузкиВБитрикс() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
		|	Б24_К_ИдентификаторыОбъектов.Объект.Наименование КАК ОбъектНаименование,
		|	Б24_К_ИдентификаторыОбъектов.Объект.Префикс КАК ОбъектПрефикс,
		|	Б24_К_ИдентификаторыОбъектов.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор
		|ИЗ
		|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
		|ГДЕ
		|	Б24_К_ИдентификаторыОбъектов.Объект = &Проект";
	
	Запрос.УстановитьПараметр("Проект", Сделка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Идентификатор = "";
	
	НоваяСделка = Истина;
	Пока Выборка.Следующий() Цикл
		ДополнительныйИдентификатор = Выборка.ДополнительныйИдентификатор;
		Идентификатор 				= СтрЗаменить(Выборка.Идентификатор,"S_",""); 
		НоваяСделка = Ложь;
	КонецЦикла;
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(Константы.НастройкаПодключенияКБитриксПоУмолчанию.Получить());  

	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
 	СтруктураНастроек.Вставить("КоличествоПовторенийПриОшибках", 2);
	СтруктураНастроек.Вставить("ВыполненоБезОшибок", Истина);
	СтруктураНастроек.Операция = СтруктураНастроек.ТипыОперацийСинхронизации.Сделки;
	
	
	ОбъектНаименование 			= Сделка.Наименование;
	ОбъектПрефикс 				= Сделка.Префикс;
	ОбъектНомер1С 				= Формат(Сделка.Номер,"ЧГ=0");
	
	Если НЕ ПустаяСтрока(Идентификатор) Тогда
		мДанные = ПолучитьДанныеОСДелкеПоИдентификатору(Идентификатор, СтруктураНастроек);
		Если мДанные.Количество() > 0 Тогда
			Наименование 	= мДанные[0].Получить("TITLE");
			Префикс 		= мДанные[0].Получить("UF_CRM_1718639017074");
			Номер1С 		= мДанные[0].Получить("UF_CRM_DEAL_3855415931910");
			
			Если Префикс = ОбъектПрефикс И Наименование = ОбъектНаименование  И Номер1С = ОбъектНомер1С Тогда   
				Возврат;	
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	КлючСделки = XMLСтрока(Сделка);

	ТелоЗапроса = "fields[TITLE]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ОбъектНаименование) + 
	"&fields[CATEGORY_ID]=2&fields[TYPE_ID]=SALE&fields[UF_CRM_1718639017074]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ОбъектПрефикс)+ ""
	+ "&fields[UF_CRM_DEAL_3855415931910]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ОбъектНомер1С)+ ""; 
	Если НоваяСделка Тогда
		СформированныеДанные = "cmd["+КлючСделки+"]=crm.deal.add?" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТелоЗапроса);	
	Иначе
		СформированныеДанные = "cmd["+КлючСделки+"]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.deal.update?id=" + Идентификатор + "&"  + ТелоЗапроса);	
	КонецЕсли;
	
	ВыгрузкаДанныхНаПортал(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Сделка, СформированныеДанные, Сделка, НоваяСделка);
	
КонецПроцедуры // СинхронизацияСделокиСБитрикс()

Процедура ВыгрузкаДанныхНаПортал(СтруктураНастроек, ТипДанных, СформированныеДанные, Сделка, НоваяСделка)
	
	Пакет = 1;
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, СформированныеДанные);
	
	Если СтруктураОтвета = Неопределено Тогда
		СтруктураНастроек.ВыполненоБезОшибок = Ложь;
		Возврат;	
	КонецЕсли;
	
	Если НЕ НоваяСделка Тогда
		Возврат;	
	КонецЕсли;
	
	ВремКлючиИдВладельцев 	= Новый Соответствие;
	ВремКлючиИдДел 			= Новый Соответствие;
	
	result = СтруктураОтвета.Получить("result");
	Если result <> Неопределено Тогда
		
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда
			
			Для каждого ТекСтрока Из result2 Цикл
				
				Если ТипЗнч(ТекСтрока) = Тип("Булево") ИЛИ ТипЗнч(ТекСтрока) = Тип("Соответствие") ИЛИ СтрДлина(ТекСтрока.Ключ) < 30 Тогда
					Продолжить;
				КонецЕсли;
				
				МенеджерЗаписей = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
				МенеджерЗаписей.ТипДанных 					= ТипДанных;
				МенеджерЗаписей.Портал						= СтруктураНастроек.Портал;
				МенеджерЗаписей.Объект 						= Сделка;
				МенеджерЗаписей.Идентификатор 				= "S_" + ТекСтрока.Значение; 
				МенеджерЗаписей.ДополнительныйИдентификатор = Строка(ТекСтрока.Ключ); 
				
				МенеджерЗаписей.Записать();
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьДанныеОСДелкеПоИдентификатору(Идентификатор, СтруктураНастроек)

	ТелоHTTPЗапроса = "&filter[ID]=" + Идентификатор + "&start=-1&select[]=UF_CRM_1718639017074&select[]=TITLE&select[]=UF_CRM_DEAL_3855415931910";	
	//Наименование полей для получения из битрикс
	//https://gettotop.ru/crm/polzovatelskie-polya-v-bitrix24-peredacha-znachenij-v-lidax/
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.deal.list", ТелоHTTPЗапроса); 
	
	Возврат СтруктураОтвета.Получить("result");

КонецФункции // ПолучитьДанныеОСДелкеПоИдентификатору(Идентификатор, СтруктураНастроек)

