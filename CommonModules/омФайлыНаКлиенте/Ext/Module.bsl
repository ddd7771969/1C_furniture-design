&НаКлиенте
Функция ПереместитьФайлНаСервер(ИмяФайла, УИД_Вызова = Неопределено, Адрес = Неопределено, УдалитьФайлПослеЗагрузки = Ложь) Экспорт 
	
	стЗавершениеОбратныйВызов = Новый Структура("УИД_Вызова, УдалитьФайлПослеЗагрузки, ИмяФайла"
												, УИД_Вызова, УдалитьФайлПослеЗагрузки, ИмяФайла);

	ЗавершениеОбратныйВызов 	= Новый ОписаниеОповещения("ЗавершениеОбратныйВызов", 	омФайлыНаКлиенте, стЗавершениеОбратныйВызов);
	ПрогрессОбратныйВызов 		= Новый ОписаниеОповещения("ПрогрессОбратныйВызов", 	омФайлыНаКлиенте);
	ПередНачаломОбратныйВызов 	= Новый ОписаниеОповещения("ПередНачаломОбратныйВызов", омФайлыНаКлиенте); 
	
	НачатьПомещениеФайлаНаСервер(ЗавершениеОбратныйВызов, ПрогрессОбратныйВызов, ПередНачаломОбратныйВызов,
									Адрес, ИмяФайла);
	

КонецФункции // ПереместитьФайлНаСервер()


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗавершениеОбратныйВызов(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт

	Если ДополнительныеПараметры.УдалитьФайлПослеЗагрузки Тогда
	
		УдалитьФайлЗавершение(ДополнительныеПараметры.ИмяФайла);
	
	КонецЕсли;

	Если ЗначениеЗаполнено(ДополнительныеПараметры.УИД_Вызова) Тогда
		
		ИнформацияОФайле = Новый Структура("Ошибка, ИмяФайла, УИД_Вызова, РазмерФайла, АдресФайла",
		"", ДополнительныеПараметры.ИмяФайла, ДополнительныеПараметры.УИД_Вызова);
		
		Если ОписаниеПомещенногоФайла = Неопределено Тогда
			
			ИнформацияОФайле.Ошибка 		= "Неизвестная ошибка.";
			
		Иначе
			
			ИнформацияОФайле.РазмерФайла 	= ОписаниеПомещенногоФайла.СсылкаНаФайл.Размер();
			ИнформацияОФайле.АдресФайла 	= ОписаниеПомещенногоФайла.Адрес;
			
		КонецЕсли; 
		
		Оповестить("ЗавершенПереместитьФайлНаСервер", ИнформацияОФайле);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрогрессОбратныйВызов(ПомещаемыйФайл, Помещено, ОтказОтПомещенияФайла, ДополнительныеПараметры) Экспорт

	ТекстСообщения = СтрШаблон(НСтр("ru = 'Загрузка файла %1'; en = 'Uploading file %1'"), ПомещаемыйФайл.Имя);
	РазмерФайла = СтрШаблон(Нстр("ru = 'Размер файла %1 байт'; en = 'File size %1 bytes'"), ПомещаемыйФайл.Размер());
	
	Состояние(ТекстСообщения, Помещено, РазмерФайла, БиблиотекаКартинок.Документ);

КонецПроцедуры

&НаКлиенте
Процедура ПередНачаломОбратныйВызов(ПомещаемыйФайл, ОтказОтПомещенияФайла, ДополнительныеПараметры) Экспорт

	МаксимальныйРазмерФайла = омФайлы.ПолучитьМаксимальныйРазмерФайла();
	
	Если ПомещаемыйФайл.Размер() > МаксимальныйРазмерФайла Тогда
		ОтказОтПомещенияФайла = Истина;
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Отказ. Загружаемый файл «%1» имеет размер более %2 МГ';
		|en = 'Failure. The uploaded file «%1» is larger than %2 MG'"), ПомещаемыйФайл.Имя, МаксимальныйРазмерФайла);
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
		ОтказОтПомещенияФайла = Истина;
		
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Загрузка файла'; en = 'Uploading file'"),, ПомещаемыйФайл.Имя);
	КонецЕсли;  

КонецПроцедуры 


&НаКлиенте
Процедура УдалитьФайлЗавершение(ПутьКФайлу)

	Попытка
	
		Файл = Новый Файл(ПутьКФайлу);
		
		Если Файл.Существует() Тогда
		
			УдалитьФайлыАсинх(ПутьКФайлу);	
		
		КонецЕсли;
	
	Исключение
	
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
