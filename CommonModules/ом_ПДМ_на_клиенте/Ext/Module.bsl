Процедура УправлениеКонтроляВыгрузкиВPDM(Запуск = Ложь, Интервал = 6, КоличествоПопыток = 150) Экспорт
	
	Если Запуск Тогда
		
		Если КоличествоПопыток > 0 Тогда
			
			Если омВыгрузкаCRMНаСевере.УстановитьСчетчикПоискаФайлаОтветаВыгрузкиPDM(Неопределено, Ложь) = 0 Тогда
				ПодключитьОбработчикОжидания("НачатьКонтрольФайлаВыгрузкиВPDM", Интервал);
			КонецЕсли;
			
			омВыгрузкаCRMНаСевере.УстановитьСчетчикПоискаФайлаОтветаВыгрузкиPDM(КоличествоПопыток);
			
		Иначе
			
			ОтключитьОбработчикОжидания("НачатьКонтрольФайлаВыгрузкиВPDM");
			
		КонецЕсли;
		
	Иначе
		ОтключитьОбработчикОжидания("НачатьКонтрольФайлаВыгрузкиВPDM");
	КонецЕсли;	
	
КонецПроцедуры // ЗапускКонтролляВыгрузкиВPDM()

Процедура ВыполнитьСценарийЧтениеФайлов(НайденныеФайлы, ДополнительныеПараметры) Экспорт  
	
	Для Каждого Файл Из НайденныеФайлы Цикл
		РазобратьИмяФайла(Файл);
	КонецЦикла; 	
	
КонецПроцедуры // ВыполнитьСценарийЗагрузкиФайлаНаСервер()

Процедура РазобратьИмяФайла(Файл)
	
	мПрефиксовДляАнализаОшибки = Новый Массив(5);
	мПрефиксовДляАнализаОшибки[0] = "GCS";
	мПрефиксовДляАнализаОшибки[1] = "Task";
	мПрефиксовДляАнализаОшибки[2] = "MP";
	мПрефиксовДляАнализаОшибки[3] = "UP";
	мПрефиксовДляАнализаОшибки[4] = "TT";
	
	мДанныеФайла = СтрРазделить(Файл.ИмяБезРасширения, "_");
	
	КоличествоЭлементов = мДанныеФайла.Количество();
	
	ТекстДок = Новый ТекстовыйДокумент;  
	
	Если КоличествоЭлементов = 2 Тогда
		
		ЗначениеСуфикса = мДанныеФайла[1];
		
		Если НЕ ЗначениеСуфикса = "1" Тогда
			УдалитьФайлыАсинх(Файл.ПолноеИмя);
			Возврат;
		КонецЕсли;
		
		КодЭлемента 	= мДанныеФайла[0];
		ИмяСправочника 	= "ИзделияКомплект";
		
		ПараметрыЧтения 	= Новый Структура("ТекстДок, КодКомплект, Файл", ТекстДок, КодЭлемента, Файл);
		ОписаниеОповещения 	= Новый ОписаниеОповещения("ПослеЧтенияФайлаКомплект", ом_ПДМ_на_клиенте, ПараметрыЧтения); 
		
	ИначеЕсли КоличествоЭлементов = 3 Тогда 
		
		ИмяПрефикса 	= мДанныеФайла[0];
		ЗначениеСуфикса = мДанныеФайла[2];
		
		Если ЗначениеСуфикса = "0" И мПрефиксовДляАнализаОшибки.Найти(ИмяПрефикса) = Неопределено Тогда
			УдалитьФайлыАсинх(Файл.ПолноеИмя);
			Возврат;
		КонецЕсли;
		
		Если ИмяПрефикса = "P" Тогда
			
			КодЭлемента		= мДанныеФайла[1];
			ИмяСправочника 	= "Проекты";
			
			ПараметрыЧтения = Новый Структура("ТекстДок, КодСделки, Файл", ТекстДок, КодЭлемента, Файл);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЧтенияФайлаПроекты", ом_ПДМ_на_клиенте, ПараметрыЧтения); 
			
		ИначеЕсли ИмяПрефикса = "G" Тогда
			
			КодЭлемента		= мДанныеФайла[1];
			ИмяСправочника 	= "ГабаритныеЧертежи";
			
			ПараметрыЧтения = Новый Структура("ТекстДок, КодЭлемента, Файл", ТекстДок, КодЭлемента, Файл);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЧтенияФайлаГабаритныеЧертежи", ом_ПДМ_на_клиенте, ПараметрыЧтения); 
			
		ИначеЕсли ИмяПрефикса = "K" Тогда
			
			КодЭлемента		= мДанныеФайла[1];
			ИмяСправочника 	= "Конструктор";
			
			ПараметрыЧтения = Новый Структура("ЗначениеСуфикса, КодЭлемента, Файл", ЗначениеСуфикса, КодЭлемента, Файл);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЧтенияФайлаКонструктор", ом_ПДМ_на_клиенте, ПараметрыЧтения); 
			
		ИначеЕсли ИмяПрефикса = "C" Тогда
			
			КодЭлемента		= мДанныеФайла[1];
			ИмяСправочника 	= "Композиции";
			
			ПараметрыЧтения = Новый Структура("ТекстДок, КодЭлемента, Файл", ТекстДок, КодЭлемента, Файл);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЧтенияФайлаКомпозиции", ом_ПДМ_на_клиенте, ПараметрыЧтения); 

		ИначеЕсли ИмяПрефикса = "GCS" Тогда
			
			КодЭлемента	= мДанныеФайла[1];
			ИмяСправочника 	= "ГабаритныеЧертежи";
			
			ПараметрыЧтения = Новый Структура("ЗначениеСуфикса, КодЭлемента, Файл", ЗначениеСуфикса, КодЭлемента, Файл);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеИзмененияСтатусаГЧ", ом_ПДМ_на_клиенте, ПараметрыЧтения); 

		ИначеЕсли ИмяПрефикса = "TT" Тогда
			
			КодЭлемента	= мДанныеФайла[1];
			
			ПараметрыЧтения = Новый Структура("ТекстДок, ЗначениеСуфикса, КодЭлемента, Файл", ТекстДок, ЗначениеСуфикса, КодЭлемента, Файл);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПереносаТЗ", ом_ПДМ_на_клиенте, ПараметрыЧтения); 

		ИначеЕсли ИмяПрефикса = "MP" Тогда
			
			КодЭлемента	= мДанныеФайла[1];
			ИмяСправочника 	= "ИзделияКомплект";
			
			ПараметрыЧтения = Новый Структура("ТекстДок, ЗначениеСуфикса, КодЭлемента, Файл", ТекстДок, ЗначениеСуфикса, КодЭлемента, Файл);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗабратьПрава", ом_ПДМ_на_клиенте, ПараметрыЧтения); 

		ИначеЕсли ИмяПрефикса = "UP" Тогда
			
			КодЭлемента	= мДанныеФайла[1];
			ИмяСправочника 	= "ИзделияКомплект";
			
			ПараметрыЧтения = Новый Структура("ЗначениеСуфикса, КодЭлемента, Файл", ЗначениеСуфикса, КодЭлемента, Файл);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеРаздатьПрава", ом_ПДМ_на_клиенте, ПараметрыЧтения); 

		Иначе	
			Возврат;
		КонецЕсли;
		
	Иначе
		Возврат;                        
	КонецЕсли;
	
	ТекстДок.НачатьЧтение(ОписаниеОповещения, Файл.ПолноеИмя, КодировкаТекста.UTF8,	Символы.ВК + Символы.ПС); 			
	
КонецПроцедуры // РазобратьИмяФайла(Файл)  

Процедура ПослеЧтенияФайлаГабаритныеЧертежи(Параметры) Экспорт
	
	КодPDM = омРаботаССтроками.ОставитьТолькоЦифры(Параметры.ТекстДок.ПолучитьТекст());
	
	Если НЕ(КодPDM = "0" ИЛИ ПустаяСтрока(КодPDM)) Тогда
		омГабаритныйЧертежНаСервере.УстановитьКодПДМДляГабаритныйЧертеж(Параметры.КодЭлемента, КодPDM); 
	КонецЕсли;
	
	УдалитьФайлыАсинх(Параметры.Файл.ПолноеИмя);
КонецПроцедуры

Процедура ПослеЧтенияФайлаКомпозиции(Параметры) Экспорт
	
	КодPDM = омРаботаССтроками.ОставитьТолькоЦифры(Параметры.ТекстДок.ПолучитьТекст());
	
	Если НЕ(КодPDM = "0" ИЛИ ПустаяСтрока(КодPDM)) Тогда
		омКомпозицииСервер.УстановитьКодПДМДляКомпозиции(Параметры.КодЭлемента, КодPDM); 
	КонецЕсли;
	
	УдалитьФайлыАсинх(Параметры.Файл.ПолноеИмя);
КонецПроцедуры

Процедура ПослеЧтенияФайлаКонструктор(Параметры) Экспорт
	
	Если Параметры.ЗначениеСуфикса = "1" Тогда
		Сообщить("Конструктор изменен.");
	ИначеЕсли Параметры.ЗначениеСуфикса = "0" Тогда
		Сообщить("Ошибка при изменении Конструктора.");
	Иначе
		стПараметры = Новый Структура("КодКомплекта", Параметры.КодЭлемента);
		ОткрытьФорму("ОбщаяФорма.ИзменениеКонструктора", стПараметры, , , , , ,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
	
	УдалитьФайлыАсинх(Параметры.Файл.ПолноеИмя);
КонецПроцедуры

Процедура ПослеЧтенияФайлаПроекты(Параметры) Экспорт
	
	КодЭлемента = омРаботаССтроками.ОставитьТолькоЦифры(Параметры.ТекстДок.ПолучитьТекст());
	
	Если НЕ(КодЭлемента = "0" ИЛИ ПустаяСтрока(КодЭлемента)) Тогда
		омПроекты.УстановитьКодПДМДляСделки(Параметры.КодСделки, КодЭлемента); 
	КонецЕсли;
	
	УдалитьФайлыАсинх(Параметры.Файл.ПолноеИмя);
КонецПроцедуры

Процедура ПослеЧтенияФайлаКомплект(Параметры) Экспорт
	
	КодКомплектаПДМ = омРаботаССтроками.ОставитьТолькоЦифры(Параметры.ТекстДок.ПолучитьТекст());
	
	Если НЕ(КодКомплектаПДМ = "0" ИЛИ ПустаяСтрока(КодКомплектаПДМ)) Тогда
		омИзделиеКомплект.УстановитьКодПДМКодКомплекта(Параметры.КодКомплект, КодКомплектаПДМ); 
		Оповестить("ИзменилсяКодКомплектаПДМ", Новый Структура("КодКомплект", КодКомплектаПДМ));
	КонецЕсли;
	
	УдалитьФайлыАсинх(Параметры.Файл.ПолноеИмя);
	
КонецПроцедуры

Процедура ПослеИзмененияСтатусаГЧ(Параметры) Экспорт
	
	стГабаритныйЧертеж = омPDMНаСервере.ПолучитьГабаритныйЧертежПоКоду(Параметры.КодЭлемента);
	
	Сообщить(СтрШаблон("%1 статус %2", ?(Параметры.ЗначениеСуфикса = "0", "Не изменился", "Изменился"), стГабаритныйЧертеж.Наименование));
	
	
	УдалитьФайлыАсинх(Параметры.Файл.ПолноеИмя);
	
КонецПроцедуры

Процедура ПослеЗабратьПрава(Параметры) Экспорт
	
	Сообщить(?(Параметры.ЗначениеСуфикса = "0", "Не удалось зарегестрировать файлы", "Файлы зарегистрировать"));
	
	УдалитьФайлыАсинх(Параметры.Файл.ПолноеИмя);
	
	Оповестить("ИзменилсяНаборПравPDM", Новый Структура("КодЭлемента, ЗначениеСуфикса", Параметры.КодЭлемента, Параметры.ЗначениеСуфикса));

КонецПроцедуры

Процедура ПослеРаздатьПрава(Параметры) Экспорт
	
	Сообщить(?(Параметры.ЗначениеСуфикса = "0", "Права не назначены", "Права назначены"));
	
	УдалитьФайлыАсинх(Параметры.Файл.ПолноеИмя);
	
	Оповестить("ИзменилсяНаборПравPDM", Новый Структура("КодЭлемента, ЗначениеСуфикса", Параметры.КодЭлемента, Параметры.ЗначениеСуфикса));
	
КонецПроцедуры

Процедура ПослеПереносаТЗ(Параметры) Экспорт
	
	ПрефиксКода = Лев(Параметры.КодЭлемента, 1);
	КодЭлемента = Сред(Параметры.КодЭлемента, 2);
	
	стДанные = омКомплекты.ПолучитьСсылкеПоКодуПДМ(Параметры.КодЭлемента);
	
	Если Параметры.ЗначениеСуфикса = "0" Тогда
	
		ТекстСообщения 	= " Не удалось перенести файл ТЗ для " + стДанные.Наименование;
		СтрокаОтвета 	= "";
	
	Иначе
		
		ТекстСообщения 	= " Файл ТЗ перенесен для " + стДанные.Наименование;
		СтрокаОтвета 	= Параметры.ТекстДок.ПолучитьТекст();
		
		омКанбанТЗ.ПереместитьРеквизитВОжидание(стДанные.Комплект);
		
		Оповестить("ЗавершенПереносТЗ", Новый Структура("КодКомплект", Параметры.КодЭлемента)); 
		
	КонецЕсли;
	
	Сообщить(Формат(ТекущаяДата(), "ДЛФ=DT") + ТекстСообщения + " " + СтрокаОтвета);
	
	УдалитьФайлыАсинх(Параметры.Файл.ПолноеИмя);
	

КонецПроцедуры

Функция ПолучитьПутьКВыгузке(ТипВыгрузки, стДанныеВыгрузки) Экспорт
	
	ПутьКФайлуEXE = "";
	
	Если ТипВыгрузки = "Комплект" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXE;
	ИначеЕсли ТипВыгрузки = "Проект" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEПроекта;
	ИначеЕсли ТипВыгрузки = "Конструктор" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEКонструктор;
	ИначеЕсли ТипВыгрузки = "Композиция" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEКомпозиция;
	ИначеЕсли ТипВыгрузки = "ГабаритныйЧертеж" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEГабаритныйЧерчеж;
	ИначеЕсли ТипВыгрузки = "СтатусГЧ" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEИзменениеСтатусаГЧ;
	ИначеЕсли ТипВыгрузки = "ЗагрузкаТЗ" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEЗагрузкаТЗ;
	ИначеЕсли ТипВыгрузки = "РаздатьПрава" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEРаздатьПрава;
	ИначеЕсли ТипВыгрузки = "ЗабратьПрава" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEЗабратьПрава;
	ИначеЕсли ТипВыгрузки = "КонтрольРегистрации" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEЕстьРазрегестрированные;
	КонецЕсли;
	
	Возврат ПутьКФайлуEXE;

КонецФункции // ПолучитьПутьКВыгузке()

