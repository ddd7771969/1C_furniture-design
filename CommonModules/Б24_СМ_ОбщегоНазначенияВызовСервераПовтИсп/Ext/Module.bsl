
#Область ПолучениеНастроек

Функция ПолучитьМассивНастроекОВыгружаемыхДанных(ТолькоДляРегистрацииИзменений = Ложь) Экспорт
	
	Результат = Новый Массив;
	
	мНастройкиПодключения = Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьМассивНастроекСинхронизации();
	
	Для Каждого НастройкаПодключения Из мНастройкиПодключения Цикл
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("НастройкаПодключения"			, НастройкаПодключения);
		СтруктураДанных.Вставить("СпособСинхронизацииДанных"	, "");
		СтруктураДанных.Вставить("ИнформацияОВыгружаемыхДанных"	, Новый Массив);
		
		НастройкиСинхронизации = Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
		Если НастройкиСинхронизации.НеРегистрироватьИзмененияАвтоматически И ТолькоДляРегистрацииИзменений Тогда
			Продолжить;
		КонецЕсли;                                                                         
		
		СтруктураДанных.Вставить("СпособСинхронизацииДанных", НастройкиСинхронизации.СпособСинхронизацииДанных);       
		
		СохраненныеНастройки = НастройкиСинхронизации.Настройки.Получить();
		Если СохраненныеНастройки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого ТекСтрока Из СохраненныеНастройки Цикл
			
			Если ТекСтрока.Данные = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТекСтрока.Данные.Сущность1С = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТекСтрока.Данные.ВыгружатьНаПортал <> Истина Тогда
				Продолжить;
			КонецЕсли;
			
			ИнформацияОВыгружаемыхДанных = Новый Структура;
			ИнформацияОВыгружаемыхДанных.Вставить("ИдСмартПроцесса"				, ТекСтрока.Ид);
			ИнформацияОВыгружаемыхДанных.Вставить("ТипВладельцаСмартПроцесса"	, ТекСтрока.ТипВладельца);
			ИнформацияОВыгружаемыхДанных.Вставить("Наименование"				, ТекСтрока.Наименование);
			ИнформацияОВыгружаемыхДанных.Вставить("ТипСущности1С"				, ТекСтрока.Данные.ТипСущности1С);
			ИнформацияОВыгружаемыхДанных.Вставить("Сущность1С"					, ?(ТекСтрока.Данные.Сущность1С = Неопределено, "", ТекСтрока.Данные.Сущность1С.Значение));
			ИнформацияОВыгружаемыхДанных.Вставить("ТабличнаяЧасть1С"			, ?(ТекСтрока.Данные.ТабличнаяЧасть1С = Неопределено, "", ТекСтрока.Данные.ТабличнаяЧасть1С.Значение));
			
			СтруктураДанных.ИнформацияОВыгружаемыхДанных.Добавить(ИнформацияОВыгружаемыхДанных);		   
					   
		КонецЦикла;
		
		Результат.Добавить(СтруктураДанных);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкиСинхронизации(НастройкаПодключения) Экспорт
	
	СохраненныеНастройки = Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	
	Возврат СохраненныеНастройки.Настройки.Получить();
	
КонецФункции

Функция ПолучитьМассивНастроекСинхронизации(КлючНастройки = "", ЗначениеНастройки = "") Экспорт
	
	Запрос = Новый Запрос;
	Если НЕ КлючНастройки = "" Тогда
		Если Тип("Структура") = ТипЗнч(ЗначениеНастройки) Тогда
			Запрос.УстановитьПараметр("ПараметрПоиска", ЗначениеНастройки.НастройкаПодключения);
		Иначе
			Запрос.УстановитьПараметр("ПараметрПоиска", ЗначениеНастройки);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СМ_НастройкиСмартПроцессов.НастройкаПодключения КАК НастройкаПодключения
	|ИЗ
	|	РегистрСведений.Б24_СМ_НастройкиСмартПроцессов КАК Б24_СМ_НастройкиСмартПроцессов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_СМ_НастройкиСмартПроцессов.НастройкаПодключения = Б24_К_НастройкиПодключения.Ссылка
	|ГДЕ
	|	Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ";
	
	Если НЕ КлючНастройки = "" Тогда
		Запрос.Текст = Запрос.Текст + "
		|" + " И Б24_СМ_НастройкиСмартПроцессов." + КлючНастройки + " = &ПараметрПоиска";
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("НастройкаПодключения");
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НастройкаПодключения"					, Справочники.Б24_К_НастройкиПодключения.ПустаяСсылка());
	Результат.Вставить("СпособСинхронизацииДанных"				, "Только ручной режим");
	Результат.Вставить("КоличествоПовторенийПриОшибках"			, 1);
	Результат.Вставить("НеРегистрироватьИзмененияАвтоматически"	, Ложь);
	Результат.Вставить("ПорядокВыполненияСинхронизации"			, "Сперва Битрикс24, затем 1С");
	Результат.Вставить("ИдентификаторРегламентногоЗадания"		, Неопределено);
	Результат.Вставить("ИгнорироватьОшибкиВоВремяСинхронизации"	, Ложь);
	Результат.Вставить("Настройки"								, Новый ХранилищеЗначения(Неопределено));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СМ_НастройкиСмартПроцессов.НастройкаПодключения КАК НастройкаПодключения,
	|	Б24_СМ_НастройкиСмартПроцессов.Настройки КАК Настройки,
	|	Б24_СМ_НастройкиСмартПроцессов.СпособСинхронизацииДанных КАК СпособСинхронизацииДанных,
	|	Б24_СМ_НастройкиСмартПроцессов.КоличествоПовторенийПриОшибках КАК КоличествоПовторенийПриОшибках,
	|	Б24_СМ_НастройкиСмартПроцессов.НеРегистрироватьИзмененияАвтоматически КАК НеРегистрироватьИзмененияАвтоматически,
	|	Б24_СМ_НастройкиСмартПроцессов.ПорядокВыполненияСинхронизации КАК ПорядокВыполненияСинхронизации,
	|	Б24_СМ_НастройкиСмартПроцессов.ИдентификаторРегламентногоЗадания КАК ИдентификаторРегламентногоЗадания,
	|	Б24_СМ_НастройкиСмартПроцессов.ИгнорироватьОшибкиВоВремяСинхронизации КАК ИгнорироватьОшибкиВоВремяСинхронизации
	|ИЗ
	|	РегистрСведений.Б24_СМ_НастройкиСмартПроцессов КАК Б24_СМ_НастройкиСмартПроцессов
	|ГДЕ
	|	Б24_СМ_НастройкиСмартПроцессов.НастройкаПодключения = &НастройкаПодключения";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ЗаполнитьЗначенияСвойств(Результат, Выборка);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Результат.Настройки.Получить() = Неопределено Тогда
		Результат.Настройки = Новый ХранилищеЗначения(РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьНастройкиСинхронизацииПоУмолчанию());	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

#КонецОбласти

Функция СоздатьПолучитьСвойствоДляХраненияЗначенийСвойствБитрикс24() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	ТипыДанныхДляОбменаСПорталом = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();
	
	НаименованиеСвойства = "Служебное свойство для хранения значений свойств Битрикс24";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаименованиеСвойства", НаименованиеСвойства);
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|ГДЕ
	|	ДополнительныеРеквизитыИСведения.Заголовок = &НаименованиеСвойства
	|	И ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение = ИСТИНА";
	ВыполненныйЗапрос = Запрос.Выполнить();	
	
	Если ВыполненныйЗапрос.Пустой() Тогда
		
		СвойствоОбъект 	= ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.СоздатьЭлемент();
		СвойствоОбъект.Комментарий 	= НСтр("ru = 'Создано автоматически для Битрикс24'");
		СвойствоОбъект.Наименование = НаименованиеСвойства;
		СвойствоОбъект.Заголовок 	= НаименованиеСвойства;
		СвойствоОбъект.ЗаголовокФормыВыбораЗначения = НаименованиеСвойства;
		СвойствоОбъект.ЗаголовокФормыЗначения 		= НаименованиеСвойства;
		
		лТип = Тип("СправочникСсылка.ЗначенияСвойствОбъектов");
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(лТип);
		
		СвойствоОбъект.ЭтоДополнительноеСведение = Истина;
		СвойствоОбъект.ТипЗначения 	= Новый ОписаниеТипов(МассивТипов);
		СвойствоОбъект.Доступен 	= Ложь;
		СвойствоОбъект.Виден 		= Ложь;
		СвойствоОбъект.ДополнительныеЗначенияИспользуются 	= Истина;
		
		СвойствоОбъект.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
		СвойствоОбъект.Записать();
		СвойствоСсылка = СвойствоОбъект.Ссылка; 
		
	Иначе
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СвойствоСсылка = Выборка.Ссылка; 
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СвойствоСсылка;
	
КонецФункции
   
Функция ПолучитьСтруктуруКлючейСмартПроцесса(НастройкаПодключения, ТипВладельцаСмартПроцесса) Экспорт
	
	СтруктураНастроек = Б24_СМ_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);
	
	КлючиСмартПроцесса = Новый Структура;
	КлючиСмартПроцесса.Вставить("КлючиШапки", Новый Соответствие);
	КлючиСмартПроцесса.Вставить("КлючиТЧ"	, Новый Соответствие);
	
	ПараметрыЗапроса 	= "&entityTypeId=" + ТипВладельцаСмартПроцесса;
	Метод 				= "/rest/crm.item.fields";	
	
	Результат = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, Метод, ПараметрыЗапроса); 
	Если Результат <> Неопределено Тогда
		result = Результат.Получить("result");
		Если result <> Неопределено Тогда
			fields = result.Получить("fields");
			Если fields <> Неопределено Тогда
				Для каждого ТекЭлемент Из fields Цикл
					КлючиСмартПроцесса.КлючиШапки.Вставить(ТекЭлемент.Ключ, Новый Структура("Тип, Настройки", ТекЭлемент.Значение.Получить("type"), ТекЭлемент.Значение.Получить("settings")));
				КонецЦикла;
			КонецЕсли;                           
		КонецЕсли;                             
	КонецЕсли;
	
	ПараметрыЗапроса 	= "&entityTypeId=" + ТипВладельцаСмартПроцесса;
	Метод 				= "/rest/crm.item.productrow.fields";	
	
	Результат = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, Метод, ПараметрыЗапроса); 
	Если Результат <> Неопределено Тогда
		result = Результат.Получить("result");
		Если result <> Неопределено Тогда
			fields = result.Получить("fields");
			Если fields <> Неопределено Тогда
				Для каждого ТекЭлемент Из fields Цикл
					КлючиСмартПроцесса.КлючиТЧ.Вставить(ТекЭлемент.Ключ, Новый Структура("Тип, Настройки", ТекЭлемент.Значение.Получить("type"), ТекЭлемент.Значение.Получить("settings")));
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат КлючиСмартПроцесса;
	
КонецФункции
