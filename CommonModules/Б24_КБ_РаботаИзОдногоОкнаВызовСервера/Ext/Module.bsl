
#Область ImportSession

Процедура ImportSession(ПередаваемыеПараметры, АдресРезультата) Экспорт
	
	Портал 				= ПередаваемыеПараметры.Портал;
	ИдСессии 			= ПередаваемыеПараметры.ИдСессии;
	АдресСущности		= ПередаваемыеПараметры.Адрес;
	ИдПользователя  	= ПередаваемыеПараметры.ИдПользователя;

УстановитьПривилегированныйРежим(Истина);
	
	ДобавитьНавигационнуюСсылкуИзБитрикс24(ПередаваемыеПараметры);
	
	Б24_К_ОбщегоНазначенияВызовСервера.ПроверитьФоновоеЗаданиеВОчереди(ПередаваемыеПараметры);
	
КонецПроцедуры

#КонецОбласти


#Область ImportEntity

Процедура ImportEntity(ПередаваемыеПараметры, АдресРезультата) Экспорт
	
	НастройкаПодключения 	= ПередаваемыеПараметры.НастройкаПодключения;
	Портал 					= ПередаваемыеПараметры.Портал;
	ИдВладельца 			= ПередаваемыеПараметры.ИдВладельца;
	ТипВладельца 			= ПередаваемыеПараметры.ТипВладельца;
	ИдПолучателя			= ПередаваемыеПараметры.ИдПолучателя;
	ТипПолучателя 			= ПередаваемыеПараметры.ТипПолучателя;
	ТипДокумента 			= ПередаваемыеПараметры.ТипДокумента;
	ИдСессии 				= ПередаваемыеПараметры.ИдСессии;
	ИдПользователя  		= ПередаваемыеПараметры.ИдПользователя;
	АдресСущности			= ПередаваемыеПараметры.Адрес;
	
	УстановитьПривилегированныйРежим(Истина);
									
	ДобавитьНавигационнуюСсылкуИзБитрикс24(ПередаваемыеПараметры);  
	
	Если ТипВладельца = "1" Тогда      		
		СформированныеДанные = СформироватьКомпаниюКонтактПоЛиду(НастройкаПодключения, ИдВладельца);	                                                                       
	ИначеЕсли  ТипВладельца = "2" Тогда     
		СформированныеДанные = СформироватьДанныеПоСделке(НастройкаПодключения, ИдВладельца);	                                                                       
	ИначеЕсли  ТипВладельца = "3" Тогда     
		СформированныеДанные = СформироватьКомпаниюКонтактПоИд(НастройкаПодключения, "Контакт", ИдВладельца);	
	ИначеЕсли  ТипВладельца = "4" Тогда     
		СформированныеДанные = СформироватьКомпаниюКонтактПоИд(НастройкаПодключения, "Компания", ИдВладельца);
	ИначеЕсли  ТипВладельца = "31" Тогда
		СформированныеДанные = СформироватьДанныеПоСмартПроцессу(НастройкаПодключения, ТипВладельца, ИдВладельца);     
	Иначе
		СформированныеДанные = СформироватьДанныеПоСмартПроцессу(НастройкаПодключения, ТипВладельца, ИдВладельца);     
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(СформированныеДанные) Тогда
		
		НаборЗаписей = РегистрыСведений.Б24_КБ_ДанныеДляНовыхДокументовИзБитрикс24.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдСессии.Установить(ИдСессии);
		НаборЗаписей.Отбор.НастройкаПодключения.Установить(НастройкаПодключения);
		НаборЗаписей.Записать();
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИдСессии 				= ИдСессии;
		НоваяЗапись.НастройкаПодключения 	= НастройкаПодключения;
		НоваяЗапись.Объект 					= СформированныеДанные.Организация;
		НоваяЗапись.ТипОбъекта				= "Организация";		
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИдСессии 				= ИдСессии;
		НоваяЗапись.НастройкаПодключения 	= НастройкаПодключения;
		НоваяЗапись.Объект 					= СформированныеДанные.Партнер;
		НоваяЗапись.ТипОбъекта				= "Партнер";		
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИдСессии 				= ИдСессии;
		НоваяЗапись.НастройкаПодключения 	= НастройкаПодключения;
		НоваяЗапись.Объект 					= СформированныеДанные.Контрагент;
		НоваяЗапись.ТипОбъекта				= "Контрагент";		
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИдСессии 				= ИдСессии;
		НоваяЗапись.НастройкаПодключения 	= НастройкаПодключения;
		НоваяЗапись.Объект 					= СформированныеДанные.Валюта;
		НоваяЗапись.ТипОбъекта				= "Валюта";		
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИдСессии 				= ИдСессии;
		НоваяЗапись.НастройкаПодключения 	= НастройкаПодключения;
		НоваяЗапись.Значение 				= СформированныеДанные.Сумма;
		НоваяЗапись.ТипОбъекта				= "СуммаДокумента";		
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИдСессии 				= ИдСессии;
		НоваяЗапись.НастройкаПодключения 	= НастройкаПодключения;
		НоваяЗапись.Значение 				= ИдВладельца;
		НоваяЗапись.ТипОбъекта				= "ИдентификаторВладельца";		
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИдСессии 				= ИдСессии;
		НоваяЗапись.НастройкаПодключения 	= НастройкаПодключения;
		НоваяЗапись.Значение 				= ТипВладельца;
		НоваяЗапись.ТипОбъекта				= "ТипВладельца";		
		
		Итератор = 0;
		Для каждого ТекТовар Из СформированныеДанные.Товары Цикл
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.ИдСессии 	= ИдСессии;
			НоваяЗапись.НастройкаПодключения 	= НастройкаПодключения;
			НоваяЗапись.ТипОбъекта				= "Товар_" + Строка(Итератор);		
			НоваяЗапись.Значение 				= ТекТовар;
			Итератор = Итератор + 1;
			
		КонецЦикла;
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИдСессии 	= ИдСессии;
		НоваяЗапись.НастройкаПодключения 	= НастройкаПодключения;
		НоваяЗапись.ТипОбъекта				= "КонецДанных";		
		
		НаборЗаписей.Записать(Истина);
		
	КонецЕсли;

	Б24_К_ОбщегоНазначенияВызовСервера.ПроверитьФоновоеЗаданиеВОчереди(ПередаваемыеПараметры);
	
КонецПроцедуры

Функция ОписаниеДляПодстановкиВОбъекты() 

	Результат = Новый Структура;
	Результат.Вставить("Ошибки"		, Новый СписокЗначений);
	Результат.Вставить("Партнер"	, Неопределено);
	Результат.Вставить("Контрагент"	, Неопределено);
	Результат.Вставить("Организация", Справочники.Организации.ОрганизацияПоУмолчанию());
	Результат.Вставить("Валюта"		, Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ВалютаПоУмолчанию());
	Результат.Вставить("Сумма"		, 0);
	Результат.Вставить("Товары"		, Новый Массив);

	Возврат Результат;

КонецФункции


#Область ФормированиеПоЛиду

Функция СформироватьКомпаниюКонтактПоЛиду(НастройкаПодключения, Ид) 
	
	Результат = ОписаниеДляПодстановкиВОбъекты();
	 
	СтруктураНастроек 	= Б24_КБ_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		
		Результат.Ошибки.Добавить("3", "Could not connect");
		Возврат Результат;
		
	КонецЕсли;
	
	НастройкиИнтеграцииСервисов = РегистрыСведений.Б24_КБ_НастройкиИнтеграцииСервисов.ПолучитьНастройкиИнтеграцииСервисовПоНастройкеПодключения(НастройкаПодключения);
	СтруктураНастроек.Вставить("НастройкиКлиентов", НастройкиИнтеграцииСервисов.Настройки.Получить().НастройкиКлиентов);
	
	ТелоHTTPЗапроса = "&id=" + Формат(Ид, "ЧГ=0");	
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.lead.get", ТелоHTTPЗапроса); 
	
	Если СтруктураОтвета = Неопределено Тогда
		
		Результат.Ошибки.Добавить("4", "Could not get lead");
		Возврат Результат;
		
	КонецЕсли;
	
	Лид = СтруктураОтвета.Получить("result");
	
	Если Лид = Неопределено Тогда
		
		Результат.Ошибки.Добавить("5", "Error getting lead");
		Возврат Результат;
		
	КонецЕсли;
	
	ИдКомпании    	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Лид.Получить("COMPANY_ID"),"ЧГ=0"));
	ИдКонтакта     	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Лид.Получить("CONTACT_ID"),"ЧГ=0"));
	Результат.Сумма	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(Лид.Получить("OPPORTUNITY"),"ЧГ=0"));
	
	Если ЗначениеЗаполнено(ИдКомпании) И ИдКомпании <> "0" Тогда
		СформироватьКомпаниюКонтактПоИд(НастройкаПодключения, "Компания", ИдКомпании); 
		ИнформацияОИдентификаторах = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Компания, ИдКомпании);
		
		Если ИнформацияОИдентификаторах <> Неопределено Тогда
			Результат.Партнер = ИнформацияОИдентификаторах.Объект;
		КонецЕсли;  
		
	ИначеЕсли ЗначениеЗаполнено(ИдКонтакта) И ИдКонтакта <> "0" Тогда
		СформироватьКомпаниюКонтактПоИд(НастройкаПодключения, "Контакт", ИдКонтакта); 
		ИнформацияОИдентификаторах = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Контакт, ИдКонтакта);
		
		Если ИнформацияОИдентификаторах <> Неопределено Тогда
			Результат.Партнер = ИнформацияОИдентификаторах.Объект;
		КонецЕсли;
		
	Иначе
		
		КомпанияКонтакт	= ПолучитьПартнераПоЛиду(СтруктураНастроек, Лид);
		Результат.Партнер = КомпанияКонтакт;	
		
		Если КомпанияКонтакт = Неопределено Тогда
			
			Результат.Ошибки.Добавить("6", "Error creating client 1C");
			Возврат Результат;	
			
		КонецЕсли;
		
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КомпанияКонтакт, "ЮрФизЛицо") = Перечисления.КомпанияЧастноеЛицо.Компания Тогда 
			ИдКлиента = Б24_К_ОбменКомпанийКонтактовСервер.СформироватьСтруктуруДанныхКомпании(СтруктураНастроек, КомпанияКонтакт);
			ТелоЗапроса ="&fields[COMPANY_ID]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдКлиента);
		Иначе
			ИдКлиента = Б24_К_ОбменКомпанийКонтактовСервер.СформироватьСтруктуруДанныхКонтакта(СтруктураНастроек, КомпанияКонтакт);	
			ТелоЗапроса ="&fields[CONTACT_ID]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдКлиента);
		КонецЕсли;
		
		Если ИдКлиента <> "" Тогда
			
			СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.lead.update?id="+Формат(Ид, "ЧГ=0"), ТелоЗапроса); 
			
			Если СтруктураОтвета = Неопределено Тогда
				
				Результат.Ошибки.Добавить("8", "Error updating lead");
				Возврат Результат;
				
			КонецЕсли;
			
			result = СтруктураОтвета.Получить("result");
			
			Если result = Ложь ИЛИ result = Неопределено Тогда
				
				Результат.Ошибки.Добавить("8", "Error updating lead");
				Возврат Результат;
				
			КонецЕсли;
			
		Иначе
			
			Результат.Ошибки.Добавить("7", "Error unloading client");
			Возврат Результат;
		
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.Партнер) И НЕ ЗначениеЗаполнено(Результат.Контрагент) Тогда
		Результат.Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("Партнер", Результат.Партнер); 	
	КонецЕсли;	
	
	Возврат Результат;
		
КонецФункции

Функция ПолучитьПартнераПоЛиду(СтруктураНастроек, Лид)
	
	КомпанияКонтакт = Неопределено;
	
	НаименованиеКомпании = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Лид.Получить("COMPANY_TITLE"));
	Имя 				 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Лид.Получить("NAME"));
	Отчество 			 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Лид.Получить("SECOND_NAME"));
	Фамилия 			 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Лид.Получить("LAST_NAME"));
	ДатаРождения 		 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата")  , Лид.Получить("BIRTHDATE"));
	Телефоны 			 = Лид.Получить("PHONE");
	Почты 				 = Лид.Получить("EMAIL");
	Должность 			 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Лид.Получить("POST"));
	
	ПодробныеДанные = Новый Структура;
	ПодробныеДанные.Вставить("ИнформацияОРеквизитах", Новый Массив);
	
	ИнформацияОКомпанияхКонтактах = Новый Массив;
	ИнформацияОКлиенте = Новый Соответствие;
	
	ИнформацияОКлиенте.Вставить("ID"			, "");
	ИнформацияОКлиенте.Вставить("ORIGIN_ID"		, "");
	ИнформацияОКлиенте.Вставить("TITLE"			, НаименованиеКомпании);
	ИнформацияОКлиенте.Вставить("IS_MY_COMPANY"	, "N");
	ИнформацияОКлиенте.Вставить("PHONE"			, Телефоны);
	ИнформацияОКлиенте.Вставить("EMAIL"			, Почты);
	ИнформацияОКлиенте.Вставить("COMMENTS"		, "");
	ИнформацияОКлиенте.Вставить("NAME"			, Имя);
	ИнформацияОКлиенте.Вставить("LAST_NAME"		, Фамилия);
	ИнформацияОКлиенте.Вставить("SECOND_NAME"	, Отчество);
	ИнформацияОКлиенте.Вставить("POST"			, Должность);
	ИнформацияОКлиенте.Вставить("COMPANY_ID"	, "");
	ИнформацияОКлиенте.Вставить("BIRTHDATE"		, ДатаРождения);
	
	ИнформацияОКомпанияхКонтактах.Добавить(ИнформацияОКлиенте);
	
	Если ЗначениеЗаполнено(НаименованиеКомпании) И НЕ (ЗначениеЗаполнено(Фамилия) ИЛИ ЗначениеЗаполнено(Имя)) Тогда
		ПодробныеДанные.Вставить("ИнформацияОКомпаниях", ИнформацияОКомпанияхКонтактах);
		КомпанияКонтакт = Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьКомпании(СтруктураНастроек, ПодробныеДанные, Истина);
	ИначеЕсли ЗначениеЗаполнено(НаименованиеКомпании) И (ЗначениеЗаполнено(Фамилия) ИЛИ ЗначениеЗаполнено(Имя)) Тогда
		ПодробныеДанные.Вставить("ИнформацияОКонтактах", ИнформацияОКомпанияхКонтактах);
		КомпанияКонтакт = Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьКонтакты(СтруктураНастроек, ПодробныеДанные, Истина);	
	ИначеЕсли НЕ ЗначениеЗаполнено(НаименованиеКомпании) И (ЗначениеЗаполнено(Фамилия) ИЛИ ЗначениеЗаполнено(Имя)) Тогда
		ПодробныеДанные.Вставить("ИнформацияОКонтактах", ИнформацияОКомпанияхКонтактах);
		КомпанияКонтакт = Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьКонтакты(СтруктураНастроек, ПодробныеДанные, Истина);	
	Иначе
		КомпанияКонтакт = Неопределено;
	КонецЕсли;
	
	Возврат КомпанияКонтакт;
	
КонецФункции

#КонецОбласти


#Область ФормированиеКомпанииКонтакта

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Функция СформироватьКомпаниюКонтактПоИд(НастройкаПодключения, ТипВладельца, Ид) 
	
	Результат = ОписаниеДляПодстановкиВОбъекты();
	
	СтруктураНастроек 	= Б24_КБ_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		
		Результат.Ошибки.Добавить("3", "Could not connect");
		Возврат Результат;
		
	КонецЕсли;
	
	НастройкиИнтеграцииСервисов = РегистрыСведений.Б24_КБ_НастройкиИнтеграцииСервисов.ПолучитьНастройкиИнтеграцииСервисовПоНастройкеПодключения(НастройкаПодключения);
	СтруктураНастроек.Вставить("НастройкиКлиентов", НастройкиИнтеграцииСервисов.Настройки.Получить().НастройкиКлиентов);
	
	
	Если СтруктураНастроек.НастройкиКлиентов.ЗагружатьКонтрагентовМодулемСинхронизации Тогда
		
		МодульЗагрузкаСервер = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ЗагрузкаСервер");
		Результат.Партнер = МодульЗагрузкаСервер.СформироватьДанныеДляДругогоМодуля(НастройкаПодключения, ТипВладельца, Ид);
	
	ИначеЕсли СтруктураНастроек.НастройкиКлиентов.ЗагружатьКонтрагентов Тогда
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("Клиент"	, Новый Массив);
		СтруктураДанных.Вставить("Реквизит"	, Новый Массив);
		СтруктураДанных.Вставить("Адреса"	, Новый Массив);
		СтруктураДанных.Вставить("БанкСчета", Новый Массив);
		
		Если ТипВладельца = "Компания" Тогда
			МетодКлиента = "crm.company.get?id=" + Ид; 	
			МетодРеквизита = "crm.requisite.list?filter[ENTITY_ID]=" + Ид + "&filter[ENTITY_TYPE_ID]=4" + "&start=-1"; 	
		Иначе
			МетодКлиента = "crm.contact.get?id=" + Ид; 
			МетодРеквизита = "crm.requisite.list?filter[ENTITY_ID]=" + Ид + "&filter[ENTITY_TYPE_ID]=3" + "&start=-1"; 	
		КонецЕсли;
		
		ТелоHTTPЗапроса = "";
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[K]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодКлиента);
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[R]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодРеквизита);
		
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
		
		Если СтруктураОтвета = Неопределено Тогда
			
			Результат.Ошибки.Добавить("10", "Error getting customer information");
			Возврат Результат;
			
		КонецЕсли;
		
		result = СтруктураОтвета.Получить("result");                                             
		Если result <> Неопределено Тогда                                                         
			result2 = result.Получить("result");
			Если result2 <> Неопределено Тогда 	
				
				ДанныеКлиента 		= result2.Получить("K"); 
				ДанныеРеквизита 	= result2.Получить("R"); 
				
				Если ДанныеКлиента <> Неопределено Тогда
					СтруктураДанных.Клиент.Добавить(ДанныеКлиента);	
				КонецЕсли;
				
				Если ДанныеРеквизита <> Неопределено Тогда
					СтруктураДанных.Реквизит = ДанныеРеквизита;	
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		ПодробныеДанные = Новый Структура;
		ПодробныеДанные.Вставить("ИнформацияОРеквизитах", СтруктураДанных.Реквизит);
		ПодробныеДанные.Вставить("ИнформацияОКонтактах", СтруктураДанных.Клиент);
		
		Если ТипВладельца = "Компания" Тогда
			ПодробныеДанные.Вставить("ИнформацияОКомпаниях", СтруктураДанных.Клиент);
			Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьКомпании(СтруктураНастроек, ПодробныеДанные);
			
			ИнформацияОИдентификаторах = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Компания, Ид);
			Если ЗначениеЗаполнено(ИнформацияОИдентификаторах) Тогда 
				ИнформацияОКлиенте 	= ИнформацияОИдентификаторах.Объект;
				Результат.Партнер 	= ИнформацияОКлиенте;        
			КонецЕсли;
			
		Иначе
			ПодробныеДанные.Вставить("ИнформацияОКонтактах", СтруктураДанных.Клиент);
			Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьКонтакты(СтруктураНастроек, ПодробныеДанные);	
			
			ИнформацияОИдентификаторах = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Контакт, Ид);
			Если ЗначениеЗаполнено(ИнформацияОИдентификаторах) Тогда 
				ИнформацияОКлиенте  = ИнформацияОИдентификаторах.Объект;
				Результат.Партнер 	= ИнформацияОКлиенте;        
			КонецЕсли;   
			
		КонецЕсли;
		
		Если ИнформацияОКлиенте = Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
	
		// Предполагаем, что запросов будет менее 51   т.е. меньше 25 реквизитов(на каждый 2 запроса) и у каждого меньше 50 банк. счетов.
		КоличествоЗапросов = 0;
		ТелоHTTPЗапроса = "";
		
		Для каждого ТекРеквизит Из СтруктураДанных.Реквизит Цикл 

			Если КоличествоЗапросов = 50 Тогда
				Прервать;
			КонецЕсли;
			
			ИдРеквизита 	= Формат(ТекРеквизит.Получить("ID"), "ЧГ=0");  
			
			МетодАдреса 	= "crm.address.list?filter[ENTITY_ID]=" + ИдРеквизита + "&filter[ENTITY_TYPE_ID]=8"; 	
			МетодБанкСчета 	= "crm.requisite.bankdetail.list?filter[ENTITY_ID]=" + ИдРеквизита; 	
			
			ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[A]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодАдреса);
			ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[B]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодБанкСчета);

			КоличествоЗапросов = КоличествоЗапросов + 1;

		КонецЦикла;
		
		Если ТелоHTTPЗапроса <> "" Тогда
		
			СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
			
			result = СтруктураОтвета.Получить("result");                                             
			Если result <> Неопределено Тогда                                                         
				result2 = result.Получить("result");
				Если result2 <> Неопределено Тогда 			
					
					ДанныеАдресов		= result2.Получить("A"); 
					ДанныеБанкСчетов 	= result2.Получить("B"); 
					
					Если ДанныеАдресов <> Неопределено Тогда
						СтруктураДанных.Адреса = ДанныеАдресов;	
					КонецЕсли;
					
					Если ДанныеБанкСчетов <> Неопределено Тогда
						СтруктураДанных.БанкСчета = ДанныеБанкСчетов;	
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьРеквизиты(СтруктураНастроек, ПодробныеДанные);
		
		Если НЕ СтруктураНастроек.НеЗагружатьАдресаРеквизитов Тогда
			Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьАдресаРеквизитов(СтруктураНастроек, СтруктураДанных.Адреса);  
		КонецЕсли;         
		
		Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьБанковскиеСчетаКомпанийКонтактов(СтруктураНастроек, СтруктураДанных.БанкСчета);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.Партнер) И НЕ ЗначениеЗаполнено(Результат.Контрагент) Тогда
		Результат.Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("Партнер", Результат.Партнер); 	
	КонецЕсли;	
	
	Возврат Результат;	

КонецФункции

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#КонецОбласти


#Область ФормированиеПоСделке

Функция СформироватьДанныеПоСделке(НастройкаПодключения, Ид) Экспорт
	
	Результат = ОписаниеДляПодстановкиВОбъекты();
	
	СтруктураНастроек 	= Б24_КБ_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		
		Результат.Ошибки.Добавить("3", "Could not connect");
		Возврат Результат;
		
	КонецЕсли;
	
	НастройкиИнтеграцииСервисов = РегистрыСведений.Б24_КБ_НастройкиИнтеграцииСервисов.ПолучитьНастройкиИнтеграцииСервисовПоНастройкеПодключения(НастройкаПодключения);
	СтруктураНастроек.Вставить("НастройкиКлиентов", НастройкиИнтеграцииСервисов.Настройки.Получить().НастройкиКлиентов);
	
	ТелоHTTPЗапроса = "&filter[ID]="+Формат(Ид, "ЧГ=0") + "&start=-1";	
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.deal.list", ТелоHTTPЗапроса); 
	
	Если СтруктураОтвета = Неопределено Тогда
		
		Результат.Ошибки.Добавить("9", "Could not get deal");
		Возврат Результат;
		
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	
	Если result = Неопределено Тогда
		
		Результат.Ошибки.Добавить("9", "Could not get deal");
		Возврат Результат;
		
	КонецЕсли;
	
	Для каждого ТекСделка Из result Цикл
		
		ИдКомпании    	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекСделка.Получить("COMPANY_ID"),"ЧГ=0"));
		ИдКонтакта     	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекСделка.Получить("CONTACT_ID"),"ЧГ=0"));
		Результат.Сумма	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ТекСделка.Получить("OPPORTUNITY"),"ЧГ=0"));
		
		КодВалюты 	   	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьКодПоВалюте(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекСделка.Получить("CURRENCY_ID"),"ЧГ=0")));
		Валюта = Справочники.Валюты.НайтиПоКоду(КодВалюты);
		Если ЗначениеЗаполнено(Валюта) Тогда
			Результат.Валюта = Валюта;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИдКомпании) И ИдКомпании <> "0" Тогда
			СформироватьКомпаниюКонтактПоИд(НастройкаПодключения, "Компания", ИдКомпании); 
			ИнформацияОИдентификаторах = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Компания, ИдКомпании);
			
			Если ИнформацияОИдентификаторах <> Неопределено Тогда
				Результат.Партнер = ИнформацияОИдентификаторах.Объект;
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(ИдКонтакта) И ИдКонтакта <> "0" Тогда
			СформироватьКомпаниюКонтактПоИд(НастройкаПодключения, "Контакт", ИдКонтакта); 
			ИнформацияОИдентификаторах = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Контакт, ИдКонтакта);
			
			Если ИнформацияОИдентификаторах <> Неопределено Тогда
				Результат.Партнер = ИнформацияОИдентификаторах.Объект;
			КонецЕсли;
			
		Иначе
			Результат.Ошибки.Добавить("15", "client not found");
			ИнформацияОКлиенте = Неопределено;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Результат.Партнер) И НЕ ЗначениеЗаполнено(Результат.Контрагент) Тогда
			Результат.Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("Партнер", Результат.Партнер); 	
		КонецЕсли;	
		
		Если СтруктураНастроек.НастройкиИнтеграцииСервисов.ЭкспортТоваровИзСделок Тогда
			
			ТелоHTTPЗапроса = "ID=" + Формат(Ид, "ЧГ=0");	
			СтруктураОтветаТоваров = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.deal.productrows.get", ТелоHTTPЗапроса); 
		
			Если СтруктураОтветаТоваров <> Неопределено Тогда
				
				resultSku = СтруктураОтветаТоваров.Получить("result");
				Если resultSku <> Неопределено Тогда
					Если ТипЗнч(resultSku) = Тип("Массив") Тогда
						Для каждого ТоварСделки Из resultSku Цикл
							
							МодульЗагрузкаСервер = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ЗагрузкаСервер");
							Результат.Товары.Добавить(МодульЗагрузкаСервер.СформироватьДанныеДляДругогоМодуля(НастройкаПодключения, "ТоварыСделки", ТоварСделки));
							
						КонецЦикла;	
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				Результат.Ошибки.Добавить("9", "Could not get sku deal");
			КонецЕсли;

		 КонецЕсли;
		
		Прервать;
		
	КонецЦикла;
	
	Возврат Результат;	
		
КонецФункции

#КонецОбласти


#Область ФормированиеПоСмартПроцессу

Функция СформироватьДанныеПоСмартПроцессу(НастройкаПодключения, ТипВладельца, Ид) 
	
	Результат = ОписаниеДляПодстановкиВОбъекты();
	
	СтруктураНастроек 	= Б24_КБ_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		
		Результат.Ошибки.Добавить("3", "Could not connect");
		Возврат Результат;
		
	КонецЕсли;
	
	НастройкиИнтеграцииСервисов = РегистрыСведений.Б24_КБ_НастройкиИнтеграцииСервисов.ПолучитьНастройкиИнтеграцииСервисовПоНастройкеПодключения(НастройкаПодключения);
	СтруктураНастроек.Вставить("НастройкиКлиентов", НастройкиИнтеграцииСервисов.Настройки.Получить().НастройкиКлиентов);
	
	ТелоHTTPЗапроса = "&entityTypeId="+Формат(ТипВладельца, "ЧГ=0")+"&id="+Формат(Ид, "ЧГ=0") + "&start=-1";	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.item.get", ТелоHTTPЗапроса); 
	
	Если СтруктураОтвета = Неопределено Тогда
		
		Результат.Ошибки.Добавить("9", "Could not get deal");
		Возврат Результат;
		
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	
	Если result = Неопределено Тогда
		
		Результат.Ошибки.Добавить("9", "Could not get deal");
		Возврат Результат;
		
	КонецЕсли;
	
	Для каждого ТекЭлемент Из result Цикл
		
		СмартПроцесс = ТекЭлемент.Значение; 
		
		ИдКомпании    	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(СмартПроцесс.Получить("companyId"),"ЧГ=0"));
		ИдКонтакта     	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(СмартПроцесс.Получить("contactId"),"ЧГ=0"));
		
		СуммаСмартПроцесса = СмартПроцесс.Получить("opportunity");
		Если ТипЗнч(СуммаСмартПроцесса) = Тип("Число") Тогда
			Результат.Сумма	= СуммаСмартПроцесса;	
		Иначе          
			Результат.Сумма	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), СуммаСмартПроцесса);
		КонецЕсли;
		
		КодВалюты = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьКодПоВалюте(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(СмартПроцесс.Получить("currencyId"),"ЧГ=0")));
		Валюта = Справочники.Валюты.НайтиПоКоду(КодВалюты);
		Если ЗначениеЗаполнено(Валюта) Тогда
			Результат.Валюта = Валюта;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИдКомпании) И ИдКомпании <> "0" Тогда
			СформироватьКомпаниюКонтактПоИд(НастройкаПодключения, "Компания", ИдКомпании); 
			ИнформацияОИдентификаторах = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Компания, ИдКомпании);
			
			Если ИнформацияОИдентификаторах <> Неопределено Тогда
				Результат.Партнер = ИнформацияОИдентификаторах.Объект;
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(ИдКонтакта) И ИдКонтакта <> "0" Тогда
			СформироватьКомпаниюКонтактПоИд(НастройкаПодключения, "Контакт", ИдКонтакта); 
			ИнформацияОИдентификаторах = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Контакт, ИдКонтакта);
			
			Если ИнформацияОИдентификаторах <> Неопределено Тогда
				Результат.Партнер = ИнформацияОИдентификаторах.Объект;
			КонецЕсли;
			
		Иначе
			Результат.Ошибки.Добавить("15", "client not found");
			ИнформацияОКлиенте = Неопределено;
		КонецЕсли;             
		
		Если ЗначениеЗаполнено(Результат.Партнер) И НЕ ЗначениеЗаполнено(Результат.Контрагент) Тогда
			Результат.Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("Партнер", Результат.Партнер); 	
		КонецЕсли;	
		
		Если СтруктураНастроек.НастройкиИнтеграцииСервисов.ЭкспортТоваровИзСделок Тогда
			
			ИдЭлемента = Формат(Ид, "ЧГ=0");
			
			Если ТипВладельца = "31" Тогда
				ТипВладельцаСмартПроцесса16 = "SI"; 
			Иначе
				ТипВладельцаСмартПроцесса16 = "T" + Б24_К_ОбщегоНазначенияВызовСервера.КонвертацияДесятичногоЧислаВШестнадцатеричное(ТипВладельца);
			КонецЕсли;
			
			ТелоЗапросаТЧ = "&filter[%3DownerType]="+ТипВладельцаСмартПроцесса16+"&filter[%3DownerId]="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдЭлемента);  //свои особенности
			ДанныеЭлементаТЧСмартПроцесса = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/crm.item.productrow.list", "id", ТелоЗапросаТЧ, "productRows");
			Для каждого ТоварСмартПроцесса Из ДанныеЭлементаТЧСмартПроцесса Цикл
				МодульЗагрузкаСервер = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ЗагрузкаСервер");
				Результат.Товары.Добавить(МодульЗагрузкаСервер.СформироватьДанныеДляДругогоМодуля(НастройкаПодключения, "ТоварыСмартПроцесса", ТоварСмартПроцесса));
			КонецЦикла;	

		 КонецЕсли;
		
		Прервать;
		
	КонецЦикла;
	
	Возврат Результат;	
		
КонецФункции

#КонецОбласти

#КонецОбласти


#Область getAnalytics

Процедура getAnalytics(ПередаваемыеПараметры, АдресРезультата) Экспорт
	
	НастройкаПодключения 	= ПередаваемыеПараметры. НастройкаПодключения;
	Портал 					= ПередаваемыеПараметры.Портал;
	ИдСессии 				= ПередаваемыеПараметры.ИдСессии;
	ИдПользователя  		= ПередаваемыеПараметры.ИдПользователя;
	АдресСущности			= ПередаваемыеПараметры.Адрес;
	
УстановитьПривилегированныйРежим(Истина);
									
	ДобавитьНавигационнуюСсылкуИзБитрикс24(ПередаваемыеПараметры);  

	Б24_К_ОбщегоНазначенияВызовСервера.ПроверитьФоновоеЗаданиеВОчереди(ПередаваемыеПараметры);
	
КонецПроцедуры

#КонецОбласти


#Область  РаботаИзОдногоОкна

Функция ИспользуетсяРежимСлайдера() Экспорт
	
	Результат = Истина;
		
	СпособОткрытия1С = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "СпособОткрытия1С");
	
	Если СпособОткрытия1С = "Горячий старт" Тогда
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьНаличиеНавигационнойСсылкиИзБитрикс24ПоПользователюИПорталу(ИдПользователя, Портал) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдПользователя"	, ИдПользователя);
	Запрос.УстановитьПараметр("Портал"			, Портал);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_КБ_НавигационныеСсылки.Ссылка КАК Ссылка,
	|	Б24_КБ_НавигационныеСсылки.Сессия КАК Сессия,
	|	Б24_КБ_НавигационныеСсылки.Браузер КАК Браузер,
	|	Б24_КБ_НавигационныеСсылки.ИдПользователя КАК ИдПользователя,
	|	Б24_КБ_НавигационныеСсылки.Портал КАК Портал,
	|	Б24_К_НастройкиПодключения.Ссылка КАК НастройкаПодключения
	|ИЗ
	|	РегистрСведений.Б24_КБ_НавигационныеСсылки КАК Б24_КБ_НавигационныеСсылки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_КБ_НавигационныеСсылки.Портал = Б24_К_НастройкиПодключения.Портал
	|ГДЕ
	|	Б24_КБ_НавигационныеСсылки.ИдПользователя = &ИдПользователя
	|	И Б24_КБ_НавигационныеСсылки.Портал = &Портал
	|	И Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Выборка.Следующий();
		
		Результат = Новый Структура;
		
		Результат.Вставить("Ссылка"					, Выборка.Ссылка);
		Результат.Вставить("Сессия"					, Выборка.Сессия);
		Результат.Вставить("Браузер"				, Выборка.Браузер);
		Результат.Вставить("НастройкаПодключения"	, Выборка.НастройкаПодключения);
		
		Запись = РегистрыСведений.Б24_КБ_НавигационныеСсылки.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.Прочитать();
		Запись.Удалить();
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьНаличиеНавигационнойСсылкиИзБитрикс24ПоСессии(Сессия) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сессия", Сессия);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_КБ_НавигационныеСсылки.Ссылка КАК Ссылка,
	|	Б24_КБ_НавигационныеСсылки.Сессия КАК Сессия,
	|	Б24_КБ_НавигационныеСсылки.Браузер КАК Браузер,
	|	Б24_КБ_НавигационныеСсылки.ИдПользователя КАК ИдПользователя,
	|	Б24_КБ_НавигационныеСсылки.Портал КАК Портал,
	|	Б24_К_НастройкиПодключения.Ссылка КАК НастройкаПодключения
	|ИЗ
	|	РегистрСведений.Б24_КБ_НавигационныеСсылки КАК Б24_КБ_НавигационныеСсылки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_КБ_НавигационныеСсылки.Портал = Б24_К_НастройкиПодключения.Портал
	|ГДЕ
	|	Б24_КБ_НавигационныеСсылки.Сессия = &Сессия
	|	И Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Выборка.Следующий();
		
		Результат = Новый Структура;
		
		Результат.Вставить("Ссылка"					, Выборка.Ссылка);
		Результат.Вставить("Сессия"					, Выборка.Сессия);
		Результат.Вставить("Браузер"				, Выборка.Браузер);
		Результат.Вставить("НастройкаПодключения"	, Выборка.НастройкаПодключения);
		
		Запись = РегистрыСведений.Б24_КБ_НавигационныеСсылки.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.Прочитать();
		Запись.Удалить();
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьНавигационнуюСсылкуИзБитрикс24(ДанныеСПортала) Экспорт
	
	НоваяЗапись = РегистрыСведений.Б24_КБ_НавигационныеСсылки.СоздатьМенеджерЗаписи();
	НоваяЗапись.ИдПользователя 			= ДанныеСПортала.ИдПользователя;
	НоваяЗапись.Портал 					= ДанныеСПортала.Портал;
	НоваяЗапись.Ссылка 					= ДанныеСПортала.Адрес;
	НоваяЗапись.Сессия 					= ДанныеСПортала.ИдСессии;
	НоваяЗапись.Браузер 				= ДанныеСПортала.Браузер;
	НоваяЗапись.Записать();
	
КонецПроцедуры


Функция ПроверитьДанные(СессияБитрикс24) Экспорт 
	
УстановитьПривилегированныйРежим(Истина);

	Результат = Новый Структура;
	Результат.Вставить("ДанныеПолучены"			, Ложь);
	Результат.Вставить("НастройкаПодключения"	, Справочники.Б24_К_НастройкиПодключения.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СессияБитрикс24", СессияБитрикс24);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_КБ_ДанныеДляНовыхДокументовИзБитрикс24.ИдСессии КАК ИдСессии,
	|	Б24_КБ_ДанныеДляНовыхДокументовИзБитрикс24.Объект КАК Объект,
	|	Б24_КБ_ДанныеДляНовыхДокументовИзБитрикс24.ТипОбъекта КАК ТипОбъекта,
	|	Б24_КБ_ДанныеДляНовыхДокументовИзБитрикс24.Значение КАК Значение,
	|	Б24_КБ_ДанныеДляНовыхДокументовИзБитрикс24.НастройкаПодключения КАК НастройкаПодключения
	|ИЗ
	|	РегистрСведений.Б24_КБ_ДанныеДляНовыхДокументовИзБитрикс24 КАК Б24_КБ_ДанныеДляНовыхДокументовИзБитрикс24
	|ГДЕ
	|	Б24_КБ_ДанныеДляНовыхДокументовИзБитрикс24.ИдСессии = &СессияБитрикс24";
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если ЗначениеЗаполнено(Выборка.ТипОбъекта) Тогда
				
				Если Лев(Выборка.ТипОбъекта, 6) = "Товар_" Тогда
					
					Если НЕ ЗначениеЗаполнено(Выборка.Значение) Тогда
						Продолжить;
					КонецЕсли;
					
					Если НЕ Результат.Свойство("Товары") Тогда
						Результат.Вставить("Товары", Новый Массив);	
					КонецЕсли;
					
					ДанныеJSON = Б24_К_RestApiВызовСервера.ПрочитатьJSONНаСервере(Выборка.Значение, Истина);
					
					ЕдиницаИзмерения = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.УпаковкиЕдиницыИзмерения"), ДанныеJSON.Получить("ЕдиницаИзмерения"));
					
					СтруктураСтроки = Новый Структура;
					СтруктураСтроки.Вставить("Б24_К_ИдентификаторПозиции"	, ДанныеJSON.Получить("Б24_К_ИдентификаторПозиции"));
					СтруктураСтроки.Вставить("Номенклатура"					, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Номенклатура"), ДанныеJSON.Получить("Номенклатура")));
					СтруктураСтроки.Вставить("Характеристика"				, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.ХарактеристикиНоменклатуры"), ДанныеJSON.Получить("Характеристика")));
					СтруктураСтроки.Вставить("Содержание"					, ДанныеJSON.Получить("Содержание"));
					СтруктураСтроки.Вставить("Упаковка"						, ЕдиницаИзмерения);
					СтруктураСтроки.Вставить("Количество"					, ДанныеJSON.Получить("Количество"));
					СтруктураСтроки.Вставить("КоличествоУпаковок"			, ДанныеJSON.Получить("Количество"));
					СтруктураСтроки.Вставить("Цена"							, ДанныеJSON.Получить("Цена"));
					СтруктураСтроки.Вставить("СтавкаНДС"					, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.СтавкиНДС"), ДанныеJSON.Получить("СтавкаНДС")));
					СтруктураСтроки.Вставить("СуммаВключаетНДС"				, ДанныеJSON.Получить("СуммаВключаетНДС"));
					СтруктураСтроки.Вставить("ПроцентРучнойСкидки"			, ДанныеJSON.Получить("ПроцентРучнойСкидки"));
					
					Результат.Товары.Добавить(СтруктураСтроки);
					
				Иначе
					Результат.Вставить(Выборка.ТипОбъекта, ?(ЗначениеЗаполнено(Выборка.Объект), Выборка.Объект, Выборка.Значение)); 
				КонецЕсли;
			КонецЕсли;	
			
			Если Выборка.ТипОбъекта = "КонецДанных" Тогда
				Результат.ДанныеПолучены = Истина;
				Результат.НастройкаПодключения = Выборка.НастройкаПодключения;
			КонецЕсли;
			
			Запись = РегистрыСведений.Б24_КБ_ДанныеДляНовыхДокументовИзБитрикс24.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Запись.Прочитать();
			Запись.Удалить();
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Результат.Свойство("Товары") И ЗначениеЗаполнено(Результат.ИдентификаторВладельца) Тогда
		
		ТипДанных = Б24_К_RestApiВызовСервера.ПолучитьТипДанныхДелаПоТипуВладельца(Результат.ТипВладельца);
		
		мЗначенияДокументовОригиналов = Новый Массив;
		мЗначенияДокументовОригиналов.Добавить(Неопределено);
		мЗначенияДокументовОригиналов.Добавить(Документы.ЗаказКлиента.ПустаяСсылка());
		мЗначенияДокументовОригиналов.Добавить(Документы.ЗаказПоставщику.ПустаяСсылка());
		мЗначенияДокументовОригиналов.Добавить(Документы.ВозвратТоваровОтКлиента.ПустаяСсылка());
		мЗначенияДокументовОригиналов.Добавить(Документы.ВозвратТоваровПоставщику.ПустаяСсылка());
		мЗначенияДокументовОригиналов.Добавить(Документы.РеализацияТоваровУслуг.ПустаяСсылка());
		мЗначенияДокументовОригиналов.Добавить(Документы.ПриобретениеТоваровУслуг.ПустаяСсылка());
		мЗначенияДокументовОригиналов.Добавить(Документы.АктВыполненныхРабот.ПустаяСсылка());
		мЗначенияДокументовОригиналов.Добавить(Документы.КоммерческоеПредложениеКлиенту.ПустаяСсылка());
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТипДанных"		, ТипДанных);
		Запрос.УстановитьПараметр("Портал"			, Результат.НастройкаПодключения.Портал);
		Запрос.УстановитьПараметр("Идентификатор"	, Результат.ИдентификаторВладельца);
		Запрос.УстановитьПараметр("мДокументы"		, мЗначенияДокументовОригиналов); 
		Запрос.Текст = "ВЫБРАТЬ
		|	Б24_К_ИдентификаторыДел.ТипОбъекта КАК ТипОбъекта,
		|	Б24_К_ИдентификаторыДел.ИдентификаторОбъекта КАК ИдентификаторОбъекта
		|ИЗ
		|	РегистрСведений.Б24_К_ИдентификаторыДел КАК Б24_К_ИдентификаторыДел
		|ГДЕ
		|	Б24_К_ИдентификаторыДел.ТипДанных = &ТипДанных
		|	И Б24_К_ИдентификаторыДел.Портал = &Портал
		|	И Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор = &Идентификатор";
		
		ВыполненныйЗапрос = Запрос.Выполнить();
		Если ВыполненныйЗапрос.Пустой() Тогда
			мДанные = Новый Массив;  
		Иначе        
			
			мДанные = Новый Массив;  
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл   
				мДанные.Добавить(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоИмениИИдентификатору(Выборка.ТипОбъекта, Выборка.ИдентификаторОбъекта));	
			КонецЦикла;
			
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТипДанных"		, ТипДанных);
		Запрос.УстановитьПараметр("Портал"			, Результат.НастройкаПодключения.Портал);
		Запрос.УстановитьПараметр("Идентификатор"	, Результат.ИдентификаторВладельца);
		Запрос.УстановитьПараметр("мДокументы"		, мЗначенияДокументовОригиналов);
		Запрос.УстановитьПараметр("мДанные"			, мДанные);
		Запрос.Текст = "ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
		|	ЗаказКлиентаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ ВТ_ТоварыДокументов
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
		|		ПО (ЗаказКлиента.Ссылка = ЗаказКлиентаТовары.Ссылка)
		|ГДЕ
		|	ЗаказКлиента.Ссылка В(&мДанные)
		|	И ЗаказКлиента.ДокументОснование В(&мДокументы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КоммерческоеПредложениеКлиентуТовары.Номенклатура,
		|	КоммерческоеПредложениеКлиентуТовары.Характеристика,
		|	КоммерческоеПредложениеКлиентуТовары.Количество
		|ИЗ
		|	Документ.КоммерческоеПредложениеКлиенту.Товары КАК КоммерческоеПредложениеКлиентуТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КоммерческоеПредложениеКлиенту КАК КоммерческоеПредложениеКлиенту
		|		ПО КоммерческоеПредложениеКлиентуТовары.Ссылка = КоммерческоеПредложениеКлиенту.Ссылка
		|ГДЕ
		|	КоммерческоеПредложениеКлиенту.Ссылка В(&мДанные)
		|	И КоммерческоеПредложениеКлиенту.ДокументОснование В(&мДокументы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказПоставщикуТовары.Номенклатура,
		|	ЗаказПоставщикуТовары.Характеристика,
		|	ЗаказПоставщикуТовары.Количество
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК ЗаказПоставщику
		|		ПО ЗаказПоставщикуТовары.Ссылка = ЗаказПоставщику.Ссылка
		|ГДЕ
		|	ЗаказПоставщику.Ссылка В(&мДанные)
		|	И ЗаказПоставщику.ДокументОснование В(&мДокументы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровОтКлиентаТовары.Номенклатура,
		|	ВозвратТоваровОтКлиентаТовары.Характеристика,
		|	ВозвратТоваровОтКлиентаТовары.Количество
		|ИЗ
		|	Документ.ВозвратТоваровОтКлиента.Товары КАК ВозвратТоваровОтКлиентаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента
		|		ПО ВозвратТоваровОтКлиентаТовары.Ссылка = ВозвратТоваровОтКлиента.Ссылка
		|ГДЕ
		|	ВозвратТоваровОтКлиента.Ссылка В(&мДанные)
		|	И ВозвратТоваровОтКлиента.ДокументРеализации В(&мДокументы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровПоставщикуТовары.Номенклатура,
		|	ВозвратТоваровПоставщикуТовары.Характеристика,
		|	ВозвратТоваровПоставщикуТовары.Количество
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику.Товары КАК ВозвратТоваровПоставщикуТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|		ПО ВозвратТоваровПоставщикуТовары.Ссылка = ВозвратТоваровПоставщику.Ссылка
		|ГДЕ
		|	ВозвратТоваровПоставщику.Ссылка В(&мДанные)
		|	И ВозвратТоваровПоставщику.ДокументПоступления В(&мДокументы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализацияТоваровУслугТовары.Номенклатура,
		|	РеализацияТоваровУслугТовары.Характеристика,
		|	РеализацияТоваровУслугТовары.Количество
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ПО РеализацияТоваровУслугТовары.Ссылка = РеализацияТоваровУслуг.Ссылка
		|ГДЕ
		|	РеализацияТоваровУслуг.Ссылка В(&мДанные)
		|	И РеализацияТоваровУслуг.ЗаказКлиента В(&мДокументы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АктВыполненныхРаботУслуги.Номенклатура,
		|	АктВыполненныхРаботУслуги.Характеристика,
		|	АктВыполненныхРаботУслуги.Количество
		|ИЗ
		|	Документ.АктВыполненныхРабот.Услуги КАК АктВыполненныхРаботУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктВыполненныхРабот КАК АктВыполненныхРабот
		|		ПО АктВыполненныхРаботУслуги.Ссылка = АктВыполненныхРабот.Ссылка
		|ГДЕ
		|	АктВыполненныхРабот.Ссылка В(&мДанные)
		|	И АктВыполненныхРабот.ЗаказКлиента В(&мДокументы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугТовары.Номенклатура,
		|	ПриобретениеТоваровУслугТовары.Характеристика,
		|	ПриобретениеТоваровУслугТовары.Количество
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
		|		ПО ПриобретениеТоваровУслугТовары.Ссылка = ПриобретениеТоваровУслуг.Ссылка
		|ГДЕ
		|	ПриобретениеТоваровУслуг.Ссылка В(&мДанные)
		|	И ПриобретениеТоваровУслуг.ЗаказПоставщику В(&мДокументы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТоварыДокументов.Номенклатура КАК Номенклатура,
		|	ВТ_ТоварыДокументов.Характеристика КАК Характеристика,
		|	СУММА(ВТ_ТоварыДокументов.Количество) КАК Количество
		|ИЗ
		|	ВТ_ТоварыДокументов КАК ВТ_ТоварыДокументов
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ТоварыДокументов.Номенклатура,
		|	ВТ_ТоварыДокументов.Характеристика";
		ВыполненныйЗапрос = Запрос.Выполнить();
		
		Если НЕ ВыполненныйЗапрос.Пустой() Тогда
			
			тзнДанных = ВыполненныйЗапрос.Выгрузить();
			Для каждого ТекСтрока Из тзнДанных Цикл 
				Для каждого ТекЭлемент Из Результат.Товары Цикл
					Если ТекЭлемент.Номенклатура = ТекСтрока.Номенклатура И ТекЭлемент.Характеристика = ТекСтрока.Характеристика Тогда
						
						ПеременнаяУсловия = ТекЭлемент.Количество - ТекСтрока.Количество;
						Если ПеременнаяУсловия < 0 Тогда
							ТекСтрока.Количество 	= ТекСтрока.Количество - ТекЭлемент.Количество;		
							ТекЭлемент.Количество 			= 0;
							ТекЭлемент.КоличествоУпаковок 	= 0;
						Иначе
							ТекСтрока.Количество 			= 0;
							ТекЭлемент.Количество 			= ПеременнаяУсловия;
							ТекЭлемент.КоличествоУпаковок 	= ПеременнаяУсловия;
						КонецЕсли;
						
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;
		
		НовыйМассивТоваров = Новый Массив;
		Для каждого ТекЭлемент Из Результат.Товары Цикл
			Если ТекЭлемент.Количество > 0 Тогда
				НовыйМассивТоваров.Добавить(ТекЭлемент);
			КонецЕсли;
		КонецЦикла;
		Результат.Товары = НовыйМассивТоваров;
		
	КонецЕсли;
	
	Возврат Результат; 
	
КонецФункции

Процедура ЗакрытиеСлайдера(ИдСессии, НастройкаПодключения) Экспорт
	
	Если НЕ ЗначениеЗаполнено(НастройкаПодключения) Тогда
		Возврат;
	КонецЕсли;
	
УстановитьПривилегированныйРежим(Истина);
	
	СтруктураНастроек = Б24_КБ_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТелоЗапроса = "&COMMAND=close&MODULE_ID=1ctotal&PARAMS[session]="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдСессии);
	Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/pull.application.event.add", ТелоЗапроса);
	
КонецПроцедуры

Процедура ПривязатьОбъектИнтеграцииСервисовКНастройкеПодключения(НастройкаПодключения, ТипВладельца, Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НоваяЗапись = РегистрыСведений.Б24_КБ_ОбъектыСозданныеЧерезСлайдер.СоздатьМенеджерЗаписи();
	НоваяЗапись.ТипОбъекта 				= Объект.Метаданные().ПолноеИмя();
	НоваяЗапись.ИдентификаторОбъекта 	= XMLСтрока(Объект);
	НоваяЗапись.ТипДанных 				= Б24_К_RestApiВызовСервера.ПолучитьТипДанныхДелаПоТипуВладельца(ТипВладельца);
	НоваяЗапись.НастройкаПодключения	= НастройкаПодключения; 
	НоваяЗапись.Записать();
	
КонецПроцедуры


Процедура ВыгрузитьТоварыДокументовВСделкуСмартПроцесс(НастройкаПодключения, ДокументНовый, Документ, ТипВладельца, ИдентификаторВладельца) Экспорт
	
УстановитьПривилегированныйРежим(Истина);
	
	Если ДокументНовый И НЕ ЗначениеЗаполнено(ИдентификаторВладельца) Тогда        // Документ не является прородителем
		Возврат;
	КонецЕсли;  

	ИмпортТоваровВСделки = РегистрыСведений.Б24_КБ_НастройкиИнтеграцииСервисов.ПолучитьСохраненнуюНастройкуИнтеграцииСервисовПоНастройкеПодключения(НастройкаПодключения, "ИмпортТоваровВСделки", Ложь);  
	
	Если НЕ ИмпортТоваровВСделки Тогда
		Возврат;
	КонецЕсли;  
	
УстановитьПривилегированныйРежим(Истина);

	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыЗапроса.Вставить("ДокументНовый"			, ДокументНовый);
	ПараметрыЗапроса.Вставить("Документ"				, Документ);
	ПараметрыЗапроса.Вставить("ИдентификаторВладельца"	, ИдентификаторВладельца);
	ПараметрыЗапроса.Вставить("ТипВладельца"			, ТипВладельца);              
	
	Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_КБ_РаботаИзОдногоОкнаВызовСервера.ВыгрузитьТоварыДокументовВСделкуСмартПроцессВФоне", "Обновление информации на портале", ПараметрыЗапроса, 3); 

КонецПроцедуры

Процедура ВыгрузитьТоварыДокументовВСделкуСмартПроцессВФоне(ПередаваемыеПараметры, АдресРезультата) Экспорт
	
	Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(1);
	
	НастройкаПодключения 	= ПередаваемыеПараметры.НастройкаПодключения;
	ДокументНовый 			= ПередаваемыеПараметры.ДокументНовый;
	Документ 				= ПередаваемыеПараметры.Документ;
	ИдентификаторВладельца 	= ПередаваемыеПараметры.ИдентификаторВладельца;
	ТипВладельца 			= ПередаваемыеПараметры.ТипВладельца;
	ТипДелаПоВладельцу		= Б24_К_RestApiВызовСервера.ПолучитьТипДанныхДелаПоТипуВладельца(ТипВладельца);
	
	Если ДокументНовый И НЕ ЗначениеЗаполнено(ИдентификаторВладельца) Тогда
		Перейти ~КонецПроцедурыВФоне;
	КонецЕсли;	
	
	Если НЕ ДокументНовый Тогда
		Идентификатор = "SKIP";	 // Чтобы случайно не засчитался ""
	Иначе
		Идентификатор = ИдентификаторВладельца;	
	КонецЕсли;	
	
	ТипыДанных = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();       
	мТипыДанных = Новый Массив;
	мТипыДанных.Добавить(ТипыДанных.ДелоСделки);
	мТипыДанных.Добавить(ТипыДанных.ДелоСчета);
	мТипыДанных.Добавить(ТипыДанных.ДелоСмартПроцесса);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("мТипыДанных"			, мТипыДанных);
	Запрос.УстановитьПараметр("Портал"	 			, НастройкаПодключения.Портал);
	Запрос.УстановитьПараметр("Документ"	 		, Документ);
	Запрос.УстановитьПараметр("ТипОбъекта"			, Документ.Метаданные().ПолноеИмя());
	Запрос.УстановитьПараметр("ИдентификаторОбъекта", XMLСтрока(Документ));
	Запрос.УстановитьПараметр("Идентификатор"		, Идентификатор);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор,
	|	Б24_К_ИдентификаторыДел.ТипДанных КАК ТипДанных,
	|	Б24_К_ИдентификаторыДел.Портал КАК Портал,
	|	Б24_К_НастройкиПодключения.Ссылка КАК НастройкаПодключения,
	|	Б24_К_ИдентификаторыДел.ИдентификаторОбъекта КАК ИдентификаторОбъекта
	|ПОМЕСТИТЬ ВТ_ИдентификаторыПоДокументу
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыДел КАК Б24_К_ИдентификаторыДел
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_К_ИдентификаторыДел.Портал = Б24_К_НастройкиПодключения.Портал
	|ГДЕ
	|	Б24_К_ИдентификаторыДел.ТипДанных В(&мТипыДанных)
	|	И Б24_К_ИдентификаторыДел.ТипОбъекта = &ТипОбъекта
	|	И Б24_К_ИдентификаторыДел.ИдентификаторОбъекта = &ИдентификаторОбъекта
	|	И Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор,
	|	Б24_К_ИдентификаторыДел.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыДел.ТипДанных КАК ТипДанных,
	|	Б24_К_ИдентификаторыДел.Портал КАК Портал,
	|	Б24_К_НастройкиПодключения.Ссылка КАК НастройкаПодключения
	|ПОМЕСТИТЬ ВТ_ИдентификаторыПоИдВладельца
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыДел КАК Б24_К_ИдентификаторыДел
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_К_ИдентификаторыДел.Портал = Б24_К_НастройкиПодключения.Портал
	|ГДЕ
	|	Б24_К_ИдентификаторыДел.ТипДанных В(&мТипыДанных)
	|	И Б24_К_ИдентификаторыДел.Портал = &Портал
	|	И Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор = &Идентификатор
	|	И Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор,
	|	ВТ_ИдентификаторыПоДокументу.НастройкаПодключения КАК НастройкаПодключения,
	|	Б24_К_ИдентификаторыДел.ТипОбъекта КАК ТипОбъекта,
	|	Б24_К_ИдентификаторыДел.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
	|	Б24_К_ИдентификаторыДел.ТипДанных КАК ТипДела,
	|	Б24_К_ИдентификаторыДел.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыДел КАК Б24_К_ИдентификаторыДел
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыПоДокументу КАК ВТ_ИдентификаторыПоДокументу
	|		ПО Б24_К_ИдентификаторыДел.ТипДанных = ВТ_ИдентификаторыПоДокументу.ТипДанных
	|			И Б24_К_ИдентификаторыДел.Портал = ВТ_ИдентификаторыПоДокументу.Портал
	|			И Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор = ВТ_ИдентификаторыПоДокументу.ДополнительныйИдентификатор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор,
	|	ВТ_ИдентификаторыПоИдВладельца.НастройкаПодключения,
	|	Б24_К_ИдентификаторыДел.ТипОбъекта,
	|	Б24_К_ИдентификаторыДел.ИдентификаторОбъекта,
	|	Б24_К_ИдентификаторыДел.ТипДанных,
	|	Б24_К_ИдентификаторыДел.Идентификатор
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыДел КАК Б24_К_ИдентификаторыДел
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыПоИдВладельца КАК ВТ_ИдентификаторыПоИдВладельца
	|		ПО Б24_К_ИдентификаторыДел.ТипДанных = ВТ_ИдентификаторыПоИдВладельца.ТипДанных
	|			И Б24_К_ИдентификаторыДел.Портал = ВТ_ИдентификаторыПоИдВладельца.Портал
	|			И Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор = ВТ_ИдентификаторыПоИдВладельца.ДополнительныйИдентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Данные.ТипОбъекта КАК ТипОбъекта,
	|	ВТ_Данные.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
	|	ВТ_Данные.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор,
	|	ВТ_Данные.НастройкаПодключения КАК НастройкаПодключения,
	|	ВТ_Данные.ТипДела КАК ТипДела,
	|	ВТ_Данные.Идентификатор КАК Идентификатор
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Данные.НастройкаПодключения,
	|	ВТ_Данные.ДополнительныйИдентификатор,
	|	ВТ_Данные.ТипОбъекта,
	|	ВТ_Данные.ТипОбъекта,
	|	ВТ_Данные.ИдентификаторОбъекта,
	|	ВТ_Данные.ТипДела,
	|	ВТ_Данные.Идентификатор";
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	мДокументы = Новый Массив;

	лДокумент = Неопределено;
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл     

			
			НайденныйОбъектСсылка  = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоИмениИИдентификатору(Выборка.ТипОбъекта, Выборка.ИдентификаторОбъекта);	
			Если ЗначениеЗаполнено(НайденныйОбъектСсылка) Тогда
				
				ОписаниеДанных = Новый Структура;
				ОписаниеДанных.Вставить("НастройкаПодключения"	, Выборка.НастройкаПодключения);
				ОписаниеДанных.Вставить("ТипДела"				, Выборка.ТипДела);
				ОписаниеДанных.Вставить("Документ"				, НайденныйОбъектСсылка);
				ОписаниеДанных.Вставить("Идентификатор"			, Выборка.Идентификатор);
				ОписаниеДанных.Вставить("ДопИдентификатор"		, Выборка.ДополнительныйИдентификатор);
				
				мДокументы.Добавить(ОписаниеДанных);
				
				Если Документ = НайденныйОбъектСсылка Тогда
					лДокумент 				= НайденныйОбъектСсылка;
					ИдентификаторВладельца 	= ?(ЗначениеЗаполнено(ИдентификаторВладельца), ИдентификаторВладельца, Выборка.ДополнительныйИдентификатор);
				КонецЕсли; 
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторВладельца) И НЕ ЗначениеЗаполнено(лДокумент) Тогда   
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("НастройкаПодключения"	, НастройкаПодключения);
		ОписаниеДанных.Вставить("ТипДела"				, ТипДелаПоВладельцу);
		ОписаниеДанных.Вставить("Документ"				, Документ);
		ОписаниеДанных.Вставить("Идентификатор"			, ТипВладельца + "_" + ИдентификаторВладельца);
		ОписаниеДанных.Вставить("ДопИдентификатор"		, ИдентификаторВладельца);
		
		мДокументы.Добавить(ОписаниеДанных);   
		
	КонецЕсли;
	
	ДокументыОригиналы = Новый Массив;
	Для каждого ТекДокумент Из мДокументы Цикл
		мДокументыОснования = Б24_К_ВыгрузкаДелВызовСервера.ПолучитьДокументыОснования(ТекДокумент.Документ);	
		Если мДокументыОснования.Количество() = 0 Тогда ДокументыОригиналы.Добавить(ТекДокумент) КонецЕсли; 
	КонецЦикла;
	
	Б24_КС_ВыгрузкаСервер.ВыгрузитьТоварыДокументовВСделкуСмартПроцесс(ДокументыОригиналы);
	
~КонецПроцедурыВФоне:	
	
	Б24_К_ОбщегоНазначенияВызовСервера.ПроверитьФоновоеЗаданиеВОчереди(ПередаваемыеПараметры);
	
КонецПроцедуры

Функция ПолучитьКонтрагентаПоПартнеру(Партнер) Экспорт

	Возврат Справочники.Контрагенты.НайтиПоРеквизиту("Партнер", Партнер);

КонецФункции


Функция ПроверитьНаличиеРеквизита(ВидОбъекта, ИмяОбъекта, Реквизит) Экспорт  
	
	Если ВидОбъекта = "Документ" Тогда
		Реквизиты = Метаданные.Документы[ИмяОбъекта].Реквизиты;
	Иначе  
		Реквизиты = Метаданные.Справочники[ИмяОбъекта].Реквизиты;
	КонецЕсли;
	
	Возврат Реквизиты.Найти(Реквизит) <> Неопределено;
	
КонецФункции

Функция ПроверитьНаличиеТабличнойЧасти(ВидОбъекта, ИмяОбъекта, ТабличнаяЧасть) Экспорт  
	
	Если ВидОбъекта = "Документ" Тогда
		ТабличныеЧасти = Метаданные.Документы[ИмяОбъекта].ТабличныеЧасти;
	Иначе  
		ТабличныеЧасти = Метаданные.Справочники[ИмяОбъекта].ТабличныеЧасти;
	КонецЕсли;
	
	Возврат ТабличныеЧасти.Найти(ТабличнаяЧасть) <> Неопределено;
	
КонецФункции

Функция ПроверитьНаличиеРеквизитаТЧ(ВидОбъекта, ИмяОбъекта, ТабличнаяЧасть, Реквизит) Экспорт  

	Если ВидОбъекта = "Документ" Тогда
		ТабличныеЧасти = Метаданные.Документы[ИмяОбъекта].ТабличныеЧасти;
	Иначе  
		ТабличныеЧасти = Метаданные.Справочники[ИмяОбъекта].ТабличныеЧасти;
	КонецЕсли;
	
	Если ТабличныеЧасти.Найти(ТабличнаяЧасть) <> Неопределено Тогда
		Реквизиты = ТабличныеЧасти.Найти(ТабличнаяЧасть).Реквизиты;
		Возврат Реквизиты.Найти(Реквизит) <> Неопределено;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

