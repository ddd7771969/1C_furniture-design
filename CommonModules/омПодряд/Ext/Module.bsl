
Процедура СоздатьЗаписьЧастичногоПодряда(СтруктураПараметров) Экспорт

    МенеджерЗаписи = РегистрыСведений.ЧастичныйПодряд.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураПараметров);
	МенеджерЗаписи.Период = ТекущаяДата();
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Функция СтруктураПараметровДляЗаписиЧастичногоПодряда() Экспорт

	СтруктураПараметров = Новый Структура();
	
	СтруктураПараметров.Вставить("ИзделиеКомплект", Справочники.ИзделияКомплект.ПустаяСсылка());
	СтруктураПараметров.Вставить("Автор", 			ПараметрыСеанса.ТекущийПользователь);
	СтруктураПараметров.Вставить("ДатаВозврата", 	Дата(1, 1, 1));
	СтруктураПараметров.Вставить("Комментарий", 	"");
	СтруктураПараметров.Вставить("Подрядчик", 		Справочники.Контрагенты.ПустаяСсылка());
	СтруктураПараметров.Вставить("Проект", 			Справочники.Проекты.ПустаяСсылка());

	Возврат СтруктураПараметров;

КонецФункции // ПолучитьСтруктуруПараметровДляЗаписиЧастичногоПодряда()

Функция ПолучитьДанныеЧастичногоПодряда(Проект = Неопределено, Подрядчик = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЧастичныйПодрядСрезПоследних.ИзделиеКомплект КАК ИзделиеКомплект,
		|	ЧастичныйПодрядСрезПоследних.Автор КАК Автор,
		|	ЧастичныйПодрядСрезПоследних.ДатаВозврата КАК ДатаВозврата,
		|	ЧастичныйПодрядСрезПоследних.Комментарий КАК Комментарий,
		|	ЧастичныйПодрядСрезПоследних.Подрядчик КАК Подрядчик,
		|	ЧастичныйПодрядСрезПоследних.Проект КАК Проект
		|ИЗ
		|	РегистрСведений.ЧастичныйПодряд.СрезПоследних КАК ЧастичныйПодрядСрезПоследних
		|ГДЕ
		|	НЕ ЧастичныйПодрядСрезПоследних.Завершен
		|	И ВЫБОР
		|			КОГДА &Проект = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЧастичныйПодрядСрезПоследних.Проект = &Проект
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Подрядчик = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЧастичныйПодрядСрезПоследних.Подрядчик = &Подрядчик
		|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("Подрядчик", 	?(ЗначениеЗаполнено(Подрядчик), Подрядчик, 	Неопределено));
	Запрос.УстановитьПараметр("Проект", 	?(ЗначениеЗаполнено(Проект), 	Проект, 	Неопределено));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьДанныеЧастичногоПодряда(Проект) Экспорт
