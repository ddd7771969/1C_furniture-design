		  
#Область ПолучениеНастроек

Функция ПолучитьМассивНастроекВыгрузки(КлючНастройки = "", ЗначениеНастройки = "") Экспорт
	
	Запрос = Новый Запрос;
	Если НЕ КлючНастройки = "" Тогда
		Запрос.УстановитьПараметр("ПараметрПоиска", ЗначениеНастройки);
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СУ_Настройки.НастройкаПодключения КАК НастройкаПодключения
	|ИЗ
	|	РегистрСведений.Б24_СУ_Настройки КАК Б24_СУ_Настройки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_СУ_Настройки.НастройкаПодключения = Б24_К_НастройкиПодключения.Ссылка
	|ГДЕ
	|	Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ";
	
	Если НЕ КлючНастройки = "" Тогда
		Запрос.Текст = Запрос.Текст + "
		|" + " И Б24_СУ_Настройки."+КлючНастройки+" = &ПараметрПоиска";
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("НастройкаПодключения");
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьМассивНастроекОВыгружаемыхДанных() Экспорт
	
	Результат = Новый Массив;
	
	мНастройкиПодключения = Б24_СУ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьМассивНастроекВыгрузки();
	Для Каждого НастройкаПодключения Из мНастройкиПодключения Цикл
		
		НастройкиВыгрузки = Б24_СУ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНастройкуПоНастройкеПодключения(НастройкаПодключения);
		
		СохраненныеНастройки = НастройкиВыгрузки.Настройки.Получить();
		Если СохраненныеНастройки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("НастройкаПодключения"	, НастройкаПодключения);
		СтруктураДанных.Вставить("Портал"				, НастройкаПодключения.Портал);
		СтруктураДанных.Вставить("ВыгружатьЦены"		, Ложь);  
		СтруктураДанных.Вставить("Прайсы"				, Новый Массив);
		СтруктураДанных.Вставить("СпособВыгрузкиЦен"	, "");
		СтруктураДанных.Вставить("ВыгружатьОстатки"		, Ложь);
		СтруктураДанных.Вставить("Склады"				, Новый Массив);
		СтруктураДанных.Вставить("СпособВыгрузкиОстатков", "");
		СтруктураДанных.Вставить("ВыгружатьОтгрузки"	, Ложь);
		СтруктураДанных.Вставить("ВыгружатьКартинки"	, Ложь);
		СтруктураДанных.Вставить("СпособВыгрузкиКартинок", "");   
		
		Если СохраненныеНастройки.Свойство("НастройкииЗаказов") Тогда
			СтруктураДанных.ВыгружатьОтгрузки = СохраненныеНастройки.НастройкииЗаказов.ОтгружатьТовары И СохраненныеНастройки.НастройкииЗаказов.РезервироватьТовары;
		КонецЕсли;
		
		Если СохраненныеНастройки.Свойство("НастройкиТоваров") Тогда
			СтруктураДанных.ВыгружатьКартинки = СохраненныеНастройки.НастройкиТоваров.ВыгружатьКартинки;  
			СтруктураДанных.СпособВыгрузкиКартинок 	= СохраненныеНастройки.НастройкиТоваров.СпособВыгрузки;  
		КонецЕсли;
		
		Если СохраненныеНастройки.Свойство("НастройкиЦен") Тогда
			СтруктураДанных.Прайсы 				= СохраненныеНастройки.НастройкиЦен.Прайсы;  
			СтруктураДанных.ВыгружатьЦены 		= СтруктураДанных.Прайсы.Количество() > 0;   
			СтруктураДанных.СпособВыгрузкиЦен 	= СохраненныеНастройки.НастройкиЦен.СпособВыгрузки;  
		КонецЕсли;
		
		Если СохраненныеНастройки.Свойство("НастройкиОстатков") Тогда
			СтруктураДанных.Склады 					= СохраненныеНастройки.НастройкиОстатков.Склады;  
			СтруктураДанных.ВыгружатьОстатки 		= СтруктураДанных.Склады.Количество() > 0;  
			СтруктураДанных.СпособВыгрузкиОстатков 	= СохраненныеНастройки.НастройкиОстатков.СпособВыгрузки;  
		КонецЕсли;
		
		Результат.Добавить(СтруктураДанных);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкуПоНастройкеПодключения(НастройкаПодключения) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НастройкаПодключения"					, Справочники.Б24_К_НастройкиПодключения.ПустаяСсылка());
	Результат.Вставить("КоличествоПовторенийПриОшибках"			, 3);
	Результат.Вставить("Настройки"								, Новый ХранилищеЗначения(Неопределено));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СУ_Настройки.НастройкаПодключения КАК НастройкаПодключения,
	|	Б24_СУ_Настройки.КоличествоПовторенийПриОшибках КАК КоличествоПовторенийПриОшибках,
	|	Б24_СУ_Настройки.Настройки КАК Настройки
	|ИЗ
	|	РегистрСведений.Б24_СУ_Настройки КАК Б24_СУ_Настройки
	|ГДЕ
	|	Б24_СУ_Настройки.НастройкаПодключения = &НастройкаПодключения";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ЗаполнитьЗначенияСвойств(Результат, Выборка);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Результат.Настройки.Получить() = Неопределено Тогда
		Результат.Настройки = Новый ХранилищеЗначения(РегистрыСведений.Б24_СУ_Настройки.ПолучитьНастройкиПоУмолчанию());	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьЗначениеНастройкиПоНастройкеПодключения(НастройкаПодключения, ИмяНастройки, Родитель = Неопределено) Экспорт
	
	НастройкиПоНастройкеПодключения = Б24_СУ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНастройкуПоНастройкеПодключения(НастройкаПодключения);
	
	Настройки = НастройкиПоНастройкеПодключения.Настройки.Получить();
	
	Попытка
		Если Родитель <> Неопределено Тогда
			Результат = Настройки[Родитель][ИмяНастройки];
		Иначе
			Результат = Настройки[ИмяНастройки];
		КонецЕсли;    
	Исключение
		Результат = Неопределено;	
	КонецПопытки;     
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


