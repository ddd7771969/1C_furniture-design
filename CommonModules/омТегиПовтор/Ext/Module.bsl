Функция ПолучитьТаблицуТегов(мТеги = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Теги.Ссылка КАК Ссылка,
		|	Теги.Текст КАК Текст,
		|	Теги.ЦветТекста КАК ЦветТекста,
		|	Теги.ЦветФона КАК ЦветФона
		|ИЗ
		|	Справочник.Теги КАК Теги
		|ГДЕ
		|	НЕ Теги.ПометкаУдаления
		|	И ВЫБОР
		|			КОГДА &ИспользоватьТеги
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Теги.Ссылка В (&мТеги)
		|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("ИспользоватьТеги", ?(мТеги = Неопределено, Ложь, Истина));
	Запрос.УстановитьПараметр("мТеги", мТеги);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьТаблицуТегов()

Функция ПолучитьТегКомментарий() Экспорт

	ОбъектОтчета = Отчеты.ОбщиеНастройки.Создать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиПроекта.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.НастройкиПроекта КАК НастройкиПроекта
		|ГДЕ
		|	НастройкиПроекта.ИмяНастройки = &ИмяНастройки";
	
	Запрос.УстановитьПараметр("ИмяНастройки", ОбъектОтчета.НаименованиеКолонкиДляТегаКомментарий());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл

		Возврат Выборка.Значение;
		
	КонецЦикла;
	
	Возврат Справочники.Теги.ПустаяСсылка();

КонецФункции // ПолучитьТегКомментарий()

Функция ПолучитьДанныеТегаКомментарий()

	стВозврата = Новый Структура("Текст, ЦветТекста, ЦветФона", );
	
	ОбъектОтчета = Отчеты.ОбщиеНастройки.Создать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиПроекта.Значение КАК Значение
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрСведений.НастройкиПроекта КАК НастройкиПроекта
		|ГДЕ
		|	НастройкиПроекта.ИмяНастройки = &ИмяНастройки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Теги.Текст КАК Текст,
		|	Теги.ЦветТекста КАК ЦветТекста,
		|	Теги.ЦветФона КАК ЦветФона
		|ИЗ
		|	вт КАК вт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Теги КАК Теги
		|		ПО вт.Значение = Теги.Ссылка";
	
	Запрос.УстановитьПараметр("ИмяНастройки", ОбъектОтчета.НаименованиеКолонкиДляТегаКомментарий());
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	Если Выборка.Количество() = 0 Тогда
	
		стВозврата.Текст 		= "";
		стВозврата.ЦветТекста 	= Новый Цвет(0, 0, 0);
		стВозврата.ЦветФона 	= Новый Цвет(200, 255, 200);
	
	Иначе
		
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(стВозврата, Выборка);
	
	КонецЕсли;
	
	Возврат стВозврата;
	
КонецФункции // ПолучитьДанныеТегаКомментарий()
