//ОбъектДанных композиция комплект
Функция ПолучитьДанныеОбъекта(ОбъектДанных, КонструкторПользователь = Неопределено) Экспорт
	
	стВозврата = Новый Структура("КонструкторПользователь, КонструкторСотрудник, ТекущаяСтадия", );
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеКомплектаСрезПоследних.Конструктор КАК КонструкторСотрудник,
		|	Пользователи.Ссылка КАК КонструкторПользователь,
		|	ДанныеКомплектаСрезПоследних.ТекущаяСтадия КАК ТекущаяСтадия
		|ИЗ
		|	РегистрСведений.ДанныеКомплекта.СрезПоследних КАК ДанныеКомплектаСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ДанныеКомплектаСрезПоследних.Конструктор = Пользователи.Сотрудник
		|ГДЕ
		|	ДанныеКомплектаСрезПоследних.Комплект = &ОбъектДанных
		|	И НЕ ДанныеКомплектаСрезПоследних.Конструктор = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ОбъектДанных", ОбъектДанных);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(стВозврата, Выборка);
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(стВозврата.КонструкторСотрудник) И ЗначениеЗаполнено(КонструкторПользователь) Тогда
	
		КонструкторСотрудник = омСотрудники.ПолучитьСотруднткаПоПользователю(КонструкторПользователь);
		ИзменитьКонструктораНаСервереОдинОбъект(ОбъектДанных, КонструкторСотрудник);
		
		стВозврата.КонструкторСотрудник 	= КонструкторСотрудник;
		стВозврата.КонструкторПользователь 	= КонструкторПользователь;
	
	КонецЕсли;
	
	Возврат стВозврата;
	
КонецФункции // ПолучитьДанныеОбъекта()

Процедура ИзменитьКонструктораНаСервере(ОбъектТЗ, Конструктор) Экспорт

	Если ТипЗнч(ОбъектТЗ) = тип("Массив") Тогда
		
		Для каждого х Из ОбъектТЗ Цикл
			
			ИзменитьКонструктораНаСервереОдинОбъект(х, Конструктор);
			
		КонецЦикла;
		
	Иначе
		
		ИзменитьКонструктораНаСервереОдинОбъект(ОбъектТЗ, Конструктор);
		
	КонецЕсли;
	
	
КонецПроцедуры // ИзменитьКонструктораНаСервере()

Процедура ИзменитьКонструктораНаСервереОдинОбъект(ОбъектТЗ, Конструктор)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ *
		|ИЗ
		|	РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект = &ОбъектТЗ) КАК ДанныеКомплектаСрезПоследних";
	
	Запрос.УстановитьПараметр("ОбъектТЗ", ОбъектТЗ);
	
	МенеджерЗаписи 			= РегистрыСведений.ДанныеКомплекта.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Комплект = ОбъектТЗ; 
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
	КонецЦикла;
	
	МенеджерЗаписи.Период 		= ТекущаяДата(); 
	МенеджерЗаписи.Автор 		= ПараметрыСеанса.ТекущийПользователь;
	МенеджерЗаписи.Конструктор 	= Конструктор;
	МенеджерЗаписи.Комплект 	= ОбъектТЗ; 
	
    МенеджерЗаписи.Записать();
	
КонецПроцедуры // ИзменитьКонструктораНаСервере()

Функция ВозможностьИзменитьКонструктора(ОбъектТЗ) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Задачи_РКД.Ссылка КАК Задачи_РКД
		|ИЗ
		|	Задача.Задачи_РКД КАК Задачи_РКД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймингЗадач.СрезПоследних КАК ТаймингЗадачСрезПоследних
		|		ПО Задачи_РКД.Ссылка = ТаймингЗадачСрезПоследних.Задача
		|ГДЕ
		|	Задачи_РКД.ОбъектТЗ = &ОбъектТЗ
		|	И Задачи_РКД.ТипЗадачи В(&ТипЗадачи)
		|	И НЕ Задачи_РКД.Выполнена
		|	И НЕ ТаймингЗадачСрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЗадач.ОжиданиеКонструктором)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Задачи_ПКД.Ссылка КАК Задачи_ПКД
		|ИЗ
		|	Задача.Задачи_ПКД КАК Задачи_ПКД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймингЗадач.СрезПоследних КАК ТаймингЗадачСрезПоследних
		|		ПО Задачи_ПКД.Ссылка = ТаймингЗадачСрезПоследних.Задача
		|ГДЕ
		|	Задачи_ПКД.ОбъектТЗ = &ОбъектТЗ
		|	И Задачи_ПКД.ТипЗадачи В(&ТипЗадачи)
		|	И НЕ Задачи_ПКД.Выполнена
		|	И НЕ ТаймингЗадачСрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЗадач.ОжиданиеКонструктором)";
	
	Запрос.УстановитьПараметр("ОбъектТЗ", ОбъектТЗ);
	Запрос.УстановитьПараметр("ТипЗадачи", омЗадачиПКДПовтор.ТипыЗадачКонструктора());
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[0].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Задачи_РКД;	
	КонецЦикла;
	
	Выборка = Результат[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Задачи_ПКД;	
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции // ВозможностьИзменитьКонструктора()

Процедура ИзменитьИсполнителяВЗадаче(ОбъектТЗ, Конструктор) Экспорт
	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Задачи_ПКД.Ссылка КАК Задача,
		|	ЕСТЬNULL(ТаймингЗадачСрезПоследних.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияЗадач.ОжиданиеКонструктором)) КАК Состояние
		|ИЗ
		|	Задача.Задачи_ПКД КАК Задачи_ПКД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймингЗадач.СрезПоследних КАК ТаймингЗадачСрезПоследних
		|		ПО Задачи_ПКД.Ссылка = ТаймингЗадачСрезПоследних.Задача
		|ГДЕ
		|	(ТаймингЗадачСрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЗадач.ОжиданиеКонструктором)
		|			ИЛИ ТаймингЗадачСрезПоследних.Состояние ЕСТЬ NULL)
		|	И Задачи_ПКД.ОбъектТЗ = &ОбъектТЗ
		|	И НЕ Задачи_ПКД.Выполнена
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Задачи_РКД.Ссылка КАК Задача,
		|	ЕСТЬNULL(ТаймингЗадачСрезПоследних.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияЗадач.ОжиданиеКонструктором)) КАК Состояние
		|ИЗ
		|	Задача.Задачи_РКД КАК Задачи_РКД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймингЗадач.СрезПоследних КАК ТаймингЗадачСрезПоследних
		|		ПО Задачи_РКД.Ссылка = ТаймингЗадачСрезПоследних.Задача
		|ГДЕ
		|	(ТаймингЗадачСрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЗадач.ОжиданиеКонструктором)
		|			ИЛИ ТаймингЗадачСрезПоследних.Состояние ЕСТЬ NULL)
		|	И Задачи_РКД.ОбъектТЗ = &ОбъектТЗ
		|	И НЕ Задачи_РКД.Выполнена";
	
	Запрос.УстановитьПараметр("ОбъектТЗ", ОбъектТЗ);
	
	Результат 	= Запрос.ВыполнитьПакет();
	Выборка 	= Результат[0].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Задача) Тогда
			
			ОбъектЗадача = Выборка.Задача.ПолучитьОбъект(); 
			ОбъектЗадача.Исполнитель = омСотрудники.ПолучитьПользователяПоСотруднтку(Конструктор);	
			ОбъектЗадача.Записать();
			
		КонецЕсли;
	КонецЦикла;
	
	Выборка 	= Результат[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Задача) Тогда
			
			ОбъектЗадача = Выборка.Задача.ПолучитьОбъект(); 
			ОбъектЗадача.Исполнитель = омСотрудники.ПолучитьПользователяПоСотруднтку(Конструктор);	
			ОбъектЗадача.Записать();
			
		КонецЕсли;
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ИзменитьЗначениеПерехода(Комплект, НаименованиеИзменения) Экспорт

	Проект = Комплект.Проект;
	
	МенеджерЗаписи = РегистрыСведений.ДатыКомплекта.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Проект 	= Проект;
	МенеджерЗаписи.Комплект = Комплект;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Проект 	= Проект;
	МенеджерЗаписи.Комплект = Комплект;
	
	МенеджерЗаписи[НаименованиеИзменения] = МенеджерЗаписи[НаименованиеИзменения] + 1;
	
	МенеджерЗаписи.Записать();

КонецПроцедуры
