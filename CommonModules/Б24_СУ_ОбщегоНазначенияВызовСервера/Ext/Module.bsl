
#Область ОбщиеПроцедурыИФункцииВыгрузки

Процедура ВыгрузкаВРеальномВремени(ИнформацияОНастройке) Экспорт

	НастройкаПодключения = ИнформацияОНастройке.НастройкаПодключения;
	
	ТипыДанныхДляОбменаСПорталом = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();  
	ТипыОпераций 				 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыОпераций();
	ИменаИспользуемыхРеглЗаданий = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьИменаИспользуемыхРеглЗаданий();
	
	Если ИнформацияОНастройке.ВыгружатьЦены И ИнформацияОНастройке.СпособВыгрузкиЦен = "ВРежимеРеальногоВремени" Тогда
		Если РегистрыСведений.Б24_СУ_ТаблицаИзменений.НаличиеИзмененийПоТипуДанных(ИнформацияОНастройке.НастройкаПодключения, ТипыДанныхДляОбменаСПорталом.Цена) Тогда
			
			НаименованиеФоновогоЗадания = ИменаИспользуемыхРеглЗаданий.ВыгрузкаЦен + " на: " + ИнформацияОНастройке.Портал;
			Если НЕ Б24_К_ОбщегоНазначенияВызовСервера.СейчасВыполняетсяЗадание(НаименованиеФоновогоЗадания) Тогда
				УникальныйИдентификатор = Новый УникальныйИдентификатор;
				ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
				ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
				ПараметрыВыполнения.ОжидатьЗавершение = 0;
				
				ПараметрыВыгрузки = ПараметрыВыполненияОбменаВФоне(НастройкаПодключения, ТипыОпераций.Цены, Истина);
				ДлительныеОперации.ВыполнитьВФоне("Б24_СУ_ОбщегоНазначенияВызовСервера.ВыполнитьОбменВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ИнформацияОНастройке.ВыгружатьОстатки И ИнформацияОНастройке.СпособВыгрузкиОстатков = "ВРежимеРеальногоВремени" Тогда
		Если РегистрыСведений.Б24_СУ_ТаблицаИзменений.НаличиеИзмененийПоТипуДанных(ИнформацияОНастройке.НастройкаПодключения, ТипыДанныхДляОбменаСПорталом.Остаток) Тогда
			
			НаименованиеФоновогоЗадания = ИменаИспользуемыхРеглЗаданий.ВыгрузкаОстатков + " на: " + ИнформацияОНастройке.Портал;  
			Если НЕ Б24_К_ОбщегоНазначенияВызовСервера.СейчасВыполняетсяЗадание(НаименованиеФоновогоЗадания) Тогда
				УникальныйИдентификатор = Новый УникальныйИдентификатор;
				ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
				ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
				ПараметрыВыполнения.ОжидатьЗавершение = 0;
				
				ПараметрыВыгрузки = ПараметрыВыполненияОбменаВФоне(НастройкаПодключения, ТипыОпераций.Остатки, Истина);
				ДлительныеОперации.ВыполнитьВФоне("Б24_СУ_ОбщегоНазначенияВызовСервера.ВыполнитьОбменВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
			КонецЕсли;   
			
		КонецЕсли;
	КонецЕсли;
	
	Если ИнформацияОНастройке.ВыгружатьКартинки И ИнформацияОНастройке.СпособВыгрузкиКартинок = "ВРежимеРеальногоВремени" Тогда
		Если РегистрыСведений.Б24_СУ_ТаблицаИзменений.НаличиеИзмененийПоТипуДанных(ИнформацияОНастройке.НастройкаПодключения, ТипыДанныхДляОбменаСПорталом.КартинкаФайлТовара2) 
			ИЛИ РегистрыСведений.Б24_СУ_ТаблицаИзменений.НаличиеИзмененийПоТипуДанных(ИнформацияОНастройке.НастройкаПодключения, ТипыДанныхДляОбменаСПорталом.КартинкаФайлПредложения) Тогда
			
			НаименованиеФоновогоЗадания = ИменаИспользуемыхРеглЗаданий.ВыгрузкаКартинок + " на: " + ИнформацияОНастройке.Портал;  
			Если НЕ Б24_К_ОбщегоНазначенияВызовСервера.СейчасВыполняетсяЗадание(НаименованиеФоновогоЗадания) Тогда
				УникальныйИдентификатор = Новый УникальныйИдентификатор;
				ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
				ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
				ПараметрыВыполнения.ОжидатьЗавершение = 0;            
				
				мТипыОпераций = Новый Массив;
				мТипыОпераций.Добавить(ТипыОпераций.КартинкиФайлыТоваров);	
				мТипыОпераций.Добавить(ТипыОпераций.КартинкиФайлыПредложений);	
				ПараметрыВыгрузки = ПараметрыВыполненияОбменаВФоне(НастройкаПодключения, мТипыОпераций, Истина);
				ДлительныеОперации.ВыполнитьВФоне("Б24_СУ_ОбщегоНазначенияВызовСервера.ВыполнитьОбменВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
			КонецЕсли;   
			
		КонецЕсли;
	КонецЕсли;
	
	Если ИнформацияОНастройке.ВыгружатьОтгрузки Тогда
		Если РегистрыСведений.Б24_СУ_ТаблицаИзменений.НаличиеИзмененийПоТипуДанных(ИнформацияОНастройке.НастройкаПодключения, ТипыДанныхДляОбменаСПорталом.Отгрузка) Тогда
			
			НаименованиеФоновогоЗадания = ИменаИспользуемыхРеглЗаданий.ВыгрузкаОтгрузок + " на: " + ИнформацияОНастройке.Портал;
			Если НЕ Б24_К_ОбщегоНазначенияВызовСервера.СейчасВыполняетсяЗадание(НаименованиеФоновогоЗадания) Тогда
				УникальныйИдентификатор = Новый УникальныйИдентификатор;
				ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
				ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
				ПараметрыВыполнения.ОжидатьЗавершение = 0;
				
				ПараметрыВыгрузки = ПараметрыВыполненияОбменаВФоне(НастройкаПодключения, ТипыОпераций.Отгрузки, Истина);
				ДлительныеОперации.ВыполнитьВФоне("Б24_СУ_ОбщегоНазначенияВызовСервера.ВыполнитьОбменВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


Процедура ВыполнитьОбменВФоне(ПараметрыВыгрузки, АдресРезультата = Неопределено) Экспорт
	
	НастройкаПодключения = ПараметрыВыгрузки.НастройкаПодключения;
	//Если Б24_К_СлужебныйВызовСервера.МодульСинхронизацииДоступен(НастройкаПодключения) = Ложь Тогда
	//	Возврат;	
	//КонецЕсли;
	
	СтруктураНастроек = СформироватьСтруктуруНастроек(НастройкаПодключения, ПараметрыВыгрузки.ИнтерактивныйЗапуск);
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;       
	
	
	Если ПараметрыВыгрузки.ВыполнятьЗагрузку Тогда
		
		СтруктураНастроек.Логирование.Измерение1 = "Загрузка";                                         
		
		ЗагружаемыеСущности = Новый Структура;
		ЗагружаемыеСущности.Вставить("ЗагружатьЗаказы"		, Б24_СУ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗначениеНастройкиПоНастройкеПодключения(НастройкаПодключения, "РезервироватьТовары", "НастройкииЗаказов") = Истина);
		ЗагружаемыеСущности.Вставить("ЗагружатьОтгрузки"	, Б24_СУ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьЗначениеНастройкиПоНастройкеПодключения(НастройкаПодключения, "ОтгружатьТовары", "НастройкииЗаказов") = Истина);
		
		Б24_СУ_ЗагрузкаСервер.ЗагрузкаИзменений(СтруктураНастроек, ЗагружаемыеСущности);		
		
	КонецЕсли;
	
	Если ПараметрыВыгрузки.ВыполнятьВыгрузку Тогда
		
		Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(5);    // ожидание завершения транзакцийв
		
		СтруктураНастроек.Логирование.Измерение1 = "Выгрузка";
		
		Для каждого ТекТипОперации Из ПараметрыВыгрузки.ТипыОпераций Цикл
			
			СтруктураНастроек.Операция = ТекТипОперации;
			
			Б24_СУ_ВыгрузкаСервер.ВыгрузкаДанныеПоТипуДанных(СтруктураНастроек, ПараметрыВыгрузки.ПолнаяВыгрузка);	
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(ПолучитьСообщенияПользователю(Истина), АдресРезультата);

КонецПроцедуры

Функция ПараметрыВыполненияОбменаВФоне(НастройкаПодключения, ТипыОпераций, ВыполнятьВыгрузку, ВыполнятьЗагрузку = Ложь, ПолнаяВыгрузка = Ложь, ИнтерактивныйЗапуск = Ложь) Экспорт
	
	ПараметрыВыгрузки = Новый Структура;  
	ПараметрыВыгрузки.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	Если ТипЗнч(ТипыОпераций) = Тип("Массив") Тогда        
		ПараметрыВыгрузки.Вставить("ТипыОпераций"	, ТипыОпераций);
	Иначе         
		мТипыОпераций = Новый Массив;
		мТипыОпераций.Добавить(ТипыОпераций);
		ПараметрыВыгрузки.Вставить("ТипыОпераций", мТипыОпераций);
	КонецЕсли;
	
	ПараметрыВыгрузки.Вставить("ВыполнятьВыгрузку"	, ВыполнятьВыгрузку);
	ПараметрыВыгрузки.Вставить("ВыполнятьЗагрузку"	, ВыполнятьЗагрузку);
	ПараметрыВыгрузки.Вставить("ПолнаяВыгрузка"		, ПолнаяВыгрузка);
	ПараметрыВыгрузки.Вставить("ИнтерактивныйЗапуск", ПолнаяВыгрузка);
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

Процедура ОбменПоРасписанию(НастройкаПодключения, Операция) Экспорт
	
	мТипыОпераций = Новый Массив;
	
	ТипыОпераций = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыОпераций();
	
	НастройкаОбмена = Б24_СУ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНастройкуПоНастройкеПодключения(НастройкаПодключения);
	
	Настройки = НастройкаОбмена.Настройки.Получить();
	
	Если Настройки.НастройкиЦен.СпособВыгрузки = "ПоРасписанию" И (Операция = "Общее" ИЛИ Операция = "Цены") Тогда
		мТипыОпераций.Добавить(ТипыОпераций.Цены);
	КонецЕсли;
	
	Если Настройки.НастройкиОстатков.СпособВыгрузки = "ПоРасписанию" И (Операция = "Общее" ИЛИ Операция = "Остатки") Тогда
		мТипыОпераций.Добавить(ТипыОпераций.Остатки);
	КонецЕсли;
	
	Если Настройки.НастройкиОстатков.СпособВыгрузки = "ПоРасписанию" И (Операция = "Общее" ИЛИ Операция = "Картинки") Тогда
		мТипыОпераций.Добавить(ТипыОпераций.КартинкиФайлыТоваров);
		мТипыОпераций.Добавить(ТипыОпераций.КартинкиФайлыПредложений);
	КонецЕсли;
	
	мТипыОпераций.Добавить(ТипыОпераций.Отгрузка);
	
	ПараметрыВыгрузки = ПараметрыВыполненияОбменаВФоне(НастройкаПодключения, мТипыОпераций, Истина, Истина);
	
	ВыполнитьОбменВФоне(ПараметрыВыгрузки, "");
	
КонецПроцедуры

#КонецОбласти

#Область СтандартизированныеПроцедурыИФункции

Функция СформироватьСтруктуруНастроек(НастройкаПодключения, ИнтерактивныйЗапуск = Ложь) Экспорт
		          
	//Если НЕ Б24_К_СлужебныйВызовСервера.МодульСкладскойУчетДоступен(НастройкаПодключения) Тогда
	//	Возврат Неопределено;
	//КонецЕсли;
	
	Если РегистрыСведений.Б24_СУ_Настройки.ПолучитьМассивНастроекВыгрузки("НастройкаПодключения", НастройкаПодключения).Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураНастроек = Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Истина);
	Если СтруктураНастроек = Неопределено Тогда
		ТипыСообщений = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыСообщений();
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, ТипыСообщений.КритическаяОшибка, "Невозможно получить общие настройки. Продолжение невозможно.");
		Возврат СтруктураНастроек; 
	КонецЕсли;     
	
	СтруктураНастроек.ИдентификаторПодключения = СтруктураНастроек.ИдентификаторПодключения + ПолучитьПостфиксИдКоннектора();
	СтруктураНастроек.ПараметрКоннектора = "auth_connector=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(СтруктураНастроек.ИдентификаторПодключения);
	
	Если ИнтерактивныйЗапуск Тогда 
		СтруктураНастроек.Вставить("ИнтерактивныйЗапуск"	, Истина);
	КонецЕсли;
	
	СтруктураНастроек.Вставить("КоличествоДанныхВПакетеПоУмолчанию"	, 50); 	
	СтруктураНастроек.Вставить("КлючиПодчиненныхБатчЗапросов"		, Новый Соответствие); 	
	
	СтруктураНастроек.Вставить("ВыполненоБезОшибок"				, Истина);
	СтруктураНастроек.Вставить("ИдПроцессаЗагрузки"				, "");
	СтруктураНастроек.Вставить("ТаблицаСопоставленияИзменений"	, Новый Массив);
	СтруктураНастроек.Вставить("НеобновляемыеДанныеНаПортале"	, Новый Массив);       // например данные которые не должны обновляться на сайте(флаг)
	
	СтруктураНастроек.Вставить("ВыгружаемыеТовары"			, Неопределено);
	СтруктураНастроек.Вставить("ВыгружаемыеПредложения"		, Неопределено);
	СтруктураНастроек.Вставить("КартинкиИФайлы"			 	, Новый Массив);

	НастройкиВыгрузки = Б24_СУ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНастройкуПоНастройкеПодключения(НастройкаПодключения);
	СтруктураНастроек.Вставить("КоличествоПовторенийПриОшибках", НастройкиВыгрузки.КоличествоПовторенийПриОшибках);

	СохраненныеНастройки = НастройкиВыгрузки.Настройки.Получить();
	СтруктураНастроек.Вставить("НастройкиОстатков"	, СохраненныеНастройки.НастройкиОстатков);
	СтруктураНастроек.Вставить("НастройкиТоваров"	, СохраненныеНастройки.НастройкиТоваров);
	СтруктураНастроек.Вставить("НастройкиЦен"		, СохраненныеНастройки.НастройкиЦен);
	СтруктураНастроек.Вставить("НастройкииЗаказов"	, СохраненныеНастройки.НастройкииЗаказов);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Портал", СтруктураНастроек.Портал);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ПользователиБитрикс24.Идентификатор КАК ИдПользователя,
	|	Б24_К_ПользователиБитрикс24.Пользователь1С КАК Пользователь1С
	|ИЗ
	|	РегистрСведений.Б24_К_ПользователиБитрикс24 КАК Б24_К_ПользователиБитрикс24
	|ГДЕ
	|	Б24_К_ПользователиБитрикс24.Портал = &Портал";
	Запрос.Выполнить().Выгрузить();
	
	СтруктураНастроек.Вставить("ТаблицаСопоставленияПользователей", Запрос.Выполнить().Выгрузить()); 
	
	Возврат СтруктураНастроек;

КонецФункции

Функция ПолучитьПостфиксИдКоннектора() Экспорт
	
	Возврат "_SALE";
	
КонецФункции   

#КонецОбласти
