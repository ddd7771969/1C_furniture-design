
Функция ПроверитьНаличиеМессенджераБитрикс24() Экспорт
	
	#Если ВебКлиент Тогда
		Возврат Истина;
	#Иначе
		
		Попытка
			
			Соединение = Новый HTTPСоединение("127.0.0.1:20141",,,,,1);
		    Запрос 		= Новый HTTPЗапрос("http://127.0.0.1:20141/icon.png");
		    Соединение.Получить(Запрос);

			Возврат Истина;
			
		Исключение
			Возврат Ложь;
		КонецПопытки;
		
	#КонецЕсли
	
КонецФункции

Процедура ПозвонитьЧерезБитрикс24(НомерТелефона) Экспорт

	Если НЕ ЗначениеЗаполнено(НомерТелефона) Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоНастроек = Б24_К_ТелефонияВызовСервера.ПолучитьКоличествоНастроекПодключенияСТелефонией();
	
	Если КоличествоНастроек > 1 Тогда
		
		ВыбЗнач = Неопределено;
		
		Массив = Новый Массив;
		Массив.Добавить(Тип("СправочникСсылка.Б24_К_НастройкиПодключения"));
		ОписаниеТипов = Новый ОписаниеТипов(Массив);
		
		Оповещение = Новый ОписаниеОповещения("ПослеВводаНастройки", ЭтотОбъект, НомерТелефона);

		ПоказатьВводЗначения(Оповещение, ВыбЗнач, "Укажите источник звонка", ОписаниеТипов);	
		
	ИначеЕсли КоличествоНастроек = 1 Тогда
		
		ПослеВводаНастройки(Б24_К_ТелефонияВызовСервера.ПолучитьНастройкуПодключенияПоУмолчаниюСТелефонией(), НомерТелефона);	
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВводаНастройки(ВыбЗнач, НомерТелефона) Экспорт
	
	Если ВыбЗнач <> Неопределено Тогда
		
		Если НЕ Б24_К_СлужебныйВызовСервера.МодульТелефонияДоступен(ВыбЗнач) Тогда
			Б24_К_СлужебныйКлиент.ОповеститьОбНедоступностиФункционалаТелефонияПоТарифномуПлану();
			Возврат;
		КонецЕсли;                          
		
		ТелефонияБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(ВыбЗнач, "ТелефонияБитрикс24");
		
		Если ТелефонияБитрикс24 = Истина Тогда
			
			Портал = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(ВыбЗнач, "Портал");
			
			Если ЗначениеЗаполнено(Портал) Тогда
				КлючЗвонка = "bx://v2/"+Портал+"/callto/phone/"+НомерТелефона;
				НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения("ПослеПодключения", ЭтотОбъект, КлючЗвонка));
			КонецЕсли;
			
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю("У источника не включена телефония");
			ПозвонитьЧерезБитрикс24(НомерТелефона);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры 

Процедура ПослеПодключения(Подключено, ПередаваемыеПараметры)Экспорт 
    Если Подключено Тогда
        НачатьЗапускПриложения(Новый ОписаниеОповещения("ПослеЗапуска", ЭтотОбъект, ПередаваемыеПараметры),ПередаваемыеПараметры);
    Иначе
        НачатьУстановкуРасширенияРаботыСФайлами(Новый ОписаниеОповещения("ПослеУстановки", ЭтотОбъект, ПередаваемыеПараметры));
    КонецЕсли;
КонецПроцедуры

Процедура ПослеУстановки(ПередаваемыеПараметры)Экспорт 
    НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения("ПослеПодключения", ЭтотОбъект, ПередаваемыеПараметры))
КонецПроцедуры

Процедура ПослеЗапуска(Результат, ПередаваемыеПараметры)Экспорт 
    ПоказатьОповещениеПользователя("Звонок",,,БиблиотекаКартинок.ПозвонитьИлиОтправитьSMS);
КонецПроцедуры
