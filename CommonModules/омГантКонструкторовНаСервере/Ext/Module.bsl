#Область Диаграмма
	
Функция ОбновитьГантНаСервере(стПараметры) Экспорт
	
	Диаграмма				= стПараметры.Диаграмма;
	СтруктураДанныеГантаВр	= ПолучитьТаблицуДанных();
	Конструктор				= стПараметры.Конструктор;
	Подразделение			= стПараметры.Подразделение;
	Проект					= стПараметры.Проект;
	Сортировка				= стПараметры.Сортировка;
	
	ДанныеТочки = ПолучитьДанныеТочки(Диаграмма);
	
	ПолучитьДанныеДиаграммы(Конструктор, Подразделение, Проект, СтруктураДанныеГантаВр);
	
	СтруктураДанныеГанта	= ПолучитьТаблицуДанных();
	
	Если Сортировка = 0 Тогда
		
		стОтбор = Новый Структура("Проект", );
		
		Для каждого х Из СтруктураДанныеГантаВр.тзПроекты Цикл
			
			стОтбор.Проект = х.Проект;
			
			Для каждого текСтрока Из СтруктураДанныеГантаВр.тзДанныеГанта.НайтиСтроки(стОтбор) Цикл
				ЗаполнитьЗначенияСвойств(СтруктураДанныеГанта.тзДанныеГанта.Добавить(), текСтрока);
			КонецЦикла;
		
		КонецЦикла;
		
	ИначеЕсли Сортировка = 1 Тогда  
		
		стОтбор = Новый Структура("Исполнитель", );
		
		Для каждого х Из СтруктураДанныеГантаВр.тзИсполнитель Цикл
			
			стОтбор.Исполнитель = х.Исполнитель;
			
			Для каждого текСтрока Из СтруктураДанныеГантаВр.тзДанныеГанта.НайтиСтроки(стОтбор) Цикл
				ЗаполнитьЗначенияСвойств(СтруктураДанныеГанта.тзДанныеГанта.Добавить(), текСтрока);
			КонецЦикла;
		
		КонецЦикла;
		
	ИначеЕсли Сортировка = 2 Тогда 
		СтруктураДанныеГантаВр.тзДанныеГанта.Сортировать("ОбъектТЗНаименование, ДатаНачала, ДатаНачалаПлан");
		СтруктураДанныеГанта.тзДанныеГанта = СтруктураДанныеГантаВр.тзДанныеГанта;
	КонецЕсли;
	
	СтруктураДанныеГантаВр = Неопределено;
	
	стПромежуточныеДанные = ПолучитьПромежуточныеДанные(СтруктураДанныеГанта.тзДанныеГанта);  
	
	стПараметры.Вставить("тзДанныеГанта", СтруктураДанныеГанта.тзДанныеГанта);
	
	ПостроитьДиаграммуГанта(стПараметры, стПромежуточныеДанные);
	
	
КонецФункции 

Функция ВывестиПроект(стПараметры, текПроект, ДанныеТочки = Неопределено, тзЦвета = Неопределено, стОтборПроект = Неопределено) Экспорт
	Диаграмма		= стПараметры.Диаграмма;
	Конструктор		= стПараметры.Конструктор;
	Подразделение	= стПараметры.Подразделение;
	ВыводитьПроект	= стПараметры.ВыводитьПроект;
	тзДанныеГанта	= стПараметры.тзДанныеГанта;
	Обновить		= стПараметры.Обновить;


	Если НЕ ВыводитьПроект Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Если стОтборПроект = Неопределено Тогда
		стОтборПроект 	= Новый Структура("Проект",);
	КонецЕсли; 
	
	стОтборПроект.Проект = текПроект;
	
	НайденыеСтроки = тзДанныеГанта.НайтиСтроки(стОтборПроект);
	
	Если НайденыеСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Обновить Тогда
		ДанныеТочки = ПолучитьДанныеТочки(Диаграмма);
		тзЦвета 	= омДиаграммаГанта.ПолучитьТаблицуЦветов();
	КонецЕсли;
	
	ДанныеТочки.Точка 					= текПроект;
	ДанныеТочки.СсылкаЗначение 			= текПроект;
	ДанныеТочки.Редактирование 			= Ложь;
	ДанныеТочки.ЭтоГруппа 				= Ложь;
	ДанныеТочки.ЕстьРасшифровка			= Истина;
	ДанныеТочки.СократитьНаименование 	= Ложь;
	ДанныеТочки.ЭтоПлан					= Ложь;
	ДанныеТочки.РодительДиаграммы 		= Неопределено;
	ДанныеТочки.Приоритет 		  		= "";
	ДанныеТочки.Переходы	  	  		= "";
	
	стДаты = ПолучитьДатаНачалаОкончания(НайденыеСтроки); 	
	
	ДанныеТочки.ДатаНачала 			= стДаты.ДатаНачала;
	ДанныеТочки.ДатаОкончания	 	= стДаты.ДатаОкончания;
	ДанныеТочки.Продолжительность 	= Окр((стДаты.ДатаОкончания - стДаты.ДатаНачала) / (24 * 3600), 1);
	ДанныеТочки.НаименованиеТочки 	= "";
	ДанныеТочки.Этап 				= "";
	
	ВывестиТочку(Диаграмма, ДанныеТочки, тзЦвета, , Обновить);
	
	ДанныеТочки.РодительДиаграммы = текПроект;

	Если Обновить Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеТочки;

КонецФункции

Функция ВывестиИсполнитель(стПараметры, текИсполнитель, ДанныеТочки = Неопределено, тзЦвета = Неопределено, стОтборИсполнитель = Неопределено) Экспорт

	Диаграмма		= стПараметры.Диаграмма;
	тзДанныеГанта	= стПараметры.тзДанныеГанта;
	Подразделение	= стПараметры.Подразделение;
	Проект			= стПараметры.Проект;
	ВыводитьПроект	= стПараметры.ВыводитьПроект;
	Обновить		= стПараметры.Обновить;
	
	Если стОтборИсполнитель = Неопределено Тогда
		стОтборИсполнитель 	= Новый Структура("Исполнитель, Проект", );
		стОтборИсполнитель.Исполнитель 	= текИсполнитель;
		стОтборИсполнитель.Проект 		= Проект;
	КонецЕсли;
	
	НайденыеСтроки = тзДанныеГанта.НайтиСтроки(стОтборИсполнитель);
	
	Если НайденыеСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Обновить Тогда
		ДанныеТочки = ПолучитьДанныеТочки(Диаграмма);
		тзЦвета 	= омДиаграммаГанта.ПолучитьТаблицуЦветов();
	КонецЕсли;
	
	ДанныеТочки.Точка 					= текИсполнитель;
	ДанныеТочки.СсылкаЗначение 			= текИсполнитель;
	ДанныеТочки.Редактирование 			= Ложь;
	ДанныеТочки.ЭтоГруппа 				= Ложь;
	ДанныеТочки.ЕстьРасшифровка			= Ложь;
	ДанныеТочки.СократитьНаименование 	= Ложь;
	ДанныеТочки.ЭтоПлан					= Ложь;
	ДанныеТочки.Приоритет 		  		= "";
	ДанныеТочки.Переходы	  	  		= "";
	
	стДаты = ПолучитьДатаНачалаОкончания(НайденыеСтроки); 	
		
	ДанныеТочки.ДатаНачала 			= стДаты.ДатаНачала;
	ДанныеТочки.ДатаОкончания	 	= стДаты.ДатаОкончания;
	ДанныеТочки.Продолжительность 	= Окр((стДаты.ДатаОкончания - стДаты.ДатаНачала) / (24 * 3600), 1);
	ДанныеТочки.НаименованиеТочки 	= "";
	ДанныеТочки.Этап 				= "";
	
	ВывестиТочку(Диаграмма, ДанныеТочки, тзЦвета, , Обновить);
	
	Если Обновить Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеТочки;

КонецФункции

Функция ПолучитьТаблицуДанных() Экспорт

	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.ИзделияКомплект"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Композиции"));
	
	МассивТиповЗадачи = Новый Массив;
	МассивТиповЗадачи.Добавить(Тип("ЗадачаСсылка.Задачи_ПКД"));
	МассивТиповЗадачи.Добавить(Тип("ЗадачаСсылка.Задачи_РКД"));
	
	ТипСтрока 			= Новый ОписаниеТипов("Строка");
	
	ТипБулево 			= Новый ОписаниеТипов("Булево");
	
	КвалификаторыДаты 	= Новый КвалификаторыДаты(ЧастиДаты.Дата);
	ТипДата				= Новый ОписаниеТипов("Дата", , , , , КвалификаторыДаты);
	
	КвалификаторыЧисло 	= Новый КвалификаторыЧисла(5, 0, ДопустимыйЗнак.Неотрицательный);
	ТипЧисло5_0			= Новый ОписаниеТипов("Число", );
	
	КвалификаторыЧисло 	= Новый КвалификаторыЧисла(5, 1, ДопустимыйЗнак.Неотрицательный);
	ТипЧисло5_1			= Новый ОписаниеТипов("Число", );
	
	тзДанныеГанта = Новый ТаблицаЗначений;
	тзДанныеГанта.Колонки.Добавить("ОбъектТЗ", 				Новый ОписаниеТипов(МассивТипов));
	тзДанныеГанта.Колонки.Добавить("Исполнитель", 			Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	тзДанныеГанта.Колонки.Добавить("Подразделение",			Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	тзДанныеГанта.Колонки.Добавить("Состояние",				Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияЗадач"));
	тзДанныеГанта.Колонки.Добавить("Комментарий", 			ТипСтрока);
	тзДанныеГанта.Колонки.Добавить("ДатаНачала", 			ТипДата);
	тзДанныеГанта.Колонки.Добавить("Проект", 				Новый ОписаниеТипов("СправочникСсылка.Проекты"));
	тзДанныеГанта.Колонки.Добавить("ОбъектТЗНаименование", 	ТипСтрока);
	тзДанныеГанта.Колонки.Добавить("Этап", 					ТипСтрока);
	тзДанныеГанта.Колонки.Добавить("Задача", 				Новый ОписаниеТипов(МассивТиповЗадачи));
	тзДанныеГанта.Колонки.Добавить("Продолжительность", 	ТипЧисло5_0);
	тзДанныеГанта.Колонки.Добавить("ДатаОкончания", 		ТипДата);
	тзДанныеГанта.Колонки.Добавить("Край", 					ТипДата);
	тзДанныеГанта.Колонки.Добавить("ДатаНачалаПлан", 		ТипДата);
	тзДанныеГанта.Колонки.Добавить("ДатаОкончанияПлан",		ТипДата);
	тзДанныеГанта.Колонки.Добавить("ПланРазработкиВДнях", 	ТипЧисло5_1);
	тзДанныеГанта.Колонки.Добавить("ЭтоПлан", 				ТипБулево);
	тзДанныеГанта.Колонки.Добавить("Переходы", 				ТипЧисло5_0);
	тзДанныеГанта.Колонки.Добавить("ПродленныйЭтап", 		ТипБулево);
	тзДанныеГанта.Колонки.Добавить("Приоритет", 			Новый ОписаниеТипов("СправочникСсылка.Приоритеты"));
	
	тзПроекты = Новый ТаблицаЗначений;
	тзПроекты.Колонки.Добавить("Проект", 				Новый ОписаниеТипов("СправочникСсылка.Проекты"));
	тзПроекты.Колонки.Добавить("ПроектНаименование",	ТипСтрока);
	тзПроекты.Колонки.Добавить("ПроектНомер",			ТипЧисло5_0);
	тзПроекты.Индексы.Добавить("Проект");

	тзИсполнитель = Новый ТаблицаЗначений;
	тзИсполнитель.Колонки.Добавить("Исполнитель", 				Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	тзИсполнитель.Колонки.Добавить("ИсполнительНаименование",	ТипСтрока);
	тзИсполнитель.Индексы.Добавить("Исполнитель");

	тзПодразделение = Новый ТаблицаЗначений;
	тзПодразделение.Колонки.Добавить("Подразделение", 				Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	тзПодразделение.Колонки.Добавить("ПодразделениеНаименование",	ТипСтрока);
	тзПодразделение.Индексы.Добавить("Подразделение");

	Возврат Новый Структура("тзДанныеГанта, тзПроекты, тзИсполнитель, тзПодразделение"
					       , тзДанныеГанта, тзПроекты, тзИсполнитель, тзПодразделение);

КонецФункции // ПолучитьТаблицуДанных()


Процедура ПостроитьДиаграммуГанта(стПараметры, стПромежуточныеДанные)
	
	Диаграмма		= стПараметры.Диаграмма;
	Конструктор		= стПараметры.Конструктор;
	Подразделение	= стПараметры.Подразделение;
	Проект			= стПараметры.Проект;
	ВыводитьПроект	= стПараметры.ВыводитьПроект;
	тзДанныеГанта	= стПараметры.тзДанныеГанта;
	Обновить		= стПараметры.Обновить;
	Сортировка		= стПараметры.Сортировка;

	Диаграмма.Очистить();

	тзЦвета 	= омДиаграммаГанта.ПолучитьТаблицуЦветов();
	
	ПустаяДата 	= Дата(1, 1, 1);
	
	ДанныеТочки = ПолучитьДанныеТочки(Диаграмма);
	
	стОтборПроект 	= Новый Структура("Проект", );
	
	Если Сортировка = 0 Тогда        
		
		стОтборИсполнитель 	= Новый Структура("Исполнитель, Проект", );
		стОтборОбъектТЗ 	= Новый Структура("ОбъектТЗ, Исполнитель, Проект", );
	
		Для каждого текПроект Из стПромежуточныеДанные.мПроект Цикл
			
			ДанныеТочки.РодительДиаграммы 	= Неопределено;
			ВывестиПроект(стПараметры, текПроект, ДанныеТочки, тзЦвета, стОтборПроект);
			стОтборИсполнитель.Проект 		= текПроект; 
			стОтборОбъектТЗ.Проект 			= текПроект; 
			
			Для каждого текИсполнитель Из стПромежуточныеДанные.мИсполнитель Цикл
				
				ДанныеТочки.РодительДиаграммы 	= текПроект;
				
				стОтборИсполнитель.Исполнитель	= текИсполнитель; 
				ВывестиИсполнитель(стПараметры, текИсполнитель, ДанныеТочки, тзЦвета, стОтборИсполнитель);
				ДанныеТочки.РодительДиаграммы 	= текИсполнитель;
				стОтборОбъектТЗ.Исполнитель 	= текИсполнитель; 
				
				Для каждого текОбъектТЗ Из стПромежуточныеДанные.мОбъектТЗ Цикл
					ВывестиОбъектТЗ(Диаграмма, тзДанныеГанта, текОбъектТЗ, стОтборОбъектТЗ, тзЦвета, ДанныеТочки);
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;     
		
	ИначеЕсли Сортировка = 1 Тогда
		
		стОтборИсполнитель 	= Новый Структура("Исполнитель", );
		стОтборОбъектТЗ 	= Новый Структура("ОбъектТЗ, Исполнитель", );
		Для каждого текИсполнитель Из стПромежуточныеДанные.мИсполнитель Цикл
			ДанныеТочки.РодительДиаграммы 	= Неопределено;
			стОтборИсполнитель.Исполнитель	= текИсполнитель; 
			ВывестиИсполнитель(стПараметры, текИсполнитель, ДанныеТочки, тзЦвета, стОтборИсполнитель);
			ДанныеТочки.РодительДиаграммы = текИсполнитель;
			стОтборОбъектТЗ.Исполнитель = текИсполнитель;
			Для каждого текОбъектТЗ Из стПромежуточныеДанные.мОбъектТЗ Цикл
				ВывестиОбъектТЗ(Диаграмма, тзДанныеГанта, текОбъектТЗ, стОтборОбъектТЗ, тзЦвета, ДанныеТочки);
			КонецЦикла;
		КонецЦикла;
		
	Иначе
		
		стОтборОбъектТЗ 	= Новый Структура("ОбъектТЗ", );

		
		Для каждого текОбъектТЗ Из стПромежуточныеДанные.мОбъектТЗ Цикл
			ВывестиОбъектТЗ(Диаграмма, тзДанныеГанта, текОбъектТЗ, стОтборОбъектТЗ, тзЦвета, ДанныеТочки);
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры 

Процедура ВывестиОбъектТЗ(Диаграмма, тзДанныеГанта, текОбъектТЗ, стОтборОбъектТЗ, тзЦвета, ДанныеТочки)

	стОтборОбъектТЗ.ОбъектТЗ = текОбъектТЗ;
	НайденыеСтроки = тзДанныеГанта.НайтиСтроки(стОтборОбъектТЗ);
	
	Если НайденыеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеТочки.Точка 					= текОбъектТЗ;
	ДанныеТочки.СсылкаЗначение 			= текОбъектТЗ;
	ДанныеТочки.Редактирование 			= Истина;
	ДанныеТочки.ЭтоГруппа 				= Ложь;
	ДанныеТочки.ЕстьРасшифровка			= Истина;
	ДанныеТочки.СократитьНаименование 	= Истина;
	
	ПустаяДата = Дата(1, 1, 1);
	
	Для каждого НайденаяСтрока Из НайденыеСтроки Цикл
		
		Если НайденаяСтрока.ДатаНачала = ПустаяДата Тогда
		
			Продолжить;	
		
		КонецЕсли; 
		
		ЗаполнитьЗначенияСвойств(ДанныеТочки, НайденаяСтрока); 
		ДанныеТочки.ДатаОкончания	  = НайденаяСтрока.ДатаОкончания;
		ДанныеТочки.Продолжительность = НайденаяСтрока.Продолжительность;
		ДанныеТочки.НаименованиеТочки = НайденаяСтрока.ОбъектТЗНаименование + "." + НайденаяСтрока.Состояние;
		ДанныеТочки.ЭтоПлан 		  = НайденаяСтрока.ЭтоПлан;
		ДанныеТочки.Состояние 		  = НайденаяСтрока.Состояние;
		ДанныеТочки.Приоритет 		  = НайденаяСтрока.Приоритет;
		ДанныеТочки.Переходы	  	  = НайденаяСтрока.Этап + НайденаяСтрока.Переходы;
		
		ВывестиТочку(Диаграмма, ДанныеТочки, тзЦвета);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьДанныеТочки(Диаграмма)
	
	ПустаяДата 		= Дата(1, 1, 1);
	Серия = Диаграмма.УстановитьСерию("Задачи");
	
	ДанныеТочки = Новый Структура();
	
	ДанныеТочки.Вставить("Точка", 															);
	ДанныеТочки.Вставить("Серия", 					Серия									);
	ДанныеТочки.Вставить("ДатаНачала", 				ПустаяДата								);
	ДанныеТочки.Вставить("ДатаОкончания", 			ПустаяДата								);
	ДанныеТочки.Вставить("Этап", 					""										);
	ДанныеТочки.Вставить("НаименованиеТочки",		""										);
	ДанныеТочки.Вставить("Проект", 															);
	ДанныеТочки.Вставить("Исполнитель",														);
	ДанныеТочки.Вставить("Продолжительность", 		0										);
	ДанныеТочки.Вставить("СсылкаЗначение", 													); 
	ДанныеТочки.Вставить("РодительДиаграммы", 												); 
	ДанныеТочки.Вставить("Редактирование", 			Ложь									); 
	ДанныеТочки.Вставить("ЭтоГруппа", 				Ложь									); 
	ДанныеТочки.Вставить("СократитьНаименование",	Ложь									); 
	ДанныеТочки.Вставить("ЕстьРасшифровка", 		Ложь									); 
	ДанныеТочки.Вставить("Задача", 															); 
	ДанныеТочки.Вставить("Приоритет", 														); 
	ДанныеТочки.Вставить("Переходы",				0										); 
	ДанныеТочки.Вставить("ЭтоПлан", 				Ложь									); 
	ДанныеТочки.Вставить("Состояние", 				Перечисления.СостоянияЗадач.ПустаяСсылка()); 
	
	Возврат ДанныеТочки;	
	
КонецФункции // ПолучитьДанныеТочки() 

Функция ПолучитьДатаНачалаОкончания(НайденыеСтроки)
	
	СекундВСутках = 3600 * 24;
	ПустаяДата 		= Дата(1, 1, 1);
	
	стДаты = Новый Структура("ДатаНачала, ДатаОкончания", ПустаяДата, ПустаяДата);
	
	Для каждого НайденаяСтрока Из НайденыеСтроки Цикл
		
		Если НайденаяСтрока.ДатаНачала = ПустаяДата И НайденаяСтрока.ДатаНачалаПлан = ПустаяДата Тогда
			Продолжить;	
		КонецЕсли;
		
		Если стДаты.ДатаНачала = ПустаяДата Тогда
			Если НайденаяСтрока.ДатаНачала = ПустаяДата Тогда
				стДаты.ДатаНачала = НайденаяСтрока.ДатаНачалаПлан;
			ИначеЕсли НайденаяСтрока.ДатаНачалаПлан = ПустаяДата Тогда	
				стДаты.ДатаНачала = НайденаяСтрока.ДатаНачала;
			Иначе
				стДаты.ДатаНачала = Мин(НайденаяСтрока.ДатаНачала, НайденаяСтрока.ДатаНачалаПлан);
			КонецЕсли;
		Иначе
			Если стДаты.ДатаНачала > НайденаяСтрока.ДатаНачала И НайденаяСтрока.ДатаНачала > ПустаяДата Тогда
				стДаты.ДатаНачала = НайденаяСтрока.ДатаНачала;
			КонецЕсли;
			Если стДаты.ДатаНачала > НайденаяСтрока.ДатаНачалаПлан И НайденаяСтрока.ДатаНачалаПлан > ПустаяДата Тогда
				стДаты.ДатаНачала = НайденаяСтрока.ДатаНачалаПлан;
			КонецЕсли;
		КонецЕсли;
		
		Если НайденаяСтрока.Продолжительность = 0 Тогда
			ДатаОкончанияСВыходными = НайденаяСтрока.ДатаНачала + СекундВСутках
		Иначе
			ДатаОкончанияСВыходными = омДаты.ДобавитьДниСУчетомВыходных(НайденаяСтрока.ДатаНачала, НайденаяСтрока.Продолжительность);
		КонецЕсли;
		
		Если ДатаОкончанияСВыходными > стДаты.ДатаОкончания Тогда
			стДаты.ДатаОкончания = ДатаОкончанияСВыходными;	
		КонецЕсли;
		
	КонецЦикла; 
	
	Возврат стДаты;

КонецФункции // ПолучитьДатаНачалаОкончания()

Функция ВывестиТочку(Диаграмма, ДанныеТочки, тзЦвета, ТекущийЦвет = Неопределено, Обновить = Ложь)

	СукундВСутках 	= 24 * 3600;
	ПустаяДата 		= Дата(1, 1, 1);
	
	Если ДанныеТочки.ДатаНачала = ПустаяДата Тогда
	
		Возврат Неопределено;	
	
	КонецЕсли;
	
	Если Обновить Тогда

		КомплектТочка 			= Диаграмма.УстановитьТочку(ДанныеТочки.СсылкаЗначение, ДанныеТочки.РодительДиаграммы);
		КомплектТочка.Текст 	= ДанныеТочки.НаименованиеТочки;
		Значение = Диаграмма.ПолучитьЗначение(КомплектТочка, ДанныеТочки.Серия);
		Если Значение.Количество() = 0 Тогда
			Возврат Неопределено;	
		КонецЕсли;
		
		Значение.Очистить();	
		
		Интервал 				= Значение.Добавить();
	Иначе
		
		КомплектТочка 			= Диаграмма.УстановитьТочку(ДанныеТочки.СсылкаЗначение, ДанныеТочки.РодительДиаграммы);
		КомплектТочка.Текст 	= ДанныеТочки.НаименованиеТочки;
		Значение = Диаграмма.ПолучитьЗначение(КомплектТочка, ДанныеТочки.Серия);
		Значение.Редактирование = ДанныеТочки.Редактирование И ДанныеТочки.ЭтоПлан;
		Значение.Данные.Переходы = ДанныеТочки.Переходы;
		
		Интервал 				= Значение.Добавить();

	КонецЕсли; 
	
	Диаграмма.РазвернутьТочку(КомплектТочка, Истина);
	
	Интервал.Начало = ДанныеТочки.ДатаНачала;
	Интервал.Конец 	= ?(ДанныеТочки.ДатаОкончания - ДанныеТочки.ДатаНачала < СукундВСутках, ДанныеТочки.ДатаНачала + СукундВСутках, ДанныеТочки.ДатаОкончания);
	Интервал.Текст 	= ДанныеТочки.Этап;
	
	Если НЕ ТекущийЦвет = Неопределено Тогда
		Интервал.Цвет			= ТекущийЦвет;
	ИначеЕсли НЕ тзЦвета = Неопределено Тогда 
		Если ДанныеТочки.ЭтоПлан Тогда
			ТекущийЭтап = "ПланКонструирование";	
		Иначе
			ТекущийЭтап = ДанныеТочки.Этап;	
		КонецЕсли;
		Интервал.Цвет			= омДиаграммаГанта.ПолучитьЦвет(ТекущийЭтап, тзЦвета, ДанныеТочки.ЭтоГруппа, ДанныеТочки.СократитьНаименование);
	КонецЕсли;
	
	Если ДанныеТочки.ЕстьРасшифровка Тогда
		Интервал.Расшифровка 	= Новый Структура("Проект, Этап, СсылкаКомплект, Продолжительность, Исполнитель, Состояние"
		, ДанныеТочки.Проект, ДанныеТочки.Этап, ДанныеТочки.СсылкаЗначение, ДанныеТочки.Продолжительность
		, ДанныеТочки.Исполнитель, ДанныеТочки.Состояние);
	Иначе
		Интервал.Расшифровка 	= Новый Структура("Продолжительность", ДанныеТочки.Продолжительность);
	КонецЕсли;
	
	Если НЕ ДанныеТочки.Задача = Неопределено Тогда
	
		Интервал.Расшифровка.Вставить("Задача", ДанныеТочки.Задача);	
	
	КонецЕсли;
	
	Если ДанныеТочки.СократитьНаименование Тогда
		Интервал.Текст = Лев(Интервал.Текст, 1);
	КонецЕсли;
	
	Значение.Текст = УстановитьТекстЗначения(ДанныеТочки.ДатаНачала, ДанныеТочки.ДатаОкончания, ДанныеТочки.Этап); 
	
	Возврат Интервал;
	
КонецФункции // ВывестиТочку()

Функция УстановитьТекстЗначения(ДатаНачала, ДатаОкончания, Этап)
	
	Возврат Этап + " " + Формат(ДатаНачала, "ДФ=dd.MM.yy") + " - " + Формат(ДатаОкончания, "ДФ=dd.MM.yy");
	
КонецФункции

Функция ПолучитьПромежуточныеДанные(тзДанныеГанта)
	
	ОписаниеЧисло = Новый ОписаниеТипов("Число");
	
	мОбъектТЗ 		= Новый Массив;
	мПроект 		= Новый Массив;
	мИсполнитель 	= Новый Массив;

	ПустаяДата = Дата(1, 1, 1); 
	
    стПромежуточныеДанные 	= Новый Структура("ДатаНачала, 	ДатаОкончания, КрайПКД, 	КрайРКД",
											   ПустаяДата,  ПустаяДата,    ПустаяДата, 	ПустаяДата);
											   
	ПриостановкаКонструктором 	= Перечисления.СостоянияЗадач.ПриостановкаКонструктором;										   
	СостояниеОжидания 			= Перечисления.СостоянияЗадач.ОжиданиеКонструктором;
	
	мДобавитьСтроки = Новый Массив;
	НаименованиеКолонокТаблицы = омОбщиеФункции.КолонкиТаблицыЗначений(тзДанныеГанта);
	ТД = НачалоДня(ТекущаяДата());
	
	Для каждого х Из тзДанныеГанта Цикл 
		
		Если х.Состояние = СостояниеОжидания Тогда
		
			х.ЭтоПлан = Истина;	
		
		Иначе 
		
			ДатаНачалоПаузы = ПолучтьДанныеОПаузеКомплекта(х.Задача, ПриостановкаКонструктором, х);
			
		КонецЕсли;

		
		Если х.ЭтоПлан Тогда
			
			х.ДатаНачала 	= х.ДатаНачалаПлан;	
			
		КонецЕсли;

		Если х.ДатаНачала > ПустаяДата И НЕ х.ПродленныйЭтап И х.ДатаОкончания = ПустаяДата Тогда
			х.ДатаОкончания = омДаты.ДобавитьДниСУчетомВыходных(х.ДатаНачала, х.Продолжительность); 			
		КонецЕсли;
		
		Если х.ДатаНачала > ПустаяДата Тогда
		
			Если стПромежуточныеДанные.ДатаНачала = ПустаяДата Тогда
			
				стПромежуточныеДанные.ДатаНачала = х.ДатаНачала;
				
			ИначеЕсли стПромежуточныеДанные.ДатаНачала > х.ДатаНачала Тогда 
				
				стПромежуточныеДанные.ДатаНачала = х.ДатаНачала;
			
			КонецЕсли;
			
			Если стПромежуточныеДанные.ДатаОкончания < х.ДатаОкончания Тогда
			
				стПромежуточныеДанные.ДатаОкончания = х.ДатаОкончания;	
			
			КонецЕсли;
		
		КонецЕсли; 
		
		ДобавитьВМассив(х.Проект, 		мПроект);
		ДобавитьВМассив(х.Исполнитель, 	мИсполнитель);
		ДобавитьВМассив(х.ОбъектТЗ, 	мОбъектТЗ);
		
	КонецЦикла;
	
	Для каждого ЭлементМассиваДанных Из мДобавитьСтроки Цикл
	
		ЗаполнитьЗначенияСвойств(тзДанныеГанта.Добавить(), ЭлементМассиваДанных);
	
	КонецЦикла;
	
	стПромежуточныеДанные.Вставить("мОбъектТЗ", 	мОбъектТЗ);
	стПромежуточныеДанные.Вставить("мПроект", 		мПроект);
	стПромежуточныеДанные.Вставить("мИсполнитель", 	мИсполнитель);

	Возврат стПромежуточныеДанные; 

КонецФункции // ПолучитьпромежуточныеДанные()

Функция ПолучтьДанныеОПаузеКомплекта(Задача, ПриостановкаКонструктором, ТекущаяСтрокаТаблицы)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 2
	               |	ТаймингЗадач.Состояние КАК Состояние,
	               |	ТаймингЗадач.Период КАК Период,
	               |	ЕСТЬNULL(ПлановоеЗначениеПроектированияСрезПоследних.ПланРазработкиВДнях, 0) КАК ПланРазработкиВДнях
	               |ИЗ
	               |	РегистрСведений.ТаймингЗадач КАК ТаймингЗадач
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановоеЗначениеПроектирования.СрезПоследних КАК ПлановоеЗначениеПроектированияСрезПоследних
	               |		ПО ТаймингЗадач.Задача = ПлановоеЗначениеПроектированияСрезПоследних.Задача
	               |ГДЕ
	               |	ТаймингЗадач.Задача = &Задача
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТаймингЗадач.Период УБЫВ";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	Запрос.УстановитьПараметр("Состояние", ПриостановкаКонструктором);
	
	Результат 	= Запрос.ВыполнитьПакет();
	Выборка 	= Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 2 Тогда
		Выборка.Следующий();
		Состояние0 				= Выборка.Состояние;
		Период0					= НачалоДня(Выборка.Период);
		ПланРазработкиВДнях0	= Выборка.ПланРазработкиВДнях;

		Выборка.Следующий();
		Состояние1 				= Выборка.Состояние;
		Период1					= НачалоДня(Выборка.Период);
		ПланРазработкиВДнях1	= Выборка.ПланРазработкиВДнях;
		
		ТД = НачалоДня(ТекущаяДата()); 
		СекундВДне = 24 * 3600;
		
		Если Состояние0 = ПриостановкаКонструктором Тогда
			ТекущаяСтрокаТаблицы.ДатаНачала 		= Период1;
			ТекущаяСтрокаТаблицы.Этап 				= "Пауза";
			ТекущаяСтрокаТаблицы.ПродленныйЭтап 	= Истина;
			ТекущаяСтрокаТаблицы.Продолжительность 	= (ТД - Период1) / СекундВДне;
			ТекущаяСтрокаТаблицы.ДатаОкончания 		= ТД;
			
		Иначе
			ТекущаяСтрокаТаблицы.ДатаОкончания = омДаты.ДобавитьДниСУчетомВыходных(ТекущаяСтрокаТаблицы.ДатаНачала, ТекущаяСтрокаТаблицы.Продолжительность); 
			
			Если ТекущаяСтрокаТаблицы.ДатаОкончания < ТД Тогда
				ТекущаяСтрокаТаблицы.ДатаОкончания 		= ТД;
				ТекущаяСтрокаТаблицы.Этап 				= "ЗадачаПросрочена";
				ТекущаяСтрокаТаблицы.Продолжительность 	= (ТД - Период1) / СекундВДне;
				ТекущаяСтрокаТаблицы.ПродленныйЭтап 	= Истина;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	
	Возврат Дата(1, 1, 1);	

КонецФункции // ПолучтьДанныеОПаузеКомплекта()

Процедура ДобавитьВМассив(Элемент, МассивДляДобавления)

	Если МассивДляДобавления.Найти(Элемент) = Неопределено Тогда
	
		МассивДляДобавления.Добавить(Элемент);	
	
	КонецЕсли;	

КонецПроцедуры

Процедура ПолучитьДанныеДиаграммы(Конструктор, Подразделение, Проект, СтруктураДанныеГанта)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Задачи_ПКД.Ссылка КАК Задача,
		|	Задачи_ПКД.ОбъектТЗ КАК ОбъектТЗ,
		|	Задачи_ПКД.ОбъектТЗ.Проект КАК Проект,
		|	ПодразделенияСрезПоследних.Подразделение КАК Подразделение,
		|	Задачи_ПКД.ТипЗадачи КАК ТипЗадачи,
		|	""ПКД"" КАК Этап
		|ПОМЕСТИТЬ втЗадачи
		|ИЗ
		|	Задача.Задачи_ПКД КАК Задачи_ПКД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Подразделения.СрезПоследних КАК ПодразделенияСрезПоследних
		|		ПО Задачи_ПКД.Исполнитель.Сотрудник = ПодразделенияСрезПоследних.Сотрудник
		|			И (ПодразделенияСрезПоследних.НомерСтрокиТаблицы = 1)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Задачи_ПКД.ОбъектТЗ.Проект = &Проект
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Конструктор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Задачи_ПКД.Исполнитель = &Конструктор
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ПодразделенияСрезПоследних.Подразделение = &Подразделение
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ПодразделенияСрезПоследних.НомерСтрокиТаблицы = 1
		|		КОНЕЦ
		|	И НЕ Задачи_ПКД.Выполнена
		|	И Задачи_ПКД.ТипЗадачи В(&ТипЗадачи)
		|	И НЕ Задачи_ПКД.ОбобщеннаяЗадача
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Задачи_РКД.Ссылка,
		|	Задачи_РКД.ОбъектТЗ,
		|	Задачи_РКД.ОбъектТЗ.Проект,
		|	ПодразделенияСрезПоследних.Подразделение,
		|	Задачи_РКД.ТипЗадачи,
		|	""РКД""
		|ИЗ
		|	Задача.Задачи_РКД КАК Задачи_РКД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Подразделения.СрезПоследних КАК ПодразделенияСрезПоследних
		|		ПО Задачи_РКД.Исполнитель.Сотрудник = ПодразделенияСрезПоследних.Сотрудник
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Задачи_РКД.ОбъектТЗ.Проект = &Проект
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Конструктор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Задачи_РКД.Исполнитель = &Конструктор
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ПодразделенияСрезПоследних.Подразделение = &Подразделение
		|		КОНЕЦ
		|	И НЕ Задачи_РКД.Выполнена
		|	И ПодразделенияСрезПоследних.НомерСтрокиТаблицы = 1
		|	И Задачи_РКД.ТипЗадачи В(&ТипЗадачи)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втЗадачи.Задача КАК Задача,
		|	ТаймингЗадач.Состояние КАК Состояние,
		|	ТаймингЗадач.Исполнитель КАК Исполнитель,
		|	ТаймингЗадачКомментарии.Комментарий КАК Комментарий,
		|	ДанныеКомплекта.ВнешняяДатаНачалоПроектирования КАК НачалоПКД,
		|	ДанныеКомплекта.ВнешняяДатаОкончаниеПроектирования КАК КрайПКД,
		|	ДанныеКомплекта.ВнешняяДатаНачалоРКД КАК НачалоРКД,
		|	ДанныеКомплекта.ВнешняяДатаОкончаниеРКД КАК КрайРКД,
		|	ТаймингЗадачКомментарии.УИД КАК УИД,
		|	ДанныеКомплекта.Приоритет КАК Приоритет
		|ПОМЕСТИТЬ втТайминг
		|ИЗ
		|	втЗадачи КАК втЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймингЗадач.СрезПоследних(
		|				,
		|				НЕ Задача.Выполнена
		|					И ТипЗадачи В (&ТипЗадачи)
		|					И ВЫБОР
		|						КОГДА &Конструктор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|							ТОГДА ИСТИНА
		|						ИНАЧЕ Задача.Исполнитель = &Конструктор
		|					КОНЕЦ
		|					И НЕ Задача.ОбобщеннаяЗадача) КАК ТаймингЗадач
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймингЗадачКомментарии КАК ТаймингЗадачКомментарии
		|			ПО ТаймингЗадач.УИД = ТаймингЗадачКомментарии.УИД
		|		ПО втЗадачи.Задача = ТаймингЗадач.Задача
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних КАК ДанныеКомплекта
		|		ПО втЗадачи.ОбъектТЗ = ДанныеКомплекта.Комплект
		|ГДЕ
		|	НЕ(ДанныеКомплекта.ВнешняяДатаНачалоПроектирования = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ ДанныеКомплекта.ВнешняяДатаНачалоПроектирования ЕСТЬ NULL)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втЗадачи.ОбъектТЗ КАК ОбъектТЗ,
		|	ЕСТЬNULL(втТайминг.Исполнитель, втЗадачи.Задача.Исполнитель) КАК Исполнитель,
		|	втЗадачи.Подразделение КАК Подразделение,
		|	ЕСТЬNULL(втТайминг.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияЗадач.ОжиданиеКонструктором)) КАК Состояние,
		|	ЕСТЬNULL(втТайминг.Комментарий, """") КАК Комментарий,
		|	ВЫБОР
		|		КОГДА втЗадачи.Этап = ""ПКД""
		|			ТОГДА ЕСТЬNULL(втТайминг.НачалоПКД, ДАТАВРЕМЯ(1, 1, 1))
		|		ИНАЧЕ ЕСТЬNULL(втТайминг.НачалоРКД, ДАТАВРЕМЯ(1, 1, 1))
		|	КОНЕЦ КАК ДатаНачала,
		|	ВЫБОР
		|		КОГДА втЗадачи.Этап = ""ПКД""
		|			ТОГДА ЕСТЬNULL(втТайминг.КрайПКД, ДАТАВРЕМЯ(1, 1, 1))
		|		ИНАЧЕ ЕСТЬNULL(втТайминг.КрайРКД, ДАТАВРЕМЯ(1, 1, 1))
		|	КОНЕЦ КАК Край,
		|	ВЫБОР
		|		КОГДА ПлановоеЗначениеПроектированияСрезПоследних.ПланРазработкиВДнях ЕСТЬ NULL
		|			ТОГДА ВЫБОР
		|					КОГДА втЗадачи.Этап = ""ПКД""
		|						ТОГДА ЕСТЬNULL(ДатыКомплекта.НормаВремени_ПКД, 0)
		|					ИНАЧЕ ЕСТЬNULL(ДатыКомплекта.НормаВремени_РКД, 0)
		|				КОНЕЦ
		|		ИНАЧЕ ПлановоеЗначениеПроектированияСрезПоследних.ПланРазработкиВДнях
		|	КОНЕЦ КАК Продолжительность,
		|	втЗадачи.Проект КАК Проект,
		|	втЗадачи.Проект.Наименование КАК ПроектНаименование,
		|	ВЫБОР
		|		КОГДА втТайминг.Исполнитель ЕСТЬ NULL
		|			ТОГДА втЗадачи.Задача.Исполнитель.Наименование
		|		ИНАЧЕ втТайминг.Исполнитель.Наименование
		|	КОНЕЦ КАК ИсполнительНаименование,
		|	втЗадачи.Подразделение.Наименование КАК ПодразделениеНаименование,
		|	втЗадачи.ОбъектТЗ.Наименование КАК ОбъектТЗНаименование,
		|	втЗадачи.Задача КАК Задача,
		|	ЕСТЬNULL(втЗадачи.Задача.ОбобщеннаяЗадача, ЛОЖЬ) КАК ОбобщеннаяЗадача,
		|	ЕСТЬNULL(ПлановоеЗначениеПроектированияСрезПоследних.ДатаНачала, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачалаПлан,
		|	ЕСТЬNULL(ПлановоеЗначениеПроектированияСрезПоследних.ПланРазработкиВДнях, 0) КАК ПланРазработкиВДнях,
		|	втЗадачи.Этап КАК Этап,
		|	втТайминг.Приоритет КАК Приоритет,
		|	ЕСТЬNULL(ДатыКомплекта.ПереходРедакция, 0) + ЕСТЬNULL(ДатыКомплекта.ПереходРекламация, 0) + 1 КАК Переходы,
		|	втЗадачи.Проект.Номер КАК ПроектНомер
		|ИЗ
		|	втЗадачи КАК втЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ втТайминг КАК втТайминг
		|		ПО втЗадачи.Задача = втТайминг.Задача
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановоеЗначениеПроектирования.СрезПоследних КАК ПлановоеЗначениеПроектированияСрезПоследних
		|		ПО втЗадачи.Задача = ПлановоеЗначениеПроектированияСрезПоследних.Задача
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|		ПО втЗадачи.ОбъектТЗ = ДатыКомплекта.Комплект
		|ГДЕ
		|	(ВЫБОР
		|				КОГДА втЗадачи.Этап = ""ПКД""
		|					ТОГДА ЕСТЬNULL(втТайминг.НачалоПКД, ДАТАВРЕМЯ(1, 1, 1))
		|				ИНАЧЕ ЕСТЬNULL(втТайминг.НачалоРКД, ДАТАВРЕМЯ(1, 1, 1))
		|			КОНЕЦ > ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ЕСТЬNULL(ПлановоеЗначениеПроектированияСрезПоследних.ДатаНачала, ДАТАВРЕМЯ(1, 1, 1)) > ДАТАВРЕМЯ(1, 1, 1))";
	
	Запрос.УстановитьПараметр("Конструктор", 	Конструктор);
	Запрос.УстановитьПараметр("Подразделение", 	Подразделение);
	Запрос.УстановитьПараметр("Проект", 		Проект);
	Запрос.УстановитьПараметр("ТипЗадачи", 		омЗадачиПКДПовтор.ТипыЗадачКонструктора());
	
	Выборка =  Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		//тзДанныеГанта, тзПроекты, тзИсполнитель, тзПодразделение
		ЗаполнитьЗначенияСвойств(СтруктураДанныеГанта.тзДанныеГанта.Добавить(), Выборка);
		
		Если СтруктураДанныеГанта.тзПроекты.Найти(Выборка.Проект) = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтруктураДанныеГанта.тзПроекты.Добавить(), Выборка);
		КонецЕсли;
		
		Если СтруктураДанныеГанта.тзИсполнитель.Найти(Выборка.Исполнитель) = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтруктураДанныеГанта.тзИсполнитель.Добавить(), Выборка);
		КонецЕсли;
		
		Если СтруктураДанныеГанта.тзПодразделение.Найти(Выборка.Подразделение) = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтруктураДанныеГанта.тзПодразделение.Добавить(), Выборка);
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДанныеГанта.тзПроекты.Сортировать("ПроектНомер");
	СтруктураДанныеГанта.тзИсполнитель.Сортировать("ИсполнительНаименование");
	СтруктураДанныеГанта.тзПодразделение.Сортировать("ПодразделениеНаименование");
	СтруктураДанныеГанта.тзДанныеГанта.Сортировать("ДатаНачала, ДатаНачалаПлан");

КонецПроцедуры // ПолучитьДанныеДиаграммы()      

#КонецОбласти
