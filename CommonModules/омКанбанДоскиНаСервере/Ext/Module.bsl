Функция ПолучитьДоскиСУчетомПрав(Пользователь = Неопределено) Экспорт

	Если Пользователь = Неопределено Тогда
	
		Пользователь = ПараметрыСеанса.ТекущийПользователь;	
	
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанДоски.УИД КАК УИД,
		|	КанбанДоски.Наименование КАК Наименование
		|ИЗ
		|	РегистрСведений.КанбанДоски КАК КанбанДоски
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КанбанПрава КАК КанбанПрава
		|		ПО КанбанДоски.УИД = КанбанПрава.Доска
		|			И (КанбанПрава.Пользователь = &Пользователь)
		|ГДЕ
		|	(КанбанПрава.Просмотр
		|			ИЛИ КанбанПрава.Добавление
		|			ИЛИ КанбанПрава.Измениение
		|			ИЛИ КанбанПрава.Удаление)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	стДоски = Новый СписокЗначений;
	
	Пока Выборка.Следующий() Цикл
		стДоски.Добавить(Выборка.УИД, Выборка.Наименование);
	КонецЦикла;
	
	Возврат стДоски;	

КонецФункции // ПолучитьДоскиСучетомПрав()

Процедура ЗаписатПереход(Данные, УИДДоски, стДополнительныеДанные = Неопределено) Экспорт
	
	УИДКарточка 	= Данные.Получить("itemId");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанПереходКарточкиСрезПоследних.УИДКудаДорожка КАК УИДКудаДорожка,
		|	КанбанПереходКарточкиСрезПоследних.УИДКудаКолонка КАК УИДКудаКолонка,
		|	КанбанПереходКарточкиСрезПоследних.НомерВКолонке КАК НомерВКолонке,
		|	КанбанПереходКарточкиСрезПоследних.Окончание КАК Окончание
		|ИЗ
		|	РегистрСведений.КанбанПереходКарточки.СрезПоследних(, УИДКарточка = &УИДКарточка) КАК КанбанПереходКарточкиСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КанбанКолонки.ДействиеПриПопадании КАК ДействиеПриПопадании
		|ИЗ
		|	РегистрСведений.КанбанКолонки КАК КанбанКолонки
		|ГДЕ
		|	КанбанКолонки.УИД = &categoryId";
	
	Запрос.УстановитьПараметр("УИДКарточка", УИДКарточка);
	Запрос.УстановитьПараметр("categoryId", Данные.Получить("categoryId"));
	
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[0].Выбрать();
	
	стПараметры = Новый Структура();
	
	Пока Выборка.Следующий() Цикл
		
		стПараметры.Вставить("текНомерВКолонке", 	Выборка.НомерВКолонке);
	    стПараметры.Вставить("текУИДКудаКолонка", 	Выборка.УИДКудаКолонка);
	    стПараметры.Вставить("текУИДКудаДорожка", 	Выборка.УИДКудаДорожка);
	    стПараметры.Вставить("текОкончание", 		Выборка.Окончание);
		
	КонецЦикла;
	
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
	    стПараметры.Вставить("Действие", 			Выборка.ДействиеПриПопадании);
		
	КонецЦикла;
	
	стПараметры.Вставить("новНомерВКолонке", 	Данные.Получить("insertBeforeIndex"));
	стПараметры.Вставить("новУИДКудаКолонка", 	Данные.Получить("categoryId"));
	стПараметры.Вставить("новУИДКудаДорожка", 	Данные.Получить("trackId"));
	стПараметры.Вставить("новОкончание", 		Ложь);
	стПараметры.Вставить("УИДКарточка", 		УИДКарточка);
	
	стПараметры.новНомерВКолонке = ?(стПараметры.новНомерВКолонке = -1, 0, стПараметры.новНомерВКолонке);
	
	МенеджерЗаписиПереход = РегистрыСведений.КанбанПереходКарточки.СоздатьМенеджерЗаписи();
	МенеджерЗаписиПереход.Период			= ТекущаяДата();
	МенеджерЗаписиПереход.УИДПерехода 		= Новый УникальныйИдентификатор();
	МенеджерЗаписиПереход.УИДКарточка 		= УИДКарточка;
	МенеджерЗаписиПереход.УИДКудаКолонка 	= стПараметры.новУИДКудаКолонка;
	МенеджерЗаписиПереход.УИДКудаДорожка 	= стПараметры.новУИДКудаДорожка;
	МенеджерЗаписиПереход.Пользователь 		= ПараметрыСеанса.ТекущийПользователь;
	МенеджерЗаписиПереход.НомерВКолонке 	= стПараметры.новНомерВКолонке;
	МенеджерЗаписиПереход.Окончание 		= стПараметры.новОкончание;
	
	МенеджерЗаписиПереход.Записать();
	
	ИзменитьНомерВКолонкеПослеПеремещения(стПараметры);
	
	ИнкриментВерсии(УИДДоски);
	
	Если стПараметры.Свойство("Действие") Тогда
	
		Если НЕ ПустаяСтрока(стПараметры.Действие) Тогда
		
			мПараметры = Новый Массив(4);
			мПараметры[0] = стПараметры.новНомерВКолонке;
			мПараметры[1] = стПараметры.новУИДКудаКолонка;
			мПараметры[2] = стПараметры.новУИДКудаДорожка;
			мПараметры[3] = стПараметры.УИДКарточка;
			
			ОбщегоНазначения.ВыполнитьМетодКонфигурации("омКанбанДоскиНаСервере." + стПараметры.Действие, мПараметры);	
		
		КонецЕсли;	
	
	КонецЕсли;
	
КонецПроцедуры // ЗаписатПереход() 

Процедура ИзменитьНомерВКолонкеПослеПеремещения(стПараметры)

	Если стПараметры.текУИДКудаКолонка = стПараметры.новУИДКудаКолонка И стПараметры.текУИДКудаДорожка = стПараметры.новУИДКудаДорожка Тогда
		
		Если НЕ стПараметры.новНомерВКолонке = стПараметры.текНомерВКолонке Тогда
			ПеремещениеВОднойКолонкеИДорожке(стПараметры.текУИДКудаКолонка, стПараметры.текУИДКудаДорожка, стПараметры.новНомерВКолонке, стПараметры.текНомерВКолонке, стПараметры.УИДКарточка);	
		КонецЕсли;	
		
	Иначе
		
		
		
	КонецЕсли; 

КонецПроцедуры

Процедура ПеремещениеВОднойКолонкеИДорожке(УИДКудаКолонка, УИДКудаДорожка, новНомерВКолонке, текНомерВКолонке, УИДКарточка)

	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
    ТД 					= ТекущаяДата();
	
	Если новНомерВКолонке < текНомерВКолонке Тогда
	
		ПервыйНомер 	= новНомерВКолонке;
		ПоследнийНомер 	= текНомерВКолонке;
		Индекс 			= 1;
	
	Иначе
	
		ПервыйНомер 	= текНомерВКолонке;
		ПоследнийНомер 	= новНомерВКолонке;
		Индекс 			= -1;
	
	КонецЕсли; 
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанПереходКарточкиСрезПоследних.УИДКарточка КАК УИДКарточка,
		|	КанбанПереходКарточкиСрезПоследних.НомерВКолонке КАК НомерВКолонке,
		|	КанбанПереходКарточкиСрезПоследних.Окончание КАК Окончание,
		|	КанбанПереходКарточкиСрезПоследних.УИДКудаДорожка КАК УИДКудаДорожка,
		|	КанбанПереходКарточкиСрезПоследних.УИДПерехода КАК УИДПерехода
		|ИЗ
		|	РегистрСведений.КанбанПереходКарточки.СрезПоследних(, ) КАК КанбанПереходКарточкиСрезПоследних
		|ГДЕ
		|	КанбанПереходКарточкиСрезПоследних.НомерВКолонке >= &ПервыйНомер
		|	И КанбанПереходКарточкиСрезПоследних.НомерВКолонке <= &ПоследнийНомер
		|	И КанбанПереходКарточкиСрезПоследних.УИДКарточка <> &УИДКарточка
		|	И КанбанПереходКарточкиСрезПоследних.УИДКудаДорожка = &УИДКудаДорожка
		|	И КанбанПереходКарточкиСрезПоследних.УИДКудаКолонка = &УИДКудаКолонка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерВКолонке";
	
	Запрос.УстановитьПараметр("ПервыйНомер", 	ПервыйНомер);
	Запрос.УстановитьПараметр("ПоследнийНомер", ПоследнийНомер);
	Запрос.УстановитьПараметр("УИДКудаКолонка", УИДКудаКолонка);
	Запрос.УстановитьПараметр("УИДКудаДорожка", УИДКудаДорожка);
	Запрос.УстановитьПараметр("УИДКарточка", 	УИДКарточка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	
	Пока Выборка.Следующий() Цикл
		

		МенеджерЗаписи = РегистрыСведений.КанбанПереходКарточки.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.НомерВКолонке 	= Выборка.НомерВКолонке + Индекс;
		МенеджерЗаписи.Окончание 		= Выборка.Окончание;
		МенеджерЗаписи.Период 			= ТД;
		МенеджерЗаписи.УИДКарточка 		= Выборка.УИДКарточка;
		МенеджерЗаписи.Пользователь 	= ТекущийПользователь;
		МенеджерЗаписи.УИДКудаКолонка 	= УИДКудаКолонка;
		МенеджерЗаписи.УИДКудаДорожка 	= Выборка.УИДКудаДорожка;
		МенеджерЗаписи.УИДПерехода 		= Новый УникальныйИдентификатор();
		
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	

КонецПроцедуры

Функция ПолучитьНомерВерсии(УИДДоски) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанДоски.НомерВерсии КАК НомерВерсии
		|ИЗ
		|	РегистрСведений.КанбанДоски КАК КанбанДоски
		|ГДЕ
		|	КанбанДоски.УИД = &УИДДоски";
	
	Запрос.УстановитьПараметр("УИДДоски", УИДДоски);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.НомерВерсии;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции // ПолучитьНомерВерсии()

Функция ПолучитьСсылкуПоУИДРеквизита(УИДРеквизита) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанКарточки.Ресурс КАК Ресурс
		|ИЗ
		|	РегистрСведений.КанбанКарточки КАК КанбанКарточки
		|ГДЕ
		|	КанбанКарточки.УИД = &УИДРеквизита";
	
	Запрос.УстановитьПараметр("УИДРеквизита", УИДРеквизита);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ресурс;
	КонецЦикла;   
	
	Возврат Неопределено;

КонецФункции // ПолучитьСсылкуПоУИДРеквизита()

Процедура ПеревестиКарточкуВВыполнено(УИДДоски, Ресурс, Комментарий = "") Экспорт

	УИДКарточка = ПолучитьУИДКарточки(УИДДоски, Ресурс); 
	
	Если ПустаяСтрока(УИДКарточка) Тогда
		Возврат;
	КонецЕсли;
	
	стПараметрыПерехода = омКанбанНачальноеЗаполнение.ПолучитьСтруктуруДляЭлементовКанбана("ПереходКарточки", Комментарий);
	
	стПараметрыПерехода.Доска 			= УИДДоски;
	стПараметрыПерехода.НомерВКолонке 	= 0;
	стПараметрыПерехода.УИДКудаКолонка 	= "1";
	стПараметрыПерехода.УИДКудаДорожка 	= "1";
	стПараметрыПерехода.УИДКарточка 	= УИДКарточка;
	
	УИДПерехода = омКанбанНачальноеЗаполнение.СоздатьДвижениеКарточки(стПараметрыПерехода); 
		
КонецПроцедуры

Функция ПолучитьУИДКарточки(УИДДоски, Ресурс) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанКарточки.УИД КАК УИД
		|ИЗ
		|	РегистрСведений.КанбанКарточки КАК КанбанКарточки
		|ГДЕ
		|	КанбанКарточки.Доска = &Доска
		|	И КанбанКарточки.Ресурс = &Ресурс";
	
	Запрос.УстановитьПараметр("Доска", 	УИДДоски);
	Запрос.УстановитьПараметр("Ресурс", Ресурс);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.УИД;
	КонецЦикла;
	
    Возврат "";
	
КонецФункции // ПолучитьУИДКарточки()

Функция ИнкриментВерсии(УИДДоски) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.КанбанДоски.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.УИД = УИДДоски;
	
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.НомерВерсии = ?(МенеджерЗаписи.НомерВерсии > 999999999, 1, МенеджерЗаписи.НомерВерсии + 1);
	
	МенеджерЗаписи.Записать();

КонецФункции // ИнкриментВерсии()

Функция ПолучитьКомментарийОбъекта(УИДРеквизита) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗ_Изделий.ОбъектТЗ КАК ОбъектТЗ,
		|	МАКСИМУМ(ТЗ_Изделий.Версия) КАК Версия
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрСведений.КанбанКарточки КАК КанбанКарточки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТЗ_Изделий КАК ТЗ_Изделий
		|		ПО КанбанКарточки.Ресурс = ТЗ_Изделий.ОбъектТЗ
		|ГДЕ
		|	КанбанКарточки.УИД = &УИДРеквизита
		|
		|СГРУППИРОВАТЬ ПО
		|	ТЗ_Изделий.ОбъектТЗ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТЗ_Изделий.Комментарий КАК Комментарий,
		|	ТЗ_Изделий.ОбъектТЗ КАК ОбъектТЗ,
		|	ТЗ_Изделий.ОбъектТЗ.Наименование КАК ОбъектТЗНаименование
		|ИЗ
		|	РегистрСведений.ТЗ_Изделий КАК ТЗ_Изделий
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт КАК вт
		|		ПО ТЗ_Изделий.ОбъектТЗ = вт.ОбъектТЗ
		|			И ТЗ_Изделий.Версия = вт.Версия";
	
	Запрос.УстановитьПараметр("УИДРеквизита", УИДРеквизита);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	стВозврата = Новый Структура("УИДРеквизита, ОбъектТЗНаименование, Комментарий, ОбъектТЗ", УИДРеквизита, "", "", Справочники.ИзделияЗаказчика.ПустаяСсылка());
	
	Пока Выборка.Следующий() Цикл 
		ЗаполнитьЗначенияСвойств(стВозврата, Выборка);
	КонецЦикла;   
	
	Возврат стВозврата;
	

КонецФункции // ПолучитьКомментарийОбъекта()


#Область ДополнительныеКоманды 

Процедура СоздатьЗадачуКонтрольЗадачи(УИДКарточки) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанКарточки.Ресурс КАК ОбъектТЗ,
		|	ЕСТЬNULL(КонтрольТЗ.Ссылка, ЗНАЧЕНИЕ(Задача.КонтрольТЗ.ПустаяСсылка)) КАК ЗадачаКонтрольТЗ,
		|	КанбанКарточки.Ресурс.Проект КАК Проект
		|ИЗ
		|	РегистрСведений.КанбанКарточки КАК КанбанКарточки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Задача.КонтрольТЗ КАК КонтрольТЗ
		|		ПО КанбанКарточки.Ресурс = КонтрольТЗ.ОбъектТЗ
		|ГДЕ
		|	КанбанКарточки.УИД = &УИДКарточки";
	
	Запрос.УстановитьПараметр("УИДКарточки", УИДКарточки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(Выборка.ЗадачаКонтрольТЗ) Тогда
			
			ЗадачаКонтрольТЗ = Задачи.КонтрольТЗ.СоздатьЗадачу();
			ЗадачаКонтрольТЗ.Наименование 			= Строка(Выборка.ОбъектТЗ) + " проверить ТЗ";
			ЗадачаКонтрольТЗ.Дата 					= ТекущаяДата();
			ЗадачаКонтрольТЗ.ОбъектТЗ 				= Выборка.ОбъектТЗ;
			ЗадачаКонтрольТЗ.Версия 				= 1;
			ЗадачаКонтрольТЗ.Проект 				= Выборка.Проект;
			ЗадачаКонтрольТЗ.Статус 				= 0;
			ЗадачаКонтрольТЗ.КрайняяДатаИсполнения 	= ЗадачаКонтрольТЗ.Дата + 24 * 3600 * 3;
			ЗадачаКонтрольТЗ.Исполнитель 			= омСотрудники.СотрудникиОбъекта(Выборка.ОбъектТЗ).ВедущийКонструктор;
			
			ЗадачаКонтрольТЗ.Записать();
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


	
#КонецОбласти