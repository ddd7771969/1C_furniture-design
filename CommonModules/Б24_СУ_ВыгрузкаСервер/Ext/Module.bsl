
Процедура ВыгрузкаДанныеПоТипуДанных(СтруктураНастроек, ПолнаяВыгрузка = Ложь) Экспорт

	ТипыОпераций = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыОпераций();
	
	Если СтруктураНастроек.Операция = ТипыОпераций.Цены Тогда 
		
		ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Цена;
		
		Если СтруктураНастроек.НастройкиЦен.Прайсы.Количество() = 0 Тогда     
			
			НаборЗаписей = РегистрыСведений.Б24_СУ_ПакетыВыгрузки.СоздатьНаборЗаписей(); 
			НаборЗаписей.Отбор.НастройкаПодключения.Установить(СтруктураНастроек.НастройкаПодключения);
			НаборЗаписей.Отбор.ТипДанных.Установить(ТипДанных);
			НаборЗаписей.Записать();	
			
			НаборЗаписей = РегистрыСведений.Б24_СУ_ТаблицаИзменений.СоздатьНаборЗаписей();  
			НаборЗаписей.Отбор.НастройкаПодключения.Установить(СтруктураНастроек.НастройкаПодключения);
			НаборЗаписей.Отбор.ТипДанных.Установить(ТипДанных);
			НаборЗаписей.Записать();	
			
			Возврат;  
			
		КонецЕсли;
		
		ФормированиеПакетовДанных(СтруктураНастроек, ТипДанных, ПолнаяВыгрузка);
		
		УдалениеИзменений(СтруктураНастроек.НастройкаПодключения, ТипДанных, СтруктураНастроек.ВремяЗапускаВМиллисекундах);
		
		СформироватьИВыгрузитьНаПортал(СтруктураНастроек, ТипДанных);
		
	ИначеЕсли СтруктураНастроек.Операция = ТипыОпераций.Остатки Тогда
		
		ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Остаток;
		
		Если СтруктураНастроек.НастройкиОстатков.Склады.Количество() = 0 Тогда     
			
			НаборЗаписей = РегистрыСведений.Б24_СУ_ПакетыВыгрузки.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.НастройкаПодключения.Установить(СтруктураНастроек.НастройкаПодключения);
			НаборЗаписей.Отбор.ТипДанных.Установить(ТипДанных);
			НаборЗаписей.Записать();	
			
			НаборЗаписей = РегистрыСведений.Б24_СУ_ТаблицаИзменений.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.НастройкаПодключения.Установить(СтруктураНастроек.НастройкаПодключения);
			НаборЗаписей.Отбор.ТипДанных.Установить(ТипДанных);
			НаборЗаписей.Записать();	
			
			Возврат;  
			
		КонецЕсли;
		
		ФормированиеПакетовДанных(СтруктураНастроек, ТипДанных, ПолнаяВыгрузка);
		
		УдалениеИзменений(СтруктураНастроек.НастройкаПодключения, ТипДанных, СтруктураНастроек.ВремяЗапускаВМиллисекундах);
		
		СформироватьИВыгрузитьНаПортал(СтруктураНастроек, ТипДанных);
		
	ИначеЕсли СтруктураНастроек.Операция = ТипыОпераций.Отгрузки Тогда
		
		ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка;
		
		ФормированиеПакетовДанных(СтруктураНастроек, ТипДанных, ПолнаяВыгрузка);
		
		УдалениеИзменений(СтруктураНастроек.НастройкаПодключения, ТипДанных, СтруктураНастроек.ВремяЗапускаВМиллисекундах);
		
		СформироватьИВыгрузитьНаПортал(СтруктураНастроек, ТипДанных);
		
	ИначеЕсли СтруктураНастроек.Операция = ТипыОпераций.КартинкиФайлыТоваров Тогда
		
		ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.КартинкаФайлТовара2;
		
		ФормированиеПакетовДанных(СтруктураНастроек, ТипДанных, ПолнаяВыгрузка);
		
		УдалениеИзменений(СтруктураНастроек.НастройкаПодключения, ТипДанных, СтруктураНастроек.ВремяЗапускаВМиллисекундах);
		
		СформироватьИВыгрузитьНаПортал(СтруктураНастроек, ТипДанных);
		
	ИначеЕсли СтруктураНастроек.Операция = ТипыОпераций.КартинкиФайлыПредложений Тогда
		
		ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.КартинкаФайлПредложения;
		
		ФормированиеПакетовДанных(СтруктураНастроек, ТипДанных, ПолнаяВыгрузка);
		
		УдалениеИзменений(СтруктураНастроек.НастройкаПодключения, ТипДанных, СтруктураНастроек.ВремяЗапускаВМиллисекундах);
		
		СформироватьИВыгрузитьНаПортал(СтруктураНастроек, ТипДанных);
		
	КонецЕсли;

КонецПроцедуры


Процедура ФормированиеПакетовДанных(СтруктураНастроек, ТипДанных, ПолнаяВыгрузка)

	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Информация, "Формирование пакетов выгрузки для объектов с типом: " + Строка(ТипДанных));
	
	Если ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Цена Тогда

		Запрос = Новый Запрос;   
		Запрос.УстановитьПараметр("ДатаФормирования"			, СтруктураНастроек.ДатаФормирования); 
		Запрос.УстановитьПараметр("Портал"						, СтруктураНастроек.Портал); 
		Запрос.УстановитьПараметр("НастройкаПодключения"		, СтруктураНастроек.НастройкаПодключения); 
		Запрос.УстановитьПараметр("ТипДанныхП"					, СтруктураНастроек.ТипыОбъектовОбмена.Предложение); 
		Запрос.УстановитьПараметр("ВремяЗаписиВМиллисекундах"	, СтруктураНастроек.ВремяЗапускаВМиллисекундах); 
		Запрос.УстановитьПараметр("ТипДанных"					, ТипДанных); 
		Запрос.УстановитьПараметр("Прайсы"						, СтруктураНастроек.НастройкиЦен.Прайсы); 
		Запрос.Текст = "ВЫБРАТЬ
		|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
		|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК Справочник.ХарактеристикиНоменклатуры) КАК ПодчиненныйОбъект
		|ПОМЕСТИТЬ ВТ_Вариации
		|ИЗ
		|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
		|ГДЕ
		|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
		|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхП
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект,
		|	ПодчиненныйОбъект
		|;
		|";
		
		Если ПолнаяВыгрузка Тогда
			
			Запрос.Текст = Запрос.Текст + "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	&НастройкаПодключения КАК НастройкаПодключения,
			|	&ТипДанных КАК ТипДанных,
			|	ВТ_Вариации.Объект КАК Объект,
			|	ВТ_Вариации.ПодчиненныйОбъект КАК ПодчиненныйОбъект
			|ИЗ
			|	ВТ_Вариации КАК ВТ_Вариации";
			
		Иначе
			
			Запрос.Текст = Запрос.Текст + "
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	Б24_СУ_ТаблицаИзменений.Объект КАК Объект,
				|	Б24_СУ_ТаблицаИзменений.ПодчиненныйОбъект КАК ПодчиненныйОбъект
				|ПОМЕСТИТЬ ВТ_Данные
				|ИЗ
				|	РегистрСведений.Б24_СУ_ТаблицаИзменений КАК Б24_СУ_ТаблицаИзменений
				|ГДЕ
				|	Б24_СУ_ТаблицаИзменений.ТипДанных = &ТипДанных
				|	И Б24_СУ_ТаблицаИзменений.НастройкаПодключения = &НастройкаПодключения
				|	И Б24_СУ_ТаблицаИзменений.ВремяЗаписиВМиллисекундах < &ВремяЗаписиВМиллисекундах
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	Б24_СУ_ПакетыВыгрузки.Объект,
				|	Б24_СУ_ПакетыВыгрузки.ПодчиненныйОбъект
				|ИЗ
				|	РегистрСведений.Б24_СУ_ПакетыВыгрузки КАК Б24_СУ_ПакетыВыгрузки
				|ГДЕ
				|	Б24_СУ_ПакетыВыгрузки.НастройкаПодключения = &НастройкаПодключения
				|	И Б24_СУ_ПакетыВыгрузки.ТипДанных = &ТипДанных
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	&НастройкаПодключения КАК НастройкаПодключения,
				|	&ТипДанных КАК ТипДанных,
				|	ВТ_Вариации.Объект КАК Объект,
				|	ВТ_Вариации.ПодчиненныйОбъект КАК ПодчиненныйОбъект
				|ИЗ
				|	ВТ_Данные КАК ВТ_Данные
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Вариации КАК ВТ_Вариации
				|		ПО ВТ_Данные.Объект = ВТ_Вариации.Объект
				|			И ВТ_Данные.ПодчиненныйОбъект = ВТ_Вариации.ПодчиненныйОбъект";  
			
		КонецЕсли;
		
		ТзнДанных = Запрос.Выполнить().Выгрузить();
		
		ЗагрузкаТаблицыДанныхВРегистрПакетов(СтруктураНастроек, ТзнДанных, ТипДанных);     
		
	ИначеЕсли ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Остаток Тогда
		
		Запрос = Новый Запрос;   
		Запрос.УстановитьПараметр("ДатаФормирования"			, СтруктураНастроек.ДатаФормирования); 
		Запрос.УстановитьПараметр("Портал"						, СтруктураНастроек.Портал); 
		Запрос.УстановитьПараметр("НастройкаПодключения"		, СтруктураНастроек.НастройкаПодключения); 
		Запрос.УстановитьПараметр("ТипДанныхП"					, СтруктураНастроек.ТипыОбъектовОбмена.Предложение); 
		Запрос.УстановитьПараметр("ВремяЗаписиВМиллисекундах"	, СтруктураНастроек.ВремяЗапускаВМиллисекундах); 
		Запрос.УстановитьПараметр("ТипДанных"					, ТипДанных); 
		Запрос.УстановитьПараметр("Склады"						, СтруктураНастроек.НастройкиОстатков.Склады); 
		Запрос.Текст = "ВЫБРАТЬ
		|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
		|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК Справочник.ХарактеристикиНоменклатуры) КАК ПодчиненныйОбъект
		|ПОМЕСТИТЬ ВТ_Вариации
		|ИЗ
		|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
		|ГДЕ
		|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
		|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхП
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект,
		|	ПодчиненныйОбъект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Склады.ИдСкладаБ24 КАК ИдСкладаБ24,
		|	Склады.НаименованиеСкладаБ24 КАК НаименованиеСкладаБ24,
		|	Склады.Склад1С КАК Склад1С
		|ПОМЕСТИТЬ ВТ_Склады
		|ИЗ
		|	&Склады КАК Склады
		|ГДЕ
		|	Склады.ИдСкладаБ24 <> """"
		|	И Склады.Склад1С <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|;
		|";
		
		Если ПолнаяВыгрузка Тогда
			
			Запрос.Текст = Запрос.Текст + "
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗЛИЧНЫЕ  
				|	&НастройкаПодключения КАК НастройкаПодключения,
				|	&ТипДанных КАК ТипДанных,
				|	ВТ_Склады.Склад1С КАК Ключ,
				|	ВТ_Вариации.Объект КАК Объект,
				|	ВТ_Вариации.ПодчиненныйОбъект КАК ПодчиненныйОбъект
				|ИЗ
				|	ВТ_Вариации КАК ВТ_Вариации,
				|	ВТ_Склады КАК ВТ_Склады";
			
		Иначе
			
			Запрос.Текст = Запрос.Текст + "
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ 
				|	Б24_СУ_ТаблицаИзменений.Объект КАК Объект,
				|	Б24_СУ_ТаблицаИзменений.ПодчиненныйОбъект КАК ПодчиненныйОбъект,
				|	Б24_СУ_ТаблицаИзменений.Ключ КАК Ключ
				|ПОМЕСТИТЬ ВТ_Данные
				|ИЗ
				|	РегистрСведений.Б24_СУ_ТаблицаИзменений КАК Б24_СУ_ТаблицаИзменений
				|ГДЕ
				|	Б24_СУ_ТаблицаИзменений.ТипДанных = &ТипДанных
				|	И Б24_СУ_ТаблицаИзменений.НастройкаПодключения = &НастройкаПодключения
				|	И Б24_СУ_ТаблицаИзменений.ВремяЗаписиВМиллисекундах < &ВремяЗаписиВМиллисекундах
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	Б24_СУ_ПакетыВыгрузки.Объект,
				|	Б24_СУ_ПакетыВыгрузки.ПодчиненныйОбъект,
				|	Б24_СУ_ПакетыВыгрузки.Ключ
				|ИЗ
				|	РегистрСведений.Б24_СУ_ПакетыВыгрузки КАК Б24_СУ_ПакетыВыгрузки
				|ГДЕ
				|	Б24_СУ_ПакетыВыгрузки.НастройкаПодключения = &НастройкаПодключения
				|	И Б24_СУ_ПакетыВыгрузки.ТипДанных = &ТипДанных
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	&НастройкаПодключения КАК НастройкаПодключения,
				|	&ТипДанных КАК ТипДанных,
				|	ВТ_Склады.Склад1С КАК Ключ,
				|	ВТ_Вариации.Объект КАК Объект,
				|	ВТ_Вариации.ПодчиненныйОбъект КАК ПодчиненныйОбъект
				|ИЗ
				|	ВТ_Данные КАК ВТ_Данные
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Вариации КАК ВТ_Вариации
				|		ПО ВТ_Данные.Объект = ВТ_Вариации.Объект
				|			И ВТ_Данные.ПодчиненныйОбъект = ВТ_Вариации.ПодчиненныйОбъект
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Склады КАК ВТ_Склады
				|		ПО ВТ_Данные.Ключ = ВТ_Склады.Склад1С";  
			
		КонецЕсли;
		
		ТзнДанных = Запрос.Выполнить().Выгрузить();
		
		ЗагрузкаТаблицыДанныхВРегистрПакетов(СтруктураНастроек, ТзнДанных, ТипДанных);
		
	ИначеЕсли ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка Тогда
		
		Запрос = Новый Запрос;   
		Запрос.УстановитьПараметр("ДатаФормирования"			, СтруктураНастроек.ДатаФормирования); 
		Запрос.УстановитьПараметр("Портал"						, СтруктураНастроек.Портал); 
		Запрос.УстановитьПараметр("НастройкаПодключения"		, СтруктураНастроек.НастройкаПодключения); 
		Запрос.УстановитьПараметр("ТипДанныхЗ"					, СтруктураНастроек.ТипыОбъектовОбмена.Заказ); 
		Запрос.УстановитьПараметр("ВремяЗаписиВМиллисекундах"	, СтруктураНастроек.ВремяЗапускаВМиллисекундах); 
		Запрос.УстановитьПараметр("ТипДанных"					, ТипДанных); 
		Запрос.Текст = "ВЫБРАТЬ
		|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект
		|ПОМЕСТИТЬ ВТ_Заказы
		|ИЗ
		|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
		|ГДЕ
		|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
		|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Б24_СУ_ТаблицаИзменений.Объект КАК Объект,
		|	Б24_СУ_ТаблицаИзменений.ПодчиненныйОбъект КАК ПодчиненныйОбъект,
		|	Б24_СУ_ТаблицаИзменений.Ключ КАК Ключ
		|ПОМЕСТИТЬ ВТ_Данные
		|ИЗ
		|	РегистрСведений.Б24_СУ_ТаблицаИзменений КАК Б24_СУ_ТаблицаИзменений
		|ГДЕ
		|	Б24_СУ_ТаблицаИзменений.ТипДанных = &ТипДанных
		|	И Б24_СУ_ТаблицаИзменений.НастройкаПодключения = &НастройкаПодключения
		|	И Б24_СУ_ТаблицаИзменений.ВремяЗаписиВМиллисекундах < &ВремяЗаписиВМиллисекундах
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Б24_СУ_ПакетыВыгрузки.Объект,
		|	Б24_СУ_ПакетыВыгрузки.ПодчиненныйОбъект,
		|	Б24_СУ_ПакетыВыгрузки.Ключ
		|ИЗ
		|	РегистрСведений.Б24_СУ_ПакетыВыгрузки КАК Б24_СУ_ПакетыВыгрузки
		|ГДЕ
		|	Б24_СУ_ПакетыВыгрузки.НастройкаПодключения = &НастройкаПодключения
		|	И Б24_СУ_ПакетыВыгрузки.ТипДанных = &ТипДанных
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НастройкаПодключения КАК НастройкаПодключения,
		|	&ТипДанных КАК ТипДанных,
		|	ВТ_Данные.Объект КАК Объект,
		|	ВТ_Данные.ПодчиненныйОбъект КАК ПодчиненныйОбъект,
		|	ВТ_Данные.Ключ КАК Ключ
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Заказы КАК ВТ_Заказы
		|			ПО ВТ_Заказы.Объект = РеализацияТоваровУслуг.ЗаказКлиента
		|		ПО ВТ_Данные.Объект = РеализацияТоваровУслуг.Ссылка";
		
		ТзнДанных = Запрос.Выполнить().Выгрузить();
		
		ЗагрузкаТаблицыДанныхВРегистрПакетов(СтруктураНастроек, ТзнДанных, ТипДанных);
		
	ИначеЕсли ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.КартинкаФайлТовара2 Тогда
		
		ТипДанныхВладельца = ?(ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.КартинкаФайлТовара2, СтруктураНастроек.ТипыОбъектовОбмена.Товар2, СтруктураНастроек.ТипыОбъектовОбмена.Предложение);
		
		Запрос = Новый Запрос;   
		Запрос.УстановитьПараметр("ДатаФормирования"			, СтруктураНастроек.ДатаФормирования); 
		Запрос.УстановитьПараметр("Портал"						, СтруктураНастроек.Портал); 
		Запрос.УстановитьПараметр("НастройкаПодключения"		, СтруктураНастроек.НастройкаПодключения); 
		Запрос.УстановитьПараметр("ТипДанныхВладельца"			, ТипДанныхВладельца); 
		Запрос.УстановитьПараметр("ВремяЗаписиВМиллисекундах"	, СтруктураНастроек.ВремяЗапускаВМиллисекундах); 
		Запрос.УстановитьПараметр("ТипДанных"					, ТипДанных); 
		Запрос.Текст = "ВЫБРАТЬ
		|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
		|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК Справочник.ХарактеристикиНоменклатуры) КАК ПодчиненныйОбъект
		|ПОМЕСТИТЬ ВТ_Владелец
		|ИЗ
		|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
		|ГДЕ
		|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
		|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхВладельца
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект,
		|	ПодчиненныйОбъект
		|;
		|";
		
		Если ПолнаяВыгрузка Тогда
			
			Запрос.Текст = Запрос.Текст + "
				|////////////////////////////////////////////////////////////////////////////////    
				|ВЫБРАТЬ                  
				|	&НастройкаПодключения КАК НастройкаПодключения,  
				|	&ТипДанных КАК ТипДанных,
				|	НоменклатураПрисоединенныеФайлы.Ссылка КАК Объект    
				|ИЗ
				|	ВТ_Владелец КАК ВТ_Владелец
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
				|		ПО ВТ_Владелец.Объект = НоменклатураПрисоединенныеФайлы.ВладелецФайла"; 
			
		Иначе
			
			Запрос.Текст = Запрос.Текст + "
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	Б24_СУ_ТаблицаИзменений.Объект КАК Объект
				|ПОМЕСТИТЬ ВТ_ДанныеПром
				|ИЗ
				|	РегистрСведений.Б24_СУ_ТаблицаИзменений КАК Б24_СУ_ТаблицаИзменений
				|ГДЕ
				|	Б24_СУ_ТаблицаИзменений.ТипДанных = &ТипДанных
				|	И Б24_СУ_ТаблицаИзменений.НастройкаПодключения = &НастройкаПодключения
				|	И Б24_СУ_ТаблицаИзменений.ВремяЗаписиВМиллисекундах < &ВремяЗаписиВМиллисекундах
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	Б24_СУ_ПакетыВыгрузки.Объект
				|ИЗ
				|	РегистрСведений.Б24_СУ_ПакетыВыгрузки КАК Б24_СУ_ПакетыВыгрузки
				|ГДЕ
				|	Б24_СУ_ПакетыВыгрузки.НастройкаПодключения = &НастройкаПодключения
				|	И Б24_СУ_ПакетыВыгрузки.ТипДанных = &ТипДанных
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ВТ_ДанныеПром.Объект КАК Объект
				|ПОМЕСТИТЬ ВТ_Данные
				|ИЗ
				|	ВТ_ДанныеПром КАК ВТ_ДанныеПром
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
				|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Владелец КАК ВТ_Владелец
				|			ПО (ВТ_Владелец.Объект = НоменклатураПрисоединенныеФайлы.ВладелецФайла)
				|		ПО ВТ_ДанныеПром.Объект = НоменклатураПрисоединенныеФайлы.Ссылка
				|
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	&НастройкаПодключения КАК НастройкаПодключения,
				|	&ТипДанных КАК ТипДанных,
				|	ВТ_Данные.Объект КАК Объект
				|ИЗ
				|	ВТ_Данные КАК ВТ_Данные";    
			
		КонецЕсли;
		
		ТзнДанных = Запрос.Выполнить().Выгрузить();
		
		ЗагрузкаТаблицыДанныхВРегистрПакетов(СтруктураНастроек, ТзнДанных, ТипДанных);
			
	КонецЕсли;

КонецПроцедуры

Процедура ЗагрузкаТаблицыДанныхВРегистрПакетов(СтруктураНастроек, ТаблицаВыгрузки, ТипДанных)
	
	ТаблицаВыгрузки.Колонки.Добавить("Пакет");
	
	РазбивкаТаблицыДанныхНаПакеты(СтруктураНастроек, ТипДанных, ТаблицаВыгрузки);
	
	НаборЗаписей = РегистрыСведений.Б24_СУ_ПакетыВыгрузки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НастройкаПодключения.Установить(СтруктураНастроек.НастройкаПодключения);
	НаборЗаписей.Отбор.ТипДанных.Установить(ТипДанных);
	НаборЗаписей.Загрузить(ТаблицаВыгрузки);
	НаборЗаписей.Записать();             
	
КонецПроцедуры

Процедура РазбивкаТаблицыДанныхНаПакеты(СтруктураНастроек, ТипДанных, Таблица)
	
	Если ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Остаток Тогда
		КоличествоДанныхВПакете = 500;	
	ИначеЕсли ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.КартинкаФайлТовара2 ИЛИ ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.КартинкаФайлПредложения Тогда
		КоличествоДанныхВПакете = Цел(СтруктураНастроек.КоличествоДанныхВПакетеПоУмолчанию/5);	  		
	Иначе
		КоличествоДанныхВПакете = СтруктураНастроек.КоличествоДанныхВПакетеПоУмолчанию;	
	КонецЕсли;
	
	Итератор 	= 0;
	Пакет 		= 1;
	
	Для каждого ТекСтр Из Таблица Цикл
		
		Если Не ЗначениеЗаполнено(ТекСтр.Объект) Тогда
			Продолжить;
		КонецЕсли;
		
		Итератор = Итератор + 1;
		
		Если Итератор > КоличествоДанныхВПакете Тогда
			Пакет = Пакет + 1;
			Итератор = 1;
		КонецЕсли;
		
		ТекСтр.Пакет = Пакет;
		
	КонецЦикла;
	
КонецПроцедуры

Функция КоличествоВыгружаемыхПакетов(СтруктураНастроек, ТипДанных)
	
	Результат = 0;
	
	Запрос = Новый Запрос;    	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	Б24_СУ_ПакетыВыгрузки.Пакет КАК Пакет
	|ИЗ
	|	РегистрСведений.Б24_СУ_ПакетыВыгрузки КАК Б24_СУ_ПакетыВыгрузки
	|ГДЕ
	|	Б24_СУ_ПакетыВыгрузки.ТипДанных = &ТипДанных
	|	И Б24_СУ_ПакетыВыгрузки.НастройкаПодключения = &НастройкаПодключения
	|
	|УПОРЯДОЧИТЬ ПО
	|	Пакет УБЫВ";
	Запрос.УстановитьПараметр("ТипДанных", ТипДанных);
	Запрос.УстановитьПараметр("НастройкаПодключения", СтруктураНастроек.НастройкаПодключения);
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		тзнДанных = ВыполненныйЗапрос.Выгрузить();	
		Результат = тзнДанных[0].Пакет;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьПакетДанных(СтруктураНастроек, ТипДанных, Пакет)
	
	Запрос = Новый Запрос;    	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СУ_ПакетыВыгрузки.Объект КАК Объект,
	|	Б24_СУ_ПакетыВыгрузки.ПодчиненныйОбъект КАК ПодчиненныйОбъект,
	|	Б24_СУ_ПакетыВыгрузки.Ключ КАК Ключ
	|ИЗ
	|	РегистрСведений.Б24_СУ_ПакетыВыгрузки КАК Б24_СУ_ПакетыВыгрузки
	|ГДЕ
	|	Б24_СУ_ПакетыВыгрузки.ТипДанных = &ТипДанных
	|	И Б24_СУ_ПакетыВыгрузки.Пакет = &Пакет
	|	И Б24_СУ_ПакетыВыгрузки.НастройкаПодключения = &НастройкаПодключения
	|
	|УПОРЯДОЧИТЬ ПО
	|	Б24_СУ_ПакетыВыгрузки.Пакет";
	Запрос.УстановитьПараметр("ТипДанных", ТипДанных);
	Запрос.УстановитьПараметр("Пакет", Пакет);
	Запрос.УстановитьПараметр("НастройкаПодключения", СтруктураНастроек.НастройкаПодключения);
	
	тзнДанных = Запрос.Выполнить().Выгрузить();
	
	Возврат тзнДанных; 	
	
КонецФункции

Процедура УдалениеИзПакетаДанных(СтруктураНастроек,  ТипДанных, Ключ, Пакет, Данные) Экспорт
	
	НаборЗаписей = РегистрыСведений.Б24_СУ_ПакетыВыгрузки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пакет.Установить(Пакет);
	НаборЗаписей.Отбор.НастройкаПодключения.Установить(СтруктураНастроек.НастройкаПодключения);
	НаборЗаписей.Отбор.ТипДанных.Установить(ТипДанных);
	
	Если Ключ <> Неопределено Тогда
		НаборЗаписей.Отбор.Ключ.Установить(Ключ);
	КонецЕсли;
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		НаборЗаписей.Отбор.Объект.Установить(Данные.Объект);
		НаборЗаписей.Отбор.ПодчиненныйОбъект.Установить(Данные.ПодчиненныйОбъект);
	Иначе
		НаборЗаписей.Отбор.Объект.Установить(Данные);
	КонецЕсли;
	
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

Процедура УдалениеИзменений(НастройкаПодключения, ТипДанных, ВремяЗапускаВМиллисекундах)

	БлокировкаДанных = Новый БлокировкаДанных;
	БлокировкаДанных.Добавить("РегистрСведений.Б24_СУ_ТаблицаИзменений");
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	Попытка
		БлокировкаДанных.Заблокировать();	
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("НастройкаПодключения"		, НастройкаПодключения);
		Запрос.УстановитьПараметр("ВремяЗаписиВМиллисекундах"	, ВремяЗапускаВМиллисекундах);
		Запрос.УстановитьПараметр("ТипДанных"					, ТипДанных);
		Запрос.Текст = "ВЫБРАТЬ
		|	Б24_СУ_ТаблицаИзменений.НастройкаПодключения КАК НастройкаПодключения,
		|	Б24_СУ_ТаблицаИзменений.ТипДанных КАК ТипДанных,
		|	Б24_СУ_ТаблицаИзменений.Ключ КАК Ключ,
		|	Б24_СУ_ТаблицаИзменений.Объект КАК Объект,
		|	Б24_СУ_ТаблицаИзменений.ПодчиненныйОбъект КАК ПодчиненныйОбъект,
		|	Б24_СУ_ТаблицаИзменений.ВремяЗаписиВМиллисекундах КАК ВремяЗаписиВМиллисекундах
		|ИЗ
		|	РегистрСведений.Б24_СУ_ТаблицаИзменений КАК Б24_СУ_ТаблицаИзменений
		|ГДЕ
		|	Б24_СУ_ТаблицаИзменений.НастройкаПодключения = &НастройкаПодключения
		|	И Б24_СУ_ТаблицаИзменений.ВремяЗаписиВМиллисекундах > &ВремяЗаписиВМиллисекундах
		|	И Б24_СУ_ТаблицаИзменений.ТипДанных = &ТипДанных";
		
		ТзнИзменений = Запрос.Выполнить().Выгрузить();
		
		НаборЗаписей = РегистрыСведений.Б24_СУ_ТаблицаИзменений.СоздатьНаборЗаписей(); 
		НаборЗаписей.Отбор.НастройкаПодключения.Установить(НастройкаПодключения); 
		НаборЗаписей.Отбор.ТипДанных.Установить(ТипДанных); 
		НаборЗаписей.Загрузить(ТзнИзменений); 
		НаборЗаписей.Записать(Истина);

 ЗафиксироватьТранзакцию();
	Исключение
 ОтменитьТранзакцию();
	КонецПопытки;  

КонецПроцедуры


Процедура СформироватьИВыгрузитьНаПортал(СтруктураНастроек, ТипДанных)

	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Информация, "Формирование данных для объектов с типом: " + Строка(ТипДанных));
	
	КоличествоПакетов = КоличествоВыгружаемыхПакетов(СтруктураНастроек, ТипДанных);
	
	Для Пакет = 1 По КоличествоПакетов Цикл
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Информация, "Формирование пакета : " + Строка(Пакет));
		
		МассивДанных = ПолучитьПакетДанных(СтруктураНастроек, ТипДанных, Пакет);
		
		СформированныеДанные = Новый Массив;
		
		Если ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Цена Тогда
			СформированныеДанные = СформироватьДанныеПоЦенам(СтруктураНастроек, МассивДанных, Пакет);	
		ИначеЕсли ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Остаток Тогда
			СформированныеДанные = СформироватьДанныеПоОстаткам(СтруктураНастроек, МассивДанных, Пакет);
		ИначеЕсли ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка Тогда
			СформированныеДанные = СформироватьДанныеПоОтгрузкам(СтруктураНастроек, МассивДанных, Пакет);        
		ИначеЕсли ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.КартинкаФайлТовара2 ИЛИ ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.КартинкаФайлПредложения Тогда
			СформированныеДанные = СформироватьДанныеПоКартинкам(СтруктураНастроек, ТипДанных, МассивДанных, Пакет);        
		КонецЕсли;
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Информация, "Выгрузки данных пакета на портал. Тип данных: " + Строка(ТипДанных));
		
		ВыгрузкаДанныхНаПортал(СтруктураНастроек, ТипДанных, СформированныеДанные, Пакет);
		
	КонецЦикла;

КонецПроцедуры

Процедура ВыгрузкаДанныхНаПортал(СтруктураНастроек, ТипДанных, СформированныеДанные, Пакет)
	
	Если СформированныеДанные.Количество() > 0 Тогда
		
		ТелоHTTPЗапроса= "";
		Для каждого ТекЭлемент Из СформированныеДанные Цикл
			ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + ТекЭлемент;	
		КонецЦикла;
		
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
		
		Если СтруктураОтвета = Неопределено Тогда
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
			Возврат;	
		КонецЕсли;
		
		ВремКлючиИдВладельцев 	= Новый Соответствие;
		ВремКлючиИдДел 			= Новый Соответствие;
		
		result = СтруктураОтвета.Получить("result");
		Если result <> Неопределено Тогда
			
			result2 = result.Получить("result");
			Если result2 <> Неопределено Тогда
				
				Для каждого ТекСтрока Из result2 Цикл      
					
					Если НЕ ТекСтрока.Ключ = "AmountOfGoods" Тогда
						Если ТипЗнч(ТекСтрока) = Тип("Булево") ИЛИ ТипЗнч(ТекСтрока) = Тип("Соответствие") ИЛИ СтрДлина(ТекСтрока.Ключ) < 30 Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
						
					ДанныеКлюча = СтруктураНастроек.КлючиПодчиненныхБатчЗапросов.Получить(ТекСтрока.Ключ);
					
					Если ДанныеКлюча = Неопределено Тогда
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Не найден объект с ид:" + Строка(ТекСтрока.Ключ));  
						СтруктураНастроек.ВыполненоБезОшибок = Ложь;
						Продолжить;
					КонецЕсли;
					
					Если ДанныеКлюча.Тип = СтруктураНастроек.ТипыОбъектовОбмена.Цена Тогда 
						
						УдалениеИзПакетаДанных(СтруктураНастроек, ДанныеКлюча.Тип, ДанныеКлюча.Ключ, Пакет, ДанныеКлюча);
						
					ИначеЕсли ДанныеКлюча.Тип = СтруктураНастроек.ТипыОбъектовОбмена.Остаток Тогда 
						
						МассивДанных = ПолучитьПакетДанных(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Остаток, Пакет);
						Для каждого ТекЭлемент Из МассивДанных Цикл      
							ДанныеКлюча = Новый Структура;
							ДанныеКлюча.Вставить("Объект"			, ТекЭлемент.Объект);
							ДанныеКлюча.Вставить("ПодчиненныйОбъект", ТекЭлемент.ПодчиненныйОбъект);
							УдалениеИзПакетаДанных(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Остаток, ТекЭлемент.Ключ, Пакет, ДанныеКлюча);    
						КонецЦикла;
						
					ИначеЕсли ДанныеКлюча.Тип = СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка Тогда 
						
						shipments = ТекСтрока.Значение.Получить("shipments");
						Если shipments <> Неопределено Тогда
							
							Для каждого ТекИнфОтгрузка Из shipments Цикл
								
								Если ТипЗнч(ТекИнфОтгрузка) = Тип("Соответствие") Тогда
									ТекОтгрузка = ТекИнфОтгрузка; 
								Иначе
									ТекОтгрузка = ТекИнфОтгрузка.Значение; 
								КонецЕсли;
								
								ВнешнийКодОтгрузки 	= ТекОтгрузка.Получить("xmlId"); 
								Отгрузка 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ДокументСсылка.РеализацияТоваровУслуг"), ВнешнийКодОтгрузки);
								ИдОтгрузки 	= Формат(ТекОтгрузка.Получить("id"), "ЧГ=0"); 
								
								Если ЗначениеЗаполнено(Отгрузка) Тогда
									РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ДанныеКлюча.Тип, Отгрузка, ИдОтгрузки); 
									
									УдалениеИзПакетаДанных(СтруктураНастроек, ДанныеКлюча.Тип, ДанныеКлюча.Ключ, Пакет, Отгрузка);
									
									ПозицииОтгрузки = ТекОтгрузка.Получить("shipmentItems");
									Если ТипЗнч(ПозицииОтгрузки) = Тип("Массив") Тогда
										Для Каждого ТоварОтгрузки Из ПозицииОтгрузки Цикл
											
											ИдПозицииОтгрузки	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТоварОтгрузки.Получить("id"),"ЧГ=0"));
											
											КлючСвязиПозицииСтрока	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ТоварОтгрузки.Получить("xmlId"));
											Если Лев(КлючСвязиПозицииСтрока, 2) = "S_" Тогда
												КлючСвязиПозиции = Число(Прав(КлючСвязиПозицииСтрока, СтрДлина(КлючСвязиПозицииСтрока)-2));	
											Иначе
												КлючСвязиПозиции = "";	       // не смогли проанализировать.
											КонецЕсли;
											
											ДанныеОПозиции = Новый Структура;
											ДанныеОПозиции.Вставить("Объект"			, Отгрузка);
											ДанныеОПозиции.Вставить("ПодчиненныйОбъект"	, КлючСвязиПозиции);                                            
											
											РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ТоварыОтгрузки, ДанныеОПозиции, ИдПозицииОтгрузки);
											
										КонецЦикла;
									КонецЕсли; 
									
									УдалениеИзПакетаДанных(СтруктураНастроек, ДанныеКлюча.Тип, ДанныеКлюча.Ключ, Пакет, Отгрузка);
									
								КонецЕсли;
								
							КонецЦикла;
							
						КонецЕсли;   
						
					ИначеЕсли ДанныеКлюча.Тип = СтруктураНастроек.ТипыОбъектовОбмена.КартинкаФайлТовара2 Тогда      
						
						productImage = ТекСтрока.Значение.Получить("productImage");
						Если productImage <> Неопределено Тогда
							ИдФайла = Формат(productImage.Получить("id"), "ЧГ=0");                
							РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ДанныеКлюча.Тип, ДанныеКлюча.Объект, ИдФайла);   
							УдалениеИзПакетаДанных(СтруктураНастроек, ДанныеКлюча.Тип, ДанныеКлюча.Ключ, Пакет, ДанныеКлюча);
						КонецЕсли;
						
					ИначеЕсли ДанныеКлюча.Тип = СтруктураНастроек.ТипыОбъектовОбмена.КартинкаФайлПредложения Тогда 
						
						productImage = ТекСтрока.Значение.Получить("productImage");
						Если productImage <> Неопределено Тогда
							ИдФайла = Формат(productImage.Получить("id"), "ЧГ=0"); 
							РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ДанныеКлюча.Тип, ДанныеКлюча.Объект, ИдФайла); 
							УдалениеИзПакетаДанных(СтруктураНастроек, ДанныеКлюча.Тип, ДанныеКлюча.Ключ, Пакет, ДанныеКлюча);
						КонецЕсли;
						
					КонецЕсли;  
					           
				КонецЦикла;
			КонецЕсли;
			
			result_error = result.Получить("result_error"); 
			Если result_error <> Неопределено Тогда
				
				Для каждого ТекСтрока Из result_error Цикл     					
					
					Если ТипЗнч(ТекСтрока) <> Тип("Соответствие") Тогда                  
					
						ДанныеКлюча = СтруктураНастроек.КлючиПодчиненныхБатчЗапросов.Получить(ТекСтрока.Ключ);
						
						Если ДанныеКлюча = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						лОписаниеОшибки = ТекСтрока.Значение.Получить("error_description");
						
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, лОписаниеОшибки + " у объекта:" + Строка(ДанныеКлюча.Объект)); 
						
						РазобратьОшибку(СтруктураНастроек, ДанныеКлюча.Тип, ДанныеКлюча.Объект, лОписаниеОшибки);
						
						СтруктураНастроек.ВыполненоБезОшибок = Ложь;
						
					Иначе
						
						лОписаниеОшибки = ТекСтрока.Получить("error_description");
						
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, лОписаниеОшибки); 

						СтруктураНастроек.ВыполненоБезОшибок = Ложь;
						
					КонецЕсли;					
					
				КонецЦикла;
				
			КонецЕсли;
		Иначе   
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось разобрать JSON ответа");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Тело отправляемого запроса: " + ТелоHTTPЗапроса);
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РазобратьОшибку(СтруктураНастроек, ТипДанных, Объект, ОписаниеОшибки)
	
	Если ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Цена Тогда    
		
		//Если СтрНайти(ОписаниеОшибки, "Неверный ID типа цены") > 0 Тогда	
		//	мЧасти = СтрРазделить(Объект, "#");
		//	Если мЧасти.Количество() = 3 Тогда
		//		Прайс = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.ВидыЦен"), мЧасти[0]); 
		//	    Если ЗначениеЗаполнено(Прайс) Тогда
		//			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.УдалитьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Прайс, Прайс);  
		//		КонецЕсли;
		//	КонецЕсли;
		//КонецЕсли;  
		
	ИначеЕсли ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Остаток Тогда
		
		
	ИначеЕсли ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка Тогда
		
		Если СтрНайти(ОписаниеОшибки, "Can't find shipment") > 0 ИЛИ СтрНайти(ОписаниеОшибки, "Отгрузка не найдена") > 0 Тогда
			
			ПервоеВхождение = СтрНайти(ОписаниеОшибки, "- ");	
			ВтороеВхождение = СтрНайти(ОписаниеОшибки, " ",,ПервоеВхождение+2);	
			
			Если ВтороеВхождение > 0 Тогда
				КодЭлемента 	= СокрЛП(Сред(ОписаниеОшибки, ПервоеВхождение+1, ВтороеВхождение - ПервоеВхождение-1)); 
			Иначе
				КодЭлемента 	= СокрЛП(Прав(ОписаниеОшибки, СтрДлина(ОписаниеОшибки)-ПервоеВхождение));	
			КонецЕсли;
			
			Отгрузка = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, ТипДанных, КодЭлемента);
			
			Если ЗначениеЗаполнено(Отгрузка) Тогда
				
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.УдалитьИдБ24Объекта(СтруктураНастроек, ТипДанных, Отгрузка.Объект);  
					
				НаборЗаписейИд = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.СоздатьНаборЗаписей();
				НаборЗаписейИд.Отбор.Портал.Установить(СтруктураНастроек.Портал);
				НаборЗаписейИд.Отбор.ТипДанных.Установить(СтруктураНастроек.ТипыОбъектовОбмена.ТоварыОтгрузки);
				НаборЗаписейИд.Отбор.Объект.Установить(Отгрузка.Объект);
				НаборЗаписейИд.Записать();
					
			КонецЕсли;
			
		КонецЕсли;
		
		
		
	КонецЕсли; 
	
КонецПроцедуры


Функция СформироватьДанныеПоЦенам(пСтруктураНастроек, МассивДанных, Пакет) Экспорт
	            
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	    
	
	ИспользуетсяЦенообразование20 = ПолучитьФункциональнуюОпцию("ИспользуетсяЦенообразование20");
	
	мЭлементы = Новый массив;     
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;    
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Данные"				, МассивДанных);   
	Запрос.УстановитьПараметр("Портал"				, СтруктураНастроек.Портал); 
	Запрос.УстановитьПараметр("ТипДанныхПредложение", СтруктураНастроек.ТипыОбъектовОбмена.Предложение); 
	Запрос.УстановитьПараметр("Прайсы"				, СтруктураНастроек.НастройкиЦен.Прайсы); 
	Запрос.УстановитьПараметр("ДатаФормирования"	, СтруктураНастроек.ДатаФормирования); 
	Запрос.Текст = "ВЫБРАТЬ
	|	Данные.Объект КАК Объект,
	|	Данные.ПодчиненныйОбъект КАК ПодчиненныйОбъект
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	&Данные КАК Данные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	ЕСТЬNULL(ХарактеристикиНоменклатуры.ХарактеристикаНоменклатурыДляЦенообразования, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)) КАК ХарактеристикаЦО
	|ПОМЕСТИТЬ ВТ_Вариации
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Данные КАК ВТ_Данные
	|		ПО Б24_К_ИдентификаторыОбъектов.Объект = ВТ_Данные.Объект
	|			И Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект = ВТ_Данные.ПодчиненныйОбъект
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект = ХарактеристикиНоменклатуры.Ссылка
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхПредложение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Данные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Прайсы.ИдПрайсаБ24 КАК ИдПрайсаБ24,
	|	Прайсы.НаименованиеПрайсаБ24 КАК НаименованиеПрайсаБ24,
	|	Прайсы.Прайс1С КАК Прайс1С
	|ПОМЕСТИТЬ ВТ_Прайсы
	|ИЗ
	|	&Прайсы КАК Прайсы
	|ГДЕ
	|	Прайсы.ИдПрайсаБ24 <> """"
	|	И Прайсы.Прайс1С <> ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка) 
	|;   
	|////////////////////////////////////////////////////////////////////////////////     
	|";
	Если ИспользуетсяЦенообразование20 Тогда
		Запрос.Текст =  Запрос.Текст + "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатурыСрезПоследних.Характеристика КАК Характеристика,
		|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена,
		|	ЦеныНоменклатурыСрезПоследних.ВидЦены КАК ВидЦены
		|ПОМЕСТИТЬ ВТ_Цены_ПРОМ
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|			&ДатаФормирования,
		|			ВидЦены В
		|					(ВЫБРАТЬ
		|						ВТ_Прайсы.Прайс1С
		|					ИЗ
		|						ВТ_Прайсы)
		|				И (Номенклатура, Характеристика) В
		|					(ВЫБРАТЬ
		|						ВТ_Вариации.Объект,
		|						ВТ_Вариации.ПодчиненныйОбъект
		|					ИЗ
		|						ВТ_Вариации)) КАК ЦеныНоменклатурыСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;   
		|////////////////////////////////////////////////////////////////////////////////     
		|"; 
	Иначе       
		Запрос.Текст =  Запрос.Текст + "
		|	ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЦеныНоменклатуры25СрезПоследних.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатуры25СрезПоследних.ХарактеристикаЦО КАК ХарактеристикаЦО,
		|	ЦеныНоменклатуры25СрезПоследних.Цена КАК Цена,
		|	ЦеныНоменклатуры25СрезПоследних.ВидЦены КАК ВидЦены
		|ПОМЕСТИТЬ ВТ_ЦеныЦО
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры25.СрезПоследних(
		|			&ДатаФормирования,
		|			ВидЦены В
		|					(ВЫБРАТЬ
		|						ВТ_Прайсы.Прайс1С
		|					ИЗ
		|						ВТ_Прайсы)
		|				И (Номенклатура, ХарактеристикаЦО) В
		|					(ВЫБРАТЬ
		|						ВТ_Вариации.Объект,
		|						ВТ_Вариации.ХарактеристикаЦО
		|					ИЗ
		|						ВТ_Вариации)) КАК ЦеныНоменклатуры25СрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ЦеныЦО.Номенклатура КАК Номенклатура,
		|	ВТ_ЦеныЦО.Цена КАК Цена,
		|	ВТ_ЦеныЦО.ВидЦены КАК ВидЦены,
		|	ВТ_Вариации.ПодчиненныйОбъект КАК Характеристика
		|ПОМЕСТИТЬ ВТ_Цены_ПРОМ
		|ИЗ
		|	ВТ_ЦеныЦО КАК ВТ_ЦеныЦО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Вариации КАК ВТ_Вариации
		|		ПО ВТ_ЦеныЦО.Номенклатура = ВТ_Вариации.Объект
		|			И ВТ_ЦеныЦО.ХарактеристикаЦО = ВТ_Вариации.ХарактеристикаЦО
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ЦеныЦО
		|;   
		|////////////////////////////////////////////////////////////////////////////////     
		|";
	КонецЕсли;  
	
	Запрос.Текст =  Запрос.Текст + "
	|ВЫБРАТЬ
	|	ВТ_Цены_ПРОМ.Номенклатура КАК Номенклатура,
	|	ВТ_Цены_ПРОМ.Характеристика КАК Характеристика,
	|	ВТ_Цены_ПРОМ.Цена КАК Цена,
	|	ВТ_Прайсы.ИдПрайсаБ24 КАК ИдентификаторПрайсаБ24,
	|	СправочникВалюты.Код КАК КодВалюты
	|ПОМЕСТИТЬ ВТ_Цены
	|ИЗ
	|	ВТ_Цены_ПРОМ КАК ВТ_Цены_ПРОМ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Прайсы КАК ВТ_Прайсы
	|		ПО ВТ_Цены_ПРОМ.ВидЦены = ВТ_Прайсы.Прайс1С
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Валюты КАК СправочникВалюты
	|			ПО ВидыЦен.ВалютаЦены = СправочникВалюты.Ссылка
	|		ПО ВТ_Цены_ПРОМ.ВидЦены = ВидыЦен.Ссылка 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Цены_ПРОМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Прайсы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Вариации.Идентификатор КАК ИдентификаторВладельцаБ24,
	|	ВТ_Вариации.Объект КАК Объект,
	|	ВТ_Вариации.ПодчиненныйОбъект КАК ПодчиненныйОбъект
	|ИЗ
	|	ВТ_Вариации КАК ВТ_Вариации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СформированныйЗапрос = СформироватьСтруктуруДанныхПоЦене(СтруктураНастроек, МенеджерВременныхТаблиц, Пакет, Выборка);
		
		Если ЗначениеЗаполнено(СформированныйЗапрос) Тогда
			мЭлементы.Добавить(СформированныйЗапрос);
		КонецЕсли;
		
	КонецЦикла;
	
	пСтруктураНастроек.КлючиПодчиненныхБатчЗапросов = СтруктураНастроек.КлючиПодчиненныхБатчЗапросов; 	// чтобы лог не затерся.
	пСтруктураНастроек.Логирование.НомерСообщения 	= СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат мЭлементы;	
	
КонецФункции

Функция СформироватьСтруктуруДанныхПоЦене(СтруктураНастроек, МенеджерВременныхТаблиц, Пакет, ИнформацияЦены)
	
	СтрокаЗапроса = "";
	
	ЕстьДанные = Ложь;     
	
	КлючЦены = XMLСтрока(ИнформацияЦены.Объект)+"#"+XMLСтрока(ИнформацияЦены.ПодчиненныйОбъект);	
	
	ТелоЗапроса = "fields[product][id]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИнформацияЦены.ИдентификаторВладельцаБ24);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц; 
	Запрос.УстановитьПараметр("Номенклатура"	, ИнформацияЦены.Объект);
	Запрос.УстановитьПараметр("Характеристика"	, ИнформацияЦены.ПодчиненныйОбъект);
	Запрос.Текст = 	"ВЫБРАТЬ
	|	ВТ_Цены.Цена КАК Цена,
	|	ВТ_Цены.КодВалюты КАК КодВалюты,                  
	|	ВТ_Цены.ИдентификаторПрайсаБ24 КАК ИдентификаторПрайсаБ24
	|ИЗ
	|	ВТ_Цены КАК ВТ_Цены
	|ГДЕ
	|	ВТ_Цены.Номенклатура = &Номенклатура
	|	И ВТ_Цены.Характеристика = &Характеристика"; 
	ВыполненныйЗапрос = Запрос.Выполнить();
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Итератор = 0;
		ВыборкаДанных = ВыполненныйЗапрос.Выбрать();
		Пока ВыборкаДанных.Следующий() Цикл  
			
			Итератор 	= Итератор + 1;
			ЕстьДанные	= Истина;
			
			Если НЕ ЗначениеЗаполнено(ВыборкаДанных.ИдентификаторПрайсаБ24) Тогда
				Продолжить;
			КонецЕсли;      
			
			ИтераторСтрокой = Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Формат(Итератор, "ЧГ=0"));
			КодВалюты = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьВалютуПоКоду(Лев(ВыборкаДанных.КодВалюты, 3));
			
			ТелоЗапроса = ТелоЗапроса + "&fields[product][prices]["+ИтераторСтрокой+"][catalogGroupId]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ВыборкаДанных.ИдентификаторПрайсаБ24);
			ТелоЗапроса = ТелоЗапроса + "&fields[product][prices]["+ИтераторСтрокой+"][currency]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(КодВалюты);
			ТелоЗапроса = ТелоЗапроса + "&fields[product][prices]["+ИтераторСтрокой+"][price]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ВыборкаДанных.Цена);
			
		КонецЦикла;
	
	КонецЕсли;
	
	Если ЕстьДанные Тогда
		СтрокаЗапроса = "cmd["+КлючЦены+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("catalog.price.modify?" + ТелоЗапроса);	 
		СтруктураНастроек.КлючиПодчиненныхБатчЗапросов.Вставить(КлючЦены, ПолучитьСтруктуруКлючейБатчЗапросов(СтруктураНастроек.ТипыОбъектовОбмена.Цена, ИнформацияЦены.Объект, ИнформацияЦены.ПодчиненныйОбъект));
	Иначе           
		
		ДанныеКлюча = Новый Структура;
		ДанныеКлюча.Вставить("Объект"			, ИнформацияЦены.Объект);
		ДанныеКлюча.Вставить("ПодчиненныйОбъект", ИнформацияЦены.ПодчиненныйОбъект);
		
		УдалениеИзПакетаДанных(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Цена, Неопределено, Пакет, ДанныеКлюча);    
		
	КонецЕсли;
	
	Возврат СтрокаЗапроса;
	
КонецФункции


Функция СформироватьДанныеПоОстаткам(СтруктураНастроек, МассивДанных, Пакет)
	            
	мЭлементы = Новый массив;     
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;            
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц; 
	Запрос.УстановитьПараметр("Данные"				, МассивДанных);   
	Запрос.УстановитьПараметр("Портал"				, СтруктураНастроек.Портал); 
	Запрос.УстановитьПараметр("ТипДанныхПредложение", СтруктураНастроек.ТипыОбъектовОбмена.Предложение); 
	Запрос.УстановитьПараметр("Склады"				, СтруктураНастроек.НастройкиОстатков.Склады); 
	Запрос.УстановитьПараметр("ДатаФормирования"	, СтруктураНастроек.ДатаФормирования); 
	Запрос.Текст = "ВЫБРАТЬ
	|	Данные.Ключ КАК Ключ,
	|	Данные.Объект КАК Объект,
	|	Данные.ПодчиненныйОбъект КАК ПодчиненныйОбъект
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	&Данные КАК Данные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Данные.Ключ КАК Ключ,
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Вариации
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Данные КАК ВТ_Данные
	|		ПО Б24_К_ИдентификаторыОбъектов.Объект = ВТ_Данные.Объект
	|			И Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект = ВТ_Данные.ПодчиненныйОбъект
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхПредложение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Данные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.ИдСкладаБ24 КАК ИдСкладаБ24,
	|	Склады.НаименованиеСкладаБ24 КАК НаименованиеСкладаБ24,
	|	Склады.Склад1С КАК Склад1С
	|ПОМЕСТИТЬ ВТ_Склады
	|ИЗ
	|	&Склады КАК Склады
	|ГДЕ
	|	Склады.ИдСкладаБ24 <> """"
	|	И Склады.Склад1С <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспределениеЗапасов.Склад КАК Склад,
	|	РаспределениеЗапасов.Номенклатура КАК Номенклатура,
	|	РаспределениеЗапасов.Характеристика КАК Характеристика,
	|	СУММА(РаспределениеЗапасов.ВНаличии) КАК Количество,
	|	СУММА(РаспределениеЗапасов.ВНаличии - РаспределениеЗапасов.Свободно) КАК Резерв
	|ПОМЕСТИТЬ ВТ_Запасы
	|ИЗ
	|	РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
	|ГДЕ
	|	РаспределениеЗапасов.Склад В
	|			(ВЫБРАТЬ
	|				ВТ_Склады.Склад1С
	|			ИЗ
	|				ВТ_Склады)
	|	И (РаспределениеЗапасов.Склад, РаспределениеЗапасов.Номенклатура, РаспределениеЗапасов.Характеристика) В
	|			(ВЫБРАТЬ
	|				ВТ_Вариации.Ключ,
	|				ВТ_Вариации.Объект,
	|				ВТ_Вариации.ПодчиненныйОбъект
	|			ИЗ
	|				ВТ_Вариации)
	|	И РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспределениеЗапасов.Склад,
	|	РаспределениеЗапасов.Номенклатура,
	|	РаспределениеЗапасов.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Склад,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Склады.ИдСкладаБ24 КАК ИдентификаторСкладаБ24,
	|	ВТ_Вариации.Идентификатор КАК ИдентификаторВладельцаБ24,
	|	ВТ_Склады.Склад1С КАК Склад1С,
	|	ВТ_Вариации.Объект КАК Номенклатура,
	|	ВТ_Вариации.ПодчиненныйОбъект КАК Характеристика
	|ИЗ
	|	ВТ_Вариации КАК ВТ_Вариации,
	|	ВТ_Склады КАК ВТ_Склады
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Склады
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Вариации";
	
	Итератор 	= 0;
	ТелоЗапроса = "";
	
	КлючОстатков = XMLСтрока("AmountOfGoods");	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл;
		
		Если НЕ ЗначениеЗаполнено(Выборка.ИдентификаторВладельцаБ24) Тогда
			Продолжить;	
		КонецЕсли;                  
		
		Если НЕ ЗначениеЗаполнено(Выборка.ИдентификаторСкладаБ24) Тогда
			Продолжить;	
		КонецЕсли;                  
		
		Итератор = Итератор + 1;
		ИтераторСтрокой = Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Формат(Итератор, "ЧГ=0"));
		
		Резерв 	= 0;
		Остаток = 0;
		
		ЗапросОстатка = Новый Запрос;      
		ЗапросОстатка.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		ЗапросОстатка.УстановитьПараметр("Склад"			, Выборка.Склад1С);
		ЗапросОстатка.УстановитьПараметр("Номенклатура"		, Выборка.Номенклатура);
		ЗапросОстатка.УстановитьПараметр("Характеристика"	, Выборка.Характеристика);
		ЗапросОстатка.Текст ="ВЫБРАТЬ
		|	ВТ_Запасы.Количество КАК Количество,
		|	ВТ_Запасы.Резерв КАК Резерв
		|ИЗ
		|	ВТ_Запасы КАК ВТ_Запасы
		|ГДЕ
		|	ВТ_Запасы.Склад = &Склад 
		|	И ВТ_Запасы.Номенклатура = &Номенклатура 
		|	И ВТ_Запасы.Характеристика = &Характеристика"; 
		
		ВыполненныйЗапросОстатка = ЗапросОстатка.Выполнить();
		Если НЕ ВыполненныйЗапросОстатка.Пустой() Тогда        
			ВыборкаОстатка = ВыполненныйЗапросОстатка.Выбрать();
			Пока ВыборкаОстатка.Следующий() Цикл
				Резерв 	= ВыборкаОстатка.Резерв;
				Остаток = ВыборкаОстатка.Количество;    	  
			КонецЦикла;
		КонецЕсли;
		
		ТелоЗапроса = ТелоЗапроса + "items["+ИтераторСтрокой+"][productId]=" + Выборка.ИдентификаторВладельцаБ24; 
		ТелоЗапроса = ТелоЗапроса + "&items["+ИтераторСтрокой+"][storeId]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Выборка.ИдентификаторСкладаБ24); 
		ТелоЗапроса = ТелоЗапроса + "&items["+ИтераторСтрокой+"][value][amount]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Остаток); 
		ТелоЗапроса = ТелоЗапроса + "&items["+ИтераторСтрокой+"][value][quantityReserved]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Резерв) + "&"; 
		
	КонецЦикла; 
	
	Если ЗначениеЗаполнено(ТелоЗапроса) Тогда
		мЭлементы.Добавить("cmd["+КлючОстатков+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("catalog.storeproduct.bulksave?" + ТелоЗапроса));   
		СтруктураНастроек.КлючиПодчиненныхБатчЗапросов.Вставить(КлючОстатков, ПолучитьСтруктуруКлючейБатчЗапросов(СтруктураНастроек.ТипыОбъектовОбмена.Остаток, Неопределено));   
	Иначе
		Для каждого ТекЭлемент Из МассивДанных Цикл        
			ДанныеКлюча = Новый Структура;
			ДанныеКлюча.Вставить("Объект"			, ТекЭлемент.Объект);
			ДанныеКлюча.Вставить("ПодчиненныйОбъект", ТекЭлемент.ПодчиненныйОбъект);
			УдалениеИзПакетаДанных(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Остаток, ТекЭлемент.Ключ, Пакет, ДанныеКлюча);    
		КонецЦикла;
	КонецЕсли;
	
	Возврат мЭлементы;	
	
КонецФункции


Функция СформироватьДанныеПоОтгрузкам(СтруктураНастроек, МассивДанных, Пакет)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;	
	
#Область ФормированиеВременныхТаблицДанных      
	мОтгрузки = МассивДанных.ВыгрузитьКолонку("Объект");
	
	Запрос = Новый Запрос;     
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("мОтгрузки"			, мОтгрузки);
   	Запрос.УстановитьПараметр("Портал"				, СтруктураНастроек.Портал);
	Запрос.УстановитьПараметр("ТипДанныхЗаказов"	, СтруктураНастроек.ТипыОбъектовОбмена.Заказ);
	Запрос.УстановитьПараметр("ТоварыЗаказа"		, СтруктураНастроек.ТипыОбъектовОбмена.ТоварыЗаказа);
	Запрос.УстановитьПараметр("ТоварыОтгрузки"		, СтруктураНастроек.ТипыОбъектовОбмена.ТоварыОтгрузки);  
	Запрос.УстановитьПараметр("СвойствоСлужбаДоставки", СтруктураНастроек.НастройкииЗаказов.СвойствоСлужбаДоставки);  
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.ЗаказКлиента КАК Заказ
	|ПОМЕСТИТЬ ВТ_Заказы
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&мОтгрузки)
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслуг.ЗаказКлиента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.Объект КАК Документ.ЗаказКлиента) КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗаказов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗаказов
	|	И Б24_К_ИдентификаторыОбъектов.Объект В
	|			(ВЫБРАТЬ
	|				ВТ_Заказы.Заказ
	|			ИЗ
	|				ВТ_Заказы)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Заказы.Заказ КАК Заказ,
	|	РеализацияТоваровУслуг.Ссылка КАК Отгрузка
	|ПОМЕСТИТЬ ВТ_Отгрузки
	|ИЗ
	|	ВТ_Заказы КАК ВТ_Заказы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ВТ_Заказы.Заказ = РеализацияТоваровУслуг.ЗаказКлиента
	|ГДЕ
	|	РеализацияТоваровУслуг.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Заказы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Отгрузки.Отгрузка КАК Отгрузка,
	|	ВТ_ИдентификаторыЗаказов.Объект КАК Заказ,
	|	ВТ_ИдентификаторыЗаказов.Идентификатор КАК ИдентификаторЗаказаБитрикс24
	|ПОМЕСТИТЬ ВТ_ВыгружаемыеОтгрузки
	|ИЗ
	|	ВТ_Отгрузки КАК ВТ_Отгрузки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыЗаказов КАК ВТ_ИдентификаторыЗаказов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
	|			ПО ВТ_ИдентификаторыЗаказов.Объект = ЗаказКлиента.Ссылка
	|		ПО ВТ_Отгрузки.Заказ = ВТ_ИдентификаторыЗаказов.Объект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугДополнительныеРеквизиты.Значение КАК Значение,
	|	ВТ_ВыгружаемыеОтгрузки.Отгрузка КАК Отгрузка
	|ПОМЕСТИТЬ ВТ_СлужбыДоставокОтгрузок
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ДополнительныеРеквизиты КАК РеализацияТоваровУслугДополнительныеРеквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВыгружаемыеОтгрузки КАК ВТ_ВыгружаемыеОтгрузки
	|		ПО РеализацияТоваровУслугДополнительныеРеквизиты.Ссылка = ВТ_ВыгружаемыеОтгрузки.Отгрузка
	|ГДЕ
	|	РеализацияТоваровУслугДополнительныеРеквизиты.Свойство = &СвойствоСлужбаДоставки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Отгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыгружаемыеОтгрузки.Заказ КАК Заказ,
	|	ВТ_ВыгружаемыеОтгрузки.ИдентификаторЗаказаБитрикс24 КАК ИдентификаторЗаказаБитрикс24
	|ПОМЕСТИТЬ ВТ_Заказы
	|ИЗ
	|	ВТ_ВыгружаемыеОтгрузки КАК ВТ_ВыгружаемыеОтгрузки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВыгружаемыеОтгрузки.Заказ,
	|	ВТ_ВыгружаемыеОтгрузки.ИдентификаторЗаказаБитрикс24
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ИдентификаторыЗаказов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Отгрузка,
	|	РеализацияТоваровУслуг.ЗаказКлиента КАК Заказ,
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Проведен КАК Проведен,
	|	РеализацияТоваровУслуг.Склад КАК Склад,
	|	РеализацияТоваровУслуг.Комментарий КАК Комментарий,
	|	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	|	РеализацияТоваровУслуг.Менеджер КАК Ответственный,
	|	РеализацияТоваровУслуг.ПометкаУдаления КАК ПометкаУдаления,
	|	Валюты.Ссылка КАК ВалютаДокумента,
	|	Валюты.Код КАК ВалютаКод,
	|	РеализацияТоваровУслуг.ЦенаВключаетНДС КАК СуммаВключаетНДС,
	|	ВТ_СлужбыДоставокОтгрузок.Значение КАК СлужбаДоставки
	|ПОМЕСТИТЬ ВТ_ИнформацияВыгружаемыхОтгрузках
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВыгружаемыеОтгрузки КАК ВТ_ВыгружаемыеОтгрузки
	|		ПО РеализацияТоваровУслуг.Ссылка = ВТ_ВыгружаемыеОтгрузки.Отгрузка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Валюты КАК Валюты
	|		ПО РеализацияТоваровУслуг.ВалютаВзаиморасчетов = Валюты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СлужбыДоставокОтгрузок КАК ВТ_СлужбыДоставокОтгрузок
	|		ПО РеализацияТоваровУслуг.Ссылка = ВТ_СлужбыДоставокОтгрузок.Отгрузка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_СлужбыДоставокОтгрузок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.Объект КАК Документ.ЗаказКлиента) КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК ИдКорзины,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_ИдентификаторыПозицийЗаказов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТоварыЗаказа
	|	И Б24_К_ИдентификаторыОбъектов.Объект В
	|			(ВЫБРАТЬ
	|				ВТ_ВыгружаемыеОтгрузки.Заказ
	|			ИЗ
	|				ВТ_ВыгружаемыеОтгрузки)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.Объект КАК Документ.РеализацияТоваровУслуг) КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК ИдентификаторПозиции,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_ИдентификаторыПозицийОтгрузок
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТоварыОтгрузки
	|	И Б24_К_ИдентификаторыОбъектов.Объект В(ВЫБРАТЬ ВТ_ВыгружаемыеОтгрузки.Отгрузка ИЗ ВТ_ВыгружаемыеОтгрузки)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Ссылка КАК Отгрузка,
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслугТовары.Характеристика КАК Характеристика,
	|	РеализацияТоваровУслугТовары.Количество КАК Количество,  
	|	РеализацияТоваровУслугТовары.СуммаНДС КАК СуммаНДС,
	|	РеализацияТоваровУслугТовары.Цена КАК Цена,
	|	РеализацияТоваровУслугТовары.КлючСвязи КАК КлючСвязи,
	|	ВТ_ИдентификаторыПозицийОтгрузок.ИдентификаторПозиции КАК ИдПозицииОтгрузки
	|ПОМЕСТИТЬ ВТ_ТоварыОтгрузок
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО РеализацияТоваровУслугТовары.Ссылка = РеализацияТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыПозицийОтгрузок КАК ВТ_ИдентификаторыПозицийОтгрузок
	|		ПО РеализацияТоваровУслугТовары.Ссылка = ВТ_ИдентификаторыПозицийОтгрузок.Объект    
	|			И РеализацияТоваровУслугТовары.КлючСвязи = ВТ_ИдентификаторыПозицийОтгрузок.КлючСвязи
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_ВыгружаемыеОтгрузки.Отгрузка
	|			ИЗ
	|				ВТ_ВыгружаемыеОтгрузки)
	|	И РеализацияТоваровУслугТовары.Количество > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Отгрузка   
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ИдентификаторыПозицийОтгрузок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Заказы.Заказ КАК Заказ,
	|	ЗаказКлиентаТовары.КлючСвязи КАК КлючСвязи,
	|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
	|	ЗаказКлиентаТовары.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_ТоварыЗаказов
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Заказы КАК ВТ_Заказы
	|		ПО ЗаказКлиентаТовары.Ссылка = ВТ_Заказы.Заказ
	|ГДЕ
	|	ЗаказКлиентаТовары.Количество > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заказ   
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИдентификаторыПозицийЗаказов.ИдКорзины КАК ИдКорзины,
	|	ВТ_ИдентификаторыПозицийЗаказов.КлючСвязи КАК КлючСвязи,
	|	ВТ_ТоварыЗаказов.Заказ КАК Заказ,
	|	ВТ_ТоварыЗаказов.Номенклатура КАК Номенклатура,
	|	ВТ_ТоварыЗаказов.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ВТ_ПозицииЗаказа
	|ИЗ
	|	ВТ_ИдентификаторыПозицийЗаказов КАК ВТ_ИдентификаторыПозицийЗаказов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТоварыЗаказов КАК ВТ_ТоварыЗаказов
	|		ПО ВТ_ИдентификаторыПозицийЗаказов.Объект = ВТ_ТоварыЗаказов.Заказ
	|			И ВТ_ИдентификаторыПозицийЗаказов.КлючСвязи = ВТ_ТоварыЗаказов.КлючСвязи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТоварыЗаказов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ИдентификаторыПозицийЗаказов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Заказы.Заказ КАК Заказ,
	|	ВТ_Заказы.ИдентификаторЗаказаБитрикс24 КАК ИдентификаторЗаказаБитрикс24
	|ИЗ
	|	ВТ_Заказы КАК ВТ_Заказы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ВыгружаемыеОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Заказы";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
#КонецОбласти	
	
	мЭлементы 	=  Новый массив;
	
	Пока Выборка.Следующий() Цикл

		СформированныйЗапрос = СформироватьСтруктуруДанныхПоОтгрузке(СтруктураНастроек, Пакет, Выборка, МенеджерВременныхТаблиц);
		
		Если ЗначениеЗаполнено(СформированныйЗапрос) Тогда
			мЭлементы.Добавить(СформированныйЗапрос);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат мЭлементы;	
	
КонецФункции    

Функция СформироватьСтруктуруДанныхПоОтгрузке(СтруктураНастроек, Пакет, ИнформацияЗаказа, МенеджерВременныхТаблиц)
	
	КлючЗаказа 	= XMLСтрока(ИнформацияЗаказа.Заказ);

	ЕстьКртическаяОшибка = Ложь;
	
	ПрефиксВнешнихКодовБитрикс24 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();
	ПрефиксВнешнихКодов1С 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьОписаниеПрефиксовВнешнихКодов1С();
	
	ИдентификаторЗаказаБ24 	= Прав(ИнформацияЗаказа.ИдентификаторЗаказаБитрикс24, СтрДлина(ИнформацияЗаказа.ИдентификаторЗаказаБитрикс24)-СтрДлина(ПрефиксВнешнихКодовБитрикс24.Заказы));
	ТелоЗапроса = "fields[order][id]=" + ИдентификаторЗаказаБ24;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Заказ", ИнформацияЗаказа.Заказ);
	Запрос.Текст = " ВЫБРАТЬ *
	|ИЗ
	|	ВТ_ИнформацияВыгружаемыхОтгрузках КАК ВТ_ИнформацияВыгружаемыхОтгрузках
	|ГДЕ
	|	ВТ_ИнформацияВыгружаемыхОтгрузках.Заказ = &Заказ";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	Если ВыполненныйЗапрос.Пустой() Тогда
		Возврат "";
	КонецЕсли;      
	
	НомерОтгрузки = 1;
	
	ВыборкаОтгрузок = ВыполненныйЗапрос.Выбрать();
	Пока ВыборкаОтгрузок.Следующий() Цикл
		
		ИнформацияОтгрузки 		= ВыборкаОтгрузок;          
		КлючОтгрузки 			= XMLСтрока(ИнформацияОтгрузки.Отгрузка);	
		ИдентификаторБ24 		= ИдБ24ПоСсылке(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка, ИнформацияОтгрузки.Отгрузка);	
		
		ИдДоставки = Неопределено;
		Если СтруктураНастроек.НастройкииЗаказов.Свойство("СпособыДоставки") Тогда
			НайденнаяСтрока = СтруктураНастроек.НастройкииЗаказов.СпособыДоставки.Найти(ВыборкаОтгрузок.СлужбаДоставки, "Служба1С");	
			Если НайденнаяСтрока <> Неопределено Тогда
				ИдДоставки = НайденнаяСтрока.ИдСлужбыБ24;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ИдДоставки) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Не заполнена служба доставки у отгрузки: " + Строка(ИнформацияОтгрузки.Отгрузка));       
			ЕстьКртическаяОшибка = Истина;
			Прервать;
		КонецЕсли;
		
		ИдОтветственного = Неопределено;	
		Если ЗначениеЗаполнено(ИнформацияОтгрузки.Ответственный) Тогда
			НайденнаяСтрока = СтруктураНастроек.ТаблицаСопоставленияПользователей.Найти(ИнформацияОтгрузки.Ответственный, "Пользователь1С");
			Если НайденнаяСтрока <> Неопределено Тогда
				ИдОтветственного = НайденнаяСтрока.ИдПользователя; 					
			КонецЕсли;  
		КонецЕсли;  
		
		СуммаДоставки = 0;   
		
		ЗапросДоставки = Новый Запрос;
		ЗапросДоставки.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		ЗапросДоставки.УстановитьПараметр("Отгрузка", ИнформацияОтгрузки.Отгрузка);
		ЗапросДоставки.УстановитьПараметр("НоменклатураДоставки", СтруктураНастроек.НастройкииЗаказов.НоменклатураДоставка);
		ЗапросДоставки.Текст = "ВЫБРАТЬ       
		|	ВТ_ТоварыОтгрузок.СуммаНДС КАК СуммаНДС,
		|	ВТ_ТоварыОтгрузок.Цена КАК Цена
		|ИЗ
		|	ВТ_ТоварыОтгрузок КАК ВТ_ТоварыОтгрузок
		|ГДЕ
		|	ВТ_ТоварыОтгрузок.Номенклатура = &НоменклатураДоставки
		|	И ВТ_ТоварыОтгрузок.Отгрузка = &Отгрузка"; 
		ВыполненныйЗапросДоставки = ЗапросДоставки.Выполнить(); 
		
		Если НЕ ВыполненныйЗапросДоставки.Пустой() Тогда
			ВыборкаДоставки = ВыполненныйЗапросДоставки.Выбрать();
			Пока ВыборкаДоставки.Следующий() Цикл  
				Если ИнформацияОтгрузки.СуммаВключаетНДС Тогда
					СуммаДоставки = ВыборкаДоставки.Цена;	
				Иначе
					СуммаДоставки = ВыборкаДоставки.Цена + ВыборкаДоставки.СуммаНДС;	
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли;	
			
		ЭтоНовыйЭлемент = НЕ ЗначениеЗаполнено(ИдентификаторБ24);
			                        
		Поля = Новый Структура;    
		
		Если НЕ ЭтоНовыйЭлемент Тогда Поля.Вставить("id", ИдентификаторБ24) КонецЕсли;
		
		Поля.Вставить("xmlId"	, КлючОтгрузки); 
		Поля.Вставить("deducted", ?(ИнформацияОтгрузки.Проведен, "Y", "N")); 
		
		Поля.Вставить("deliveryDocNum"		, ИнформацияОтгрузки.Номер); 
		Поля.Вставить("deliveryDocDate"		, ИнформацияОтгрузки.Дата); 
		Поля.Вставить("comments"			, ИнформацияОтгрузки.Комментарий); 
		
		Поля.Вставить("allowDelivery"		, "Y"); 
		Поля.Вставить("customPriceDelivery"	, "Y"); 
		Поля.Вставить("priceDelivery"		, СуммаДоставки); 
		Поля.Вставить("basePriceDelivery"	, СуммаДоставки); 
		Поля.Вставить("responsibleId"		, ИдОтветственного);  
		Поля.Вставить("deliveryId"			, ИдДоставки);
		
		ВалютаСимвольныйКод = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьВалютуПоКоду(Лев(ИнформацияОтгрузки.ВалютаКод, 3));
		Если ЗначениеЗаполнено(ВалютаСимвольныйКод) Тогда Поля.Вставить("currency", ВалютаСимвольныйКод) КонецЕсли;	

		НомерОтгрузкиСтрокой = Формат(НомерОтгрузки, "ЧГ=0");      
		
		Для каждого ТекПоле Из Поля Цикл
			Если ТекПоле.Значение <> Неопределено Тогда
				ТелоЗапроса = ТелоЗапроса + ?(СтрДлина(ТелоЗапроса) >0, "&", "") +"fields[order][shipments]["+НомерОтгрузкиСтрокой+"]["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
			КонецЕсли;		
		КонецЦикла;  

		ТоварыОтгрузки = Неопределено;
#Область ТоварыОтгрузки	
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Отгрузка", ВыборкаОтгрузок.Отгрузка);
		Запрос.Текст = " ВЫБРАТЬ *
		|ИЗ
		|	ВТ_ТоварыОтгрузок КАК ВТ_ТоварыОтгрузок
		|ГДЕ
		|	ВТ_ТоварыОтгрузок.Отгрузка = &Отгрузка";
		ТоварыОтгрузки = Запрос.Выполнить().Выгрузить();
#КонецОбласти	

		НомерПозицииОтгрузки = 1;     
		
		Для каждого ТоварОтгрузки Из ТоварыОтгрузки Цикл
			
			Если ТоварОтгрузки.Номенклатура = СтруктураНастроек.НастройкииЗаказов.НоменклатураДоставка Тогда
				Продолжить;
			КонецЕсли;
			
			ИдентификаторКорзиныЗаказа = "";
#Область ЗаполнениеТабличнойЧасти
			ЗапросПозицииЗаказа = Новый Запрос;
			ЗапросПозицииЗаказа.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			ЗапросПозицииЗаказа.УстановитьПараметр("Заказ"			, ИнформацияЗаказа.Заказ);	
			ЗапросПозицииЗаказа.УстановитьПараметр("Номенклатура"		, ТоварОтгрузки.Номенклатура);	
			ЗапросПозицииЗаказа.УстановитьПараметр("Характеристика"	, ТоварОтгрузки.Характеристика);	
			ЗапросПозицииЗаказа.Текст = "ВЫБРАТЬ * ИЗ ВТ_ПозицииЗаказа ГДЕ ВТ_ПозицииЗаказа.Заказ = &Заказ И ВТ_ПозицииЗаказа.Номенклатура = &Номенклатура И ВТ_ПозицииЗаказа.Характеристика = &Характеристика";
			
			ВыполненныйЗапросПозицийЗаказа = ЗапросПозицииЗаказа.Выполнить();
			Если НЕ ВыполненныйЗапросПозицийЗаказа.Пустой() Тогда
				ВыборкаПозицийЗаказа = ВыполненныйЗапросПозицийЗаказа.Выбрать();
				Пока ВыборкаПозицийЗаказа.Следующий() Цикл
					ИдентификаторКорзиныЗаказа = ВыборкаПозицийЗаказа.ИдКорзины; 
					Прервать;
				КонецЦикла;
			КонецЕсли;
#КонецОбласти	

			Если ЗначениеЗаполнено(ИдентификаторКорзиныЗаказа) Тогда

				ПоляТЧ = Новый Структура;
				
				ПоляТЧ.Вставить("basketId", ИдентификаторКорзиныЗаказа);
				
				Если ЗначениеЗаполнено(ТоварОтгрузки.ИдПозицииОтгрузки) Тогда ПоляТЧ.Вставить("id", ТоварОтгрузки.ИдПозицииОтгрузки) КонецЕсли;
				
				ПоляТЧ.Вставить("xmlId"				, "S_" + Формат(ТоварОтгрузки.КлючСвязи, "ЧГ=0"));
				ПоляТЧ.Вставить("quantity"			, ТоварОтгрузки.Количество);
				//ПоляТЧ.Вставить("orderDeliveryId"	, ИдДоставки);
				//ПоляТЧ.Вставить("reservedQuantity", ТоварОтгрузки.Количество);
				
				НомерПозицииОтгрузкиСтрокой = Формат(НомерПозицииОтгрузки, "ЧГ=0");
				
				Для каждого ТекПоле Из ПоляТЧ Цикл
					Если ТекПоле.Значение <> Неопределено Тогда
						ТелоЗапроса = ТелоЗапроса + "&fields[order][shipments]["+НомерОтгрузкиСтрокой+"][shipmentItems]["+НомерПозицииОтгрузкиСтрокой+"]["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
					КонецЕсли;		
				КонецЦикла;  
				
				НомерПозицииОтгрузки = НомерПозицииОтгрузки + 1;
				
			КонецЕсли;	
			
		КонецЦикла;

	  	НомерОтгрузки = НомерОтгрузки + 1;

 	КонецЦикла;
	
	Если ЕстьКртическаяОшибка Тогда
		Возврат "";
	КонецЕсли;
	
	СтрокаЗапроса = "cmd["+КлючЗаказа+"]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("sale.shipment.modify?"  + ТелоЗапроса);  
	
	СтруктураНастроек.КлючиПодчиненныхБатчЗапросов.Вставить(КлючЗаказа, ПолучитьСтруктуруКлючейБатчЗапросов(СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка, ИнформацияЗаказа.Заказ));
	
	Возврат СтрокаЗапроса;

КонецФункции      


Функция СформироватьДанныеПоКартинкам(СтруктураНастроек, ТипДанных, МассивДанных, Пакет) Экспорт

	мДанных = МассивДанных.ВыгрузитьКолонку("Объект");
	
	ТипДанныхВладельца = ?(ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.КартинкаФайлТовара2, СтруктураНастроек.ТипыОбъектовОбмена.Товар2, СтруктураНастроек.ТипыОбъектовОбмена.Предложение);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("мДанных"				, мДанных);
	Запрос.УстановитьПараметр("ТипДанных"			, ТипДанных);
	Запрос.УстановитьПараметр("ТипДанныхВладельца"	, ТипДанныхВладельца);
	Запрос.УстановитьПараметр("Портал"				, СтруктураНастроек.Портал);	
	Запрос.УстановитьПараметр("СписокРасширенийКартинок", Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьМассивРазрешенныхРасширенийКартинок());  
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыОбъектов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Объект В(&мДанных)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыВладельцев
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхВладельца
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НоменклатураПрисоединенныеФайлы.ВладелецФайла КАК ВладелецФайла,
	|	НоменклатураПрисоединенныеФайлы.Ссылка КАК Файл,
	|	НоменклатураПрисоединенныеФайлы.Наименование КАК Наименование,
	|	НоменклатураПрисоединенныеФайлы.ТипХраненияФайла КАК ТипХраненияФайла,
	|	НоменклатураПрисоединенныеФайлы.Расширение КАК Расширение,
	|	НоменклатураПрисоединенныеФайлы.ДатаСоздания КАК ДатаСоздания,
	|	НоменклатураПрисоединенныеФайлы.Том КАК Том,
	|	(НоменклатураПрисоединенныеФайлы.ПутьКФайлу) КАК ПутьКФайлу,
	|	ВТ_ИдентификаторыОбъектов.Идентификатор КАК ИдентификаторФайла,
	|	ВТ_ИдентификаторыВладельцев.Идентификатор КАК ИдентификаторВладельца
	|ПОМЕСТИТЬ ВТ_ПрисоединенныеФайлы
	|ИЗ
	|	ВТ_ИдентификаторыВладельцев КАК ВТ_ИдентификаторыВладельцев
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыОбъектов КАК ВТ_ИдентификаторыОбъектов
	|			ПО (ВТ_ИдентификаторыОбъектов.Объект = НоменклатураПрисоединенныеФайлы.Ссылка)
	|		ПО ВТ_ИдентификаторыВладельцев.Объект = НоменклатураПрисоединенныеФайлы.ВладелецФайла
	|ГДЕ
	|	&ТипДанных = ЗНАЧЕНИЕ(Перечисление.Б24_К_ТипыДанныхДляОбменаСПорталом.КартинкаФайлТовара2)
	|	И НЕ НоменклатураПрисоединенныеФайлы.ПометкаУдаления
	|	И НоменклатураПрисоединенныеФайлы.Ссылка В(&мДанных)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ИдентификаторыОбъектов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ИдентификаторыВладельцев
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвоичныеДанныеФайлов.Файл КАК Файл,
	|	ДвоичныеДанныеФайлов.Файл КАК ХранимыйФайл,
	|	ДвоичныеДанныеФайлов.ДвоичныеДанныеФайла КАК ДвоичныеДанныеФайла
	|ПОМЕСТИТЬ ВТ_ДвоичныеДанныеФайлов
	|ИЗ
	|	РегистрСведений.ДвоичныеДанныеФайлов КАК ДвоичныеДанныеФайлов
	|ГДЕ
	|	ДвоичныеДанныеФайлов.Файл В
	|			(ВЫБРАТЬ
	|				ВТ_ПрисоединенныеФайлы.Файл
	|			ИЗ
	|				ВТ_ПрисоединенныеФайлы)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Файл
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПрисоединенныеФайлы.ВладелецФайла КАК ВладелецФайла,
	|	ВТ_ПрисоединенныеФайлы.Файл КАК Идентификатор1С,
	|	ВТ_ПрисоединенныеФайлы.ИдентификаторФайла КАК ИдентификаторБ24,
	|	ВТ_ПрисоединенныеФайлы.ИдентификаторВладельца КАК ИдентификаторВладельцаБ24,
	|	ВТ_ПрисоединенныеФайлы.Файл КАК Файл,
	|	ВТ_ПрисоединенныеФайлы.Наименование КАК Наименование,
	|	ВТ_ПрисоединенныеФайлы.ТипХраненияФайла КАК ТипХраненияФайла,
	|	ВТ_ПрисоединенныеФайлы.Расширение КАК Расширение,
	|	ВТ_ДвоичныеДанныеФайлов.ХранимыйФайл КАК ХранимыйФайл,
	|	ВТ_ПрисоединенныеФайлы.ДатаСоздания КАК ДатаСоздания,
	|	ВЫБОР
	|		КОГДА ВТ_ПрисоединенныеФайлы.Расширение В (&СписокРасширенийКартинок)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоКартинка,
	|	ВТ_ПрисоединенныеФайлы.ПутьКФайлу КАК ПутьКФайлу,
	|	ВТ_ДвоичныеДанныеФайлов.ДвоичныеДанныеФайла КАК ДвоичныеДанныеФайла,
	|	ТомаХраненияФайлов.ПолныйПутьLinux КАК ТомПолныйПутьLinux,
	|	ТомаХраненияФайлов.ПолныйПутьWindows КАК ТомПолныйПутьWindows
	|ИЗ
	|	ВТ_ПрисоединенныеФайлы КАК ВТ_ПрисоединенныеФайлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДвоичныеДанныеФайлов КАК ВТ_ДвоичныеДанныеФайлов
	|		ПО ВТ_ПрисоединенныеФайлы.Файл = ВТ_ДвоичныеДанныеФайлов.Файл
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТомаХраненияФайлов КАК ТомаХраненияФайлов
	|		ПО ВТ_ПрисоединенныеФайлы.Том = ТомаХраненияФайлов.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ПрисоединенныеФайлы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ДвоичныеДанныеФайлов";
	Выборка = Запрос.Выполнить().Выбрать();
	
	мЭлементы 	=  Новый массив;
	
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.ИдентификаторБ24) Тогда
    		СформированныйЗапрос = "cmd[]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("catalog.productImage.delete?id="+Выборка.ИдентификаторБ24+"&productId="+Выборка.ИдентификаторВладельцаБ24); 
			мЭлементы.Добавить(СформированныйЗапрос);
		КонецЕсли;
		
		СформированныйЗапрос = СформироватьСтруктуруДанныхПоФайламТоваровПредложений(СтруктураНастроек, ТипДанных, Пакет, Выборка, НЕ ЗначениеЗаполнено(Выборка.ИдентификаторБ24));
		
		Если ЗначениеЗаполнено(СформированныйЗапрос) Тогда
			мЭлементы.Добавить(СформированныйЗапрос);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат мЭлементы;	
	
КонецФункции

Функция СформироватьСтруктуруДанныхПоФайламТоваровПредложений(СтруктураНастроек, ТипДанных, Пакет, ИнформацияФайла, ЭтоНовыйЭлемент)
	
	КлючФайла = XMLСтрока(ИнформацияФайла.Идентификатор1С);	
	
	ТелоЗапроса = "&fields[productId]="+ИнформацияФайла.ИдентификаторВладельцаБ24; 
	ТелоЗапроса = ТелоЗапроса + "&fields[type]=MORE_PHOTO";	
	
	НаименованиеФайла = ИнформацияФайла.Наименование + ?(Лев(ИнформацияФайла.Расширение, 1) = ".",ИнформацияФайла.Расширение, "." + ИнформацияФайла.Расширение);
	
	ТелоЗапроса = ТелоЗапроса + "&fileContent[1]="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(НаименованиеФайла); 
			
	Если ИнформацияФайла.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе Тогда
		Если ЗначениеЗаполнено(ИнформацияФайла.ДвоичныеДанныеФайла) Тогда
			ДвоичныеДанныеФайла = ИнформацияФайла.ДвоичныеДанныеФайла.Получить();   
			
			Если ТипЗнч(ДвоичныеДанныеФайла) = Тип("ДвоичныеДанные") Тогда
				ТелоЗапроса = ТелоЗапроса + "&fileContent[2]="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Base64Строка(ДвоичныеДанныеФайла));
			Иначе    
				ТекстИнформации = "У владельца картинки '" + Строка(ИнформацияФайла.ВладелецФайла) + "' Картинка '"+ИнформацияФайла.Наименование+"' записана не в двоичных данных";	
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Информация, ТекстИнформации);
				УдалениеИзПакетаДанных(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Цена, Неопределено, Пакет, ИнформацияФайла.Файл);    
				Возврат "";	
			КонецЕсли;
			
		КонецЕсли;		
	ИначеЕсли ИнформацияФайла.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
		ИмяФайла = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьАдресФайлаТома(СтруктураНастроек.ПлатформаWindows, ИнформацияФайла); 
		Если ЗначениеЗаполнено(ИмяФайла) И Б24_К_ОбщегоНазначенияВызовСервера.СуществуетФайл(ИмяФайла) Тогда              
			ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ИмяФайла);
			ТелоЗапроса = ТелоЗапроса + "&fileContent[2]="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Base64Строка(ДвоичныеДанныеФайла)); 
		КонецЕсли;   
	Иначе 
		УдалениеИзПакетаДанных(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Цена, Неопределено, Пакет, ИнформацияФайла.Файл);    
		Возврат "";	
	КонецЕсли;       
	
	СтрокаЗапроса = "cmd["+КлючФайла+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("catalog.productImage.add?" + ТелоЗапроса); 	
	
	СтруктураНастроек.КлючиПодчиненныхБатчЗапросов.Вставить(КлючФайла, ПолучитьСтруктуруКлючейБатчЗапросов(ТипДанных, ИнформацияФайла.Файл));
	
	Возврат СтрокаЗапроса;
	
КонецФункции


Функция ИдБ24ПоСсылке(Портал, ТипДанных, Объект, ПодчиненныйОбъект = Неопределено)
	
	Результат = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьИдОбъекта(Портал, ТипДанных, Объект, ПодчиненныйОбъект);

	Если НЕ ЗначениеЗаполнено(Результат) Тогда Результат = Неопределено КонецЕсли;
	
	Возврат Результат;
	
КонецФункции  

Функция ПолучитьСтруктуруКлючейБатчЗапросов(ТипДанных, Объект, ПодчиненныйОбъект = Неопределено, Ключ = Неопределено, ЗаписыватьПриИстина = Ложь) 
	
	Результат = Новый Структура;
	
	Результат.Вставить("Тип"						, ТипДанных);
	Результат.Вставить("Объект"						, Объект);
	Результат.Вставить("ПодчиненныйОбъект"			, ПодчиненныйОбъект);
	Результат.Вставить("Ключ"						, Ключ);
	Результат.Вставить("ЗаписыватьПриИстина"		, ЗаписыватьПриИстина);
					
	Возврат Результат;
	
КонецФункции
