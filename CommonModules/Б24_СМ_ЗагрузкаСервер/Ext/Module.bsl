
#Область ПроцедурыИФункцииФормированияДанныхЗагрузки

Функция ЗагрузитьОбновитьЭлементыСмартПроцесса(пСтруктураНастроек, ЗагружаемыйСмартПроцесс, ДанныеСмартПроцесса) Экспорт
	
	Результат = Новый Массив; 
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
#Область ФормированиеВременныхТаблицДанных	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Портал"						, СтруктураНастроек.Портал);	
	Запрос.УстановитьПараметр("ТипВладельцаСмартПроцесса"	, ЗагружаемыйСмартПроцесс.Ключ);	
	Запрос.УстановитьПараметр("ИдСмартПроцесса"				, ЗагружаемыйСмартПроцесс.Значение.ИдСмартПроцесса);	
	Запрос.УстановитьПараметр("ТипСущности1С"				, ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ТипСущности1С);	
	Запрос.УстановитьПараметр("Сущность1С"					, ?(ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.Сущность1С <> Неопределено, ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.Сущность1С.Значение, ""));	
	
	Запрос.УстановитьПараметр("ТипДанныхСчетов"				, СтруктураНастроек.ТипыОбъектовОбмена.Счет2);
	Запрос.УстановитьПараметр("ТипДанныхСделок"				, СтруктураНастроек.ТипыОбъектовОбмена.Сделка);
	Запрос.УстановитьПараметр("ТипДанныхЗаказов"			, СтруктураНастроек.ТипыОбъектовОбмена.Заказ);
	Запрос.УстановитьПараметр("ТипДанныхКомпаний"			, СтруктураНастроек.ТипыОбъектовОбмена.Компания);
	Запрос.УстановитьПараметр("ТипДанныхКонтактов"			, СтруктураНастроек.ТипыОбъектовОбмена.Контакт);	
	Запрос.УстановитьПараметр("ТипДанныхТоваров"			, СтруктураНастроек.ТипыОбъектовОбмена.Товар);			
	Запрос.УстановитьПараметр("ТипДанныхЗССП"				, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваСмартПроцесса);			
	Запрос.УстановитьПараметр("ЭтоТовар2"					, СтруктураНастроек.ВерсияТоваров = "v.2");
	Запрос.УстановитьПараметр("ТипДанныхТоваров"			, СтруктураНастроек.ТипыОбъектовОбмена.Товар);
	Запрос.УстановитьПараметр("ТипДанныхТоваров2"			, СтруктураНастроек.ТипыОбъектовОбмена.Товар2);
	Запрос.УстановитьПараметр("ТипДанныхПредложений"		, СтруктураНастроек.ТипыОбъектовОбмена.Предложение);	
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СМ_ИдентификаторыОбъектов.ИдентификаторОбъекта КАК Объект,
	|	Б24_СМ_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыСмартПроцесса
	|ИЗ
	|	РегистрСведений.Б24_СМ_ИдентификаторыОбъектов КАК Б24_СМ_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_СМ_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_СМ_ИдентификаторыОбъектов.ТипВладельцаСмартПроцесса = &ТипВладельцаСмартПроцесса
	|	И Б24_СМ_ИдентификаторыОбъектов.ИдСмартПроцесса = &ИдСмартПроцесса
	|	И Б24_СМ_ИдентификаторыОбъектов.ТипСущности1С = &ТипСущности1С
	|	И Б24_СМ_ИдентификаторыОбъектов.Сущность1С = &Сущность1С
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийПользовательскихПолей
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗССП
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор,
	|	ДополнительныйИдентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ПользователиБитрикс24.Пользователь1С КАК Объект,
	|	Б24_К_ПользователиБитрикс24.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыПользователей
	|ИЗ
	|	РегистрСведений.Б24_К_ПользователиБитрикс24 КАК Б24_К_ПользователиБитрикс24
	|ГДЕ
	|	Б24_К_ПользователиБитрикс24.Портал = &Портал
	|;
	|
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ
	//|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.Объект КАК Документ.ЗаказКлиента) КАК Объект,
	//|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	//|ПОМЕСТИТЬ ВТ_ИдентификаторыСделокЗаказов
	//|ИЗ
	//|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	//|ГДЕ
	//|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	//|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхСделок
	//|
	//|ОБЪЕДИНИТЬ ВСЕ
	//|
	//|ВЫБРАТЬ
	//|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.Объект КАК Документ.ЗаказКлиента),
	//|	Б24_К_ИдентификаторыОбъектов.Идентификатор
	//|ИЗ
	//|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	//|ГДЕ
	//|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	//|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗаказов
	//|
	//|ИНДЕКСИРОВАТЬ ПО
	//|	Идентификатор
	//|;
	|     
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыСчетов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхСчетов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыКомпаний
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКомпаний
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыКонтактов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКонтактов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ
	//|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.Объект КАК Справочник.Номенклатура) КАК Объект,
	//|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	//|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК Справочник.ХарактеристикиНоменклатуры) КАК ПодчиненныйОбъект
	//|ПОМЕСТИТЬ ВТ_ИдентификаторыТоваров
	//|ИЗ
	//|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	//|ГДЕ   
	//|	НЕ &ЭтоТовар2
	//|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	//|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхТоваров
	//|                                 
	//|ОБЪЕДИНИТЬ ВСЕ
	//|
	//|ВЫБРАТЬ
	//|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.Объект КАК Справочник.Номенклатура),
	//|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	//|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК Справочник.ХарактеристикиНоменклатуры)
	//|ИЗ
	//|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	//|ГДЕ   
	//|	&ЭтоТовар2
	//|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	//|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхТоваров2
	//|                                 
	//|ОБЪЕДИНИТЬ ВСЕ
	//|
	//|ВЫБРАТЬ
	//|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.Объект КАК Справочник.Номенклатура),
	//|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	//|	ВЫРАЗИТЬ(Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК Справочник.ХарактеристикиНоменклатуры)
	//|ИЗ
	//|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	//|ГДЕ   
	//|	&ЭтоТовар2
	//|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	//|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхПредложений
	//|                                 
	//|ИНДЕКСИРОВАТЬ ПО
	//|	Идентификатор\";
	|";
	Запрос.Выполнить();
#КонецОбласти
	
	ОбновлятьВ1С 				 	= ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ОбновлятьВ1С;
	АлгоритмПриЗагрузкеДанныхВ1С 	= ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.АлгоритмПриЗагрузкеДанныхВ1С;
	АлгоритмПослеЗагрузкиДанныхВ1С	= "";
	ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.Свойство("АлгоритмПослеЗагрузкиДанныхВ1С", АлгоритмПослеЗагрузкиДанныхВ1С);
	
	ОтборыДляЗагрузкиДанныхВ1С 	 = ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ТаблицаОтборовПоЗагрузкеДанныхВ1С;
	
	ПоляШапки		= ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ТаблицаСоответствийЗагрузки;
	ПоляТЧ 			= ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ТаблицаСоответствийЗагрузкиТабЧасть;
	
	Сущность1С 		= ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.Сущность1С.Значение;
	ТипСущности1С 	= ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ТипСущности1С;
	
	ПроводитьВ1С = Ложь;
	ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.Свойство("ПроводитьВ1С", ПроводитьВ1С);
	
	ЗагруженныеОбъекты = Новый Массив;
	
	Для каждого ТекЭлемент Из ДанныеСмартПроцесса Цикл	
		
		ВнешнийКод 			= ТекЭлемент.Получить("xmlId"); 
		ЗаголовокЭлемента 	= ТекЭлемент.Получить("title");
		
		ИдЭлемента 			= Формат(ТекЭлемент.Получить("id"),"ЧГ=0");
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);
		
		НужноЗагружать = ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, МенеджерВременныхТаблиц, ТекЭлемент, ОтборыДляЗагрузкиДанныхВ1С);
		Если НЕ НужноЗагружать Тогда
			Продолжить;	
		КонецЕсли;
		
#Область ПоискСозданиеЭлемента

		ТипЭлементаСмартПроцесса 	= Неопределено;
		МенеджерОбъекта 			= Неопределено;

		Если ТипСущности1С = "Справочник" Тогда
			ТипЭлементаСмартПроцесса = Тип("СправочникСсылка." + Сущность1С);
			МенеджерОбъекта = Справочники[Сущность1С];
		ИначеЕсли ТипСущности1С = "Документ" Тогда
			ТипЭлементаСмартПроцесса = Тип("ДокументСсылка." + Сущность1С);
			МенеджерОбъекта = Документы[Сущность1С];
		Иначе
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			Продолжить;
		КонецЕсли;

		ЭлементСсылка = Неопределено;

		УникальныйИдентификаторЭлемента = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыСмартПроцесса", ИдЭлемента);	
		Если ЗначениеЗаполнено(УникальныйИдентификаторЭлемента) Тогда
			ЭлементСсылка = XMLЗначение(ТипЭлементаСмартПроцесса, УникальныйИдентификаторЭлемента); 
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВнешнийКод) И НЕ ЗначениеЗаполнено(ЭлементСсылка) Тогда
			ЭлементСсылка = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(ТипЭлементаСмартПроцесса, ВнешнийКод);
		КонецЕсли;
		
		
		Если ЗначениеЗаполнено(ЭлементСсылка) Тогда
			
			Если НЕ ОбновлятьВ1С Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что элементы смарт процесса: "+ЗагружаемыйСмартПроцесс.Значение.Наименование+" не обновляются. Элемент: " + ЗаголовокЭлемента + " не будет обновлен.");
				
				СтруктураДляЗаписиИдентификатора = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьСтруктуруДляЗаписиИдентификатора();
				СтруктураДляЗаписиИдентификатора.ТипВладельцаСмартПроцесса 	= ЗагружаемыйСмартПроцесс.Ключ;
				СтруктураДляЗаписиИдентификатора.ИдСмартПроцесса 			= ЗагружаемыйСмартПроцесс.Значение.ИдСмартПроцесса;
				СтруктураДляЗаписиИдентификатора.ТипСущности1С 				= ТипСущности1С;
				СтруктураДляЗаписиИдентификатора.Сущность1С 				= Сущность1С;
				СтруктураДляЗаписиИдентификатора.Объект 					= ЭлементСсылка;
				СтруктураДляЗаписиИдентификатора.Идентификатор 				= ИдЭлемента;
				РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураДляЗаписиИдентификатора);
				
				Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
				
				Продолжить;
				
			Иначе
				ЭлементОбъект 	= ЭлементСсылка.ПолучитьОбъект();
			КонецЕсли;
			
		Иначе
			Если ТипСущности1С = "Документ" Тогда
				ЭлементОбъект = МенеджерОбъекта.СоздатьДокумент();
			Иначе
				ЭлементОбъект = МенеджерОбъекта.СоздатьЭлемент();
			КонецЕсли;	
		КонецЕсли;	
#КонецОбласти

		ЭтоНовыйЭлемент	= ЭлементОбъект.Ссылка.Пустая() = Истина;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ДанныеСмартПроцесса"		, ЗагружаемыйСмартПроцесс);
		ДополнительныеПараметры.Вставить("ПоляОбъекта1С"			, ПоляШапки);
		ДополнительныеПараметры.Вставить("ИдЭлементаБ24"			, ИдЭлемента);
		ДополнительныеПараметры.Вставить("Объект1С"					, ЭлементОбъект);
		ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
		ДополнительныеПараметры.Вставить("ДанныеЭлементаБ24"		, ТекЭлемент);
		ДополнительныеПараметры.Вставить("ЭтоНовыйОбъект"			, ЭтоНовыйЭлемент);
		
		ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);
		
		ИдентификаторНаправления 	= Формат(ТекЭлемент.Получить("categoryId"),"ЧГ=0");
		ИдентификаторСтадии 		= Формат(ТекЭлемент.Получить("stageId"),"ЧГ=0");
		
		НаправлениеУказываетсяСведением = Ложь;
		ЗначениеНаправления				= Неопределено;
		ДанныеНастроекНаправлений 		= ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ДанныеНастроекНаправлений;
#Область ПолучениеЗаполнениеДанныхНаправления
		Если ДанныеНастроекНаправлений <> Неопределено Тогда
			
			НайденнаяСтрока = ДанныеНастроекНаправлений.СоответствияНаправлений.Найти(ИдентификаторНаправления, "ИдНаправления");
			Если НайденнаяСтрока <> Неопределено Тогда
				ЗначениеНаправления = НайденнаяСтрока.Значение1С;   
			КонецЕсли;
			
			Попытка
				
				Если ДанныеНастроекНаправлений.ИсточникНаправленийВ1С = "Дополнительное сведение" Тогда
					НаправлениеУказываетсяСведением = Истина;
				ИначеЕсли ДанныеНастроекНаправлений.ИсточникНаправленийВ1С = "Реквизит объекта" Тогда
					
					Если ЗначениеЗаполнено(ДанныеНастроекНаправлений.Реквизит1С) Тогда
						ЭлементОбъект[ДанныеНастроекНаправлений.Реквизит1С[0].Значение] = ЗначениеНаправления;
					КонецЕсли;
					
				ИначеЕсли ДанныеНастроекНаправлений.ИсточникНаправленийВ1С = "Дополнительный реквизит" Тогда
					
					Для Каждого ЗаполнениеСвойствоТовара Из ЭлементОбъект.ДополнительныеРеквизиты Цикл
						
						Если ЗаполнениеСвойствоТовара.Свойство = ДанныеНастроекНаправлений.СвойствоОбъекта Тогда
							
							ЗаполнениеСвойствоТовара.Значение = ЗначениеНаправления;
							Если ТипЗнч(ЗаполнениеСвойствоТовара.Значение) = Тип("Строка") Тогда
								ЗаполнениеСвойствоТовара.ТекстоваяСтрока = ЗначениеНаправления;
							КонецЕсли;
							
							ЕстьРеквизит = Истина
							
						КонецЕсли;
						
					КонецЦикла;
					
					Если НЕ ЕстьРеквизит Тогда
						НовыйРеквизит = ЭлементОбъект.ДополнительныеРеквизиты.Добавить();
						НовыйРеквизит.Свойство 	= ДанныеНастроекНаправлений.СвойствоОбъекта; 
						НовыйРеквизит.Значение 	= ЗначениеНаправления; 
						Если ТипЗнч(НовыйРеквизит.Значение) = Тип("Строка") Тогда
							НовыйРеквизит.ТекстоваяСтрока = ЗначениеНаправления;
						КонецЕсли;
					КонецЕсли;	

				КонецЕсли;
				
			Исключение
			КонецПопытки;
			
		КонецЕсли;
#КонецОбласти

		СтадияУказываетсяСведением 	= Ложь;
		ЗначениеСтадии				= Неопределено;
		ДанныеНастроекСтадий 		= ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ДанныеНастроекСтадий;
#Область ПолучениеЗаполнениеДанныхСтадии
		Если ДанныеНастроекСтадий <> Неопределено Тогда
			                                          
			НайденныеСтроки = ДанныеНастроекСтадий.СоответствияСтадийНаправлений.НайтиСтроки(Новый Структура("ИдНаправления, ИдСтадии", ИдентификаторНаправления, ИдентификаторСтадии));
			Если НайденныеСтроки.Количество() > 0 Тогда
				ЗначениеСтадии = НайденныеСтроки[0].Значение1С;   
			КонецЕсли;
			
			Попытка
				
				Если ДанныеНастроекСтадий.ИсточникСтадийВ1С = "Дополнительное сведение" Тогда
					СтадияУказываетсяСведением = Истина;
				ИначеЕсли ДанныеНастроекСтадий.ИсточникСтадийВ1С = "Реквизит объекта" Тогда
					
					Если ЗначениеЗаполнено(ДанныеНастроекСтадий.Реквизит1С) Тогда
						ЭлементОбъект[ДанныеНастроекСтадий.Реквизит1С[0].Значение] = ЗначениеСтадии;
					КонецЕсли;
					
				ИначеЕсли ДанныеНастроекСтадий.ИсточникСтадийВ1С = "Дополнительный реквизит" Тогда
					
					Для Каждого ЗаполнениеСвойствоТовара Из ЭлементОбъект.ДополнительныеРеквизиты Цикл
						
						Если ЗаполнениеСвойствоТовара.Свойство = ДанныеНастроекСтадий.СвойствоОбъекта Тогда
							
							ЗаполнениеСвойствоТовара.Значение = ЗначениеСтадии;
							Если ТипЗнч(ЗаполнениеСвойствоТовара.Значение) = Тип("Строка") Тогда
								ЗаполнениеСвойствоТовара.ТекстоваяСтрока = ЗначениеСтадии;
							КонецЕсли;
							
							ЕстьРеквизит = Истина
							
						КонецЕсли;
						
					КонецЦикла;
					
					Если НЕ ЕстьРеквизит Тогда
						НовыйРеквизит = ЭлементОбъект.ДополнительныеРеквизиты.Добавить();
						НовыйРеквизит.Свойство 	= ДанныеНастроекСтадий.СвойствоОбъекта; 
						НовыйРеквизит.Значение 	= ЗначениеСтадии; 
						Если ТипЗнч(НовыйРеквизит.Значение) = Тип("Строка") Тогда
							НовыйРеквизит.ТекстоваяСтрока = ЗначениеСтадии;
						КонецЕсли;
					КонецЕсли;	

				КонецЕсли;
				
			Исключение
			КонецПопытки;
			
		КонецЕсли;
#КонецОбласти
		
#Область ЗаполнениеТоваров	
		ТоварыЭлемента = ТекЭлемент.Получить("productrow");
		Если ТоварыЭлемента <> Неопределено И ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ТабличнаяЧасть1С <> Неопределено И ЗначениеЗаполнено(ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ТабличнаяЧасть1С.Значение) Тогда
			
			ИмяТЧ = ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ТабличнаяЧасть1С.Значение;
			
			Попытка
				
				тзнТоваровАрх = ЭлементОбъект[ИмяТЧ].Выгрузить();
				
				ЭлементОбъект[ИмяТЧ].Очистить();
				
				ТабличнаяЧастьОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(ЭлементОбъект)).ТабличныеЧасти[ИмяТЧ];
				ЕстьРеквизитНоменклатура 	= ТабличнаяЧастьОбъекта.Реквизиты.Найти("Номенклатура") <> Неопределено;
				ЕстьРеквизитХарактеристика 	= ТабличнаяЧастьОбъекта.Реквизиты.Найти("Характеристика") <> Неопределено;
				
				// Костыль для искл дублей
				тзнПроверкаДублейСтрок = Новый ТаблицаЗначений;
				тзнПроверкаДублейСтрок.Колонки.Добавить("ИдТовара");
				тзнПроверкаДублейСтрок.Колонки.Добавить("ИдПозиции");
				
				Для каждого ТекПозицияТовара Из ТоварыЭлемента Цикл
					
					ИдПозицииДокумента	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекПозицияТовара.Получить("id"),"ЧГ=0")); 
					ИдТовара			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекПозицияТовара.Получить("productId"),"ЧГ=0"));
					НаименованиеТовара	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(ТекПозицияТовара.Получить("productName")));
					
				
#Область ИсключениеВозможныхДублей	
					СтруктураОтбора = Новый Структура;
					СтруктураОтбора.Вставить("ИдТовара"		, ИдТовара);
					СтруктураОтбора.Вставить("ИдПозиции"	, ИдПозицииДокумента);

					НайденныеСтрокиПроверкиДублей = тзнПроверкаДублейСтрок.НайтиСтроки(СтруктураОтбора);

					Если НайденныеСтрокиПроверкиДублей.Количество() > 0 Тогда
						Продолжить;
					Иначе
						ИнфОНовойСтрокеТовара = тзнПроверкаДублейСтрок.Добавить();
						ИнфОНовойСтрокеТовара.ИдТовара 	= ИдТовара; 
						ИнфОНовойСтрокеТовара.ИдПозиции = ИдПозицииДокумента; 
					КонецЕсли;
#КонецОбласти	
					
					Номенклатура	= Справочники.Номенклатура.ПустаяСсылка();	
					Характеристика  = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
#Область ПоискТовара	
					Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
						Запрос = Новый Запрос;
						Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
						Запрос.УстановитьПараметр("Идентификатор", ИдТовара);
						Запрос.Текст = "ВЫБРАТЬ
						|	ВТ_Идентификаторы.Объект КАК Объект,
						|	ВТ_Идентификаторы.ПодчиненныйОбъект КАК ПодчиненныйОбъект
						|ИЗ
						|	ВТ_ИдентификаторыТоваров КАК ВТ_Идентификаторы
						|ГДЕ
						|	ВТ_Идентификаторы.Идентификатор = &Идентификатор";
						ВыполненныйЗапрос = Запрос.Выполнить();
						
						Если НЕ ВыполненныйЗапрос.Пустой() Тогда
							
							Выборка = ВыполненныйЗапрос.Выбрать();
							Пока Выборка.Следующий() Цикл
								Номенклатура 	= Выборка.Объект;	
								Характеристика 	= Выборка.ПодчиненныйОбъект;	
							КонецЦикла;
							
						КонецЕсли;   
						
						Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
							ИнформацияТовара = СформироватьНоменклатуруХарактеристикуПоИд(СтруктураНастроек, ИдТовара); 
							Если ИнформацияТовара <> Неопределено Тогда  
								Номенклатура 	= ИнформацияТовара.Объект;	
								Характеристика 	= ИнформацияТовара.ПодчиненныйОбъект;	
							КонецЕсли;

						КонецЕсли;
						
					КонецЕсли;
#КонецОбласти	          
					Номенклатура = ?(ЗначениеЗаполнено(Номенклатура), Номенклатура, Справочники.Номенклатура.ПустаяСсылка());
				
					НоваяСтрока = ЭлементОбъект[ИмяТЧ].Добавить();                         
					
					Если ЕстьРеквизитНоменклатура И ЕстьРеквизитХарактеристика Тогда 
						
						НайденныеСтроки = тзнТоваровАрх.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Номенклатура, Характеристика));
						Если НайденныеСтроки.Количество() > 0 Тогда
							ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденныеСтроки[0]);
							тзнТоваровАрх.Удалить(НайденныеСтроки[0]);
						КонецЕсли;
						
					ИначеЕсли ЕстьРеквизитНоменклатура Тогда
						
						НайденныеСтроки = тзнТоваровАрх.НайтиСтроки(Новый Структура("Номенклатура", Номенклатура));
						Если НайденныеСтроки.Количество() > 0 Тогда
							ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденныеСтроки[0]);
							тзнТоваровАрх.Удалить(НайденныеСтроки[0]);
						КонецЕсли;
						
					КонецЕсли;

					ДополнительныеПараметрыТЧ = Новый Структура;
					ДополнительныеПараметрыТЧ.Вставить("ДанныеСмартПроцесса"		, ЗагружаемыйСмартПроцесс);
					ДополнительныеПараметрыТЧ.Вставить("ПоляТЧОбъекта1С"			, ПоляТЧ);
					ДополнительныеПараметрыТЧ.Вставить("Объект1С"					, ЭлементОбъект);
					ДополнительныеПараметрыТЧ.Вставить("СтрокаТЧ"					, НоваяСтрока);
					ДополнительныеПараметрыТЧ.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
					ДополнительныеПараметрыТЧ.Вставить("ДанныеЭлементаБ24"			, ТекЭлемент);
					ДополнительныеПараметрыТЧ.Вставить("ДанныеЭлементаТЧБ24"		, ТекПозицияТовара);
					ДополнительныеПараметрыТЧ.Вставить("ЭтоНовыйОбъект"				, ЭтоНовыйЭлемент);
					ДополнительныеПараметрыТЧ.Вставить("Номенклатура"				, Номенклатура);
					ДополнительныеПараметрыТЧ.Вставить("Характеристика"				, Характеристика);
					
					ЗаполнитьРеквизитыТЧОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметрыТЧ, ИмяТЧ);
				
				КонецЦикла;
				
			Исключение
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Для смарт процесса: " + ЗагружаемыйСмартПроцесс.Значение.Наименование + " не удалось заполнить табличную часть 1С.");
			КонецПопытки;
			
			
		КонецЕсли;
#КонецОбласти		
                                                                                             
 		УжеОбработан = Ложь;    
		ПараметрыДляПереопределяемыхСобытий = ПараметрыДляПереопределяемыхСобытий(СтруктураНастроек, ИдЭлемента, ЭлементОбъект, ТекЭлемент);
		Б24_СМ_ОбщегоНазначенияСерверПереопределяемый.ОбработатьСобытиеПередЗаписьюЭлемента(ЗагружаемыйСмартПроцесс.Значение.Наименование, ПараметрыДляПереопределяемыхСобытий, УжеОбработан); 
		Если НЕ УжеОбработан И ЗначениеЗаполнено(АлгоритмПриЗагрузкеДанныхВ1С) Тогда
		
			Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
			Параметры.СтруктураНастроек = СтруктураНастроек;
			Параметры.Объект1С 			= ЭлементОбъект;
			Параметры.СтруктураДанных 	= ТекЭлемент;
			Параметры.ОбъектПортала 	= ТекЭлемент;
			Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(АлгоритмПриЗагрузкеДанныхВ1С, Параметры, "Процедура");		
				
		КонецЕсли;
		
		Если ЭтоНовыйЭлемент Тогда   // Для выгрузки дела при создании в 1С   
			ЭлементОбъект.УстановитьСсылкуНового(МенеджерОбъекта.ПолучитьСсылку());
			РегистрыСведений.Б24_К_ИдентификаторыДел.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ДелоСмартПроцесса, ЭлементОбъект.ПолучитьСсылкуНового(), ЗагружаемыйСмартПроцесс.Ключ + "_", ИдЭлемента);
		КонецЕсли;		
		
		Попытка
			      
			ЭлементОбъект.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			ЭлементОбъект.ДополнительныеСвойства.Вставить("Б24_К_ОбъектМодифицирован");
			
			Если ТипСущности1С = "Документ" И ПроводитьВ1С = Истина Тогда
				
				Попытка
					ЭлементОбъект.Записать(РежимЗаписиДокумента.Проведение);	
				Исключение
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Для смарт процесса: " + ЗагружаемыйСмартПроцесс.Значение.Наименование + " не удалось провести документ. Попытка записи.");
					ЭлементОбъект.Записать();
				КонецПопытки;
				
			Иначе
				ЭлементОбъект.Записать();
			КонецЕсли;
			
			СтруктураДляЗаписиИдентификатора = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьСтруктуруДляЗаписиИдентификатора();
			СтруктураДляЗаписиИдентификатора.ТипВладельцаСмартПроцесса 	= ЗагружаемыйСмартПроцесс.Ключ;
			СтруктураДляЗаписиИдентификатора.ИдСмартПроцесса 			= ЗагружаемыйСмартПроцесс.Значение.ИдСмартПроцесса;
			СтруктураДляЗаписиИдентификатора.ТипСущности1С 				= ТипСущности1С;
			СтруктураДляЗаписиИдентификатора.Сущность1С 				= Сущность1С;
			СтруктураДляЗаписиИдентификатора.Объект 					= ЭлементОбъект.Ссылка;
			СтруктураДляЗаписиИдентификатора.Идентификатор 				= ИдЭлемента;
			РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураДляЗаписиИдентификатора);
				
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Истина)));
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан элемент: " + ЗаголовокЭлемента, ЭлементОбъект.Ссылка);
			
			ЗаполнитьДопСведенияОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры);
			
			Если НаправлениеУказываетсяСведением Тогда
				ДобавитьЗначениеСведенияОбъекту(ЭлементОбъект.Ссылка, ДанныеНастроекНаправлений.СвойствоОбъекта, ЗначениеНаправления);
			КонецЕсли;	
			
			Если СтадияУказываетсяСведением Тогда
				ДобавитьЗначениеСведенияОбъекту(ЭлементОбъект.Ссылка, ДанныеНастроекСтадий.СвойствоОбъекта, ЗначениеСтадии);
			КонецЕсли;	
			
			
	 		УжеОбработан = Ложь;    
			ПараметрыДляПереопределяемыхСобытий = ПараметрыДляПереопределяемыхСобытий(СтруктураНастроек, ИдЭлемента, ЭлементОбъект.Ссылка, ТекЭлемент);
			Б24_СМ_ОбщегоНазначенияСерверПереопределяемый.ОбработатьСобытиеПослеЗаписиЭлемента(ЗагружаемыйСмартПроцесс.Значение.Наименование, ПараметрыДляПереопределяемыхСобытий, УжеОбработан); 
			Если НЕ УжеОбработан И ЗначениеЗаполнено(АлгоритмПослеЗагрузкиДанныхВ1С) Тогда
				Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
				Параметры.СтруктураНастроек = СтруктураНастроек;
				Параметры.Объект1С 			= ЭлементОбъект.Ссылка;
				Параметры.СтруктураДанных 	= ТекЭлемент;
				Параметры.ОбъектПортала 	= ТекЭлемент;
				Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(АлгоритмПослеЗагрузкиДанныхВ1С, Параметры, "Процедура");		
			КонецЕсли;
			
		Исключение
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи элемента: " + ЗаголовокЭлемента + " возникли ошибки.", ЭлементОбъект.Ссылка);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Результат.Добавить((СформироватьПростуюСтруктуруСИд(ИдЭлемента, Ложь)));
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
	
		КонецПопытки;
		
	КонецЦикла;
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
	Возврат Результат;
	
КонецФункции


Функция ПроверитьОбъект1СНаОтборыЗагрузки(СтруктураНастроек, МенеджерВременныхТаблиц, ДанныеЭлементаБ24, ОтборыДляЗагрузки)
	
	Для каждого ТекОтбор Из ОтборыДляЗагрузки Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекОтбор.ВидСравнения) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипПоля = ТекОтбор.ТипПоля;
		
		ЗначениеДляОтбора = ПолучитьЗначениеРеквизитаОбъектаПоКлючуJSON(СтруктураНастроек, МенеджерВременныхТаблиц, ТекОтбор.КлючЗапроса, ДанныеЭлементаБ24);

		Попытка
			
			Если ТекОтбор.ВидСравнения = "Равно" Тогда
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					Если НЕ ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено Тогда	
						Возврат Ложь;	
					КонецЕсли;
				Иначе
					Если ТекОтбор.Значение <> ЗначениеДляОтбора Тогда	
						Возврат Ложь;	
					КонецЕсли;
				КонецЕсли;
					
			ИначеЕсли ТекОтбор.ВидСравнения = "Не равно" Тогда
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					Если ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено Тогда	
						Возврат Ложь;	
					КонецЕсли;
				Иначе
					Если ТекОтбор.Значение = ЗначениеДляОтбора Тогда	
						Возврат Ложь;	
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли ТекОтбор.ВидСравнения = "Больше" Тогда
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					Если ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено Тогда	
						Возврат Ложь;	
					КонецЕсли;
				Иначе
					Если ТекОтбор.Значение > ЗначениеДляОтбора Тогда	
						Возврат Ложь;	
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ТекОтбор.ВидСравнения = "Меньше" Тогда
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					Если ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено Тогда	
						Возврат Ложь;	
					КонецЕсли;
				Иначе
					Если ТекОтбор.Значение < ЗначениеДляОтбора Тогда	
						Возврат Ложь;	
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли ТекОтбор.ВидСравнения = "Содержит" Тогда
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					
					ЕстьВхождение = Ложь;
					Для каждого ТекЭлемент Из ТекОтбор.Значение Цикл
							
						ЕстьВхождение = СтрНайти(ЗначениеДляОтбора, ТекЭлемент.Значение) > 0;	
						Если ЕстьВхождение Тогда
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
					
					Если НЕ ЕстьВхождение Тогда	
						Возврат Ложь;	
					КонецЕсли;
					
				Иначе
					Если СтрНайти(ЗначениеДляОтбора, ТекОтбор.Значение) = 0 Тогда  
						Возврат Ложь;	
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли ТекОтбор.ВидСравнения = "В списке" Тогда
				
				ЕстьВхождение = Ложь;
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					
					
					ЕстьВхождение = ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено;
							
					Если НЕ ЕстьВхождение Тогда
						Возврат Ложь;	
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли ТекОтбор.ВидСравнения = "Не в списке" Тогда
				
				ЕстьВхождение = Ложь;
				
				Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
					
					
					ЕстьВхождение = НЕ ТекОтбор.Значение.НайтиПоЗначению(ЗначениеДляОтбора) <> Неопределено;
							
					Если НЕ ЕстьВхождение Тогда
						Возврат Ложь;	
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		Исключение
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось сравнить данные для фильтра по загрузке данных в 1С");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат Ложь;	
		КонецПопытки;
			
	КонецЦикла;
		
	Возврат Истина;	
	
КонецФункции

Процедура ЗаполнитьРеквизитыОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры)
	
	ПоляОбъекта1С 			= ДополнительныеПараметры.ПоляОбъекта1С;
	Объект1С 				= ДополнительныеПараметры.Объект1С;
	ДанныеЭлементаБ24		= ДополнительныеПараметры.ДанныеЭлементаБ24;
	МенеджерВременныхТаблиц	= ДополнительныеПараметры.МенеджерВременныхТаблиц;
	
	МетаданныеОбъекта 		= Метаданные.НайтиПоТипу(ТипЗнч(Объект1С));
	
	Для каждого ТекПоле Из ПоляОбъекта1С Цикл
		
		Если ТекПоле.ИсточникДанных	= "" ИЛИ ТекПоле.ИсточникДанных = "Не изменять" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекПоле.Значение = "" И НЕ ТекПоле.ИсточникДанных = "Предопределенный алгоритм 'Клиент'" 
			И НЕ ТекПоле.ИсточникДанных = "Предопределенный алгоритм 'Реквизит'"
			И НЕ ТекПоле.ИсточникДанных = "Предопределенный алгоритм 'НДС включен в цену'" Тогда
			Продолжить;
		КонецЕсли;
		
		Если  ТекПоле.ТипРеквизита = "ДополнительноеСведение" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекПоле.ТипРеквизита = "Реквизиты" Тогда
			Если МетаданныеОбъекта.Реквизиты.Найти(ТекПоле.Реквизит1С) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ЗначениеРеквизита = Неопределено;

		Попытка
			
			Если ТекПоле.ИсточникДанных = "Фиксированное значение" Тогда
				ЗначениеРеквизита = ТекПоле.Значение;	     
			ИначеЕсли ТекПоле.ИсточникДанных = "Ключ JSON" Тогда
				
				ЗначениеРеквизита = ПолучитьЗначениеРеквизитаОбъекта1СПоКонструктору(СтруктураНастроек, МенеджерВременныхТаблиц, ДополнительныеПараметры.ДанныеСмартПроцесса, ТекПоле, ДанныеЭлементаБ24);
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Из пользовательского поля" Тогда
				
				ЗначениеРеквизита = ПолучитьЗначениеРеквизитаОбъекта1СПоКонструктору(СтруктураНастроек, МенеджерВременныхТаблиц, ДополнительныеПараметры.ДанныеСмартПроцесса, ТекПоле, ДанныеЭлементаБ24);
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Произвольный алгоритм" Тогда
				
				Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
				Параметры.СтруктураНастроек 	 = СтруктураНастроек;
				Параметры.Объект1С 				 = Объект1С;
				Параметры.ОбъектПортала 		 = ДополнительныеПараметры.ДанныеЭлементаБ24;
				
				ЗначениеРеквизита = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(ТекПоле.Значение, Параметры, "Функция");		
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Предопределенный алгоритм 'Клиент'" Тогда
				
				ИдКомпании = Формат(ДанныеЭлементаБ24.Получить("companyId"), "ЧГ=0");
				ИдКонтакта = Формат(ДанныеЭлементаБ24.Получить("contactId"), "ЧГ=0");
				Если ЗначениеЗаполнено(ИдКомпании) Тогда
	            	ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", ИдКомпании);
					Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
						ЗначениеРеквизита = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Компания", ИдКомпании);
					КонецЕсли;
				Иначе
					ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", ИдКонтакта);
					Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
						ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", "#"+ИдКонтакта);
					КонецЕсли;
					Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
						ЗначениеРеквизита = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Контакт", ИдКонтакта);
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Предопределенный алгоритм 'Реквизит'" Тогда
				
				ИдКомпании = Формат(ДанныеЭлементаБ24.Получить("companyId"), "ЧГ=0");
				ИдКонтакта = Формат(ДанныеЭлементаБ24.Получить("contactId"), "ЧГ=0");
				Если ЗначениеЗаполнено(ИдКомпании) Тогда
	            	Клиент = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", ИдКомпании);
					Если НЕ ЗначениеЗаполнено(Клиент) Тогда
						Клиент = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Компания", ИдКомпании);
					КонецЕсли;
				Иначе
					Клиент = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", ИдКонтакта);
					Если НЕ ЗначениеЗаполнено(Клиент) Тогда
						Клиент = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", "#"+ИдКонтакта);
					КонецЕсли;
					Если НЕ ЗначениеЗаполнено(Клиент) Тогда
						Клиент = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Контакт", ИдКонтакта);
					КонецЕсли;
				КонецЕсли;
				
				Если ТипЗнч(Клиент) = Тип("СправочникСсылка.Партнеры") Тогда
					
					Выборка = Справочники.Контрагенты.Выбрать(,,Новый Структура("Партнер", Клиент));
					Пока Выборка.Следующий() Цикл
						ЗначениеРеквизита = Выборка.Ссылка;	
						Прервать;
					КонецЦикла;
					
				Иначе
					ЗначениеРеквизита = Клиент;	
				КонецЕсли;
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Предопределенный алгоритм 'НДС включен в цену'" Тогда
				
				ЗначениеРеквизита = Ложь;
				
				productrow = ДанныеЭлементаБ24.Получить("productrow");
				Если ТипЗнч(productrow) = Тип("Массив") Тогда
					Если productrow.Количество() > 0 Тогда
						ЗначениеРеквизита = productrow[0].Получить("taxIncluded") = "Y";
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
			Если ТекПоле.ТипРеквизита = "ДополнительныйРеквизит" И ЗначениеЗаполнено(ТекПоле.СсылкаНаСвойство) Тогда
				
				ЕстьРеквизит = Ложь;
				Для Каждого ЗаполнениеСвойствоТовара Из Объект1С.ДополнительныеРеквизиты Цикл
					
					Если ЗаполнениеСвойствоТовара.Свойство = ТекПоле.СсылкаНаСвойство Тогда
						
						ЗаполнениеСвойствоТовара.Значение = ЗначениеРеквизита;
						Если ТипЗнч(ЗаполнениеСвойствоТовара.Значение) = Тип("Строка") Тогда
							ЗаполнениеСвойствоТовара.ТекстоваяСтрока = ЗначениеРеквизита;
						КонецЕсли;
						
						ЕстьРеквизит = Истина
						
					КонецЕсли;
					
				КонецЦикла;
				
				Если НЕ ЕстьРеквизит Тогда
					НовыйРеквизит = Объект1С.ДополнительныеРеквизиты.Добавить();
					НовыйРеквизит.Свойство 	= ТекПоле.СсылкаНаСвойство; 
					НовыйРеквизит.Значение 	= ЗначениеРеквизита; 
					Если ТипЗнч(НовыйРеквизит.Значение) = Тип("Строка") Тогда
						НовыйРеквизит.ТекстоваяСтрока = ЗначениеРеквизита;
					КонецЕсли;
				КонецЕсли;	
				
			Иначе
				Объект1С[ТекПоле.Реквизит1С] = ЗначениеРеквизита;
			КонецЕсли;
			
		Исключение
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Что то пошло не так..");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;	
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыТЧОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры, ИмяТЧ)
	
	ПоляТЧОбъекта1С 		= ДополнительныеПараметры.ПоляТЧОбъекта1С;
	Объект1С 				= ДополнительныеПараметры.Объект1С;
	СтрокаТЧ 				= ДополнительныеПараметры.СтрокаТЧ;
	Номенклатура 			= ДополнительныеПараметры.Номенклатура;
	Характеристика 			= ДополнительныеПараметры.Характеристика;
	ДанныеЭлементаБ24 		= ДополнительныеПараметры.ДанныеЭлементаБ24;
	ДанныеЭлементаТЧБ24 	= ДополнительныеПараметры.ДанныеЭлементаТЧБ24;
	
	МетаданныеОбъекта 		= Метаданные.НайтиПоТипу(ТипЗнч(Объект1С));
	РеквизитыТЧОбъекта 		= МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Реквизиты;
	
	Для каждого ТекПоле Из ПоляТЧОбъекта1С Цикл
		
		Если ТекПоле.ИсточникДанных	= "" ИЛИ ТекПоле.ИсточникДанных = "Не изменять" Тогда
			Продолжить;
		КонецЕсли;
		
		МетаОписаниеРеквизита = РеквизитыТЧОбъекта.Найти(ТекПоле.Реквизит1С);
		Если МетаОписаниеРеквизита = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеРеквизита = Неопределено;

		Попытка
			
			Если ТекПоле.ИсточникДанных = "Фиксированное значение" Тогда
				ЗначениеРеквизита = ТекПоле.Значение;	     
			ИначеЕсли ТекПоле.ИсточникДанных = "Предопределенный алгоритм" Тогда
				
#Область ПредопределенныеАлгоритмы
				Если ТекПоле.Значение = "Номенклатура" Тогда
					
					ЗначениеРеквизита = Номенклатура;
					
				ИначеЕсли ТекПоле.Значение = "Характеристика" Тогда
					
					ЗначениеРеквизита = Характеристика;
					
				ИначеЕсли ТекПоле.Значение = "Содержание" Тогда
					
					Если ЗначениеЗаполнено(СтрокаТЧ.Содержание) Тогда
						ЗначениеРеквизита = СтрокаТЧ.Содержание;	
					Иначе
						ЗначениеРеквизита = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(ДанныеЭлементаТЧБ24.Получить("productName")));
					КонецЕсли;
					
				ИначеЕсли ТекПоле.Значение = "Дата отгрузки" Тогда
					
					ДатаЗавершения	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ДанныеЭлементаБ24.Получить("closedate"));
					Если НЕ ЗначениеЗаполнено(ДатаЗавершения) Тогда
						ДатаЗавершения = ТекущаяДатаСеанса();
					КонецЕсли;
					
					ЗначениеРеквизита = Дата(ДатаЗавершения);
					
				ИначеЕсли ТекПоле.Значение = "Единица измерения" Тогда
					
					ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
					
					КодЕдИзм = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеЭлементаТЧБ24.Получить("measureCode"),"ЧГ=0"));
					Если ЗначениеЗаполнено(КодЕдИзм) Тогда
						ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(КодЕдИзм);
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найдена единица измерения по ид. Попытка создания единицы измерения с Ид: " + КодЕдИзм);
						ЕдиницаИзмерения = СформироватьЕдиницуИзмеренияПоИд(СтруктураНастроек, КодЕдИзм)
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения") КонецЕсли;
	
					ЗначениеРеквизита = ЕдиницаИзмерения;
					
				ИначеЕсли ТекПоле.Значение = "Количество" Тогда
					
					ЗначениеРеквизита = Число(ДанныеЭлементаТЧБ24.Получить("quantity"));
					
				ИначеЕсли ТекПоле.Значение = "Ставка НДС" Тогда
					
					СтавкаНалога		= ДанныеЭлементаТЧБ24.Получить("taxRate");
					Если СтавкаНалога 	= Неопределено Тогда СтавкаНалога = "Без НДС" КонецЕсли;
					
					СтавкаНДС = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтавкуНДСПоСтавке(СтавкаНалога);        
					Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда СтавкаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "СтавкаНДС") КонецЕсли;
					
					ЗначениеРеквизита = СтавкаНДС;				
					
				ИначеЕсли (ТекПоле.Значение = "Цена" ИЛИ ТекПоле.Значение = "Сумма" ИЛИ ТекПоле.Значение = "Сумма НДС" 
					ИЛИ ТекПоле.Значение = "Всего" ИЛИ ТекПоле.Значение = "Сумма скидки/наценки") Тогда
					
					НалогВключен		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(ДанныеЭлементаТЧБ24.Получить("taxIncluded")))= "Y";
					СтавкаНалога		= ДанныеЭлементаТЧБ24.Получить("taxRate");
					СтавкаНалога 		= ?(ЗначениеЗаполнено(СтавкаНалога),СтавкаНалога, 0);
					
					Количество				= ДанныеЭлементаТЧБ24.Получить("quantity");
					ЦенаБезСкидкиБезНалога	= ДанныеЭлементаТЧБ24.Получить("priceNetto");
					ЦенаБезСкидкиСНалогом	= ДанныеЭлементаТЧБ24.Получить("priceBrutto");
					
					Скидка					= ДанныеЭлементаТЧБ24.Получить("discountSum");
					ПроцентСкидки			= ДанныеЭлементаТЧБ24.Получить("discountRate");
					ПроцентСкидки			= ?(ПроцентСкидки=0 И Скидка<>0, 100, ПроцентСкидки);
					
					Если НалогВключен = Истина Тогда   // баг есть
						Цена = ?(ЦенаБезСкидкиСНалогом = 0 И Скидка<>0, Скидка/ПроцентСкидки*100 ,ЦенаБезСкидкиСНалогом); // есть баг с скидкой
					Иначе
						Цена = ?(ЦенаБезСкидкиБезНалога = 0 И Скидка<>0, Скидка/ПроцентСкидки*100 ,ЦенаБезСкидкиБезНалога); // есть баг с скидкой
					КонецЕсли;
					
					СуммаРучнойСкидки 	= ?(ЗначениеЗаполнено(Скидка), Скидка*Количество, Цена*ПроцентСкидки*Количество/100);
					Сумма = Цена*Количество-СуммаРучнойСкидки;
					
					МетаданныеРеквизита = МетаданныеОбъекта.Реквизиты.Найти("НалогообложениеНДС");
					Если МетаданныеРеквизита <> Неопределено Тогда
						Если Объект1С.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС Тогда
							СуммаНДС = ?(Объект1С.ЦенаВключаетНДС, Сумма - (Сумма) / ((СтавкаНалога + 100) / 100), Сумма * СтавкаНалога / 100);
						Иначе
							СуммаНДС = 0;
						КонецЕсли;
					Иначе
						СуммаНДС = 0;
					КонецЕсли;    
					
					Если ТекПоле.Значение = "Цена" Тогда
						ЗначениеРеквизита = Цена;	
					ИначеЕсли ТекПоле.Значение = "Сумма" Тогда
						ЗначениеРеквизита = Сумма; 
					ИначеЕсли ТекПоле.Значение = "Сумма НДС" Тогда
						ЗначениеРеквизита = СуммаНДС; 
					ИначеЕсли ТекПоле.Значение = "Всего" Тогда        
						МетаданныеРеквизита = МетаданныеОбъекта.Реквизиты.Найти("ЦенаВключаетНДС");
						Если МетаданныеРеквизита <> Неопределено Тогда
							ЗначениеРеквизита = Сумма + ?(Объект1С.ЦенаВключаетНДС, 0, СуммаНДС); 
						Иначе
							ЗначениеРеквизита = Сумма;
						КонецЕсли;
					ИначеЕсли ТекПоле.Значение = "Сумма скидки/наценки" Тогда
						ЗначениеРеквизита = СуммаРучнойСкидки;
					КонецЕсли;
					
				ИначеЕсли ТекПоле.Значение = "Процент скидки/наценки" Тогда
					
					ЗначениеРеквизита = ДанныеЭлементаТЧБ24.Получить("discountRate");
					
			КонецЕсли;				
#КонецОбласти				
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Произвольный алгоритм" Тогда
				
				Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
				Параметры.СтруктураНастроек 		= СтруктураНастроек;
				Параметры.Объект1С 					= Объект1С;
				Параметры.ОбъектПортала 			= ДополнительныеПараметры.ДанныеЭлементаТЧБ24;
				Параметры.СтрокаТаблицыОбъекта1С 	= СтрокаТЧ;
				
				ЗначениеРеквизита = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(ТекПоле.Значение, Параметры, "Функция");		
				
			КонецЕсли;
				
			СтрокаТЧ[ТекПоле.Реквизит1С] = ЗначениеРеквизита;
			
		Исключение
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Что то пошло не так..");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;	
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДопСведенияОбъекта1СПоКонструктору(СтруктураНастроек, ДополнительныеПараметры)
	
	ПоляОбъекта1С 			= ДополнительныеПараметры.ПоляОбъекта1С;
	Объект1С 				= ДополнительныеПараметры.Объект1С.Ссылка;
	ДанныеЭлементаБ24		= ДополнительныеПараметры.ДанныеЭлементаБ24;
	МенеджерВременныхТаблиц	= ДополнительныеПараметры.МенеджерВременныхТаблиц;
	
	Для каждого ТекПоле Из ПоляОбъекта1С Цикл
		
		Если ТекПоле.ИсточникДанных	 = "" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекПоле.Значение = "" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекПоле.ТипРеквизита <> "ДополнительноеСведение" Тогда
			Продолжить;
		КонецЕсли;
			
		ЗначениеСвойства = Неопределено;

		Попытка
			
			Если ТекПоле.ИсточникДанных = "Фиксированное значение" Тогда
				ЗначениеСвойства = ТекПоле.Значение;	     
			ИначеЕсли ТекПоле.ИсточникДанных = "Ключ JSON" Тогда
				
				ЗначениеСвойства = ПолучитьЗначениеРеквизитаОбъекта1СПоКонструктору(СтруктураНастроек, МенеджерВременныхТаблиц, ДополнительныеПараметры.ДанныеСмартПроцесса, ТекПоле, ДанныеЭлементаБ24);
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Из пользовательского поля" Тогда
				
				ЗначениеСвойства = ПолучитьЗначениеРеквизитаОбъекта1СПоКонструктору(СтруктураНастроек, МенеджерВременныхТаблиц, ДополнительныеПараметры.ДанныеСмартПроцесса, ТекПоле, ДанныеЭлементаБ24);
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Произвольный алгоритм" Тогда
				
				Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
				Параметры.СтруктураНастроек = СтруктураНастроек;
				Параметры.Объект1С 			= Объект1С;
				Параметры.ОбъектПортала 	= ДанныеЭлементаБ24;
				
				ЗначениеСвойства = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(ТекПоле.Значение, Параметры, "Функция");		
				
			КонецЕсли;
			
			
			НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(Объект1С);
			НаборЗаписей.Прочитать();
	
			
			ЕстьСвойство = Ложь;
			Для Каждого ТекСтрока Из НаборЗаписей Цикл
				Если ТекСтрока.Свойство = ТекПоле.СсылкаНаСвойство Тогда
					ЕстьСвойство = Истина;
					ТекСтрока.Значение = ЗначениеСвойства;	
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьСвойство = Ложь Тогда
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Объект = Объект1С;
				НоваяЗапись.Свойство = ТекПоле.СсылкаНаСвойство;
				НоваяЗапись.Значение = ЗначениеСвойства;
			КонецЕсли;
		
			НаборЗаписей.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			НаборЗаписей.Записать();
				
		Исключение
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Что то пошло не так..");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;	
			
	КонецЦикла;
	
КонецПроцедуры


Процедура ДобавитьЗначениеСведенияОбъекту(Объект1С, Свойство, ЗначениеСвойства)
	
	НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект1С);
	НаборЗаписей.Прочитать();
	
	ЕстьСвойство = Ложь;
	Для Каждого ТекСтрока Из НаборЗаписей Цикл
		Если ТекСтрока.Свойство = Свойство Тогда
			ЕстьСвойство = Истина;
			ТекСтрока.Значение = ЗначениеСвойства;	
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьСвойство = Ложь Тогда
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Объект = Объект1С;
		НоваяЗапись.Свойство = Свойство;
		НоваяЗапись.Значение = ЗначениеСвойства;
	КонецЕсли;
	
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
	НаборЗаписей.Записать();
	
КонецПроцедуры


Функция ПолучитьЗначениеРеквизитаОбъекта1СПоКонструктору(СтруктураНастроек, МенеджерВременныхТаблиц, ИнформацияОСмартПроцессе, ИнформацияРеквизита1С, ДанныеЭлементаБ24)
	
	ЗначениеРеквизита = ПолучитьЗначениеРеквизитаОбъектаПоКлючуJSON(СтруктураНастроек, МенеджерВременныхТаблиц, ИнформацияРеквизита1С.Значение, ДанныеЭлементаБ24, ИнформацияОСмартПроцессе, ИнформацияРеквизита1С);
	
	Возврат ЗначениеРеквизита;
		
КонецФункции

Функция ПолучитьЗначениеРеквизитаОбъектаПоКлючуJSON(СтруктураНастроек, МенеджерВременныхТаблиц, КлючЗапроса, ДанныеЭлементаБ24, ИнформацияОСмартПроцессе = Неопределено, ИнформацияРеквизита1С = Неопределено)
	
	ЗначениеРеквизита				= Неопределено;
	ЗначениеКлюча 					= ДанныеЭлементаБ24.Получить(КлючЗапроса);
	ПрефиксыВнешнихКодовБитрикс24 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();
	
	Если ЗначениеКлюча = Неопределено Тогда
		Возврат ЗначениеРеквизита;
	КонецЕсли;
	
	ОписаниеКлюча = СтруктураНастроек.КлючиСмартПроцесса.КлючиШапки.Получить(КлючЗапроса);
	Если ОписаниеКлюча = Неопределено Тогда
		ЗначениеРеквизита = ЗначениеКлюча;
	Иначе
		
		Если ОписаниеКлюча.Тип = "string" Тогда	
			ЗначениеРеквизита = Строка(ЗначениеКлюча);	
		ИначеЕсли ОписаниеКлюча.Тип = "integer" Тогда	
			ЗначениеРеквизита = Число(ЗначениеКлюча);	
		ИначеЕсли ОписаниеКлюча.Тип = "double" Тогда	
			ЗначениеРеквизита = Число(ЗначениеКлюча);	
		ИначеЕсли ОписаниеКлюча.Тип = "datetime" Тогда	
			
			Попытка
				ЗначениеРеквизита = XMLЗначение(Тип("Дата"), ЗначениеКлюча);
			Исключение
			КонецПопытки;
			 //Дата(ЗначениеКлюча)
		ИначеЕсли ОписаниеКлюча.Тип = "date" Тогда	
			
			Попытка
				ЗначениеРеквизита = XMLЗначение(Тип("Дата"), ЗначениеКлюча);
			Исключение
				
			КонецПопытки;
			
		ИначеЕсли ОписаниеКлюча.Тип = "boolean" Тогда
			ЗначениеРеквизита = ЗначениеКлюча = "Y" ИЛИ ЗначениеКлюча = "true" ИЛИ ЗначениеКлюча = "1" ИЛИ ЗначениеКлюча = 1;	
		ИначеЕсли ОписаниеКлюча.Тип = "url" Тогда	
			ЗначениеРеквизита = Строка(ЗначениеКлюча);	
		ИначеЕсли ОписаниеКлюча.Тип = "enumeration" Тогда
			
			ИдЗначенияСвойств = Формат(ЗначениеКлюча, "ЧГ=0");
			Если ЗначениеЗаполнено(ИдЗначенияСвойств) И НЕ ИнформацияРеквизита1С = Неопределено Тогда
			
				Если ИнформацияРеквизита1С.ТипРеквизита = "ДополнительныйРеквизит" ИЛИ ИнформацияРеквизита1С.ТипРеквизита = "ДополнительноеСведение" Тогда
					
					Если ЗначениеЗаполнено(ИнформацияРеквизита1С.СсылкаНаСвойство) Тогда
						Если ИнформацияРеквизита1С.СсылкаНаСвойство.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда	
							ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыЗначенийПользовательскихПолей", ИдЗначенияСвойств);
							Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
								ЗначениеРеквизита = СформироватьЗначениеСвойстваПоИд(СтруктураНастроек, ИнформацияРеквизита1С, ИдЗначенияСвойств); 
							КонецЕсли;                                   
								
						Иначе
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Нельзя установить в  доп. реквизиты и сведения значение списочного типа пользовательского поля, когда тип не список");
						КонецЕсли;	
					КонецЕсли;	
							
				ИначеЕсли ИнформацияОСмартПроцессе <> Неопределено Тогда
					
					НаименованиеЗначенияСвойства = "";
					
					Запрос = Новый Запрос;
					Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
					Запрос.УстановитьПараметр("Идентификатор", ИдЗначенияСвойств);
					Запрос.Текст = "ВЫБРАТЬ
					|	ВТ_ИдентификаторыЗначенийПользовательскихПолей.Объект КАК Объект,
					|	ЗначенияСвойствОбъектов.Наименование КАК Наименование
					|ИЗ
					|	ВТ_ИдентификаторыЗначенийПользовательскихПолей КАК ВТ_ИдентификаторыЗначенийПользовательскихПолей
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
					|		ПО ВТ_ИдентификаторыЗначенийПользовательскихПолей.Объект = ЗначенияСвойствОбъектов.Ссылка
					|ГДЕ
					|	ВТ_ИдентификаторыЗначенийПользовательскихПолей.Идентификатор = &Идентификатор";
					ВыполненныйЗапрос = Запрос.Выполнить();
					Если ВыполненныйЗапрос.Пустой() Тогда
						
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось найти значение свойства по идентификатору: "+ИдЗначенияСвойств+". Будет произведен поиск по наименованию, а если не найден - создан");
						
						ИнформацияРеквизита1С.СсылкаНаСвойство = СтруктураНастроек.СвойствоДляХраненияЗначенийСвойств;
						
						СсылкаНаНовоеЗначение  = СформироватьЗначениеСвойстваПоИд(СтруктураНастроек, ИнформацияРеквизита1С, ИдЗначенияСвойств);
						Если НЕ СсылкаНаНовоеЗначение = Неопределено Тогда НаименованиеЗначенияСвойства = СсылкаНаНовоеЗначение.Наименование КонецЕсли;
						
					Иначе
						Выборка = ВыполненныйЗапрос.Выбрать();
						Пока Выборка.Следующий() Цикл
							НаименованиеЗначенияСвойства = Выборка.Наименование; 
							Прервать;
						КонецЦикла;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(НаименованиеЗначенияСвойства) Тогда
						
						МетаданныеОбъекта = Неопределено;
						Если ИнформацияОСмартПроцессе.Значение.ДанныеНастроек.ТипСущности1С = "Справочник" Тогда
							МетаданныеОбъекта 	= Метаданные.Справочники[ИнформацияОСмартПроцессе.Значение.ДанныеНастроек.Сущность1С.Значение];
						ИначеЕсли ИнформацияОСмартПроцессе.Значение.ДанныеНастроек.ТипСущности1С = "Документ" Тогда
							МетаданныеОбъекта = Метаданные.Документы[ИнформацияОСмартПроцессе.Значение.ДанныеНастроек.Сущность1С.Значение];
						КонецЕсли;
						
						Если МетаданныеОбъекта <> Неопределено Тогда
							
							МетаданныеРеквизита = МетаданныеОбъекта.Реквизиты.Найти(ИнформацияРеквизита1С.Реквизит1С);
							Если МетаданныеРеквизита <> Неопределено Тогда
								
								ТипыРеквизита = МетаданныеРеквизита.Тип.Типы();
								Для каждого ТекТип Из ТипыРеквизита Цикл
										
									НайденныйМетаОбъект = Метаданные.НайтиПоТипу(ТекТип);
									Если НайденныйМетаОбъект <> Неопределено Тогда
											
										Если Метаданные.Справочники.Содержит(НайденныйМетаОбъект) Тогда
												
											НайденноеЗначениеПоНаименованию = Справочники[НайденныйМетаОбъект.Имя].НайтиПоНаименованию(Строка(НаименованиеЗначенияСвойства));
											Если ЗначениеЗаполнено(НайденноеЗначениеПоНаименованию) Тогда
												ЗначениеРеквизита = НайденноеЗначениеПоНаименованию;
												Прервать;
											КонецЕсли;
												
										ИначеЕсли Метаданные.Перечисления.Содержит(НайденныйМетаОбъект) Тогда
												
											Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления[НайденныйМетаОбъект.Имя].ЗначенияПеречисления Цикл
											    Если ЗначениеПеречисления.Синоним = Строка(НаименованиеЗначенияСвойства) Тогда
											        ЗначениеРеквизита = Перечисления[НайденныйМетаОбъект.Имя][ЗначениеПеречисления.Имя];
											        Прервать;
											    КонецЕсли;
											КонецЦикла;	
												
										КонецЕсли;											
									КонецЕсли;
									
								КонецЦикла;
								
							КонецЕсли;							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
			Иначе
				ЗначениеРеквизита = ИдЗначенияСвойств;	
			КонецЕсли;
			
		ИначеЕсли ОписаниеКлюча.Тип = "employee" ИЛИ ОписаниеКлюча.Тип = "user" Тогда	
			ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыПользователей", Формат(ЗначениеКлюча, "ЧГ=0"));
		ИначеЕсли ОписаниеКлюча.Тип = "crm_company" Тогда	
			ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", Формат(ЗначениеКлюча, "ЧГ=0"));
			Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
				ЗначениеРеквизита = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Компания", Формат(ЗначениеКлюча, "ЧГ=0"));
			КонецЕсли;
		ИначеЕсли ОписаниеКлюча.Тип = "crm_contact" Тогда	
			
			ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", Формат(ЗначениеКлюча, "ЧГ=0"));
			Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
				ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", "#"+Формат(ЗначениеКлюча, "ЧГ=0"));
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
				ЗначениеРеквизита = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Контакт", Формат(ЗначениеКлюча, "ЧГ=0"));
			КонецЕсли;
		ИначеЕсли ОписаниеКлюча.Тип = "crm_currency" ИЛИ ОписаниеКлюча.Тип = "money" Тогда	
			
			ЗначениеРеквизита = Справочники.Валюты.НайтиПоКоду(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьКодПоВалюте(ЗначениеКлюча), Истина);	
			
		ИначеЕсли ОписаниеКлюча.Тип = "crm_entity" Тогда	
			
			Если ОписаниеКлюча.Настройки <> Неопределено Тогда
					
					ТипВладельца = Формат(ОписаниеКлюча.Настройки.Получить("parentEntityTypeId"), "ЧГ=0");
					Если ТипВладельца = "2" Тогда
						
						ИдентификаторСделки = ПрефиксыВнешнихКодовБитрикс24.Проекты + Формат(ЗначениеКлюча, "ЧГ=0");		
						ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыСделокЗаказов", ИдентификаторСделки);
						
						Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
							ИдентификаторЗаказа = ПрефиксыВнешнихКодовБитрикс24.Заказы + Формат(ЗначениеКлюча, "ЧГ=0");		
							ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыСделокЗаказов", ИдентификаторЗаказа);
						КонецЕсли;
						Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось найти заказ по ид:"+ Формат(ЗначениеКлюча, "ЧГ=0"));
						КонецЕсли;
					ИначеЕсли ТипВладельца = "3" Тогда
						
						ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", Формат(ЗначениеКлюча, "ЧГ=0"));
						Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
							ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", "#"+Формат(ЗначениеКлюча, "ЧГ=0"));
						КонецЕсли;
						Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
							ЗначениеРеквизита = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Контакт", Формат(ЗначениеКлюча, "ЧГ=0"));
						КонецЕсли;
			
					ИначеЕсли ТипВладельца = "4" Тогда
						
						ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", Формат(ЗначениеКлюча, "ЧГ=0"));
						Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
							ЗначениеРеквизита = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Компания", Формат(ЗначениеКлюча, "ЧГ=0"));
						КонецЕсли;
						
					ИначеЕсли ТипВладельца = "31" Тогда
						
						ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыСчетов", Формат(ЗначениеКлюча, "ЧГ=0"));
						
					Иначе            //предполагаем смарт-процесс
						
						Для каждого ТекСмартПроцесс Из СтруктураНастроек.НастройкиСинхронизации Цикл
							Если ТекСмартПроцесс.ТипВладельца = ТипВладельца Тогда 
								ЗначениеРеквизита = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, ТекСмартПроцесс.ТипВладельца, ТекСмартПроцесс.Ид, ТекСмартПроцесс.Данные.ТипСущности1С, ТекСмартПроцесс.Данные.Сущность1С.Значение, Формат(ЗначениеКлюча, "ЧГ=0"));
								Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
									ЗначениеРеквизита = СформироватьЭлементСмартПроцессаПоИд(СтруктураНастроек, ТекСмартПроцесс, Формат(ЗначениеКлюча, "ЧГ=0"));
								КонецЕсли;
							КонецЕсли;
						КонецЦикла;
				
					КонецЕсли;
					
			КонецЕсли;
		
		ИначеЕсли ОписаниеКлюча.Тип = "crm" Тогда	
			
			Если ОписаниеКлюча.Настройки <> Неопределено Тогда
				
				Для каждого ТекКлюч Из ОписаниеКлюча.Настройки Цикл
					
					ВладелецНайден = Ложь;
					
					Если ОписаниеКлюча.Настройки.Получить("DEAL") = "Y" Тогда
						
						ИдентификаторСделки = ПрефиксыВнешнихКодовБитрикс24.Проекты + Формат(ЗначениеКлюча, "ЧГ=0");		
						ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыСделокЗаказов", ИдентификаторСделки);
						Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось найти заказ по ид:"+ Формат(ЗначениеКлюча, "ЧГ=0"));
						КонецЕсли;
						ВладелецНайден = Истина;
						
					КонецЕсли;
					Если ОписаниеКлюча.Настройки.Получить("ORDER")= "Y" Тогда
						
						ИдентификаторЗаказа = ПрефиксыВнешнихКодовБитрикс24.Заказы + Формат(ЗначениеКлюча, "ЧГ=0");		
						ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыСделокЗаказов", ИдентификаторЗаказа);
						Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось найти заказ по ид:"+ Формат(ЗначениеКлюча, "ЧГ=0"));
						КонецЕсли;
						ВладелецНайден = Истина;
						
					КонецЕсли;
					Если ОписаниеКлюча.Настройки.Получить("COMPANY") = "Y" Тогда
						
						ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", Формат(ЗначениеКлюча, "ЧГ=0"));
						Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
							ЗначениеРеквизита = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Компания", Формат(ЗначениеКлюча, "ЧГ=0"));
						КонецЕсли;
						ВладелецНайден = Истина;
						
					КонецЕсли;
					
					Если ОписаниеКлюча.Настройки.Получить("CONTACT") = "Y" Тогда
						                                           
						ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", Формат(ЗначениеКлюча, "ЧГ=0"));
						Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
							ЗначениеРеквизита = ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", "#"+Формат(ЗначениеКлюча, "ЧГ=0"));
						КонецЕсли;
						Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
							ЗначениеРеквизита = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Контакт", Формат(ЗначениеКлюча, "ЧГ=0"));
						КонецЕсли;
						ВладелецНайден = Истина;
						
					КонецЕсли;
					
					Если НЕ ВладелецНайден Тогда
						Для каждого ТекСмартПроцесс Из СтруктураНастроек.НастройкиСинхронизации Цикл
							Если ОписаниеКлюча.Настройки.Получить("DYNAMIC_" + ТекСмартПроцесс.ТипВладельца) = "Y" Тогда
								ЗначениеРеквизита = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, ТекСмартПроцесс.ТипВладельца, ТекСмартПроцесс.Ид, ТекСмартПроцесс.Данные.ТипСущности1С, ТекСмартПроцесс.Данные.Сущность1С.Значение, Формат(ЗначениеКлюча, "ЧГ=0"));
								Если НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
									ЗначениеРеквизита = СформироватьЭлементСмартПроцессаПоИд(СтруктураНастроек, ТекСмартПроцесс, Формат(ЗначениеКлюча, "ЧГ=0"));
								КонецЕсли;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
		
		КонецЕсли;
	КонецЕсли;
		
	Возврат ЗначениеРеквизита;
		
КонецФункции

#КонецОбласти


#Область ПринудительнаяЗагрузкаПодчиненныхОбъектов1С

Функция СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, ТипВладельца, Ид) 
		
	Объект1С = Неопределено;
	
	Если НЕ ЗначениеЗаполнено(Ид) Тогда
		Возврат Объект1С;
	КонецЕсли;
	
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") Тогда
		
		Если СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроекСинхронизации 									= СтруктураНастроек.СтруктураНастроекДляПодсистемыСинхронизации;	
			СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации 	= СтруктураНастроек.НастройкиПодключенияАвторизации;
		Иначе
			МодульОбщегоНазначенияВызовСервера = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ОбщегоНазначенияВызовСервера");
			СтруктураНастроекСинхронизации 	= МодульОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(СтруктураНастроек.НастройкаПодключения,,3, Истина);
			Если СтруктураНастроекСинхронизации = Неопределено Тогда
				Возврат Ид;
			КонецЕсли;
		КонецЕсли;
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////// 
		СтруктураНастроекСинхронизации.Логирование = СтруктураНастроек.Логирование;
		
		МодульЗагрузкаСервер = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ЗагрузкаСервер");
		Объект1С = МодульЗагрузкаСервер.СформироватьКомпаниюКонтактПоИд(СтруктураНастроекСинхронизации, ТипВладельца, Ид);
		
		СтруктураНастроек.Логирование.НомерСообщения 		= СтруктураНастроекСинхронизации.Логирование.НомерСообщения;
		СтруктураНастроек.НастройкиПодключенияАвторизации 	= СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////			
		Если НЕ СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроек.Вставить("СтруктураНастроекДляПодсистемыСинхронизации", СтруктураНастроекСинхронизации);
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат Объект1С;	

КонецФункции

Функция СформироватьНоменклатуруХарактеристикуПоИд(СтруктураНастроек, Ид) 
		
	Результат = Неопределено;
	
	Если НЕ ЗначениеЗаполнено(Ид) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") Тогда
		
		Если СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроекСинхронизации 									= СтруктураНастроек.СтруктураНастроекДляПодсистемыСинхронизации;	
			СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации 	= СтруктураНастроек.НастройкиПодключенияАвторизации;
		Иначе
			МодульОбщегоНазначенияВызовСервера = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ОбщегоНазначенияВызовСервера");
			СтруктураНастроекСинхронизации 	= МодульОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(СтруктураНастроек.НастройкаПодключения,,3, Истина);
			Если СтруктураНастроекСинхронизации = Неопределено Тогда
				Возврат Ид;
			КонецЕсли;
		КонецЕсли;   
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////		
		СтруктураНастроекСинхронизации.Логирование = СтруктураНастроек.Логирование;
		
		МодульЗагрузкаСервер = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ЗагрузкаСервер");
		Результат = МодульЗагрузкаСервер.СформироватьНоменклатуруХарактеристикуПоИд(СтруктураНастроекСинхронизации, Ид);
		
		СтруктураНастроек.Логирование.НомерСообщения 		= СтруктураНастроекСинхронизации.Логирование.НомерСообщения;
		СтруктураНастроек.НастройкиПодключенияАвторизации 	= СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////		
		Если НЕ СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроек.Вставить("СтруктураНастроекДляПодсистемыСинхронизации", СтруктураНастроекСинхронизации);
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат Результат;	

КонецФункции

Функция СформироватьЕдиницуИзмеренияПоИд(СтруктураНастроек, Ид) 
		
	Объект1С = Неопределено;
	
	Если НЕ ЗначениеЗаполнено(Ид) Тогда
		Возврат Объект1С;
	КонецЕсли;
	
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") Тогда
		
		Если СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроекСинхронизации 									= СтруктураНастроек.СтруктураНастроекДляПодсистемыСинхронизации;	
			СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации 	= СтруктураНастроек.НастройкиПодключенияАвторизации;
		Иначе
			МодульОбщегоНазначенияВызовСервера = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ОбщегоНазначенияВызовСервера");
			СтруктураНастроекСинхронизации 	= МодульОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(СтруктураНастроек.НастройкаПодключения,,3, Истина);
			Если СтруктураНастроекСинхронизации = Неопределено Тогда
				Возврат Ид;
			КонецЕсли;
		КонецЕсли;   
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////		
		СтруктураНастроекСинхронизации.Логирование = СтруктураНастроек.Логирование;
		
		МодульЗагрузкаСервер = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ЗагрузкаСервер");
		Объект1С = МодульЗагрузкаСервер.СформироватьЕдиницуИзмеренияПоИд(СтруктураНастроекСинхронизации, Ид);
	
		СтруктураНастроек.Логирование.НомерСообщения 		= СтруктураНастроекСинхронизации.Логирование.НомерСообщения;
		СтруктураНастроек.НастройкиПодключенияАвторизации 	= СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации;    
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////			
		Если НЕ СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроек.Вставить("СтруктураНастроекДляПодсистемыСинхронизации", СтруктураНастроекСинхронизации);
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат Объект1С;	

КонецФункции

Функция СформироватьЗначениеСвойстваПоИд(СтруктураНастроек, ИнформацияРеквизита1С, Ид) 
		
	Объект1С = Неопределено;
	                                     
	Если НЕ ЗначениеЗаполнено(Ид) Тогда
		Возврат Объект1С;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИнформацияРеквизита1С.Идентификатор) Тогда
		Возврат Объект1С;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИнформацияРеквизита1С.СсылкаНаСвойство) Тогда
		Возврат Объект1С;
	КонецЕсли;
	
	ПараметрыЗапроса = "&moduleId=crm&id="+ИнформацияРеквизита1С.Идентификатор;
	ДанныеПортала = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/userfieldconfig.get", ПараметрыЗапроса); 
	Если ДанныеПортала = Неопределено Тогда
		Возврат Объект1С;
	КонецЕсли;

	result = ДанныеПортала.Получить("result");
	Если result = Неопределено Тогда
		Возврат Объект1С;
	КонецЕсли;
	
	field = result.Получить("field");
	Если field = Неопределено Тогда
		Возврат Объект1С;
	КонецЕсли;
	
	enum = field.Получить("enum");
	Если enum = Неопределено Тогда
		Возврат Объект1С;
	КонецЕсли;
	
	Для каждого ТекЭлемент Из enum Цикл
		Если Формат(ТекЭлемент.Получить("id"), "ЧГ=0") = Ид Тогда
			
			Ид 			 = Формат(ТекЭлемент.Получить("id"), "ЧГ=0");
			Наименование = ТекЭлемент.Получить("value");
			
			Если НЕ ИнформацияРеквизита1С.СсылкаНаСвойство = СтруктураНастроек.СвойствоДляХраненияЗначенийСвойств Тогда
				Объект1С = Справочники.ЗначенияСвойствОбъектов.НайтиПоНаименованию(Наименование, Истина,, ИнформацияРеквизита1С.СсылкаНаСвойство);       
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект1С) Тогда
				
				ЗначениеСвойстваОбъект = Справочники.ЗначенияСвойствОбъектов.СоздатьЭлемент();
				ЗначениеСвойстваОбъект.Владелец 		= ИнформацияРеквизита1С.СсылкаНаСвойство;
				ЗначениеСвойстваОбъект.Наименование 		= Наименование;
				ЗначениеСвойстваОбъект.Записать();
				Объект1С = ЗначениеСвойстваОбъект.Ссылка;
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан элемент: " + Наименование, Объект1С);
				
			Иначе
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Найден по наименованию: " + Наименование, Объект1С);
			КонецЕсли;
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваСмартПроцесса, Объект1С, Ид, ИнформацияРеквизита1С.Идентификатор);
			
		КонецЕсли;
	КонецЦикла;
		
	Возврат Объект1С;	

КонецФункции

Функция СформироватьЭлементСмартПроцессаПоИд(СтруктураНастроек, СмартПроцесс, Ид)
	
	Объект1С = Неопределено;
	
	Если НЕ ЗначениеЗаполнено(Ид) Тогда
		Возврат Объект1С;
	КонецЕсли;
	
	Если СмартПроцесс.Данные.Сущность1С = Неопределено Тогда
		Возврат Объект1С;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить(Ид);
	
	ЗагружаемыеСмартПроцессы = Новый Соответствие;
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("ИдСмартПроцесса"	, СмартПроцесс.Ид);
		СтруктураДанных.Вставить("Наименование"		, СмартПроцесс.Наименование);
		СтруктураДанных.Вставить("ДанныеНастроек"	, СмартПроцесс.Данные);
		СтруктураДанных.Вставить("ДанныеПортала"	, Новый Массив);
	ЗагружаемыеСмартПроцессы.Вставить(СмартПроцесс.ТипВладельца, СтруктураДанных);

	Для каждого ТекЗагружаемыйСмартПроцесс Из ЗагружаемыеСмартПроцессы Цикл
	 	Б24_СМ_ОбщегоНазначенияВызовСервера.ЗагрузитьСПорталаИОбработать(СтруктураНастроек,  ТекЗагружаемыйСмартПроцесс, МассивЭлементов);
	КонецЦикла;
	
	Объект1С = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СмартПроцесс.ТипВладельца
	, СмартПроцесс.Ид, СмартПроцесс.Данные.ТипСущности1С, СмартПроцесс.Данные.Сущность1С.Значение, Ид); 
	
	Возврат Объект1С;
	
КонецФункции


Функция ПолучитьЭлементСмартПроцессаПоИдДляДругихПодсистем(СтруктураНастроек, ТипВладельцаСмартПроцесса, ЗначениеКлюча) Экспорт
	
	Объект1С = Неопределено;
	
	Если СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСмартПроцессы") Тогда
		СтруктураНастроекСмартПроцессов = СтруктураНастроек.СтруктураНастроекДляПодсистемыСмартПроцессы;
		СтруктураНастроекСмартПроцессов.СтруктураНастроек.НастройкиПодключенияАвторизации = СтруктураНастроек.НастройкиПодключенияАвторизации;     //передаем актуальный токен
	Иначе
		
		СтруктураНастроекСмартПроцессов = Новый Структура;
		СтруктураНастроекСмартПроцессов.Вставить("СмартПроцессы", Новый Соответствие);
		
		СтруктураНастроекСмартПроцессы = Б24_СМ_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(СтруктураНастроек.НастройкаПодключения, Ложь, 3);
		Если СтруктураНастроекСмартПроцессы = Неопределено Тогда
			Возврат Объект1С;
		КонецЕсли;
		СтруктураНастроекСмартПроцессов.Вставить("СтруктураНастроек", СтруктураНастроекСмартПроцессы);
		
	КонецЕсли;
		
	СтруктураНастроекСмартПроцессов.СтруктураНастроек.Логирование = СтруктураНастроек.Логирование;            //передаем актуальный данные по логированию
	
	
	ДанныеСмартПроцесса = СтруктураНастроекСмартПроцессов.СмартПроцессы.Получить(ТипВладельцаСмартПроцесса);
	Если ДанныеСмартПроцесса = Неопределено Тогда
		
		ДанныеСмартПроцесса = Новый Структура;
		ДанныеСмартПроцесса.Вставить("СмартПроцесс"			, "");
		ДанныеСмартПроцесса.Вставить("КлючиСмартПроцесса"	, "");
		
		НастройкиСинхронизации = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(СтруктураНастроек.НастройкаПодключения);
		Если НастройкиСинхронизации.Настройки = Неопределено Тогда
			Возврат Объект1С;
		КонецЕсли;
			
		ДанныеСмартПроцессов = НастройкиСинхронизации.Настройки.Получить();
		Если ДанныеСмартПроцессов = Неопределено Тогда
			Возврат Объект1С;
		КонецЕсли;

		Для каждого ТекСмартПроцесс Из ДанныеСмартПроцессов Цикл
		
			Если ТипВладельцаСмартПроцесса = ТекСмартПроцесс.ТипВладельца Тогда
				
				ДанныеСмартПроцесса.СмартПроцесс 		= ТекСмартПроцесс;
				ДанныеСмартПроцесса.КлючиСмартПроцесса 	= Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьСтруктуруКлючейСмартПроцесса(СтруктураНастроек.НастройкаПодключения, ТекСмартПроцесс.ТипВладельца);
				
			КонецЕсли;
		
		КонецЦикла;
		
		СтруктураНастроекСмартПроцессов.СмартПроцессы.Вставить(ТипВладельцаСмартПроцесса, ДанныеСмартПроцесса);

	КонецЕсли;
	
	
	ТекСмартПроцесс = ДанныеСмартПроцесса.СмартПроцесс;
	
	Объект1С = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, ТипВладельцаСмартПроцесса, ТекСмартПроцесс.Ид, ТекСмартПроцесс.Данные.ТипСущности1С, ТекСмартПроцесс.Данные.Сущность1С.Значение, ЗначениеКлюча);
	Если НЕ ЗначениеЗаполнено(Объект1С) Тогда
		
		СтруктураНастроекСмартПроцессов.СтруктураНастроек.КлючиСмартПроцесса = ДанныеСмартПроцесса.КлючиСмартПроцесса;
		
		Объект1С = СформироватьЭлементСмартПроцессаПоИд(СтруктураНастроекСмартПроцессов.СтруктураНастроек, ТекСмартПроцесс, ЗначениеКлюча);
		
	КонецЕсли;
	
	
	Если НЕ СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСмартПроцессы") Тогда
		СтруктураНастроек.Вставить("СтруктураНастроекДляПодсистемыСмартПроцессы", СтруктураНастроекСмартПроцессов);
	КонецЕсли;
	
	СтруктураНастроек.Логирование.НомерСообщения 		= СтруктураНастроекСмартПроцессов.СтруктураНастроек.Логирование.НомерСообщения;              //чтобы не затирались. В разрезе номеров хранятся в регистре сведений
	СтруктураНастроек.НастройкиПодключенияАвторизации 	= СтруктураНастроекСмартПроцессов.СтруктураНастроек.НастройкиПодключенияАвторизации;         //возвращаем актуальный токен
	
	Возврат Объект1С;
	
КонецФункции

#КонецОбласти


#Область ПрочиеПроцедурыИФункции

Функция ПолучитьОбъект1СПоИдентификаторуБитрикс24(МенеджерВременныхТаблиц, ИмяТаблицы, Идентификатор)
	
	//МенеджерВременныхТаблиц.Таблицы[ИмяТаблицы].ПолучитьДанные().Выгрузить()
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_Идентификаторы.Объект КАК Объект
	|ИЗ
	|	"+ИмяТаблицы+" КАК ВТ_Идентификаторы
	|ГДЕ
	|	ВТ_Идентификаторы.Идентификатор = &Идентификатор";
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат = Выборка.Объект;	
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция СформироватьПростуюСтруктуруСИд(Ключ, Значение)
	
	Результат = Новый Структура("Ид_" + Ключ, Значение);
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыДляПереопределяемыхСобытий(СтруктураНастроек, ИдЭлементаБ24, Объект1С, ОбъектПортала)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтруктураНастроек"	, СтруктураНастроек);
	ДополнительныеПараметры.Вставить("Объект1С"				, Объект1С);  
	ДополнительныеПараметры.Вставить("ОбъектПортала"		, ОбъектПортала);      
	ДополнительныеПараметры.Вставить("СтруктураДанных"		, ОбъектПортала);      
	
	Возврат ДополнительныеПараметры;

КонецФункции

#КонецОбласти

