
#Область Общее  

Процедура ЗагрузкаИзменений(СтруктураНастроек, ЗагружаемыеСущности) Экспорт
	
	ВидыСобытий = РегистрыСведений.Б24_СУ_Настройки.СобытияБитрикс24();
	
	ТелоHTTPЗапроса = "&"+ СтруктураНастроек.ПараметрКоннектора + "&clear=1";	
	

	СтруктураНастроек.Логирование.Измерение2 = "Загрузка изменений";
	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Получение пакетов изменений");

	ЕстьСобытия = Истина;
	Пока ЕстьСобытия = Истина Цикл	
		
		СтруктураНастроек.ТаблицаСопоставленияИзменений.Очистить();
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("Заказы" 	, Новый Структура("Данные, Операция", Новый Массив, СтруктураНастроек.ТипыОперацийСинхронизации.Заказы));
		СтруктураДанных.Вставить("Отгрузки" , Новый Структура("Данные, Операция", Новый Массив, СтруктураНастроек.ТипыОперацийСинхронизации.Отгрузки));
		СтруктураДанных.Вставить("УдалитьЗаказы" 	, Новый Структура("Данные, Операция", Новый Массив, СтруктураНастроек.ТипыОперацийСинхронизации.УдалитьЗаказы));
		СтруктураДанных.Вставить("УдалитьОтгрузки" 	, Новый Структура("Данные, Операция", Новый Массив, СтруктураНастроек.ТипыОперацийСинхронизации.УдалитьОтгрузки));
		
		ДанныеСПортала = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/event.offline.get", ТелоHTTPЗапроса); 
		
		Если НЕ ЗначениеЗаполнено(ДанныеСПортала) Тогда
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
			ЕстьСобытия = Ложь;
			Возврат;
		КонецЕсли;
		
		result = ДанныеСПортала.Получить("result");
		
		Если НЕ ЗначениеЗаполнено(result) Тогда
			ЕстьСобытия = Ложь;
			Возврат;
		КонецЕсли;
		
		СтруктураНастроек.ИдПроцессаЗагрузки = Строка(result.Получить("process_id"));
		
		События = result.Получить("events");
		
		Если НЕ ЗначениеЗаполнено(События) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Нет изменений на портале");
			ЕстьСобытия = Ложь;
			Возврат;
		КонецЕсли;
		
		Для Каждого ТекСобытие Из События Цикл
			
			НазваниеСобытия 	= ВРег(ТекСобытие.Получить("EVENT_NAME"));
			ИнформацияОСобытии 	= ТекСобытие.Получить("EVENT_DATA");
			ИдЗаписи 			= ТекСобытие.Получить("ID");
			ИдСообщения 		= ТекСобытие.Получить("MESSAGE_ID");
			
			Если НЕ ЗначениеЗаполнено(ИнформацияОСобытии) Тогда
				Продолжить;
			КонецЕсли;
			
			ДанныеПолейСобытия 	= ИнформацияОСобытии.Получить("FIELDS");
			
			Если НЕ ЗначениеЗаполнено(ДанныеПолейСобытия) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ДанныеПолейСобытия.Получить("ENTITY_TYPE_ID") = Неопределено Тогда
				ИдЭлемента = Формат(ДанныеПолейСобытия.Получить("ID"), "ЧГ=0");
			ИначеЕсли ДанныеПолейСобытия.Получить("ENTITY_TYPE_ID") = "REQUISITE" Тогда
				ИдЭлемента = "8_" + Формат(ДанныеПолейСобытия.Получить("ENTITY_ID"), "ЧГ=0");
			ИначеЕсли Формат(ДанныеПолейСобытия.Получить("ENTITY_TYPE_ID"), "ЧГ=0") = "31" Тогда
				ИдЭлемента = Формат(ДанныеПолейСобытия.Получить("ID"), "ЧГ=0");
			Иначе
				Продолжить;
			КонецЕсли;
			
			СтруктураОтборка = Новый Структура;
			СтруктураОтборка.Вставить("ИдЗаписи"	, ИдЗаписи);
			СтруктураОтборка.Вставить("ИдСообщения"	, ИдСообщения);
			СтруктураОтборка.Вставить("Событие"		, НазваниеСобытия);
			СтруктураОтборка.Вставить("ИдЭлемента"	, ИдЭлемента);
			
			СтруктураНастроек.ТаблицаСопоставленияИзменений.Добавить(СтруктураОтборка);

			Если ЗагружаемыеСущности.ЗагружатьЗаказы Тогда
				
				Если НазваниеСобытия = ВРег(ВидыСобытий.ИзменениеЗаказа) Тогда
					СтруктураДанных.Заказы.Данные.Добавить(ИдЭлемента);
				КонецЕсли;
				
				Если НазваниеСобытия = ВРег(ВидыСобытий.УдалениеЗаказа) Тогда
					СтруктураДанных.УдалитьЗаказы.Данные.Добавить(ИдЭлемента);
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗагружаемыеСущности.ЗагружатьОтгрузки Тогда
				
				Если НазваниеСобытия = ВРег(ВидыСобытий.ИзменениеОтгрузки) Тогда
					СтруктураДанных.Отгрузки.Данные.Добавить(ИдЭлемента);
				КонецЕсли;
				
				Если НазваниеСобытия = ВРег(ВидыСобытий.УдалениеОтгрузки) Тогда
					СтруктураДанных.УдалитьОтгрузки.Данные.Добавить(ИдЭлемента);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого ТекСвойство Из СтруктураДанных Цикл
			
			ЗначениеСвойстваСтруктуры = ТекСвойство.Значение;
			
			Если ЗначениеСвойстваСтруктуры.Данные.Количество()> 0 Тогда
				
				СтруктураНастроек.Операция = ЗначениеСвойстваСтруктуры.Операция; 
				СтруктураНастроек.Логирование.Измерение3 = "Загрузка изменений в 1С с типом: " + Строка(СтруктураНастроек.Операция);
				
				ОбработкаПакетаИзменений(СтруктураНастроек, ЗначениеСвойстваСтруктуры.Данные);
				
				СтруктураНастроек.Логирование.Измерение3 = "";
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПакетаИзменений(СтруктураНастроек,  МассивЭлементов) 
	
	ТипыОперацийСинхронизации = СтруктураНастроек.ТипыОперацийСинхронизации;
	
	Если СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Заказы Тогда             
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Загрузка данных в 1С, пришедших  с портала с типом: " + Строка(СтруктураНастроек.Операция));
		
		ДанныеЗаказов = ПолучитьДанныеЗаказов(СтруктураНастроек, МассивЭлементов);
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Обработки данных в 1С, пришедших  с портала с типом: " + Строка(СтруктураНастроек.Операция));
		
		ЗагрузитьОбновитьЗаказы(СтруктураНастроек, ДанныеЗаказов);
		
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.Отгрузки Тогда
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Загрузка данных в 1С, пришедших  с портала с типом: " + Строка(СтруктураНастроек.Операция));
		
		ДанныОтгрузок = ПолучитьДанныеОтгрузок(СтруктураНастроек, МассивЭлементов);
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Обработки данных в 1С, пришедших  с портала с типом: " + Строка(СтруктураНастроек.Операция));
		
		ЗагрузитьОбновитьОтгрузки(СтруктураНастроек, ДанныОтгрузок);
		
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.УдалитьЗаказы Тогда
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Пометка на удаление удаленных заказов.");
		
		ПометитьНаУдалениеДокументы(СтруктураНастроек, МассивЭлементов, СтруктураНастроек.Операция);
		
	ИначеЕсли СтруктураНастроек.Операция = ТипыОперацийСинхронизации.УдалитьОтгрузки Тогда
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Пометка на удаление удаленных отгрузок.");
		
		ПометитьНаУдалениеДокументы(СтруктураНастроек, МассивЭлементов, СтруктураНастроек.Операция);
		
	КонецЕсли;                                                                                                                                    
	
	СтруктураНастроек.Логирование.Измерение4 = "";
	СтруктураНастроек.Логирование.Измерение5 = "";
	СтруктураНастроек.Логирование.Измерение6 = ""; 
	
КонецПроцедуры

Процедура ПометитьНаУдалениеДокументы(СтруктураНастроек, МассивЭлементов, Операция)
	
	ТипыОперацийСинхронизации = СтруктураНастроек.ТипыОперацийСинхронизации;
	ПрефиксВнешнихКодовБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();  
	
	Для каждого ТекЭлемент Из МассивЭлементов Цикл
		
		Если СтруктураНастроек.Операция = ТипыОперацийСинхронизации.УдалитьЗаказы Тогда
			
			Заказ = СсылкаПоИдБ24(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Заказ, ПрефиксВнешнихКодовБитрикс24.Заказы + ТекЭлемент);
			Если ЗначениеЗаполнено(Заказ) Тогда
				Попытка
					Заказ.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Помечен на удаление заказ: " + Строка(Заказ), Заказ.Ссылка);
				Исключение  
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не удалось пометить на удаление заказ: " + Строка(Заказ), Заказ.Ссылка);
				КонецПопытки;
			КонецЕсли;
				
		ИначеЕсли Операция = ТипыОперацийСинхронизации.УдалитьОтгрузки Тогда
			
        	Отгрузка = СсылкаПоИдБ24(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка, ТекЭлемент);
			Если ЗначениеЗаполнено(Отгрузка) Тогда
				Попытка
					Отгрузка.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Помечена на удаление отгрузка: " + Строка(Отгрузка), Отгрузка.Ссылка);
				Исключение  
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не удалось пометить на удаление отгрузку: " + Строка(Отгрузка), Отгрузка.Ссылка);
				КонецПопытки;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры    

#КонецОбласти   

#Область Заказы  

Функция ПолучитьДанныеЗаказов(СтруктураНастроек, мДанных)
	
	РезультатОбработки = Новый Соответствие;
	
	мЗапросы = Новый Массив;
	
	Инкр = 0;
	Для каждого ИдЭлемента Из мДанных Цикл     
		
		Инкр = Инкр + 1;
		ИнкрСтрокой = "_"+Формат(Инкр, "ЧГ=0");
		
		мЗапросы.Добавить("cmd[ORDER"+ИнкрСтрокой+"]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("sale.order.get?id=" + ИдЭлемента));
		мЗапросы.Добавить("cmd[CRM"+ИнкрСтрокой+"]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.orderentity.list?filter[ownerTypeId]=2&filter[orderId]=" + ИдЭлемента));
		мЗапросы.Добавить("cmd[DEAL"+ИнкрСтрокой+"]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.deal.get?id=$result[CRM"+ИнкрСтрокой+"][orderEntity][0][ownerId]"));

	КонецЦикла; 
	
	ТелоHTTPЗапроса = "";
	Инкремент = 0;
	Для каждого ТекЗапрос Из мЗапросы Цикл
		
		Если Инкремент = 48 Тогда
			ОбработатьБатчЗапросыЗаказов(СтруктураНастроек, ТелоHTTPЗапроса, РезультатОбработки);
			ТелоHTTPЗапроса = "";
			Инкремент = 0;   
		Иначе
			ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + ТекЗапрос; 	
		КонецЕсли;
		
		Инкремент = Инкремент +1;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТелоHTTPЗапроса) Тогда     
		ОбработатьБатчЗапросыЗаказов(СтруктураНастроек, ТелоHTTPЗапроса, РезультатОбработки);
	КонецЕсли;
	
	Возврат РезультатОбработки;			
	
КонецФункции

Процедура ОбработатьБатчЗапросыЗаказов(СтруктураНастроек, ТелоHTTPЗапроса, РезультатОбработки)
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	Если result <> Неопределено Тогда
		
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда
			
			Для каждого ТекЭлемент Из result2 Цикл
				
				мЧасти = СтрРазделить(ТекЭлемент.Ключ, "_");
				
				ДанныеКлюча = РезультатОбработки.Получить(мЧасти[1]); 
				Если ДанныеКлюча = Неопределено Тогда
					ДанныеКлюча = Новый Структура;
					ДанныеКлюча.Вставить("Заказ", Неопределено);	
					ДанныеКлюча.Вставить("Связка", Неопределено);	
					ДанныеКлюча.Вставить("Проект", Неопределено);	
				КонецЕсли;
				
				Если мЧасти[0] = "ORDER" Тогда    
					Если ТипЗнч(ТекЭлемент.Значение) = Тип("Соответствие") Тогда
						ДанныеКлюча.Заказ = ТекЭлемент.Значение.Получить("order");
					КонецЕсли;
				ИначеЕсли мЧасти[0] = "CRM" Тогда   
					Если ТипЗнч(ТекЭлемент.Значение) = Тип("Соответствие") Тогда
						ДанныеКлюча.Связка = ТекЭлемент.Значение.Получить("orderEntity");
					КонецЕсли;
				ИначеЕсли мЧасти[0] = "DEAL" Тогда     
					ДанныеКлюча.Сделка = ТекЭлемент.Значение;
				КонецЕсли;
				
				РезультатОбработки.Вставить(мЧасти[1], ДанныеКлюча); 
				
			КонецЦикла;
			
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьОбновитьЗаказы(СтруктураНастроек, мДанных)
		
	ПрефиксВнешнихКодовБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();
	
	МенеджерВременныхТаблиц = ПолучитьБазовыйМенеджерТаблицДляДокументов(СтруктураНастроек);
	
	ЗагруженныеОбъекты = Новый Массив;
	Для каждого ТекЭлемент Из мДанных Цикл
		
		ДанныеЗаказа = ТекЭлемент.Значение.Заказ; 
		ДанныеСделки = ТекЭлемент.Значение.Проект;
		
		Если ТипЗнч(ДанныеЗаказа) <> Тип("Соответствие") ИЛИ ТипЗнч(ДанныеСделки) <> Тип("Соответствие")  Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьКритическиеОшибки = Ложь;
		
#Область ПолучениеЗначенийЗаказа
		ИдЗаказаБитрикс24		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеЗаказа.Получить("id"),"ЧГ=0"));
		ИдСделкиБитрикс24		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеСделки.Получить("ID"),"ЧГ=0"));
		
		ВнешнийИдентификатор	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеСделки.Получить("ORIGIN_ID"),"ЧГ=0"));
		
		Отменен	 				= ДанныеЗаказа.Получить("canceled") = "Y";   
		
		ДатаНачала = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ДанныеЗаказа.Получить("dateInsert"));
		Если Не ЗначениеЗаполнено(ДатаНачала) Тогда ДатаНачала = ТекущаяДатаСеанса() КонецЕсли;
		
		КомментарийСделки 		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ДанныеСделки.Получить("COMMENTS"));
		
		КодВалюты 	   	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьКодПоВалюте(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеСделки.Получить("CURRENCY_ID"),"ЧГ=0")));
		ИдКомпании    	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеСделки.Получить("COMPANY_ID"),"ЧГ=0"));
		ИдКонтакта     	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеСделки.Получить("CONTACT_ID"),"ЧГ=0"));
		
		//ИдРеквизита	 = Формат(ПолучитьЗначениеИзМассиваСоответствий(мДанных.ИнформацияОРеквизитахСделок, "ENTITY_ID", ИдСделкиБитрикс24, "REQUISITE_ID"),"ЧГ=0");
		
		Товары = Неопределено;
		Товары = ДанныеЗаказа.Получить("basketItems");                                                                                             
		Товары = ?(Товары = Неопределено, Новый Массив, Товары);
		
		СуммаВключаетНДС = Истина;		
		Если Товары.Количество() > 0 Тогда 
			СуммаВключаетНДС = Товары[0].Получить("vatIncluded") = "Y";	
		КонецЕсли;	  
		
		Ответственный = Справочники.Пользователи.ПустаяСсылка();
		ИдОтветственного = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеСделки.Получить("ASSIGNED_BY_ID"),"ЧГ=0"));
		Если ЗначениеЗаполнено(ИдОтветственного) Тогда
			НайденнаяСтрока = СтруктураНастроек.ТаблицаСопоставленияПользователей.Найти(ИдОтветственного, "ИдПользователя");
			Если НайденнаяСтрока <> Неопределено Тогда
				Ответственный = НайденнаяСтрока.Пользователь1С; 					
			КонецЕсли;
		КонецЕсли; 
		
		Отгрузки		= ДанныеЗаказа.Получить("shipments");		
		Отгрузки 		= ?(Отгрузки = Неопределено, Новый Массив, Отгрузки);
		СтоимостьДоставки = 0;       
		Для Каждого ТекОтгрузка Из Отгрузки Цикл
			СтоимостьДоставки = СтоимостьДоставки + Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ТекОтгрузка.Получить("priceDelivery"),"ЧРД=.; ЧГ=0"));
		КонецЦикла;  
		
		ИдРеквизита			= "";
		ИдБанкСчетаРек		= "";
		Реквизиты = ДанныеЗаказа.Получить("requisiteLink");
		Если Реквизиты <> Неопределено И ТипЗнч(Реквизиты) <> Тип("Массив") Тогда
			ИдРеквизита			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты.Получить("requisiteId"),"ЧГ=0"));
			ИдБанкСчетаРек		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты.Получить("bankDetailId"),"ЧГ=0"));
		ИначеЕсли Реквизиты <> Неопределено И ТипЗнч(Реквизиты) = Тип("Массив") Тогда
			Если Реквизиты.Количество() > 0 Тогда
				ИдРеквизита			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты[0].Получить("requisiteId"),"ЧГ=0"));
				ИдБанкСчетаРек		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(Реквизиты[0].Получить("bankDetailId"),"ЧГ=0"));
			КонецЕсли;
		КонецЕсли;                                                                                                                      
#КонецОбласти
		
#Область ФильтрацияПоЗагрузке
		Если ЗагруженныеОбъекты.Найти(ИдЗаказаБитрикс24) <> Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЗаказаБитрикс24);
#КонецОбласти
		
		ЭтоНовыйЗаказ = Ложь;
#Область ПоискСозданиеЗаказа		                        
        Заказ = СсылкаПоИдБ24(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Заказ, ПрефиксВнешнихКодовБитрикс24.Заказы + ИдЗаказаБитрикс24);
		Если ЗначениеЗаполнено(ВнешнийИдентификатор) И НЕ ЗначениеЗаполнено(Заказ) Тогда
			Заказ = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ДокументСсылка.ЗаказКлиента"), ВнешнийИдентификатор);
		КонецЕсли;         
		
		Если НЕ ЗначениеЗаполнено(Заказ) Тогда 
			Заказ = СсылкаПоИдБ24(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Проект, ПрефиксВнешнихКодовБитрикс24.Проекты + ИдСделкиБитрикс24);
		КонецЕсли;     
		
		Если НЕ ЗначениеЗаполнено(Заказ) Тогда 
			
			Если Отменен Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, " Заказ с ид:  " + Строка(ИдЗаказаБитрикс24) + " будет пропущен т.к.  его в 1С нет и он отменен.");
				Продолжить;  			
			КонецЕсли;					
			
			Заказ = Документы.ЗаказКлиента.СоздатьДокумент();
			ЭтоНовыйЗаказ = Истина;
		Иначе
			Заказ = Заказ.ПолучитьОбъект();
		КонецЕсли;
#КонецОбласти	

		ИнформацияОКлиенте 		= ПолучитьИнформациюОКомпанииКонтакте(СтруктураНастроек, МенеджерВременныхТаблиц, ИдКомпании, ИдКонтакта, ИдРеквизита);
		
		Партнер					= ИнформацияОКлиенте.Партнер;
		Контрагент				= ИнформацияОКлиенте.Контрагент;
		КонтактноеЛицо			= ИнформацияОКлиенте.КонтактноеЛицо;
		ЕстьКритическиеОшибки 	= ИнформацияОКлиенте.ЕстьКритическиеОшибки; 

		Валюта = Справочники.Валюты.НайтиПоКоду(КодВалюты);
		Если НЕ ЗначениеЗаполнено(Валюта) Тогда
			Валюта = Справочники.Валюты.НайтиПоКоду("643");
		КонецЕсли;                          
		
		РеквизитыСоглашения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтруктураНастроек.НастройкииЗаказов.Соглашение, "Организация, НаправлениеДеятельности, ФормаОплаты, ХозяйственнаяОперация, ГрафикОплаты");
		
		Если НЕ ЗначениеЗаполнено(Заказ.Дата) Тогда Заказ.Дата = ДатаНачала КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Заказ.Организация) Тогда Заказ.Организация = РеквизитыСоглашения.Организация КонецЕсли;       
		Если НЕ ЗначениеЗаполнено(Заказ.Подразделение) Тогда Заказ.Подразделение = СтруктураНастроек.НастройкииЗаказов.Подразделение КонецЕсли; 
		Если НЕ ЗначениеЗаполнено(Заказ.Соглашение) Тогда Заказ.Соглашение = СтруктураНастроек.НастройкииЗаказов.Соглашение КонецЕсли; 
		Если НЕ ЗначениеЗаполнено(Заказ.НаправлениеДеятельности) Тогда Заказ.НаправлениеДеятельности = РеквизитыСоглашения.НаправлениеДеятельности КонецЕсли;   
		Если НЕ ЗначениеЗаполнено(Заказ.ФормаОплаты) Тогда Заказ.ФормаОплаты = РеквизитыСоглашения.ФормаОплаты КонецЕсли;   
		Если НЕ ЗначениеЗаполнено(Заказ.Номер) Тогда Заказ.УстановитьНовыйНомер() КонецЕсли;       
		Если НЕ ЗначениеЗаполнено(Заказ.Менеджер) Тогда Заказ.Менеджер = Ответственный КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Заказ.Приоритет) Тогда Заказ.Приоритет = Справочники.Приоритеты.НайтиПоНаименованию("Средний") КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Заказ.Статус) Тогда Заказ.Статус = Перечисления.СтатусыЗаказовКлиентов.КОбеспечению КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Заказ.СпособДоставки) Тогда Заказ.СпособДоставки = Перечисления.СпособыДоставки.Самовывоз КонецЕсли;
					
		ХозяйственнаяОперация = РеквизитыСоглашения.ХозяйственнаяОперация;
		ХозяйственнаяОперация = ?(ЗначениеЗаполнено(ХозяйственнаяОперация), ХозяйственнаяОперация,  Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
		Заказ.ХозяйственнаяОперация = ?(ЗначениеЗаполнено(Заказ.ХозяйственнаяОперация), Заказ.ХозяйственнаяОперация, ХозяйственнаяОперация);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиОплаты") Тогда
			Если НЕ ЗначениеЗаполнено(Заказ.ГрафикОплаты) Тогда Заказ.ГрафикОплаты = РеквизитыСоглашения.ГрафикОплаты КонецЕсли; 
		КонецЕсли;
		
		//НалогообложениеНДС = ЗначениеНастроекПовтИсп.НалогообложениеНДС(Заказ.Организация, Заказ.Склад, Заказ.Договор, Заказ.НаправлениеДеятельности, Заказ.Дата);			
		НалогообложениеНДС = Неопределено;			
		Заказ.НалогообложениеНДС = ?(ЗначениеЗаполнено(НалогообложениеНДС), НалогообложениеНДС,  Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС);
		
		Заказ.Комментарий = КомментарийСделки;          
		
		Заказ.ЦенаВключаетНДС = Истина;		
		Если Товары.Количество() > 0 Тогда 
			Заказ.ЦенаВключаетНДС = Товары[0].Получить("vatIncluded") = "Y";	
		КонецЕсли;	
		
		Заказ.Валюта = Валюта;
		Заказ.СкидкиРассчитаны = Истина;
		
		Заказ.Партнер 		= Партнер;
		Заказ.Контрагент 	= Контрагент;     
		  
		Если ЗначениеЗаполнено(Заказ.Партнер) И ЗначениеЗаполнено(Заказ.Контрагент) И ЗначениеЗаполнено(Заказ.Организация) Тогда   
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заказ.Соглашение, "ИспользуютсяДоговорыКонтрагентов") = Истина Тогда
				Заказ.Договор = ПолучитьДоговор(СтруктураНастроек, Заказ.Организация, Заказ.Партнер, Заказ.Контрагент, Валюта);   
			КонецЕсли;  
		КонецЕсли;  
		
		Если НЕ(ИдБанкСчетаРек = "" ИЛИ ИдБанкСчетаРек = "0") Тогда
			Заказ.БанковскийСчетКонтрагента = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.БанковскийСчетРеквизита, ИдБанкСчетаРек);  
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Заказ.БанковскийСчет) Тогда
			ВремСтруктураДанных = Новый Структура;
			ВремСтруктураДанных.Вставить("Организация"		, Заказ.Организация);
			ВремСтруктураДанных.Вставить("ФормаОплаты"		, Заказ.ФормаОплаты);
			ВремСтруктураДанных.Вставить("БанковскийСчет"	, Заказ.БанковскийСчет);
			ВремСтруктураДанных.Вставить("Валюта"			, Заказ.Валюта);
			ВремСтруктураДанных.Вставить("НаправлениеДеятельности", Заказ.НаправлениеДеятельности);
			Заказ.БанковскийСчет = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(ВремСтруктураДанных);	 
		КонецЕсли;    
		
#Область ЗаполнениеТоваров	
		тзнТоваровАрх = Заказ.Товары.Выгрузить();

		Заказ.Товары.Очистить();	
		
		тзнПривязкаПозиций = Новый ТаблицаЗначений;
		тзнПривязкаПозиций.Колонки.Добавить("ИдентификаторПозиции");
		тзнПривязкаПозиций.Колонки.Добавить("КлючСвязи");
		
		Для Каждого ТоварЗаказа Из Товары Цикл
			
			ИдПозиции			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТоварЗаказа.Получить("id"),"ЧГ=0"));
			КлючСвязиПозиции 	= Число(Прав(ИдПозиции, 5)); 
			
			Резерв				= ТоварЗаказа.Получить("reservations");       
			
			ИдТовара 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТоварЗаказа.Получить("productId"),"ЧГ=0"));
			ВнешнийКодПозиции	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(ТоварЗаказа.Получить("productXmlId")));
			
			НаименованиеТовара	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(ТоварЗаказа.Получить("name")));			
			
			Номенклатура	= Справочники.Номенклатура.ПустаяСсылка();	
			Характеристика  = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
#Область ПоискТовара	

			Если ЗначениеЗаполнено(ВнешнийКодПозиции) Тогда
				
				СтруктураИдентификаторовТовара = Б24_КС_ОбщегоНазначенияВызовСервера.ПолучитьДвеСоставныеЧастиИзСтроки(ВнешнийКодПозиции, "#");
				ИдВнешнийНоменклатуры 	= СтруктураИдентификаторовТовара.ПерваяЧасть;
				ИдВнешнийХарактеристики = СтруктураИдентификаторовТовара.ВтораяЧасть;
						
				Если ЗначениеЗаполнено(ИдВнешнийНоменклатуры) Тогда
					Номенклатура = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Номенклатура"), ИдВнешнийНоменклатуры);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ИдВнешнийХарактеристики) Тогда
					Характеристика = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.ХарактеристикиНоменклатуры"), ИдВнешнийХарактеристики);
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ИдТовара) И НЕ ЗначениеЗаполнено(Номенклатура) Тогда
				
				ИнформацияОНоменклатуре = Неопределено;	
				
				Запрос = Новый Запрос;
				Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
				Запрос.УстановитьПараметр("Идентификатор", ИдТовара);
				
				Запрос.Текст = "ВЫБРАТЬ
				|	ВТ_Идентификаторы.Объект КАК Объект,
				|	ВТ_Идентификаторы.ПодчиненныйОбъект КАК ПодчиненныйОбъект
				|ИЗ
				|	ВТ_ИдентификаторыТоваров КАК ВТ_Идентификаторы
				|ГДЕ
				|	ВТ_Идентификаторы.Идентификатор = &Идентификатор";
				
				ВыполненныйЗапрос = Запрос.Выполнить();
				
				Если НЕ ВыполненныйЗапрос.Пустой() Тогда
					Выборка = ВыполненныйЗапрос.Выбрать();
					Пока Выборка.Следующий() Цикл
						ИнформацияОНоменклатуре = Выборка;
						Прервать;
					КонецЦикла;
				КонецЕсли;
				
				Если ИнформацияОНоменклатуре = Неопределено Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не найдена информация о номенклатуре с Ид: " + ИдТовара);
					ЕстьКритическиеОшибки = Истина;
				Иначе
					Номенклатура   = ИнформацияОНоменклатуре.Объект;	
					Характеристика = ИнформацияОНоменклатуре.ПодчиненныйОбъект;	
				КонецЕсли; 
			КонецЕсли; 		
			
			Если НЕ ЗначениеЗаполнено(ВнешнийКодПозиции) И НЕ ЗначениеЗаполнено(ИдТовара) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "В заказе был указан не товар, а строка. Товар: " + НаименованиеТовара + " в сделку: " + Строка(Заказ) + " не будет указан.");
			КонецЕсли; 		
#КонецОбласти	        

			Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			
			ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ТипНоменклатуры");
			
			НоваяСтрока = Заказ.Товары.Добавить();
#Область ЗаполнениеСтрокиСтарымиДанными	
			НайденныеСтроки = тзнТоваровАрх.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Номенклатура, Характеристика));
			Если НайденныеСтроки.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденныеСтроки[0]);
			КонецЕсли;    

			ДатаОтгрузки			= НоваяСтрока.ДатаОтгрузки;
			Склад					= НоваяСтрока.Склад;
			Для каждого ТекРезерв Из Резерв Цикл  
				
				ДатаОтгрузки = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ТекРезерв.Получить("dateReserveEnd"));
				
				ИдСкладаБ24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекРезерв.Получить("storeId"),"ЧГ=0"));
				НайденнаяСтрокаСклада = СтруктураНастроек.НастройкиОстатков.Склады.Найти(ИдСкладаБ24, "ИдСкладаБ24");	
				Если НайденнаяСтрокаСклада <> Неопределено Тогда
					Склад = НайденнаяСтрокаСклада.Склад1С;
				КонецЕсли;
				
			КонецЦикла; 
			НоваяСтрока.Склад 				= Склад;
			НоваяСтрока.ДатаОтгрузки 		= ДатаОтгрузки;
#КонецОбласти	  
			
			Для каждого ТекРезерв Из Резерв Цикл  
				
				ДатаОтгрузки = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ТекРезерв.Получить("dateReserveEnd"));
				
				ИдСкладаБ24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекРезерв.Получить("storeId"),"ЧГ=0"));
				НайденнаяСтрокаСклада = СтруктураНастроек.НастройкиОстатков.Склады.Найти(ИдСкладаБ24, "ИдСкладаБ24");	
				Если НайденнаяСтрокаСклада <> Неопределено Тогда
					Склад = НайденнаяСтрокаСклада.Склад1С;
				КонецЕсли;
				
			КонецЦикла; 
			НоваяСтрока.Склад 				= Склад;
			НоваяСтрока.ДатаОтгрузки 		= ДатаОтгрузки;
			
			
            НоваяСтрока.Б24_К_ИдентификаторПозиции = ИдПозиции;    
            НоваяСтрока.КлючСвязи 	= КлючСвязиПозиции;    
            НоваяСтрока.КодСтроки 	= КлючСвязиПозиции;    

           	НоваяСтрока.Номенклатура 	= Номенклатура; 
			НоваяСтрока.Характеристика 	= Характеристика; 

			Если ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
				НоваяСтрока.Содержание = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Строка(ТоварЗаказа.Получить("name")));
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Упаковка) Тогда
					
				ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
					
				КодЕдИзм = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТоварЗаказа.Получить("measureCode"),"ЧГ=0"));
				Если ЗначениеЗаполнено(КодЕдИзм) Тогда
					ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(КодЕдИзм);;
				КонецЕсли;
					
				Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения") КонецЕсли;
	
           		НоваяСтрока.Упаковка = ЕдиницаИзмерения; 

			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.СтавкаНДС) Тогда
			
				СтавкаНалога = ТоварЗаказа.Получить("vatRate");  
				Если СтавкаНалога = Неопределено Тогда 
					СтавкаНалога = "Без НДС" 
				ИначеЕсли ТипЗнч(СтавкаНалога) = Тип("Число") Тогда
					СтавкаНалога = Окр(ТоварЗаказа.Получить("vatRate"), 5);	
				КонецЕсли;
				
				СтавкаНДС = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтавкуНДСПоСтавке(СтавкаНалога);
				Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда СтавкаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "СтавкаНДС") КонецЕсли;
				
				НоваяСтрока.СтавкаНДС = СтавкаНДС;	      
				
			КонецЕсли;   

			НоваяСтрока.Количество 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ТоварЗаказа.Получить("quantity"),"ЧРД=.; ЧГ=0"));;
			НоваяСтрока.КоличествоУпаковок 	= НоваяСтрока.Количество;     
			
			НоваяСтрока.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить;
			
			НоваяСтрока.Цена = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ТоварЗаказа.Получить("price"),"ЧРД=.; ЧГ=0"));
			
			НоваяСтрока.ПроцентАвтоматическойСкидки = 0;
			НоваяСтрока.СуммаАвтоматическойСкидки = 0;
			НоваяСтрока.ПроцентРучнойСкидки = 0;
			НоваяСтрока.СуммаРучнойСкидки = 0;
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				тзнТоваровАрх.Удалить(НайденныеСтроки[0]);
			КонецЕсли;    
			
			ПересчитатьСтрокуДокумента(Заказ, НоваяСтрока);
			
			НоваяПривязка = тзнПривязкаПозиций.Добавить();
			НоваяПривязка.ИдентификаторПозиции 	= ИдПозиции; 
			НоваяПривязка.КлючСвязи 			= КлючСвязиПозиции;
			
		КонецЦикла;  
		
#КонецОбласти
		
		ЗаполнениеДоставки(СтруктураНастроек, Заказ, СтоимостьДоставки);	

		Если Заказ.Товары.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Заказ.Товары.Количество() > 0 Тогда
			Заказ.Склад 		= Заказ.Товары[0].Склад;
			Заказ.ДатаОтгрузки 	= Заказ.Товары[0].ДатаОтгрузки;     
		КонецЕсли;
		
		Заказ.СуммаДокумента = Заказ.Товары.Итог("СуммаСНДС");
		
#Область ЗаполнениеГрафикаОплаты
		Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиОплаты") Тогда
			
			Заказ.ГрафикОплаты = Заказ.Соглашение.ГрафикОплаты;
			
			Заказ.ЭтапыГрафикаОплаты.Очистить();
			//ВзаиморасчетыСервер.ПроверитьЗаполнитьЭтапыГрафикаОплаты(Заказ);
			
		КонецЕсли;
#КонецОбласти	
		
#Область ВыполнениеАлгоритма
		Если СтруктураНастроек.НастройкииЗаказов.Свойство("АлгоритмЗаполненияЗаказа") Тогда        
			Если ЗначениеЗаполнено(СтруктураНастроек.НастройкииЗаказов.АлгоритмЗаполненияЗаказа) Тогда      
				ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Заказ, ДанныеСделки, СтруктураНастроек.НастройкииЗаказов.АлгоритмЗаполненияЗаказа);
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	

		Если Заказ.Товары.Количество() = 0 Тогда
			
			Если Заказ.Проведен Тогда
				Попытка
					Заказ.Записать(РежимЗаписиДокумента.ОтменаПроведения, РежимПроведенияДокумента.Неоперативный);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Отмена проведения у документа: " + Строка(Заказ), Заказ.Ссылка);
				Исключение  
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не удалось отменить проведение у документа: " + Строка(Заказ), Заказ.Ссылка);
				КонецПопытки;
			КонецЕсли;  
			
			Продолжить; 
			
		КонецЕсли;

#Область ПроведениеДокумента
		Заказ.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
		Заказ.ДополнительныеСвойства.Вставить("Б24_К_НеВыгружатьДело", Истина);
		Если НЕ ЕстьКритическиеОшибки Тогда
			
			Попытка
				//Заказ.Записать(РежимЗаписиДокумента.Запись);
				Заказ.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Проведен документ: " + Строка(Заказ), Заказ.Ссылка);
			Исключение
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время проведения документа: " + Строка(Заказ) + " возникли ошибки.", Заказ.Ссылка);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЕстьКритическиеОшибки = Истина;
				
				Если НЕ Заказ.Проведен Тогда
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Попытка записи заказа: " + Строка(Заказ), Заказ.Ссылка);
					
					Попытка
						Заказ.Записать(РежимЗаписиДокумента.Запись);
					Исключение
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи  документа: " + Строка(Заказ) + " возникли ошибки.", Заказ.Ссылка);
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					КонецПопытки;  
					
				КонецЕсли;
				
			КонецПопытки;    
			
		КонецЕсли;
		
#КонецОбласти		

		Если ЗначениеЗаполнено(Заказ.Ссылка) Тогда
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Проект, Заказ.Ссылка, ИдСделкиБитрикс24);
			
			ИдДелаСделки = РегистрыСведений.Б24_К_ИдентификаторыДел.ПолучитьИдОбъекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.ДелоСделки, Заказ.Ссылка);  
			РегистрыСведений.Б24_К_ИдентификаторыДел.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ДелоСделки, Заказ.Ссылка, ИдДелаСделки, ИдСделкиБитрикс24);			
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Заказ, Заказ.Ссылка, ИдЗаказаБитрикс24);   
			
			тзнПривязкаПозиций.Свернуть("ИдентификаторПозиции, КлючСвязи");
			Для Каждого ТекПозицияЗаказа Из тзнПривязкаПозиций Цикл
				
				ДанныеОПозиции = Новый Структура;
				ДанныеОПозиции.Вставить("Объект"			, Заказ.Ссылка);
				ДанныеОПозиции.Вставить("ПодчиненныйОбъект"	, ТекПозицияЗаказа.КлючСвязи);
				
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ТоварыЗаказа, ДанныеОПозиции, ТекПозицияЗаказа.ИдентификаторПозиции);

			КонецЦикла;
			
		КонецЕсли;      
		
		Если ЕстьКритическиеОшибки Тогда              
			Б24_К_RestApiВызовСервера.ДобавитьЗаписьВТаймЛайн(СтруктураНастроек, "2", ИдСделкиБитрикс24, "Ошибка", "Не удалось зарезервировать товар. Более подробная информация в журнале взаимодействий 1С", ?(ЗначениеЗаполнено(Заказ.Ссылка), Заказ.Ссылка, Неопределено));
		Иначе
			Если ЗначениеЗаполнено(Заказ.Ссылка) Тогда      
				Б24_К_RestApiВызовСервера.ДобавитьЗаписьВТаймЛайн(СтруктураНастроек, "2", ИдСделкиБитрикс24, "ОК", "Товар зарезервирован", Заказ.Ссылка);
			КонецЕсли;      
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти  

#Область Отгрузки

Функция ПолучитьДанныеОтгрузок(СтруктураНастроек, мДанных)
	
	РезультатОбработки = Новый Соответствие;
	
	мЗапросы = Новый Массив;
	
	Инкр = 0;
	Для каждого ИдЭлемента Из мДанных Цикл     
		
		Инкр = Инкр + 1;
		ИнкрСтрокой = "_"+Формат(Инкр, "ЧГ=0");
		
		мЗапросы.Добавить("cmd[SHIPMENT"+ИнкрСтрокой+"]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("sale.shipment.get?id=" + ИдЭлемента));    
		мЗапросы.Добавить("cmd[ORDER"+ИнкрСтрокой+"]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("sale.order.get?id=$result[SHIPMENT"+ИнкрСтрокой+"][shipment][orderId]"));  
		мЗапросы.Добавить("cmd[CRM"+ИнкрСтрокой+"]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.orderentity.list?filter[ownerTypeId]=2&filter[orderId]=$result[SHIPMENT"+ИнкрСтрокой+"][shipment][orderId]"));
		мЗапросы.Добавить("cmd[DEAL"+ИнкрСтрокой+"]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.deal.get?id=$result[CRM"+ИнкрСтрокой+"][orderEntity][0][ownerId]"));
		
	КонецЦикла; 
	
	ТелоHTTPЗапроса = "";
	Инкремент = 0;
	Для каждого ТекЗапрос Из мЗапросы Цикл
		
		Если Инкремент = 48 Тогда
			ОбработатьБатчЗапросыОтгрузок(СтруктураНастроек, ТелоHTTPЗапроса, РезультатОбработки);
			ТелоHTTPЗапроса = "";
			Инкремент = 0;   
		Иначе
			ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + ТекЗапрос; 	
		КонецЕсли;
		
		Инкремент = Инкремент +1;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТелоHTTPЗапроса) Тогда     
		ОбработатьБатчЗапросыОтгрузок(СтруктураНастроек, ТелоHTTPЗапроса, РезультатОбработки);
	КонецЕсли;
	
	Возврат РезультатОбработки;			
	
КонецФункции

Процедура ОбработатьБатчЗапросыОтгрузок(СтруктураНастроек, ТелоHTTPЗапроса, РезультатОбработки)
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	Если result <> Неопределено Тогда
		
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда
			
			Для каждого ТекЭлемент Из result2 Цикл
				
				мЧасти = СтрРазделить(ТекЭлемент.Ключ, "_");
				
				ДанныеКлюча = РезультатОбработки.Получить(мЧасти[1]); 
				Если ДанныеКлюча = Неопределено Тогда
					ДанныеКлюча = Новый Структура;
					ДанныеКлюча.Вставить("Отгрузка"	, Неопределено);	
					ДанныеКлюча.Вставить("Заказ", Неопределено);	
					ДанныеКлюча.Вставить("Связка", Неопределено);	
					ДанныеКлюча.Вставить("Проект", Неопределено);	
				КонецЕсли;
				
				Если мЧасти[0] = "SHIPMENT" Тогда    
					Если ТипЗнч(ТекЭлемент.Значение) = Тип("Соответствие") Тогда
						ДанныеКлюча.Отгрузка = ТекЭлемент.Значение.Получить("shipment");
					КонецЕсли;
				ИначеЕсли мЧасти[0] = "ORDER" Тогда    
					Если ТипЗнч(ТекЭлемент.Значение) = Тип("Соответствие") Тогда
						ДанныеКлюча.Заказ = ТекЭлемент.Значение.Получить("order");
					КонецЕсли;
				ИначеЕсли мЧасти[0] = "CRM" Тогда   
					Если ТипЗнч(ТекЭлемент.Значение) = Тип("Соответствие") Тогда
						ДанныеКлюча.Связка = ТекЭлемент.Значение.Получить("orderEntity");
					КонецЕсли;
				ИначеЕсли мЧасти[0] = "DEAL" Тогда     
					ДанныеКлюча.Сделка = ТекЭлемент.Значение;
				КонецЕсли;
				
				РезультатОбработки.Вставить(мЧасти[1], ДанныеКлюча); 
				
			КонецЦикла;
			
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьОбновитьОтгрузки(СтруктураНастроек, мДанных)
	
	ПрефиксВнешнихКодовБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();
	
	МенеджерВременныхТаблиц = ПолучитьБазовыйМенеджерТаблицДляДокументов(СтруктураНастроек);
	
	ЗагруженныеОбъекты = Новый Массив;
	Для каждого ТекЭлемент Из мДанных Цикл
		
		ДанныеОтгрузки 	= ТекЭлемент.Значение.Отгрузка; 
		ДанныеЗаказа 	= ТекЭлемент.Значение.Заказ;
		ДанныеСделки 	= ТекЭлемент.Значение.Проект;
		
		Если ТипЗнч(ДанныеОтгрузки) <> Тип("Соответствие") ИЛИ ТипЗнч(ДанныеЗаказа) <> Тип("Соответствие") ИЛИ ТипЗнч(ДанныеСделки) <> Тип("Соответствие")  Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьКритическиеОшибки = Ложь;
		
#Область ДанныеПортала
		ИдОтгрузкиБитрикс24		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеОтгрузки.Получить("id"),"ЧГ=0"));
		ИдЗаказаБитрикс24		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеЗаказа.Получить("id"),"ЧГ=0"));
		ИдСделкиБитрикс24		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеСделки.Получить("ID"),"ЧГ=0"));
		
		ВнешнийИдентификаторОтгрузки	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеОтгрузки.Получить("xmlId"),"ЧГ=0"));
		ВнешнийИдентификаторСделки		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеСделки.Получить("ORIGIN_ID"),"ЧГ=0"));
		
		ОтмененаОтгрузка		= ДанныеОтгрузки.Получить("canceled") = "Y";   
		ОтмененЗаказ			= ДанныеЗаказа.Получить("canceled") = "Y";   
		
		ДатаНачала = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"), ДанныеОтгрузки.Получить("dateInsert"));
		Если Не ЗначениеЗаполнено(ДатаНачала) Тогда ДатаНачала = ТекущаяДатаСеанса() КонецЕсли;
		
		КомментарийОтгрузки 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ДанныеОтгрузки.Получить("comments"));
		
		КодВалюты 	   	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьКодПоВалюте(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеОтгрузки.Получить("currency"),"ЧГ=0")));
		
		Отгружен		= ДанныеОтгрузки.Получить("deducted") = "Y";   
		ИдДоставки		= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеОтгрузки.Получить("deliveryId"),"ЧГ=0"));
		
		ИдКомпанииСделки = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеСделки.Получить("COMPANY_ID"),"ЧГ=0"));
		ИдКонтактаСделки = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеСделки.Получить("CONTACT_ID"),"ЧГ=0"));
		
		ТоварыОтгрузки = ДанныеОтгрузки.Получить("shipmentItems");                                                                                             
		ТоварыОтгрузки = ?(ТоварыОтгрузки = Неопределено, Новый Массив, ТоварыОтгрузки);
		
		ТоварыЗаказа = ДанныеЗаказа.Получить("basketItems");                                                                                             
		ТоварыЗаказа = ?(ТоварыЗаказа = Неопределено, Новый Массив, ТоварыЗаказа);
		
		КорзинаЗаказа = Новый Соответствие;
		Для каждого ТекПозицияЗаказа Из ТоварыЗаказа Цикл
			ИдКорзины = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ТекПозицияЗаказа.Получить("id"),"ЧГ=0"));
			КорзинаЗаказа.Вставить(ИдКорзины, ТекПозицияЗаказа);
		КонецЦикла;
		
		Ответственный = Справочники.Пользователи.ПустаяСсылка();
		ИдОтветственного = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ДанныеОтгрузки.Получить("empResponsibleId"),"ЧГ=0"));
		Если ЗначениеЗаполнено(ИдОтветственного) Тогда
			НайденнаяСтрока = СтруктураНастроек.ТаблицаСопоставленияПользователей.Найти(ИдОтветственного, "ИдПользователя");
			Если НайденнаяСтрока <> Неопределено Тогда
				Ответственный = НайденнаяСтрока.Пользователь1С; 					
			КонецЕсли;
		КонецЕсли;   
		
		Валюта = Справочники.Валюты.НайтиПоКоду(КодВалюты);
		Если НЕ ЗначениеЗаполнено(Валюта) Тогда
			Валюта = Справочники.Валюты.НайтиПоКоду("643");
		КонецЕсли;                          
		
		СтоимостьДоставки = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ДанныеОтгрузки.Получить("priceDelivery"),"ЧГ=0"));
#КонецОбласти
		
#Область ФильтрацияПоЗагрузке
		Если ЗагруженныеОбъекты.Найти(ИдОтгрузкиБитрикс24) <> Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдОтгрузкиБитрикс24);
#КонецОбласти

		ЗаказСсылка = Документы.ЗаказКлиента.ПустаяСсылка();
#Область ПоискЗаказа		                        
        ЗаказСсылка = СсылкаПоИдБ24(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Заказ, ПрефиксВнешнихКодовБитрикс24.Заказы + ИдЗаказаБитрикс24);
		Если ЗначениеЗаполнено(ВнешнийИдентификаторСделки) И НЕ ЗначениеЗаполнено(ЗаказСсылка) Тогда
			ЗаказСсылка = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ДокументСсылка.ЗаказКлиента"), ВнешнийИдентификаторСделки);
		КонецЕсли;
#КонецОбласти

		Если НЕ ЗначениеЗаполнено(ЗаказСсылка) Тогда         
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "У отгрузки с ид:  " + Строка(ИдОтгрузкиБитрикс24) + " не найден заказ в 1С. Отгрузка не будет загружена");
			Продолжить;  			
		КонецЕсли;

		ЭтоНоваяОтгрузка = Ложь;
#Область ПоискСозданиеОтгрузки		                        
        Отгрузка = СсылкаПоИдБ24(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка, ИдОтгрузкиБитрикс24);
		Если ЗначениеЗаполнено(ВнешнийИдентификаторОтгрузки) И НЕ ЗначениеЗаполнено(Отгрузка) Тогда
			Отгрузка = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("ДокументСсылка.РеализацияТоваровУслуг"), ВнешнийИдентификаторОтгрузки);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Отгрузка) Тогда 
			
			Если ОтмененаОтгрузка ИЛИ ОтмененЗаказ Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, " Отгрузка с ид:  " + Строка(ИдОтгрузкиБитрикс24) + " будет пропущена т.к.  его в 1С нет и она отменена.");
				Продолжить;  			
			КонецЕсли;					
			
			Отгрузка = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			ЭтоНоваяОтгрузка = Истина;     
			
		Иначе
			Отгрузка = Отгрузка.ПолучитьОбъект();
		КонецЕсли;
#КонецОбласти	   

		РеквизитыЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаказСсылка, "ОбъектРасчетов, Партнер, Контрагент, Договор, Организация, Подразделение, Соглашение, КонтактноеЛицо, Склад, БанковскийСчетКонтрагента, БанковскийСчет, НаправлениеДеятельности, Валюта, НалогообложениеНДС, ХозяйственнаяОперация, ФормаОплаты, ЦенаВключаетНДС, Товары, ГрафикОплаты");
		
		Отгрузка.ЗаказКлиента 			= ЗаказСсылка;    
		Отгрузка.РеализацияПоЗаказам 	= Истина;  
		Отгрузка.Статус 				= Перечисления.СтатусыРеализацийТоваровУслуг.Отгружено;  
		
		Отгрузка.ХозяйственнаяОперация = РеквизитыЗаказа.ХозяйственнаяОперация;
		Отгрузка.Организация 	= РеквизитыЗаказа.Организация;
		Отгрузка.Подразделение 	= РеквизитыЗаказа.Подразделение;
		
		Отгрузка.Соглашение 	= РеквизитыЗаказа.Соглашение;
		Отгрузка.Партнер 		= РеквизитыЗаказа.Партнер;
		Отгрузка.Контрагент 	= РеквизитыЗаказа.Контрагент;
		Отгрузка.КонтактноеЛицо = РеквизитыЗаказа.КонтактноеЛицо;
		
		Отгрузка.Договор 		= РеквизитыЗаказа.Договор;
		Отгрузка.Склад 			= РеквизитыЗаказа.Склад;
		
		Отгрузка.БанковскийСчетКонтрагента 		= РеквизитыЗаказа.БанковскийСчетКонтрагента;
		Отгрузка.БанковскийСчетОрганизации 		= РеквизитыЗаказа.БанковскийСчет;
		Отгрузка.НаправлениеДеятельности 		= РеквизитыЗаказа.НаправлениеДеятельности;
		
		Отгрузка.Валюта 				= Валюта;
		Отгрузка.ВалютаВзаиморасчетов 	= Валюта;    

		Отгрузка.ГрафикОплаты 			= РеквизитыЗаказа.ГрафикОплаты;
		Отгрузка.ФормаОплаты 			= РеквизитыЗаказа.ФормаОплаты;
		Отгрузка.ЦенаВключаетНДС 		= РеквизитыЗаказа.ЦенаВключаетНДС;
		Отгрузка.НалогообложениеНДС 	= РеквизитыЗаказа.НалогообложениеНДС;
		
		Отгрузка.Комментарий 		= КомментарийОтгрузки;;
		Отгрузка.СкидкиРассчитаны 	= Истина;
		
		Отгрузка.Дата = ДатаНачала;      
		Если НЕ ЗначениеЗаполнено(Отгрузка.Номер) Тогда Отгрузка.УстановитьНовыйНомер() КонецЕсли;       
		
		Если НЕ ЗначениеЗаполнено(Отгрузка.Менеджер) Тогда Отгрузка.Менеджер = Ответственный КонецЕсли;      
		Если НЕ ЗначениеЗаполнено(Отгрузка.ВариантОформленияПродажи) Тогда Отгрузка.ВариантОформленияПродажи = Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг КонецЕсли;      
		Если НЕ ЗначениеЗаполнено(Отгрузка.СпособДоставки) Тогда Отгрузка.СпособДоставки = Перечисления.СпособыДоставки.Самовывоз КонецЕсли;      
		
#Область ЗаполнениеСвойстваДоставки	
		ЗначениеДоставки = Неопределено;
		Если СтруктураНастроек.НастройкииЗаказов.Свойство("СпособыДоставки") Тогда
			НайденнаяСтрока = СтруктураНастроек.НастройкииЗаказов.СпособыДоставки.Найти(ИдДоставки, "ИдСлужбыБ24");	
			Если НайденнаяСтрока <> Неопределено Тогда
				ЗначениеДоставки = НайденнаяСтрока.Служба1С;
			КонецЕсли;
		КонецЕсли;
		
		СтрокаСвойстваОтгрузки = Отгрузка.ДополнительныеРеквизиты.Найти(СтруктураНастроек.НастройкииЗаказов.СвойствоСлужбаДоставки);
		Если СтрокаСвойстваОтгрузки = Неопределено Тогда
			СтрокаСвойстваОтгрузки = Отгрузка.ДополнительныеРеквизиты.Добавить();
		КонецЕсли;
		СтрокаСвойстваОтгрузки.Свойство = СтруктураНастроек.НастройкииЗаказов.СвойствоСлужбаДоставки;
		СтрокаСвойстваОтгрузки.Значение = ЗначениеДоставки; 
#КонецОбласти
		
#Область ЗаполнениеТоваров	 
		тзнТоваровЗаказа 	= РеквизитыЗаказа.Товары.Выгрузить();
		тзнТоваровАрх 		= Отгрузка.Товары.Выгрузить();

		Отгрузка.Товары.Очистить();	
		
		тзнПривязкаПозиций = Новый ТаблицаЗначений;
		тзнПривязкаПозиций.Колонки.Добавить("ИдентификаторПозиции");
		тзнПривязкаПозиций.Колонки.Добавить("КлючСвязи");
		
		Для Каждого ПозицияОтгрузки Из ТоварыОтгрузки Цикл
			                     
			ИдПозицииОтгрузки	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ПозицияОтгрузки.Получить("id"),"ЧГ=0"));
			ИдПозиции			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), Формат(ПозицияОтгрузки.Получить("basketId"),"ЧГ=0"));
			Количество 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Число"), Формат(ПозицияОтгрузки.Получить("quantity"),"ЧРД=.; ЧГ=0"));
			
			ВнИдСтроки = ПозицияОтгрузки.Получить("xmlId");
			Если Лев(ВнИдСтроки, 2) = "S_" Тогда
				КлючСвязиПозиции = Число(Прав(ВнИдСтроки, СтрДлина(ВнИдСтроки)-2)); 
			Иначе
				КлючСвязиПозиции = Число(Прав(ИдПозиции, 5)); 
			КонецЕсли;
			
			НайденнаяПозицияКорзины = тзнТоваровЗаказа.Найти(ИдПозиции, "Б24_К_ИдентификаторПозиции");
			Если НайденнаяПозицияКорзины = Неопределено Тогда  
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не найдены данные позиции корзины: " + Строка(ИдПозиции) + ". Отгрузка: " + ИдОтгрузкиБитрикс24, Отгрузка.Ссылка);
				ЕстьКритическиеОшибки = Истина;
				Прервать;  
			КонецЕсли;
			
			НоваяСтрока = Отгрузка.Товары.Добавить();
#Область ЗаполнениеСтрокиСтарымиДанными	
			НайденныеСтроки = тзнТоваровАрх.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", НайденнаяПозицияКорзины.Номенклатура, НайденнаяПозицияКорзины.Характеристика));
			Если НайденныеСтроки.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденныеСтроки[0]);
			КонецЕсли;
#КонецОбласти	    

	        НоваяСтрока.ЗаказКлиента = ЗаказСсылка;    
	        НоваяСтрока.ОбъектРасчетов = РеквизитыЗаказа.ОбъектРасчетов;    
			
            НоваяСтрока.КлючСвязи = КлючСвязиПозиции;    
            НоваяСтрока.КодСтроки = КлючСвязиПозиции;    
			
           	НоваяСтрока.Номенклатура 	= НайденнаяПозицияКорзины.Номенклатура; 
           	НоваяСтрока.Характеристика 	= НайденнаяПозицияКорзины.Характеристика; 
			
           	НоваяСтрока.Упаковка 			= НайденнаяПозицияКорзины.Упаковка; 
			НоваяСтрока.Склад 				= НайденнаяПозицияКорзины.Склад;   
			НоваяСтрока.Количество 			= Количество;
			НоваяСтрока.КоличествоУпаковок 	= Количество;
			
           	НоваяСтрока.СтавкаНДС 	= НайденнаяПозицияКорзины.СтавкаНДС; 
           	НоваяСтрока.Цена 		= НайденнаяПозицияКорзины.Цена; 
			НоваяСтрока.Сумма 		= НоваяСтрока.Цена*НоваяСтрока.Количество;		
			
           	НоваяСтрока.ПроцентАвтоматическойСкидки 	= 0; 
           	НоваяСтрока.СуммаАвтоматическойСкидки 		= 0; 
           	НоваяСтрока.ПроцентРучнойСкидки 			= 0; 
           	НоваяСтрока.СуммаРучнойСкидки 				= 0; 
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				тзнТоваровАрх.Удалить(НайденныеСтроки[0]);
			КонецЕсли;    
			
			ПересчитатьСтрокуДокумента(Отгрузка, НоваяСтрока);
			
			НоваяПривязка = тзнПривязкаПозиций.Добавить();
			НоваяПривязка.ИдентификаторПозиции 	= ИдПозицииОтгрузки; 
			НоваяПривязка.КлючСвязи 			= КлючСвязиПозиции;
			
		КонецЦикла;
		
#КонецОбласти		

		ЗаполнениеДоставки(СтруктураНастроек, Отгрузка, СтоимостьДоставки);	
		
#Область ЗаполнениеГрафикаОплаты
		Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиОплаты") Тогда
			
			Отгрузка.ЭтапыГрафикаОплаты.Очистить();
			//ВзаиморасчетыСервер.ПроверитьЗаполнитьЭтапыГрафикаОплаты(Отгрузка);
			
		КонецЕсли;
#КонецОбласти	

#Область ВыполнениеАлгоритма
		Если СтруктураНастроек.НастройкииЗаказов.Свойство("АлгоритмЗаполненияОтгрузки") Тогда        
			Если ЗначениеЗаполнено(СтруктураНастроек.НастройкииЗаказов.АлгоритмЗаполненияОтгрузки) Тогда      
				
				ДанныеДляАлгоритма = Новый Структура;
				ДанныеДляАлгоритма.Вставить("Проект", ДанныеСделки);	
				ДанныеДляАлгоритма.Вставить("Отгрузка", ДанныеОтгрузки);	
				
				ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Отгрузка, ДанныеДляАлгоритма, СтруктураНастроек.НастройкииЗаказов.АлгоритмЗаполненияОтгрузки);
			КонецЕсли;
		КонецЕсли;
#КонецОбласти	

		Если Отгрузка.Товары.Количество() = 0 Тогда
			
			Если Отгрузка.Проведен Тогда
				Попытка
					Отгрузка.Записать(РежимЗаписиДокумента.ОтменаПроведения, РежимПроведенияДокумента.Неоперативный);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Отмена проведения у документа: " + Строка(Отгрузка), Отгрузка.Ссылка);
				Исключение  
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не удалось отменить проведение у документа: " + Строка(Отгрузка), Отгрузка.Ссылка);
				КонецПопытки;
			КонецЕсли;  
			
			Продолжить; 
			
		КонецЕсли;       
		
#Область ПроведениеДокумента
		Отгрузка.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
		Отгрузка.ДополнительныеСвойства.Вставить("Б24_К_НеВыгружатьДело", Истина);
		
		Если НЕ ЕстьКритическиеОшибки Тогда
			
			Если Отгружен Тогда
				
				Попытка
					Отгрузка.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Проведен документ: " + Строка(Отгрузка), Отгрузка.Ссылка);
				Исключение
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время проведения документа: " + Строка(Отгрузка) + " возникли ошибки.", Отгрузка.Ссылка);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
					ЕстьКритическиеОшибки = Истина;
					
					Если НЕ Отгрузка.Проведен Тогда  
						
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Попытка записи отгрузки: " + Строка(Отгрузка), Отгрузка.Ссылка);
						
						Попытка
							Отгрузка.Записать(РежимЗаписиДокумента.Запись);   
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "записан документ: " + Строка(Отгрузка), Отгрузка.Ссылка);
						Исключение
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи  документа: " + Строка(Отгрузка) + " возникли ошибки.", Отгрузка.Ссылка);
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
						КонецПопытки; 
					КонецЕсли;
					
				КонецПопытки;     
			Иначе
				
				Если Отгрузка.Проведен Тогда
					
					Попытка
						Отгрузка.Записать(РежимЗаписиДокумента.ОтменаПроведения);  
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "отмена проведения документа: " + Строка(Отгрузка), Отгрузка.Ссылка);
					Исключение
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время отмены проведения  документа: " + Строка(Отгрузка) + " возникли ошибки.", Отгрузка.Ссылка);
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					КонецПопытки;   
					
				Иначе
					
					Попытка
						Отгрузка.Записать(РежимЗаписиДокумента.Запись);  
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "записан документ: " + Строка(Отгрузка), Отгрузка.Ссылка);
					Исключение
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи  документа: " + Строка(Отгрузка) + " возникли ошибки.", Отгрузка.Ссылка);
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					КонецПопытки;   
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
#КонецОбласти		

		Если ЗначениеЗаполнено(Отгрузка.Ссылка) Тогда
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Отгрузка, Отгрузка.Ссылка, ИдОтгрузкиБитрикс24);   
			
			Если ЗначениеЗаполнено(ЗаказСсылка) Тогда
				ИдДелаСделки = РегистрыСведений.Б24_К_ИдентификаторыДел.ПолучитьИдОбъекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.ДелоСделки, ЗаказСсылка);  
				РегистрыСведений.Б24_К_ИдентификаторыДел.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ДелоСделки, Отгрузка.Ссылка, ИдДелаСделки, ИдСделкиБитрикс24);			
			КонецЕсли;
			
			тзнПривязкаПозиций.Свернуть("ИдентификаторПозиции, КлючСвязи");
			Для Каждого ТекПозицияОтгрузки Из тзнПривязкаПозиций Цикл
				                                                
				ДанныеОПозиции = Новый Структура;
				ДанныеОПозиции.Вставить("Объект"			, Отгрузка.Ссылка);
				ДанныеОПозиции.Вставить("ПодчиненныйОбъект"	, ТекПозицияОтгрузки.КлючСвязи);
				
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ТоварыОтгрузки, ДанныеОПозиции, ТекПозицияОтгрузки.ИдентификаторПозиции);

			КонецЦикла;
			
		КонецЕсли;      
		
		Если ЕстьКритическиеОшибки Тогда     
			Б24_К_RestApiВызовСервера.ДобавитьЗаписьВТаймЛайн(СтруктураНастроек, "2", ИдСделкиБитрикс24, "Ошибка", "Ошибка при "+?(Отгружен, "проведении", "записи")+" отгрузки",?(ЗначениеЗаполнено(Отгрузка.Ссылка), Отгрузка.Ссылка, Неопределено));
		Иначе
			Если ЗначениеЗаполнено(Отгрузка.Ссылка) Тогда      
				Б24_К_RestApiВызовСервера.ДобавитьЗаписьВТаймЛайн(СтруктураНастроек, "2", ИдСделкиБитрикс24, "ОК", "Отгрузка "+?(Отгружен, "проведена", "записана"), Отгрузка.Ссылка);
			КонецЕсли;      
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти   

#Область ДанныеКлиента    

Функция ПолучитьИнформациюОКомпанииКонтакте(СтруктураНастроек, МенеджерВременныхТаблиц, ИдКомпании, ИдКонтакта, ИдРеквизита = "")
	
	Результат = Новый Структура;
	Результат.Вставить("Партнер"				, Справочники.Партнеры.ПустаяСсылка());
	Результат.Вставить("Контрагент"				, Справочники.Контрагенты.ПустаяСсылка());
	Результат.Вставить("КонтактноеЛицо"			, Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка());
	Результат.Вставить("ЕстьКритическиеОшибки"	, Ложь);
	
	Контакт			= "";
	Компания		= "";
	ЕстьКомпания	= Ложь;
	Если НЕ(ИдКомпании = "" ИЛИ ИдКомпании = "0") Тогда
		
		ЕстьКомпания = Истина;
		
		Компания 	= ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", ИдКомпании);	
		Если НЕ ЗначениеЗаполнено(Компания) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найдена компания по ид. Попытка создания компании с Ид: " + ИдКомпании);
			Компания = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Компания", ИдКомпании);
		КонецЕсли;
					
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Компания) Тогда
		Результат.Партнер = Компания;
	КонецЕсли;
	
	Если НЕ(ИдКонтакта = "" ИЛИ ИдКонтакта = "0") Тогда
		
		Если ЕстьКомпания Тогда
			Контакт 	= ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", "#"+ИдКонтакта);
		Иначе
			Контакт 	= ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", ИдКонтакта);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Контакт) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Не найден контакт по ид. Попытка создания контакта с Ид: " + ИдКонтакта);
			
			Если ЕстьКомпания Тогда
				Контакт = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "Контакт", ИдКонтакта);
			Иначе
				Контакт = СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, "КомпанияКонтакт", ИдКонтакта);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контакт) Тогда
		Если ТипЗнч(Контакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
			Результат.КонтактноеЛицо = Контакт;
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.Партнеры") Тогда
			Если ЗначениеЗаполнено(Компания) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Партнерами являются и компания и контакт. Установлен будет Из компании");
			Иначе
				Результат.Партнер = Контакт;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Результат.Партнер) Тогда
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не найдена компания/контакт с Ид: " + ИдКомпании+"/"+ИдКонтакта);
		Результат.ЕстьКритическиеОшибки = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдРеквизита) И ИдРеквизита <> "0" Тогда
		
		Результат.Контрагент 	= ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыРеквизитов", ИдРеквизита);
		ИдДляЛога	= ИдРеквизита;
		
		Если НЕ ЗначениеЗаполнено(Результат.Контрагент) Тогда
			
			ИнформацияОКонтрагенте 	=  РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит, ИдРеквизита);
			
			Если ИнформацияОКонтрагенте <> Неопределено Тогда
				Результат.Контрагент = ИнформацияОКонтрагенте.Объект; 	
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Результат.Контрагент) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не найден реквизит с Ид: " + ИдДляЛога);
				Результат.ЕстьКритическиеОшибки = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Результат.Партнер) Тогда
		Результат.Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("Партнер", Результат.Партнер); 	
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьКомпаниюКонтактПоИд(СтруктураНастроек, ТипВладельца, Ид) Экспорт 
	
	Результат = Справочники.Контрагенты.ПустаяСсылка();
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Клиент"	, Новый Массив);
	СтруктураДанных.Вставить("Реквизит"	, Новый Массив);
	СтруктураДанных.Вставить("Адреса"	, Новый Массив);
	СтруктураДанных.Вставить("БанкСчета", Новый Массив);
	
	Если ТипВладельца = "Компания" Тогда
		МетодКлиента = "crm.company.get?id=" + Ид; 	
		МетодРеквизита = "crm.requisite.list?filter[ENTITY_ID]=" + Ид + "&filter[ENTITY_TYPE_ID]=4" + "&start=-1"; 	
	Иначе
		МетодКлиента = "crm.contact.get?id=" + Ид; 
		МетодРеквизита = "crm.requisite.list?filter[ENTITY_ID]=" + Ид + "&filter[ENTITY_TYPE_ID]=3" + "&start=-1"; 	
	КонецЕсли;
	
	ТелоHTTPЗапроса = "";
	ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[K]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодКлиента);
	ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[R]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодРеквизита);
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");                                             
	Если result <> Неопределено Тогда                                                         
		result2 = result.Получить("result");
		Если result2 <> Неопределено Тогда 	
			
			ДанныеКлиента 		= result2.Получить("K"); 
			ДанныеРеквизита 	= result2.Получить("R");
			
			Если ДанныеКлиента <> Неопределено Тогда
				СтруктураДанных.Клиент.Добавить(ДанныеКлиента);	
			КонецЕсли;
			
			Если ДанныеРеквизита <> Неопределено Тогда
				СтруктураДанных.Реквизит = ДанныеРеквизита;	
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("ИнформацияОРеквизитах", СтруктураДанных.Реквизит);
	
	Если ТипВладельца = "Компания" Тогда
		ДополнительныеДанные.Вставить("ИнформацияОКомпаниях", СтруктураДанных.Клиент);        
		Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьКомпании(СтруктураНастроек, ДополнительныеДанные); 
		ИнформацияОКлиенте = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Компания, Ид);
	ИначеЕсли ТипВладельца = "Контакт" Тогда
		ДополнительныеДанные.Вставить("ИнформацияОКонтактах", СтруктураДанных.Клиент);
		Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьКонтакты(СтруктураНастроек, ДополнительныеДанные);	
		ИнформацияОКлиенте = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Контакт, Ид);
	Иначе
		ДополнительныеДанные.Вставить("ИнформацияОКонтактах", СтруктураДанных.Клиент);
		Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьКонтакты(СтруктураНастроек, ДополнительныеДанные);	
		ИнформацияОКлиенте = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Контакт, Ид);
	КонецЕсли;
	
	Если ИнформацияОКлиенте = Неопределено Тогда
		Возврат Результат;	
	Иначе
		Результат = ИнформацияОКлиенте.Объект; 	
	КонецЕсли;
	
	// Предполагаем, что запросов будет менее 51   т.е. меньше 25 реквизитов(на каждый 2 запроса) и у каждого меньше 50 банк. счетов.
	КоличествоЗапросов = 0;
	ТелоHTTPЗапроса = "";
	
	Для каждого ТекРеквизит Из ДанныеРеквизита Цикл 

		Если КоличествоЗапросов = 50 Тогда
			Прервать;
		КонецЕсли;
		
		ИдРеквизита 	= Формат(ТекРеквизит.Получить("ID"), "ЧГ=0");  
		
		МетодАдреса 	= "crm.address.list?filter[ENTITY_ID]=" + ИдРеквизита + "&filter[ENTITY_TYPE_ID]=8"; 	
		МетодБанкСчета 	= "crm.requisite.bankdetail.list?filter[ENTITY_ID]=" + ИдРеквизита; 	
		
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[A]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодАдреса);
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd[B]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(МетодБанкСчета);

		КоличествоЗапросов = КоличествоЗапросов + 1;

	КонецЦикла;
	
	Если ТелоHTTPЗапроса <> "" Тогда
	
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
		
		result = СтруктураОтвета.Получить("result");                                             
		Если result <> Неопределено Тогда                                                         
			result2 = result.Получить("result");
			Если result2 <> Неопределено Тогда 			
				
				ДанныеАдресов 		= result2.Получить("A"); 
				ДанныеБанкСчетов 	= result2.Получить("B"); 
				
				Если ДанныеАдресов <> Неопределено Тогда
					СтруктураДанных.Адреса = ДанныеАдресов;	
				КонецЕсли;
				
				Если ДанныеБанкСчетов <> Неопределено Тогда
					СтруктураДанных.БанкСчета = ДанныеБанкСчетов;	
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("ИнформацияОРеквизитах"	, ДанныеРеквизита);
	ДополнительныеДанные.Вставить("ИнформацияОАдресах"		, СтруктураДанных.Адреса);
	
	Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьРеквизиты(СтруктураНастроек, ДополнительныеДанные); 
	
	Если НЕ СтруктураНастроек.НеЗагружатьАдресаРеквизитов Тогда
		Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьАдресаРеквизитов(СтруктураНастроек, СтруктураДанных.Адреса);    
	КонецЕсли;
	
	Б24_К_ОбменКомпанийКонтактовСервер.ЗагрузитьОбновитьБанковскиеСчетаКомпанийКонтактов(СтруктураНастроек, СтруктураДанных.БанкСчета);
	
	Возврат Результат;	

КонецФункции

Функция ПолучитьДоговор(СтруктураНастроек, Организация, Партнер, Контрагент, ВалютаРасчетов)
	
	Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	
	Если ТипЗнч(Контрагент) <> Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат Договор;	
	КонецЕсли;
	
	НаименованиеДоговора = "Договор WEB - Битрикс24";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Организация"			, Организация);
	Запрос.УстановитьПараметр("Контрагент"			, Контрагент);
	Запрос.УстановитьПараметр("Партнер"				, Партнер);
	Запрос.УстановитьПараметр("ВалютаРасчетов"		, ВалютаРасчетов);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	|	ДоговорыКонтрагентов.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Организация = &Организация
	|	И ДоговорыКонтрагентов.Контрагент = &Контрагент
	|	И ДоговорыКонтрагентов.Партнер = &Партнер
	|	И ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаРасчетов
	|	И ДоговорыКонтрагентов.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем)";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		тзнДоговоров = ВыполненныйЗапрос.Выгрузить();
		
		Если тзнДоговоров.Количество() = 1 Тогда 
			Договор = тзнДоговоров[0].Ссылка;
		Иначе
			Для каждого ТекДоговор Из тзнДоговоров Цикл	
				Если ТекДоговор.Наименование = НаименованиеДоговора Тогда
					Договор = ТекДоговор.Ссылка;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Договор) Тогда
		НовыйДоговор = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
		НовыйДоговор.Наименование 			= НаименованиеДоговора;
		НовыйДоговор.Партнер	 			= Контрагент.Партнер;
		НовыйДоговор.Контрагент	 			= Контрагент;
		НовыйДоговор.Организация  			= Организация;
		НовыйДоговор.ТипДоговора            = Перечисления.ТипыДоговоров.СПокупателем;
		НовыйДоговор.Статус					= Перечисления.СтатусыДоговоровКонтрагентов.Действует;
		
		НовыйДоговор.ВалютаВзаиморасчетов	= ВалютаРасчетов;
		НовыйДоговор.ПорядокРасчетов		= Перечисления.ПорядокРасчетов.ПоЗаказам;
		НовыйДоговор.НалогообложениеНДСОпределяетсяВДокументе = Истина;;
		
		//НовыйДоговор.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
		НовыйДоговор.Записать();
		Договор = НовыйДоговор.Ссылка;
	КонецЕсли;
	
	Возврат Договор;
	
КонецФункции

#КонецОбласти

#Область Прочие    

Функция ПолучитьБазовыйМенеджерТаблицДляДокументов(СтруктураНастроек)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
#Область ФормированиеВременныхТаблицДанных		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;  
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);  
	Запрос.УстановитьПараметр("ТипДанныхКомпания"		, СтруктураНастроек.ТипыОбъектовОбмена.Компания);
	Запрос.УстановитьПараметр("ТипДанныхКонтакт"		, СтруктураНастроек.ТипыОбъектовОбмена.Контакт);
	Запрос.УстановитьПараметр("ТипДанныхРеквизит"		, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит);
	Запрос.УстановитьПараметр("ТипДанныхБанкСчет"		, СтруктураНастроек.ТипыОбъектовОбмена.БанковскийСчетРеквизита);
	Запрос.УстановитьПараметр("ТипДанныхТовар2"			, СтруктураНастроек.ТипыОбъектовОбмена.Товар2);
	Запрос.УстановитьПараметр("ТипДанныхПредложение"	, СтруктураНастроек.ТипыОбъектовОбмена.Предложение);	
	Запрос.УстановитьПараметр("ТипДанныхЕдИзм2"			, СтруктураНастроек.ТипыОбъектовОбмена.ЕдиницаИзмерения2);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыКонтактов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКонтакт
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыРеквизитов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхРеквизит
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыКомпаний
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКомпания
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЕдИзм
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЕдИзм2
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект
	|ПОМЕСТИТЬ ВТ_ИдентификаторыТоваров
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхТовар2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхПредложение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыБанкСчетов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхБанкСчет
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	Запрос.Выполнить();
#КонецОбласти	
	
	Возврат МенеджерВременныхТаблиц;

КонецФункции

Функция ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, НазваниеВременнойТаблицы, ИдЭлемента)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Идентификатор", ИдЭлемента);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_Идентификаторы.Объект КАК Объект
	|ИЗ
	|	"+НазваниеВременнойТаблицы+" КАК ВТ_Идентификаторы
	|ГДЕ
	|	ВТ_Идентификаторы.Идентификатор = &Идентификатор";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат = Выборка.Объект;
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция СсылкаПоИдБ24(Портал, ТипДанных, ИдБитрикс24)
	
	Результат = Неопределено;
	
	ДанныеПоИд = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(Портал, ТипДанных, ИдБитрикс24);  
	Если ДанныеПоИд <> Неопределено Тогда
		Результат = ДанныеПоИд.Объект;
	КонецЕсли;
			
	Возврат Результат;

КонецФункции

Процедура ПересчитатьСтрокуДокумента(Документ, СтрокаТабличнойЧасти)   
	
	СтруктураДействий = Новый Структура;
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Документ);
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	
	ОбработкаТабличнойЧастиВызовСервера.ОбработатьСтрокуТЧ(СтрокаТабличнойЧасти, СтруктураДействий, Неопределено);

КонецПроцедуры


Процедура ЗаполнениеДоставки(СтруктураНастроек, ДокументОбъект, СтоимостьДоставки)
	
	НоменклатураДоставка = СтруктураНастроек.НастройкииЗаказов.НоменклатураДоставка;
	Если СтоимостьДоставки > 0 И ЗначениеЗаполнено(НоменклатураДоставка) Тогда     
		
		РеквизитыТовара = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоменклатураДоставка, "ЕдиницаИзмерения, СтавкаНДС, ТипНоменклатуры, НаименованиеПолное");
		
		НоваяЗаписьТовара = ДокументОбъект.Товары.Добавить();
		
		НоваяЗаписьТовара.КлючСвязи = 9999;
		НоваяЗаписьТовара.КодСтроки = 9999;
		
		НоваяЗаписьТовара.Номенклатура 		= НоменклатураДоставка;
		НоваяЗаписьТовара.Характеристика 	= Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		
		НоваяЗаписьТовара.Упаковка			= РеквизитыТовара.ЕдиницаИзмерения;
		НоваяЗаписьТовара.СтавкаНДС  		= РеквизитыТовара.СтавкаНДС;
		
		Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказКлиента") Тогда
		
			НоваяЗаписьТовара.ДатаОтгрузки			= ДокументОбъект.Дата;
			НоваяЗаписьТовара.ВариантОбеспечения	= Перечисления.ВариантыОбеспечения.Отгрузить;
			
			Если РеквизитыТовара.ТипНоменклатуры 	= Перечисления.ТипыНоменклатуры.Услуга ИЛИ НЕ ЗначениеЗаполнено(НоваяЗаписьТовара.Номенклатура) Тогда
				НоваяЗаписьТовара.Содержание 		= РеквизитыТовара.НаименованиеПолное;
			КонецЕсли;  
			
		КонецЕсли;
		
		НоваяЗаписьТовара.Количество  			= 1;
		НоваяЗаписьТовара.КоличествоУпаковок  	= 1;
		
		НоваяЗаписьТовара.Цена					= СтоимостьДоставки;
		
		ПересчитатьСтрокуДокумента(ДокументОбъект, НоваяЗаписьТовара);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьАлгоритмПередЗагрузкой(СтруктураНастроек, Объект1С, ОбъектПортала, АлгоритмКлюча)
	
	Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
	Параметры.СтруктураНастроек = СтруктураНастроек;
	Параметры.Объект1С 			= Объект1С;
	Параметры.ОбъектПортала 	= ОбъектПортала;

	Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(АлгоритмКлюча, Параметры, "Процедура");		
		
КонецПроцедуры

#КонецОбласти  
