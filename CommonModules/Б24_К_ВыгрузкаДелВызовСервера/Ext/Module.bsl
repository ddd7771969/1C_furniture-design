
#Область ОбработкаСобытий

Процедура ВыгрузкаДелСправочниковПередЗаписьюДанных(Источник, Отказ) Экспорт
	
	Если ПараметрыСеанса.ВыполняетсяОбновлениеИБ Тогда
		Возврат;
	КонецЕсли;  
	
	Если Источник.ОбменДанными.Загрузка И Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		Возврат;
	КонецЕсли;    
	
	ОбъектНовый	= Источник.ЭтоНовый();
	ОбъектМодифицирован = Ложь;   
	
	Если ОбъектНовый Тогда
		ОбъектМодифицирован = Истина;
	Иначе
		МассивИсключений = Новый Массив;
		МассивИсключений.Добавить("ДатаИзменения");
		
		МассивДляПроверки = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьМассивРеквизитовДляПроверкиМодифицированности();
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ОбъектМодифицирован(Источник, МассивИсключений, МассивДляПроверки) Тогда
			ОбъектМодифицирован = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбъектНовый Тогда
		Источник.ДополнительныеСвойства.Вставить("Б24_К_ОбъектНовый");   
	КонецЕсли;

	Если ОбъектМодифицирован Тогда
		Источник.ДополнительныеСвойства.Вставить("Б24_К_ОбъектМодифицирован");   
	КонецЕсли;
	
КонецПроцедуры  

Процедура ВыгрузкаДелДокументовПередЗаписьюДанных(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если ПараметрыСеанса.ВыполняетсяОбновлениеИБ Тогда
		Возврат;
	КонецЕсли;  
	
	Если Источник.ОбменДанными.Загрузка И Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		Возврат;
	КонецЕсли;    
	
	ОбъектНовый			= Источник.ЭтоНовый();
	ОбъектМодифицирован = Ложь;
	
	Если ОбъектНовый Тогда
		//ОбъектМодифицирован = Истина;
	Иначе
		МассивИсключений = Новый Массив;
		МассивИсключений.Добавить("ДатаИзменения");
		
		МассивДляПроверки = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьМассивРеквизитовДляПроверкиМодифицированности();
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ОбъектМодифицирован(Источник, МассивИсключений, МассивДляПроверки) Тогда
			ОбъектМодифицирован = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбъектНовый Тогда
		Источник.ДополнительныеСвойства.Вставить("Б24_К_ОбъектНовый");   
	КонецЕсли;

	Если ОбъектМодифицирован Тогда
		Источник.ДополнительныеСвойства.Вставить("Б24_К_ОбъектМодифицирован");   
	КонецЕсли;
	
	Если Источник.Проведен Тогда
		Источник.ДополнительныеСвойства.Вставить("Б24_К_ДокументУжеПроведен");   
	КонецЕсли; 
	
КонецПроцедуры  

Процедура ВыгрузкаДелПриЗаписиДанных(Источник, Отказ) Экспорт
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;   
	
	Если ПараметрыСеанса.ВыполняетсяОбновлениеИБ Тогда
		Возврат;
	КонецЕсли;  
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Источник.ОбменДанными.Загрузка И Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		Возврат;
	КонецЕсли; 
	
	МетаданныеИсточника = Источник.Метаданные(); 
	Если Метаданные.Документы.Содержит(МетаданныеИсточника) Тогда
		Если МетаданныеИсточника.Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить Тогда
			Если Источник.Проведен Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Источник.ДополнительныеСвойства.Свойство("Б24_К_ДелоВыгружено") Тогда  
		Источник.ДополнительныеСвойства.Вставить("Б24_К_ДелоВыгружено", Истина);   
		ВыполнитьСобытиеДела(Источник);
	КонецЕсли;
	
	ВыполнитьСобытиеДела(Источник);
	
КонецПроцедуры

Процедура ВыгрузкаДелОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;  
	
	Если ПараметрыСеанса.ВыполняетсяОбновлениеИБ Тогда
		Возврат;
	КонецЕсли;  
	
	Источник.ДополнительныеСвойства.Вставить("Б24_К_ОбъектМодифицирован");   
	
	Если НЕ Источник.ДополнительныеСвойства.Свойство("Б24_К_ДелоВыгружено") Тогда  
		Источник.ДополнительныеСвойства.Вставить("Б24_К_ДелоВыгружено", Истина);   
		ВыполнитьСобытиеДела(Источник);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыгрузкаДелОбработкаУдаленияПроведения(Источник, Отказ) Экспорт
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;    
	
	Если ПараметрыСеанса.ВыполняетсяОбновлениеИБ Тогда
		Возврат;
	КонецЕсли;  
	
	Источник.ДополнительныеСвойства.Вставить("Б24_К_ОбъектМодифицирован");   
	
	Если НЕ Источник.ДополнительныеСвойства.Свойство("Б24_К_ДелоВыгружено") Тогда  
		Источник.ДополнительныеСвойства.Вставить("Б24_К_ДелоВыгружено", Истина);   
		ВыполнитьСобытиеДела(Источник);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьСобытиеДела(Источник)
	
	Ссылка = Источник.Ссылка;
	ДополнительныеСвойства = Источник.ДополнительныеСвойства;
	
	ДобавленаСвязка = Ложь;
	Если ДополнительныеСвойства.Свойство("Б24_К_ОбъектНовый") ИЛИ ТипЗнч(Источник) = Тип("ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств") 
		ИЛИ ТипЗнч(Источник) = Тип("ДокументОбъект.АктВыполненныхРабот") Тогда //директ банк не всегда клево работает, акты тоже замечены
		
		ДобавленаСвязка = ДобавитьСвязкуСДокументомОснования(Ссылка);
		Если ДобавленаСвязка Тогда
			Источник.ДополнительныеСвойства.Вставить("Б24_К_ДобавленаСвязка");   
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДобавленаСвязка ИЛИ ДополнительныеСвойства.Свойство("Б24_К_ОбъектМодифицирован") Тогда
		
		Если НЕ ДополнительныеСвойства.Свойство("Б24_К_НеВыгружатьДело") Тогда
			ВыгрузитьДелоВБитрикс24(Ссылка);     
		КонецЕсли;   
		
		Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("РаботаИзОдногоОкна") Тогда             
			
			МодульРаботаИзОдногоОкнаВызовСервера = ОбщегоНазначения.ОбщийМодуль("Б24_КБ_РаботаИзОдногоОкнаВызовСервера");
			
			ТипСсылки = ТипЗнч(Ссылка);
			Если ТипСсылки = Тип("ДокументСсылка.ЗаказКлиента") ИЛИ ТипСсылки = Тип("ДокументСсылка.ЗаказПоставщику") 
				ИЛИ ТипСсылки = Тип("ДокументСсылка.АктВыполненныхРабот") ИЛИ ТипСсылки = Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту")
				ИЛИ ТипСсылки = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") ИЛИ ТипСсылки = Тип("ДокументСсылка.ВозвратТоваровПоставщику") 
				ИЛИ ТипСсылки = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") ИЛИ ТипСсылки = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			
				мНастройкиПодключения = ПолучитьИспользуемыеНастройкиПодключенияОбъектомЧерезДела(Ссылка);
				Для каждого ТекЭлемент Из мНастройкиПодключения Цикл
					МодульРаботаИзОдногоОкнаВызовСервера.ВыгрузитьТоварыДокументовВСделкуСмартПроцесс(ТекЭлемент, ДополнительныеСвойства.Свойство("Б24_К_ДокументНовый"), Ссылка, "", "");	
				КонецЦикла;    
				
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;       
	
КонецПроцедуры

#КонецОбласти


#Область ВыгрузкаДел 
 
Функция ПолучитьЗапросДела(СтруктураНастроек, КлючДела, Знач ИдентификаторДела, ТелоДела) Экспорт
	
	Если ЗначениеЗаполнено(ИдентификаторДела) Тогда    
		
		Если СтруктураНастроек.НеОбновлятьДелаБитрикс24 Тогда
			Возврат "";
		КонецЕсли;
		
		ОбработатьИдентификаторДела(ИдентификаторДела);
		СтрокаЗапроса = "&cmd["+КлючДела+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.activity.update?id=" + ИдентификаторДела + "&"  + ТелоДела);	
	Иначе
		СтрокаЗапроса = "&cmd["+КлючДела+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.activity.add?" + ТелоДела);	
	КонецЕсли;
	
	Возврат СтрокаЗапроса;
	
КонецФункции

Функция ПолучитьТелоЗапросаДела(СтруктураНастроек, ТипВладельца, ДанныеДела, ПредставлениеОбъекта, ИдентификаторВладельца, КлючВладельца) Экспорт
	
	Результат = "";
	
	Если ТипЗнч(ДанныеДела) = Тип("Структура") Тогда
		НавигационнаяСсылка = ДанныеДела.НавигационнаяСсылка;
	Иначе
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ДанныеДела);	
	КонецЕсли;
	
	//Если ЗначениеЗаполнено(СтруктураНастроек.ОбщиеНастройки.ВебАдрес1С) И СтруктураНастроек.ОбщиеНастройки.ВебАдрес1С <> "<Введите https адрес информационной базы 1С на веб-сервере>" Тогда
		
		Поля = Новый Структура;		
		Поля.Вставить("OWNER_TYPE_ID"		, ТипВладельца); 
		Поля.Вставить("COMPLETED"			, "Y");
		Поля.Вставить("SUBJECT"				, ПредставлениеОбъекта);
		Поля.Вставить("PROVIDER_ID"			, "REST_APP");
		Поля.Вставить("PROVIDER_TYPE_ID"	, "1CTOTAL");
		
		Если ЗначениеЗаполнено(ДанныеДела.ВерсияДанных) Тогда     //Предотвращение разбора еще несуществующей ссылки.
			Поля.Вставить("DESCRIPTION"			, ОбновитьОписаниеДела(ДанныеДела));
		КонецЕсли;
		
		Поля.Вставить("DESCRIPTION_TYPE"	, "3");
		
		ТелоДела = "";
		Для каждого ТекПоле Из Поля Цикл
			ТелоДела = ТелоДела + ?(СтрДлина(ТелоДела) >0, "&", "") +"fields["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
		КонецЦикла;  		
		
		Если НЕ ЗначениеЗаполнено(ИдентификаторВладельца) Тогда	
			ТелоДела = ТелоДела + "&fields[OWNER_ID]=$result["+ Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(КлючВладельца)+"]";	
		Иначе
			ТелоДела = ТелоДела + "&fields[OWNER_ID]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдентификаторВладельца);	
		КонецЕсли;
		
		ДопПараметры 	= "&fields[PROVIDER_PARAMS][idAdress]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(СтруктураНастроек.ИдентификаторПодключения) + "&fields[PROVIDER_PARAMS][relAdress]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(НавигационнаяСсылка); 
		ДопПараметры2 	= "&fields[PARAMS][ENABLE_AUTHOR_CHANGE]=Y&fields[SETTINGS][ENABLE_AUTHOR_CHANGE]=Y&fields[SETTINGS][PROVIDER_PARAMS]=Y";
		
		Результат = ТелоДела + ДопПараметры + ДопПараметры2; 	
	
	//КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ОбновитьОписаниеДела(ДанныеДела)
	
	Результат = "";
	
	Если ТипЗнч(ДанныеДела) = Тип("Структура") Тогда
		
		Если ДанныеДела.ЭтоДокумент Тогда
			Если ДанныеДела.Сумма <> "" Тогда
				Результат = "<p style=""text-align: left;"">Сумма документа: "+Формат(ДанныеДела.Сумма, "")+" " + Строка(ДанныеДела.Валюта)+"</p>";	
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(ДанныеДела) Тогда
			
			МетаданныеОбъекта = ДанныеДела.Метаданные();
			
			Сумма 	= Неопределено;
			Валюта 	= "";
			Если МетаданныеОбъекта.Реквизиты.Найти("СуммаДокумента") <> Неопределено Тогда
				Сумма = ДанныеДела.СуммаДокумента;
			КонецЕсли;
			
			Если МетаданныеОбъекта.Реквизиты.Найти("Валюта") <> Неопределено Тогда
				Валюта = ДанныеДела.Валюта;
			КонецЕсли;
			
			Если Валюта = "" И МетаданныеОбъекта.Реквизиты.Найти("ВалютаДокумента") <> Неопределено Тогда
				Валюта = ДанныеДела.ВалютаДокумента;
			КонецЕсли;
			
			Если Валюта = "" И МетаданныеОбъекта.Реквизиты.Найти("ВалютаДенежныхСредств") <> Неопределено Тогда
				Валюта = ДанныеДела.ВалютаДенежныхСредств;
			КонецЕсли;
			
			Если Сумма <> Неопределено Тогда
				Результат = "<p style=""text-align: left;"">Сумма документа: "+Формат(Сумма, "")+" " + Строка(Валюта)+".</p>";	
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат Результат
	
КонецФункции

Процедура ВыгрузитьДелоВБитрикс24(ОбъектСсылка, НастройкаПодключения = Неопределено, ТипВладельца = "", ИдентификаторВладельца = "", ВыгружатьОтдельнымПотоком = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ОбъектСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляДел = Новый Массив;
	
	Если ТипВладельца = "" ИЛИ ИдентификаторВладельца = "" Тогда           
		
		мДанных = РегистрыСведений.Б24_К_ИдентификаторыДел.ПолучитьИнформациюОДелахПоОбъекту(ОбъектСсылка, НастройкаПодключения, ТипВладельца, ИдентификаторВладельца); 
		Для каждого ТекЭлемент Из мДанных Цикл
			
			Если ЗначениеЗаполнено(ТекЭлемент.Идентификатор) И ТекЭлемент.НеОбновлятьДелаБитрикс24 Тогда 
				Продолжить;
			КонецЕсли;
			
			ЕстьЗапись = Ложь;	
			Для каждого ТекЭлементДел Из ДанныеДляДел Цикл
				Если ТекЭлемент.НастройкаПодключения = ТекЭлементДел.НастройкаПодключения И ТекЭлемент.Идентификатор = ТекЭлементДел.Идентификатор 
					И ТекЭлемент.ИдентификаторВладельца = ТекЭлементДел.ИдентификаторВладельца И ТекЭлемент.ТипВладельца = ТекЭлементДел.ТипВладельца Тогда
					ЕстьЗапись = Истина;	
				КонецЕсли;
			КонецЦикла; 
			
			Если НЕ ЕстьЗапись Тогда
				ДанныеДляДел.Добавить(ТекЭлемент);	
			КонецЕсли;
			
		КонецЦикла; 
		
	КонецЕсли;	

	Если ЗначениеЗаполнено(НастройкаПодключения) И ЗначениеЗаполнено(ИдентификаторВладельца) И ЗначениеЗаполнено(ТипВладельца) И ДанныеДляДел.Количество() = 0 Тогда
		
		НовоеДело = Новый Структура;
		НовоеДело.Вставить("НастройкаПодключения"	, НастройкаПодключения);
		НовоеДело.Вставить("Идентификатор"			, "");
		НовоеДело.Вставить("ИдентификаторВладельца"	, ИдентификаторВладельца);
		НовоеДело.Вставить("ТипВладельца"			, ТипВладельца);
		НовоеДело.Вставить("Объект"					, ОбъектСсылка);
		НовоеДело.Вставить("ЭтоНовый"				, Истина);
		ДанныеДляДел.Добавить(НовоеДело);
		
	КонецЕсли;
	
	Для каждого ТекЭлемент Из ДанныеДляДел Цикл    
		ВыгрузитьИнформациюДела(ТекЭлемент.НастройкаПодключения, ТекЭлемент.Объект, ТекЭлемент.Идентификатор, ТекЭлемент.ТипВладельца, ТекЭлемент.ИдентификаторВладельца, ВыгружатьОтдельнымПотоком, ТекЭлемент.ЭтоНовый);
	КонецЦикла;
	
КонецПроцедуры           

Процедура ВыгрузитьИнформациюДела(НастройкаПодключения, ОбъектСсылка, ИдентификаторДела, ТипВладельца, ИдентификаторВладельца, ВыгружатьОтдельнымПотоком, ЭтоНовый)
	
УстановитьПривилегированныйРежим(Истина);
		
	Если НЕ Б24_К_СлужебныйВызовСервера.БазовыйМодульДоступен(НастройкаПодключения) Тогда
		Возврат;
	КонецЕсли;                          
	
	МетаданныеОбъекта = ОбъектСсылка.Метаданные();
	
	ЭтоДокумент = Метаданные.Документы.Найти(МетаданныеОбъекта.Имя) <> Неопределено;
	
	ОписаниеСсылки = Новый Структура;
	ОписаниеСсылки.Вставить("ЭтоДокумент"			, ЭтоДокумент);
	ОписаниеСсылки.Вставить("ВерсияДанных"			, ОбъектСсылка.ВерсияДанных);
	ОписаниеСсылки.Вставить("Представление"			, Строка(ОбъектСсылка));
	ОписаниеСсылки.Вставить("НавигационнаяСсылка"	, ПолучитьНавигационнуюСсылку(ОбъектСсылка));
	
	ОписаниеСсылки.Вставить("Валюта"		, "");
	ОписаниеСсылки.Вставить("Сумма"			, "");
	
	Если ЭтоДокумент Тогда
		
		Если МетаданныеОбъекта.Реквизиты.Найти("СуммаДокумента") <> Неопределено Тогда
			ОписаниеСсылки.Сумма = ОбъектСсылка.СуммаДокумента;
		КонецЕсли;
		
		Если МетаданныеОбъекта.Реквизиты.Найти("Валюта") <> Неопределено Тогда
			ОписаниеСсылки.Валюта = ОбъектСсылка.Валюта;
		ИначеЕсли МетаданныеОбъекта.Реквизиты.Найти("ВалютаДокумента") <> Неопределено Тогда
			ОписаниеСсылки.Валюта = ОбъектСсылка.ВалютаДокумента;
		ИначеЕсли МетаданныеОбъекта.Реквизиты.Найти("ВалютаДенежныхСредств") <> Неопределено Тогда
			ОписаниеСсылки.Валюта = ОбъектСсылка.ВалютаДенежныхСредств;
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыЗапроса.Вставить("ОбъектСсылка"			, ОбъектСсылка);
	ПараметрыЗапроса.Вставить("ОписаниеСсылки"			, ОписаниеСсылки);
	ПараметрыЗапроса.Вставить("ПредставлениеОбъекта"	, Строка(ОбъектСсылка));
	ПараметрыЗапроса.Вставить("ИдентификаторВладельца"	, ИдентификаторВладельца);
	ПараметрыЗапроса.Вставить("ИдентификаторДела"		, ИдентификаторДела);
	ПараметрыЗапроса.Вставить("ТипВладельца"			, ТипВладельца);
	ПараметрыЗапроса.Вставить("ЭтоНовый"				, ЭтоНовый);
	
	Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Б24_К_ВыгрузкаДелВызовСервера.ВыгрузитьИнформациюДелаВФоне", "Обновление информации на портале", ПараметрыЗапроса, 2, ВыгружатьОтдельнымПотоком); 

КонецПроцедуры

Процедура ВыгрузитьИнформациюДелаВФоне(ПередаваемыеПараметры, АдресРезультата) Экспорт
	
	НастройкаПодключения 	= ПередаваемыеПараметры.НастройкаПодключения;
	ОбъектСсылка 			= ПередаваемыеПараметры.ОбъектСсылка;
	ОписаниеСсылки 			= ПередаваемыеПараметры.ОписаниеСсылки;
	ПредставлениеОбъекта 	= ПередаваемыеПараметры.ПредставлениеОбъекта;
	ИдентификаторВладельца 	= ПередаваемыеПараметры.ИдентификаторВладельца;
	ИдентификаторДела 		= ПередаваемыеПараметры.ИдентификаторДела;
	ТипВладельца 			= ПередаваемыеПараметры.ТипВладельца;
	ЭтоНовый 				= ПередаваемыеПараметры.ЭтоНовый;
	
	ИдентификаторДела 		= ПередаваемыеПараметры.ИдентификаторДела;
	ОбработатьИдентификаторДела(ИдентификаторДела);
	
	Если НЕ ЗначениеЗаполнено(ТипВладельца) Тогда
		Перейти ~КонецПроцедурыВФоне;
	КонецЕсли;
	
	СтруктураНастроек 	= Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Истина);  
	Если СтруктураНастроек = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю("Не удалось подключиться");
		Перейти ~КонецПроцедурыВФоне;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение2 = "Выгрузка дел";
	
	HTTPЗапросДела 	= ПолучитьТелоЗапросаДела(СтруктураНастроек, ТипВладельца, ОписаниеСсылки, ПредставлениеОбъекта, ИдентификаторВладельца, "N");
	
	ТелоHTTPЗапроса = "";
	Если ЗначениеЗаполнено(ИдентификаторДела) Тогда            
		ТелоHTTPЗапроса = "cmd[N]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.activity.update?id=" + ИдентификаторДела + "&"  + HTTPЗапросДела);	
	Иначе
		ТелоHTTPЗапроса = "cmd[N]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.activity.add?" + HTTPЗапросДела);	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТелоHTTPЗапроса) Тогда
		Перейти ~КонецПроцедурыВФоне;
	КонецЕсли;
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	Если СтруктураОтвета = Неопределено Тогда
		СтруктураНастроек.ВыполненоБезОшибок = Ложь;
		Перейти ~КонецПроцедурыВФоне;
	КонецЕсли;
	
	Попытка
		
		result = СтруктураОтвета.Получить("result");                                             
		Если result <> Неопределено Тогда                                                         
			result2 = result.Получить("result");
			Если ЗначениеЗаполнено(result2) Тогда 			
				
				ТипОбъекта = Б24_К_RestApiВызовСервера.ПолучитьТипДанныхДелаПоТипуВладельца(ТипВладельца);
				
				Если НЕ ЗначениеЗаполнено(ИдентификаторДела) Тогда
					
					ИдентификаторДела = Формат(result2.Получить("N"), "ЧГ=0");	
					Если ТипОбъекта = СтруктураНастроек.ТипыОбъектовОбмена.ДелоСмартПроцесса Тогда
						ИдентификаторДела = ТипВладельца + "_" + ИдентификаторДела;	
					КонецЕсли;    
					
					РегистрыСведений.Б24_К_ИдентификаторыДел.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипОбъекта, ОбъектСсылка, ИдентификаторДела, ИдентификаторВладельца); 
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоНовый Тогда                             
			//Б24_КА_РоботыИТриггерыВызовСервера.ОбработкаСобытияТриггера(ОбъектСсылка, "ИзменениеДокумента");
			//Б24_КА_РоботыИТриггерыВызовСервера.ПроверитьЗапуститьТриггерПроведенияДокумента(ОбъектСсылка);
		КонецЕсли;
		
	Исключение
		
	КонецПопытки;
	
~КонецПроцедурыВФоне:	

	Б24_К_ОбщегоНазначенияВызовСервера.ПроверитьФоновоеЗаданиеВОчереди(ПередаваемыеПараметры);
	
КонецПроцедуры

Процедура ВыгрузитьКонфигурируемоеДелоВТаймЛайн(СтруктураНастроек, ТипВладельца, ИдентификаторВладельца, Статус, СообщениеДела, Объект = Неопределено) Экспорт
	
	ИдентификаторПодключения = СтрЗаменить(СтруктураНастроек.ИдентификаторПодключения, "_SALE", "");

	НавигационнаяСсылкаОбъекта 	= "";  
	ОписаниеДела 				= "";
	Если Объект <> Неопределено И ЗначениеЗаполнено(Объект) Тогда
		НавигационнаяСсылкаОбъекта = ПолучитьНавигационнуюСсылку(Объект); 
		ОписаниеДела = "Открыть документ: " + Строка(Объект) 
	КонецЕсли;       

	ТипТэга = "success";
	Если Статус = "Ошибка" Тогда ТипТэга = "failure" КонецЕсли;
	
	ЗаголовокТега = "Зарезервировано";
	Если ТипЗнч(Объект) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда 
		ЗаголовокТега = "Отгружено"; 
	КонецЕсли;
	
	ЗаголовокДела = "Дельце из 1С";
	СообщениеДела = СообщениеДела;    
	
	ТелоЗапроса = "&ownerId=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдентификаторВладельца);
	ТелоЗапроса = ТелоЗапроса + "&ownerTypeId=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТипВладельца);
	
	
	ТелоЗапроса = ТелоЗапроса + "&fields[completed]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("N");
	//ТелоЗапроса = ТелоЗапроса + "&fields[deadline]=" + ЗакодироватьСтрокуСервер("N");
	//ТелоЗапроса = ТелоЗапроса + "&fields[responsibleId]=" + ЗакодироватьСтрокуСервер("N");
	//ТелоЗапроса = ТелоЗапроса + "&fields[isIncomingChannel]=" + ЗакодироватьСтрокуСервер("N");
	//ТелоЗапроса = ТелоЗапроса + "&fields[originatorId]=" + ЗакодироватьСтрокуСервер("N");
	//ТелоЗапроса = ТелоЗапроса + "&fields[originId]=" + ЗакодироватьСтрокуСервер("N");
	
	
	ТелоЗапроса = ТелоЗапроса + "&layout[icon][code]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("document");
	ТелоЗапроса = ТелоЗапроса + "&layout[header][title]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ЗаголовокДела);      
	ТелоЗапроса = ТелоЗапроса + "&layout[header][tags][1][title]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ЗаголовокТега);
	ТелоЗапроса = ТелоЗапроса + "&layout[header][tags][1][type]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТипТэга);
	Если ЗначениеЗаполнено(НавигационнаяСсылкаОбъекта) Тогда
		ТелоЗапроса = ТелоЗапроса + "&layout[header][titleAction][type]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("openRestApp");
		ТелоЗапроса = ТелоЗапроса + "&layout[header][titleAction][actionParams][idAdress]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдентификаторПодключения);
		ТелоЗапроса = ТелоЗапроса + "&layout[header][titleAction][actionParams][relAdress]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(НавигационнаяСсылкаОбъекта);    
	КонецЕсли;
	
	
	ТелоЗапроса = ТелоЗапроса + "&layout[body][logo][code]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("document");
	ТелоЗапроса = ТелоЗапроса + "&layout[body][blocks][text][type]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("text");
	ТелоЗапроса = ТелоЗапроса + "&layout[body][blocks][text][properties][value]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(СообщениеДела);
	
	Если ЗначениеЗаполнено(НавигационнаяСсылкаОбъекта) Тогда 
		ТелоЗапроса = ТелоЗапроса + "&layout[body][blocks][link][type]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("link");
		ТелоЗапроса = ТелоЗапроса + "&layout[body][blocks][link][properties][text]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ОписаниеДела);  
		ТелоЗапроса = ТелоЗапроса + "&layout[body][blocks][link][properties][action][type]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("openRestApp");
		ТелоЗапроса = ТелоЗапроса + "&layout[body][blocks][link][properties][action][actionParams][idAdress]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдентификаторПодключения);
		ТелоЗапроса = ТелоЗапроса + "&layout[body][blocks][link][properties][action][actionParams][relAdress]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(НавигационнаяСсылкаОбъекта);
	КонецЕсли;
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.activity.configurable.add", ТелоЗапроса); 
	Если СтруктураОтвета <> Неопределено Тогда
		result = СтруктураОтвета.Получить("result");
		Если result = Неопределено Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось отправить сообщение у объекта " + ТипВладельца+" с ид:" + ИдентификаторВладельца);
		КонецЕсли;
	Иначе
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось отправить сообщение у объекта " + ТипВладельца+" с ид:" + ИдентификаторВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область РаботаСДелами

Функция ПолучитьИспользуемыеНастройкиПодключенияОбъектомЧерезДела(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;     
	Запрос.УстановитьПараметр("ТипОбъекта"			, Объект.Метаданные().ПолноеИмя());
	Запрос.УстановитьПараметр("ИдентификаторОбъекта", XMLСтрока(Объект));
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Б24_КБ_ОбъектыСозданныеЧерезСлайдер.НастройкаПодключения КАК НастройкаПодключения
	|ИЗ
	|	РегистрСведений.Б24_КБ_ОбъектыСозданныеЧерезСлайдер КАК Б24_КБ_ОбъектыСозданныеЧерезСлайдер
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_КБ_ОбъектыСозданныеЧерезСлайдер.НастройкаПодключения = Б24_К_НастройкиПодключения.Ссылка
	|ГДЕ
	|	Б24_КБ_ОбъектыСозданныеЧерезСлайдер.ТипОбъекта = &ТипОбъекта
	|	И Б24_КБ_ОбъектыСозданныеЧерезСлайдер.ИдентификаторОбъекта = &ИдентификаторОбъекта
	|	И Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ";
	тзнДанных = Запрос.Выполнить().Выгрузить();
	
	Возврат тзнДанных.ВыгрузитьКолонку("НастройкаПодключения");
	
КонецФункции

Функция ОбъектуПривязаныДела(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипыДанных"			, Б24_К_RestApiВызовСервера.ПолучитьТипыДанныхДел());
	Запрос.УстановитьПараметр("ТекущийОбъект"		, Объект);
	Запрос.УстановитьПараметр("ТипОбъекта"			, Объект.Метаданные().ПолноеИмя());
	Запрос.УстановитьПараметр("ИдентификаторОбъекта", XMLСтрока(Объект));
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Б24_К_ИдентификаторыДел.ТипДанных КАК ТипДанных,
	|	Б24_К_ИдентификаторыДел.Портал КАК Портал,
	|	Б24_К_ИдентификаторыДел.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыДел КАК Б24_К_ИдентификаторыДел
	|ГДЕ
	|	Б24_К_ИдентификаторыДел.ТипДанных В(&ТипыДанных)
	|	И Б24_К_ИдентификаторыДел.ТипОбъекта = &ТипОбъекта
	|	И Б24_К_ИдентификаторыДел.ИдентификаторОбъекта = &ИдентификаторОбъекта";
	ВыполненныйЗапрос = Запрос.Выполнить();  
	
	Возврат	НЕ ВыполненныйЗапрос.Пустой();
	
КонецФункции

Функция ДобавитьСвязкуСДокументомОснования(Объект)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Ложь;
	
	Если ОбъектуПривязаныДела(Объект) Тогда	
		Возврат Результат;
	КонецЕсли;
	
	мДокументыОснования = ПолучитьДокументыОснования(Объект);
	
	КС = Новый КвалификаторыСтроки(100);
	Массив = Новый Массив;
	Массив.Добавить(Тип("Строка"));    
	ОписаниеТиповС = Новый ОписаниеТипов(Массив, , КС);  
	
	ОписаниеДокументовОснований = Новый ТаблицаЗначений;
	ОписаниеДокументовОснований.Колонки.Добавить("ТипОбъекта"			, ОписаниеТиповС);	
	ОписаниеДокументовОснований.Колонки.Добавить("ИдентификаторОбъекта"	, ОписаниеТиповС);	
	
	Для каждого ТекЭлемент Из мДокументыОснования Цикл   
		
		Если НЕ ЗначениеЗаполнено(ТекЭлемент) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ Б24_К_ОбщегоНазначенияВызовСервера.ЭтоПростойТип(ТипЗнч(ТекЭлемент)) Тогда
			НоваяСтрока = ОписаниеДокументовОснований.Добавить();    
			НоваяСтрока.ТипОбъекта 				= ТекЭлемент.Метаданные().ПолноеИмя();
			НоваяСтрока.ИдентификаторОбъекта 	= XMLСтрока(ТекЭлемент);
		КонецЕсли;
		
	КонецЦикла;      
	
	ОграниченияПоСозданиюДел = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.НастройкиПодключенияСЗапретомНаСозданияДелНаОсновании(Объект.Метаданные().ПолноеИмя());
	
	ТипыДанныхДляОбменаСПорталом = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();	
	ТипыДанныхДел 				 = Б24_К_RestApiВызовСервера.ПолучитьТипыДанныхДел();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипыДанных"					, Б24_К_RestApiВызовСервера.ПолучитьТипыДанныхДел());
	Запрос.УстановитьПараметр("ДокументыОснования"			, мДокументыОснования);
	Запрос.УстановитьПараметр("ОписаниеДокументовОснований"	, ОписаниеДокументовОснований);
	Запрос.Текст = "ВЫБРАТЬ
	|	ОписаниеДокументовОснований.ТипОбъекта КАК ТипОбъекта,
	|	ОписаниеДокументовОснований.ИдентификаторОбъекта КАК ИдентификаторОбъекта
	|ПОМЕСТИТЬ ВТ_ОписаниеДокументовОснований
	|ИЗ
	|	&ОписаниеДокументовОснований КАК ОписаниеДокументовОснований
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Б24_К_НастройкиПодключения.Ссылка КАК НастройкаПодключения,
	|	Б24_К_ИдентификаторыДел.ТипДанных КАК ТипДанных,
	|	Б24_К_ИдентификаторыДел.Портал КАК Портал,
	|	Б24_К_ИдентификаторыДел.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор,
	|	Б24_К_ИдентификаторыДел.ТипОбъекта КАК ТипОбъекта,
	|	Б24_К_ИдентификаторыДел.ИдентификаторОбъекта КАК ИдентификаторОбъекта
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыДел КАК Б24_К_ИдентификаторыДел
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_К_ИдентификаторыДел.Портал = Б24_К_НастройкиПодключения.Портал
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОписаниеДокументовОснований КАК ОписаниеДокументовОснований
	|		ПО Б24_К_ИдентификаторыДел.ТипОбъекта = ОписаниеДокументовОснований.ТипОбъекта
	|			И Б24_К_ИдентификаторыДел.ИдентификаторОбъекта = ОписаниеДокументовОснований.ИдентификаторОбъекта
	|ГДЕ
	|	Б24_К_ИдентификаторыДел.ТипДанных В(&ТипыДанных)
	|	И Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если ОграниченияПоСозданиюДел.Найти(Выборка.НастройкаПодключения) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяЗапись = РегистрыСведений.Б24_К_ИдентификаторыДел.СоздатьМенеджерЗаписи();
			
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			НоваяЗапись.ТипОбъекта 					= Объект.Метаданные().ПолноеИмя();
			НоваяЗапись.ИдентификаторОбъекта		= XMLСтрока(Объект);
			НоваяЗапись.Идентификатор				= "";
			
			Если НоваяЗапись.ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоСмартПроцесса Тогда
				ЧастиИдентификатора = СтрРазделить(Выборка.Идентификатор, "_");	
				Если ЧастиИдентификатора.Количество()>0 Тогда
					НоваяЗапись.Идентификатор = ЧастиИдентификатора[0] + "_";
				КонецЕсли;
			КонецЕсли;
			
			НоваяЗапись.Записать();   
			
			Результат = Истина;
			
		КонецЦикла;
		
	КонецЕсли;  
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьДокументыОснования(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);   
	
	мДокументыОснования = Новый Массив;
	
	Попытка  //опасно без попытки.
		
		МетаданныеОбъекта = Объект.Метаданные();
		
		КлючиПоиска = Новый Массив;
		
		ИсключенияИменаРеквизитов = ИсключенияИменаРеквизитовДляСозданияДелНаОсновании();
		
		мГруппыРеквизитов = Новый Массив;
		мГруппыРеквизитов.Добавить(МетаданныеОбъекта.Реквизиты);	
		мГруппыРеквизитов.Добавить(МетаданныеОбъекта.СтандартныеРеквизиты);	
		
		Для каждого ТекТабЧасть Из МетаданныеОбъекта.ТабличныеЧасти Цикл       
			Если ТекТабЧасть.Имя = "ДополнительныеРеквизиты" Тогда
				Продолжить;
			КонецЕсли;
			мГруппыРеквизитов.Добавить(Новый Структура("ИмяТЧ, Реквизиты", ТекТабЧасть.Имя, ТекТабЧасть.Реквизиты))
		КонецЦикла;	
		
		Для каждого ГруппаРеквизитов Из мГруппыРеквизитов Цикл
			
			КоллекцияРеквизитов = Неопределено;   
			ИмяТЧ				= "";
			Если ТипЗнч(ГруппаРеквизитов) = Тип("Структура") Тогда
				КоллекцияРеквизитов = ГруппаРеквизитов.Реквизиты;
				ИмяТЧ				= ГруппаРеквизитов.ИмяТЧ;
			Иначе
				КоллекцияРеквизитов = ГруппаРеквизитов;		
			КонецЕсли; 
			
			Для каждого ТекРеквизит Из КоллекцияРеквизитов Цикл
				
				Если ИсключенияИменаРеквизитов.Найти(ТекРеквизит.Имя) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
					
				Для каждого ТипРеквизита Из ТекРеквизит.Тип.Типы() Цикл    
					
					Если ИсключениеТиповДляСозданияДелНаОсновании(Объект, ТипРеквизита) Тогда
						Продолжить;
					КонецЕсли;
					
					МетаданныеПоТипу = Метаданные.НайтиПоТипу(ТипРеквизита);
					Если МетаданныеПоТипу <> Неопределено Тогда  
						Если МетаданныеОбъекта.ВводитсяНаОсновании.Содержит(МетаданныеПоТипу) Тогда
							КлючиПоиска.Добавить(Новый Структура("ИмяТЧ, ИмяРеквизита", ИмяТЧ, ТекРеквизит.Имя));
						КонецЕсли;	
					КонецЕсли;
					
				КонецЦикла; 
				
			КонецЦикла;
			
		КонецЦикла;
		
		ИспользуемыеТЧ 		= Новый Соответствие;
		РеквизитыСтрокой 	= "";
		Для каждого ТекЭлемент Из КлючиПоиска Цикл
			Если НЕ ЗначениеЗаполнено(ТекЭлемент.ИмяТЧ) Тогда       
				Если СтрНайти(РеквизитыСтрокой, ТекЭлемент.ИмяРеквизита) = 0 Тогда
					РеквизитыСтрокой = ?(РеквизитыСтрокой = "", ТекЭлемент.ИмяРеквизита, РеквизитыСтрокой + ", " + ТекЭлемент.ИмяРеквизита);
				КонецЕсли;
			Иначе
				
				КлючТЧ = ИспользуемыеТЧ.Получить(ТекЭлемент.ИмяТЧ);
				Если КлючТЧ = Неопределено Тогда КлючТЧ = Новый Массив КонецЕсли;  
				КлючТЧ.Добавить(ТекЭлемент.ИмяРеквизита);
				
				ИспользуемыеТЧ.Вставить(ТекЭлемент.ИмяТЧ, КлючТЧ); 
					
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(РеквизитыСтрокой) Тогда
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект, РеквизитыСтрокой);   
			Для каждого ТекКлюч Из ЗначенияРеквизитов Цикл
				Если ЗначениеЗаполнено(ТекКлюч.Значение) Тогда     
					Если мДокументыОснования.Найти(ТекКлюч.Значение) = Неопределено И ТекКлюч.Значение <> Объект Тогда 
						мДокументыОснования.Добавить(ТекКлюч.Значение);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;	
		
		Для каждого ТекКлюч Из ИспользуемыеТЧ Цикл
			
			Выборка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, ТекКлюч.Ключ).Выбрать();   
			Пока Выборка.Следующий() Цикл       
				Для каждого ТекРеквизит Из ТекКлюч.Значение Цикл      
					Если ЗначениеЗаполнено(Выборка[ТекРеквизит]) Тогда  
						Если мДокументыОснования.Найти(Выборка[ТекРеквизит]) = Неопределено Тогда 
							мДокументыОснования.Добавить(Выборка[ТекРеквизит]);
						КонецЕсли;  
					КонецЕсли;  
				КонецЦикла;
			КонецЦикла;
			
		КонецЦикла;      
		
	Исключение
		ЗаписьЖурналаРегистрации("ДобавлениеСвязиЧерезДело", УровеньЖурналаРегистрации.Ошибка,,, "Не удалось привязать объект к сущности Битрикс24");
	КонецПопытки;
	
	Возврат мДокументыОснования;
	
КонецФункции

Функция ИсключениеТиповДляСозданияДелНаОсновании(Объект, ТипРеквизита)     
	
	Результат = Ложь;
	
	Если ТипРеквизита = Тип("СправочникСсылка.Контрагенты") Тогда
		Результат = Истина;  
	ИначеЕсли ТипРеквизита = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		Результат = Истина; 
	ИначеЕсли ТипРеквизита = Тип("СправочникСсылка.Партнеры") Тогда
		Результат = Истина;   
	ИначеЕсли ТипРеквизита = Тип("СправочникСсылка.СоглашенияСКлиентами") Тогда
		Результат = Истина;
	КонецЕсли;

	Возврат Результат;                                                                                  

КонецФункции

Функция ИсключенияИменаРеквизитовДляСозданияДелНаОсновании()     

	Результат = Новый Массив;     
	
	Результат.Добавить("ОснованиеПечатиСсылка");       

	Возврат Результат;

КонецФункции

Процедура ОбработатьИдентификаторДела(ИдентификаторДела)

	РазделенныйИдентификатор = СтрРазделить(ИдентификаторДела, "_");
	Если РазделенныйИдентификатор.Количество() > 1 Тогда
		ИдентификаторДела = РазделенныйИдентификатор[1];
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
