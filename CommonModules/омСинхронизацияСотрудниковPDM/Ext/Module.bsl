&НаСервере
Функция СинхронизацияСотрудников(Сотрудник = Неопределено) Экспорт
	Если Сотрудник = Неопределено Тогда
		
		стДанныеPDM = ПолучитьДанныеИзPDMСотрудники();
		
		Если НЕ ПустаяСтрока(стДанныеPDM.Ошибка) Тогда
		
			Возврат стДанныеPDM.Ошибка; 	
		
		КонецЕсли;
		
		Возврат СинхронизироватьСотрудников(стДанныеPDM);
		
	Иначе
		
		Возврат СинхронизацияСотрудника(Сотрудник);	
	
	КонецЕсли;
	
КонецФункции // СинхронизацияСотрудников()

&НаСервере
Функция СинхронизацияСотрудника(Сотрудник)
	стДанныеДляPDM 		= Справочники.ДанныеСотрудникаВPDM.ПолучитьДанныеСотрудникаДляPDM(Сотрудник);
	
	стКонтактныеДанные 	= омСотрудники.ПолучитьКонтактыСотрудников(Сотрудник);	

	стПодключение = омMsSQL.СоздатьПодключениеКБД();
	
	Если НЕ ПустаяСтрока(стПодключение.Ошибка) Тогда
		
		Возврат стПодключение.Ошибка;	
		
	КонецЕсли;
	
	Если стДанныеДляPDM.UserID = 0 И стДанныеДляPDM.Enabled Тогда
		
		обДанныеСотрудникаВPDM = стДанныеДляPDM.ДанныеСотрудникаВPDM.ПолучитьОбъект();
		
		обДанныеСотрудникаВPDM.UserID = СоздатьСотрудникаВPDM(стПодключение.Соединение, стДанныеДляPDM, стКонтактныеДанные);
		
		обДанныеСотрудникаВPDM.Записать();
		
	ИначеЕсли НЕ стДанныеДляPDM.UserID = 0 Тогда
		
		стДанныеВPDM = ПолучитьДанныеИзPDMСотрудник(стПодключение.Соединение, стДанныеДляPDM.UserID);
		
		Если НЕ ПустаяСтрока(стДанныеВPDM.Ошибка) Тогда
			
			Возврат стДанныеВPDM.Ошибка;	
			
		КонецЕсли;
		
		НужнаЗапись = Ложь;
		
		Если НЕ стДанныеДляPDM.Username = стДанныеВPDM.Username Тогда                 
			НужнаЗапись = Истина;	
		ИначеЕсли НЕ стДанныеДляPDM.Enabled = стДанныеВPDM.Enabled Тогда
			НужнаЗапись = Истина;	
		ИначеЕсли НЕ стКонтактныеДанные.АдресЭП = стДанныеВPDM.Email Тогда
			НужнаЗапись = Истина;	
		ИначеЕсли НЕ стКонтактныеДанные.НомерТелефона = стДанныеВPDM.Telephone Тогда
			НужнаЗапись = Истина;	
		КонецЕсли;
		
		Если НужнаЗапись Тогда
			текстЗапросаДляВставки = "UPDATE dbo.Users SET Username = N'"
			+ стДанныеДляPDM.Username + "', Email = N'" + стКонтактныеДанные.АдресЭП + "', Telephone = '" + стКонтактныеДанные.НомерТелефона + "', Enabled = "
			+ ?(стДанныеДляPDM.Enabled, 1, 0) + " WHERE UserID = " + стДанныеДляPDM.UserID;
			
			стПодключение = омMsSQL.СоздатьПодключениеКБД();
			
			Если НЕ ПустаяСтрока(стПодключение.Ошибка) Тогда
				
				Возврат стПодключение.Ошибка;	
				
			КонецЕсли;
			
			омMsSQL.ВыполнитьЗапросНаВставкуPDM(стПодключение.Соединение, текстЗапросаДляВставки);
			
			Попытка
				стПодключение.Соединение.Close();
			Исключение
				Возврат ОписаниеОшибки();
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат "";
КонецФункции // СинхронизацияСотрудника()

&НаСервере
Функция СоздатьСотрудникаВPDM(Соединение, стДанныеДляPDM, стКонтактныеДанные)

	текстЗапросаДляВставки = "INSERT INTO dbo.Users(Enabled, Username, Email, Telephone)  VALUES (1, N'"
	+ стДанныеДляPDM.Username + "', N'" + стКонтактныеДанные.АдресЭП + "', '" + стКонтактныеДанные.НомерТелефона + "')";

	
	ТекстЗапроса = "select Max(UserID) as max_index from dbo.Users u";
	
	омMsSQL.ВыполнитьЗапросНаВставкуPDM(Соединение, текстЗапросаДляВставки);

	Выборка = омMsSQL.ВыполнитьЗапросНаВыборкуВPDM(Соединение, ТекстЗапроса);

	max_index = 0;
	Если НЕ Выборка.BOF Тогда
		Выборка.MoveFirst();
		
		Пока НЕ Выборка.EOF Цикл
			
			max_index = Выборка.Fields("max_index").Value;
			

			Выборка.MoveNext();
		КонецЦикла;
	
	КонецЕсли;
	
	Попытка
		Соединение.Close();
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
	
   Возврат max_index;
КонецФункции // СоздатьСотрудникаВPDM(стДанныеДляPDM, стКонтактныеДанные)


&НаСервере
Функция ПолучитьДанныеИзPDMСотрудник(Соединение,UserID)
	стДанныеВPDM = Новый Структура("Username, Enabled, Email, Telephone", "");
	ТекстЗапроса = "select Username, Enabled, Email, Telephone from dbo.Users u WHERE UserID = " + Формат(UserID,"ЧН=0; ЧГ=0");
	
	Выборка = омMsSQL.ВыполнитьЗапросНаВыборкуВPDM(Соединение, ТекстЗапроса);
	
	Если НЕ Выборка.BOF Тогда
		Выборка.MoveFirst();
		
		Пока НЕ Выборка.EOF Цикл
			
			Для каждого Элемент Из стДанныеВPDM Цикл
				стДанныеВPDM[Элемент.Ключ] = Выборка.Fields(Элемент.Ключ).Value;	
			КонецЦикла;
			
			Выборка.MoveNext();
		КонецЦикла;
	
	КонецЕсли;
	
	стДанныеВPDM.Вставить("Ошибка","");
	
	Попытка
		Соединение.Close();
	Исключение
		стДанныеВPDM.Ошибка = ОписаниеОшибки();
	КонецПопытки;
	
   Возврат стДанныеВPDM;
КонецФункции // ПолучитьДанныеИзPDM()

&НаСервере
Функция СинхронизироватьСотрудников(стДанныеPDM)
	тзДанные1С = ПолучитьДанныеИЗ1С();
	
	Для каждого изPDM Из стДанныеPDM.тзДанныеPDM Цикл
		
		НайденаяСтрока = тзДанные1С.Найти(изPDM.UserID, "UserID");
		
		Если НайденаяСтрока = Неопределено Тогда
			Если изPDM.Enabled Тогда
				СоздатьСотрудника(изPDM);
			КонецЕсли;
		Иначе
			НайденаяСтрока.Обработано = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции // СинхронизироватьСотрудников(стДанныеPDM) 

&НаСервере
Функция СоздатьСотрудника(изPDM) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ДанныеСотрудникаВPDM.Ссылка, НЕОПРЕДЕЛЕНО) КАК Ссылка,
		|	Сотрудники.Ссылка КАК Сотрудник
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДанныеСотрудникаВPDM КАК ДанныеСотрудникаВPDM
		|		ПО (ДанныеСотрудникаВPDM.Сотрудник = Сотрудники.Ссылка)
		|ГДЕ
		|	Сотрудники.Наименование = &Наименование
		|	И (ДанныеСотрудникаВPDM.UserID = 0
		|			ИЛИ ДанныеСотрудникаВPDM.UserID ЕСТЬ NULL)";
	
	Запрос.УстановитьПараметр("Наименование", изPDM.Username);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Сотрудник) Тогда
			СоздатьДанныеСотрудникаВPDM(изPDM, Выборка.Ссылка, Выборка.Сотрудник);
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Сотрудник = Справочники.Сотрудники.СоздатьЭлемент();
	Сотрудник.Наименование = изPDM.Username;
	Сотрудник.Записать();
	
	СоздатьДанныеСотрудникаВPDM(изPDM, Неопределено, Сотрудник.Ссылка);
	
	Возврат Истина;
КонецФункции // СоздатьСотрудника()

&НаСервере
Процедура СоздатьДанныеСотрудникаВPDM(изPDM, СсылкаДанныеСотрудникаВPDM, Сотрудник)
	Если СсылкаДанныеСотрудникаВPDM = Неопределено Тогда
		ОбъектДанныеСотрудникаВPDM = Справочники.ДанныеСотрудникаВPDM.СоздатьЭлемент();	
		ОбъектДанныеСотрудникаВPDM.Сотрудник = Сотрудник;
	Иначе
		ОбъектДанныеСотрудникаВPDM = СсылкаДанныеСотрудникаВPDM.ПолучитьОбъект();		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ОбъектДанныеСотрудникаВPDM, изPDM);
	
	ОбъектДанныеСотрудникаВPDM.Записать();
	
	стКонтактныеДанные 	= омСотрудники.ПолучитьКонтактыСотрудников(Сотрудник);	
	
	ЗаписатьКонтактныеДанные = Ложь;
	стДанныеДляЗаписи = Новый Структура();
	
	
	Если НЕ (ПустаяСтрока(изPDM.Email) ИЛИ стКонтактныеДанные.АдресЭП = изPDM.Email) Тогда
		ЗаписатьКонтактныеДанные = Истина;
		стДанныеДляЗаписи.Вставить("АдресЭП", изPDM.Email);
	КонецЕсли;
	
	Если НЕ (ПустаяСтрока(изPDM.Telephone) ИЛИ стКонтактныеДанные.НомерТелефона = изPDM.Telephone) Тогда
		ЗаписатьКонтактныеДанные = Истина;
		стДанныеДляЗаписи.Вставить("НомерТелефона", изPDM.Telephone);
	КонецЕсли;
	
	Если ЗаписатьКонтактныеДанные Тогда
	
		ЗаписатьКонтактныеДанные(стДанныеДляЗаписи,Сотрудник);	
	
	КонецЕсли;
	

КонецПроцедуры // СоздатьДанныеСотрудникаВPDM(изPDM, Ссылка, Сотрудник)

&НаСервере
Процедура ЗаписатьКонтактныеДанные(стДанныеДляЗаписи,Сотрудник)  Экспорт
	
	ОбъектСотрудник = Сотрудник.ПолучитьОбъект();
	
	стОтбор = Новый Структура("Тип", );
	
	Если стДанныеДляЗаписи.Свойство("АдресЭП") Тогда
		стОтбор.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты; 
		НайденыеСтроки = ОбъектСотрудник.КонтактнаяИнформация.НайтиСтроки(стОтбор);	
		Если НайденыеСтроки.Количество() = 0 Тогда
			НС 	= ОбъектСотрудник.КонтактнаяИнформация.Добавить();
		Иначе
			НС  = НайденыеСтроки[0];
		КонецЕсли;	
		НС.Тип 				= Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
		НС.Вид 				= Справочники.ВидыКонтактнойИнформации.EmailСотрудника;
		НС.АдресЭП 			= стДанныеДляЗаписи.АдресЭП;
		НС.Представление	= стДанныеДляЗаписи.АдресЭП;
		АдресПоПолям 		= УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияПоПредставлению(НС.Представление, НС.Тип);
		НС.Значение 		= УправлениеКонтактнойИнформациейСлужебный.СтруктураВСтрокуJSON(АдресПоПолям);
		НС.ЗначенияПолей 	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияВXML(НС.Значение);
	КонецЕсли;	
	
	Если стДанныеДляЗаписи.Свойство("НомерТелефона") Тогда
		стОтбор.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон; 
		НайденыеСтроки = ОбъектСотрудник.КонтактнаяИнформация.НайтиСтроки(стОтбор);	
		Если НайденыеСтроки.Количество() = 0 Тогда
			НС 	= ОбъектСотрудник.КонтактнаяИнформация.Добавить();
		Иначе
			НС  = НайденыеСтроки[0];
		КонецЕсли;	
		НС.Тип 				= Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
		НС.Вид 				= Справочники.ВидыКонтактнойИнформации.ТелефонСотрудника;
		НС.АдресЭП 			= стДанныеДляЗаписи.НомерТелефона;
		НС.Представление	= стДанныеДляЗаписи.НомерТелефона;
		АдресПоПолям 		= УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияПоПредставлению(НС.Представление, НС.Тип);
		НС.Значение 		= УправлениеКонтактнойИнформациейСлужебный.СтруктураВСтрокуJSON(АдресПоПолям);
		НС.ЗначенияПолей 	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияВXML(НС.Значение);
	КонецЕсли;	
	
    ОбъектСотрудник.Записать();
КонецПроцедуры // ЗаписатьКонтактныеДанные(стДанныеДляЗаписи,Сотрудник)

&НаСервере
Функция ПолучитьДанныеИЗ1С()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеСотрудникаВPDM.Ссылка КАК Ссылка,
		|	ДанныеСотрудникаВPDM.UserID КАК UserID,
		|	ДанныеСотрудникаВPDM.Сотрудник КАК Сотрудник,
		|	ДанныеСотрудникаВPDM.Username КАК Username,
		|	ДанныеСотрудникаВPDM.Enabled КАК Enabled,
		|	СотрудникиКонтактнаяИнформация.АдресЭП КАК АдресЭП,
		|	СотрудникиКонтактнаяИнформация.НомерТелефона КАК НомерТелефона
		|ИЗ
		|	Справочник.ДанныеСотрудникаВPDM КАК ДанныеСотрудникаВPDM
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники.КонтактнаяИнформация КАК СотрудникиКонтактнаяИнформация
		|		ПО ДанныеСотрудникаВPDM.Сотрудник = СотрудникиКонтактнаяИнформация.Ссылка
		|			И (СотрудникиКонтактнаяИнформация.Тип В (&Тип))";
	
	Запрос.УстановитьПараметр("Тип", омСотрудники.ПолучитьТипыКонтактнойИнформации());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	тзДанные1С = Новый ТаблицаЗначений;
	тзДанные1С.Колонки.Добавить("UserID",			Новый ОписаниеТипов("Число"));
	тзДанные1С.Колонки.Добавить("Username",			Новый ОписаниеТипов("Строка"));
	тзДанные1С.Колонки.Добавить("Enabled",			Новый ОписаниеТипов("Булево"));
	тзДанные1С.Колонки.Добавить("Email",			Новый ОписаниеТипов("Строка"));
	тзДанные1С.Колонки.Добавить("Telephone",		Новый ОписаниеТипов("Строка"));
	тзДанные1С.Колонки.Добавить("АдресЭП",			Новый ОписаниеТипов("Строка"));
	тзДанные1С.Колонки.Добавить("НомерТелефона",	Новый ОписаниеТипов("Строка"));
	тзДанные1С.Колонки.Добавить("Сотрудник",		Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	тзДанные1С.Колонки.Добавить("Обработано",		Новый ОписаниеТипов("Булево"));
	
	тзДанные1С.Индексы.Добавить("UserID");
	
	Пока Выборка.Следующий() Цикл
		НайденаяСтрока = тзДанные1С.Найти(Выборка.UserID);
		
		Если НайденаяСтрока = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(тзДанные1С.Добавить(), Выборка);
		Иначе
		    Если ЗначениеЗаполнено(Выборка.АдресЭП) Тогда
				НайденаяСтрока.АдресЭП = Выборка.АдресЭП;	
			КонецЕсли;
		    Если ЗначениеЗаполнено(Выборка.НомерТелефона) Тогда
				НайденаяСтрока.НомерТелефона = Выборка.НомерТелефона;	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат тзДанные1С;
КонецФункции // ПолучитьДанныеИЗ1С()

&НаСервере
Функция ПолучитьДанныеИзPDMСотрудники()
	
	стПодключение = омMsSQL.СоздатьПодключениеКБД();
	
	Если НЕ ПустаяСтрока(стПодключение.Ошибка) Тогда
		
		Возврат Новый Структура("Ошибка", стПодключение.Ошибка);	
		
	КонецЕсли;
	
	ТекстЗапроса = "select UserID, Username, Enabled, Email, Telephone from dbo.Users u";
	
	тзДанныеPDM = Новый ТаблицаЗначений;
	тзДанныеPDM.Колонки.Добавить("UserID",		Новый ОписаниеТипов("Число"));
	тзДанныеPDM.Колонки.Добавить("Username",	Новый ОписаниеТипов("Строка"));
	тзДанныеPDM.Колонки.Добавить("Enabled",		Новый ОписаниеТипов("Булево"));
	тзДанныеPDM.Колонки.Добавить("Email",		Новый ОписаниеТипов("Строка"));
	тзДанныеPDM.Колонки.Добавить("Telephone",	Новый ОписаниеТипов("Строка"));
	
	тзДанныеPDM.Индексы.Добавить("UserID");

	Выборка = омMsSQL.ВыполнитьЗапросНаВыборкуВPDM(стПодключение.Соединение, ТекстЗапроса);
	
	мUserID = Новый Массив;
	
	Если НЕ Выборка.BOF Тогда
		Выборка.MoveFirst();
		
		Пока НЕ Выборка.EOF Цикл
			НС = тзДанныеPDM.Добавить();
			НС.UserID 		= Выборка.Fields("UserID").Value; 
			НС.Username 	= Выборка.Fields("Username").Value; 
			НС.Enabled 		= Выборка.Fields("Enabled").Value; 
			НС.Email 		= Выборка.Fields("Email").Value; 
			НС.Telephone 	= Выборка.Fields("Telephone").Value;
			
			мUserID.Добавить(НС.UserID);
			
			Выборка.MoveNext();
		КонецЦикла;
	
	КонецЕсли;
	
	
	
	Попытка
		стПодключение.Соединение.Close();
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
   Возврат Новый Структура("тзДанныеPDM, мUserID, Ошибка",тзДанныеPDM, мUserID, "");
КонецФункции // ПолучитьДанныеИзPDM()
