//Если последний символ стПараметров.Первый = "@", тогда символ "@" игнорируется и к строке добавляется значение стПараметров.Второй
//Получается один параметр длиной до 39 знаков
&НаСервере
Процедура ЗаписатьВЖурналСобытий(КодСобытия = 0,Источник,стПараметров = Неопределено,ОписаниеСобытия = "",СтатусСобытия = Неопределено,ТипСобытия = Неопределено) Экспорт
	
	ПроизвольноеОписание 	= НЕ ПустаяСтрока(ОписаниеСобытия);
	Автор 					= ПараметрыСеанса.ТекущийПользователь;
	Дата 					= ТекущаяДата();
	
	НаборЗаписей = РегистрыСведений.ЖурналСобытий.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Дата.Установить(Дата);
	НаборЗаписей.Отбор.Автор.Установить(Автор);
	НаборЗаписей.Отбор.КодСобытия.Установить(КодСобытия);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Автор 			= Автор;
	НоваяЗапись.Дата 			= Дата;
	НоваяЗапись.Источник 		= Источник; 
	НоваяЗапись.КодСобытия 		= КодСобытия;
	НоваяЗапись.ИмяКомпьютера 	= ИмяКомпьютера();
	
	Если ПроизвольноеОписание Тогда
		НачатьТранзакцию();
		Попытка
			Для х = 1 По 5 Цикл
				НомерЗаписи = ПопыткаЗаписатьПроизвольноеОписание(ОписаниеСобытия,СтатусСобытия,ТипСобытия);
				Если НЕ ПустаяСтрока(НомерЗаписи) Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ПустаяСтрока(НомерЗаписи) Тогда
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
			НоваяЗапись.ЗначениеПараметра1 = НомерЗаписи;
			НаборЗаписей.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
		КонецПопытки;
	Иначе 
		Если Тип("Структура") = ТипЗнч(стПараметров) Тогда
			НоваяЗапись.ЗначениеПараметра1 = стПараметров.Первый;
			НоваяЗапись.ЗначениеПараметра2 = стПараметров.Второй;
		КонецЕсли;
		НоваяЗапись.ПроизвольноеОписание = ПроизвольноеОписание;
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры // ЗаписатьВЖурналСобытий()

&НаСервере
Функция ПопыткаЗаписатьПроизвольноеОписание(ОписаниеСобытия,СтатусСобытия,ТипСобытия)
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ПроизвольноеОписаниеСобытий");
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Разделяемый;
		БлокировкаДанных.Заблокировать(); 
		
		КодПроизвольногоСобытия = "0";
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(ПроизвольноеОписаниеСобытий.КодПроизвольногоСобытия), ""0"") КАК КодПроизвольногоСобытия
		|ИЗ
		|	РегистрСведений.ПроизвольноеОписаниеСобытий КАК ПроизвольноеОписаниеСобытий";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл                                              
			КодПроизвольногоСобытия = Выборка.КодПроизвольногоСобытия;
		КонецЦикла;
		
		КодПроизвольногоСобытия = Формат(Число(КодПроизвольногоСобытия) + 1,"ЧЦ=20; ЧВН=; ЧГ=0"); 
		
		НаборЗаписей = РегистрыСведений.ПроизвольноеОписаниеСобытий.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.КодПроизвольногоСобытия.Установить(КодПроизвольногоСобытия);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.КодПроизвольногоСобытия = КодПроизвольногоСобытия;
		НоваяЗапись.ОписаниеСобытия 		= ОписаниеСобытия;
		НоваяЗапись.СтатусСобытия 			= СтатусСобытия;
		НоваяЗапись.ТипСобытия 				= ТипСобытия;
		НоваяЗапись.Дата 					= ТекущаяДата();
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
		Возврат КодПроизвольногоСобытия;
	Исключение
		ОтменитьТранзакцию();
		Возврат "";
	КонецПопытки;
	
КонецФункции // ПопыткаЗаписатьПроизвольноеОписание(ОписаниеСобытия,СтатусСобытия)

&НаСервере
Функция ЗаполнитьПараметрыЖурналаСобытий(стрДанные,стрДанные2 = "",ВозможноОбъединение = Истина) Экспорт
	стПараметров = Новый Структура("Первый,Второй","","");
	Если СтрДлина(стрДанные) > 20 И ВозможноОбъединение Тогда
		стПараметров.Первый = Лев(стрДанные,19) + "@";	
		стПараметров.Второй = Сред(стрДанные,20);	
	Иначе
		стПараметров.Первый = стрДанные;
		стПараметров.Второй = стрДанные2;	
	КонецЕсли;	
	
	Возврат стПараметров
КонецФункции // ЗаполнитьПараметрыЖурналаСобытий() 

&НаСервере
Функция СоединитьПараметры(стПараметров,ОдинПараметр = Неопределено) Экспорт
	Если СтрДлина(стПараметров.Первый) = 20 Тогда
		Если Прав(стПараметров.Первый,1) = "@" Тогда
			ОдинПараметр = Истина;
			Возврат Лев(стПараметров.Первый,19) + стПараметров.Второй;	
		КонецЕсли;
	КонецЕсли;
	ОдинПараметр = Ложь;	
	Возврат стПараметров.Первый; 
КонецФункции // СоединитьПараметры()

