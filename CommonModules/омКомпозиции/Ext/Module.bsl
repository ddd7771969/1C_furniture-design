
Функция ЗаполнитьКоличествоДнейКомпозиции(мКомпозиции) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК Ссылка,
		|	ИзделияКомплект.Родитель.Композиция КАК Композиция,
		|	ЕСТЬNULL(ДатыКомплекта.ДнейПКД, 0) КАК ДнейПКД,
		|	ЕСТЬNULL(ДатыКомплекта.НормаВремени_ПКД, 0) КАК НормаВремени_ПКД,
		|	ЕСТЬNULL(ДатыКомплекта.НормаДоработки_ПКД, 0) КАК НормаДоработки_ПКД,
		|	ЕСТЬNULL(ДанныеКомплектаСрезПоследних.Конструктор, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)) КАК Конструктор,
		|	ЕСТЬNULL(ДатыКомплекта.НачалоПКД, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоПКД,
		|	ЕСТЬNULL(ДатыКомплекта.ОкончаниеПКД, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеПКД,
		|	ИзделияКомплект.Проект КАК Проект
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|		ПО ((ВЫРАЗИТЬ(ДатыКомплекта.Комплект КАК Справочник.ИзделияКомплект)) = ИзделияКомплект.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних КАК ДанныеКомплектаСрезПоследних
		|		ПО ((ВЫРАЗИТЬ(ДанныеКомплектаСрезПоследних.Комплект КАК Справочник.ИзделияКомплект)) = ИзделияКомплект.Ссылка)
		|ГДЕ
		|	ИзделияКомплект.Родитель.Композиция В(&Композиция)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыКомплекта.ДнейПКД КАК ДнейПКД,
		|	ДатыКомплекта.НачалоПКД КАК НачалоПКД,
		|	ДатыКомплекта.НормаВремени_ПКД КАК НормаВремени_ПКД,
		|	ДатыКомплекта.НормаДоработки_ПКД КАК НормаДоработки_ПКД,
		|	ДатыКомплекта.Комплект КАК Композиция
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|ГДЕ
		|	ВЫРАЗИТЬ(ДатыКомплекта.Комплект КАК Справочник.Композиции) В (&Композиция)";
	
	Запрос.УстановитьПараметр("Композиция", мКомпозиции);
	
	РезультатЗапроса 			= Запрос.ВыполнитьПакет();
	тзДанные 					= РезультатЗапроса[0].Выгрузить();
	тзТекущиеДанныеКомпозиции 	= РезультатЗапроса[1].Выгрузить();
	

	мКомпозиции = омТЧ_НаКлиентеСервере.КолонкуВМассив(тзДанные, "Композиция"); 
	
	стОтбор = Новый Структура("Композиция", );
	ПустаяДата = Дата(1, 1, 1);
	
	
	Для каждого ТекущаяКомпозиция Из мКомпозиции Цикл
	
		ДнейПКД 			= 0;
		НормаДоработки_ПКД 	= 0;
		НормаВремени_ПКД 	= 0;
		стОтбор.Композиция 	= ТекущаяКомпозиция;
		НачалоПКД 			= ПустаяДата;
		Проект				= Справочники.Проекты.ПустаяСсылка();
		
		Для каждого текДанные Из тзДанные.НайтиСтроки(стОтбор) Цикл
		
			ДнейПКД 			= ДнейПКД 				+ текДанные.ДнейПКД;				
			НормаДоработки_ПКД 	= НормаДоработки_ПКД 	+ текДанные.НормаДоработки_ПКД;				
			НормаВремени_ПКД 	= НормаВремени_ПКД 		+ текДанные.НормаВремени_ПКД;
			Проект				= текДанные.Проект;
			
			Если НЕ текДанные.НачалоПКД = ПустаяДата Тогда
			
				Если НачалоПКД = ПустаяДата Тогда
				
					НачалоПКД = текДанные.НачалоПКД;	
				
				ИначеЕсли НачалоПКД > текДанные.НачалоПКД Тогда
				
					НачалоПКД = текДанные.НачалоПКД;	
				
				КонецЕсли;	
			
			КонецЕсли;
		
		КонецЦикла;
		
		НайденаяСтрока = тзТекущиеДанныеКомпозиции.Найти(текДанные.Композиция, "Композиция");
		
		ТребуетсяИзменения = Истина;
		
		Если НЕ НайденаяСтрока = Неопределено Тогда
		
			Если ДнейПКД = НайденаяСтрока.ДнейПКД 
				И НормаДоработки_ПКД = НайденаяСтрока.НормаДоработки_ПКД 
				И НормаВремени_ПКД = НайденаяСтрока.НормаВремени_ПКД 
				И НачалоПКД = НайденаяСтрока.НачалоПКД Тогда
			
				ТребуетсяИзменения = Ложь;
			
			КонецЕсли;	
		
		КонецЕсли; 
		
		Если ТребуетсяИзменения Тогда 
			
		
			НаборЗаписей = РегистрыСведений.ДатыКомплекта.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Проект.Установить(Проект);
			НаборЗаписей.Отбор.Комплект.Установить(текДанные.Композиция);
			НоваяЗапись = НаборЗаписей.Добавить();
			
			НоваяЗапись.Проект 				= Проект;
			НоваяЗапись.Комплект 			= текДанные.Композиция;
			НоваяЗапись.ДнейПКД 			= ?(ДнейПКД 			= 0, 1, ДнейПКД);
			НоваяЗапись.НормаДоработки_ПКД 	= ?(НормаДоработки_ПКД 	= 0, 1, НормаДоработки_ПКД);
			НоваяЗапись.НормаВремени_ПКД 	= ?(НормаВремени_ПКД 	= 0, 1, НормаВремени_ПКД);
			НоваяЗапись.НачалоПКД 			= НачалоПКД;
			
			Если НЕ НачалоПКД = ПустаяДата Тогда
				НоваяЗапись.ОкончаниеПКД = омДаты.ДобавитьДниСУчетомВыходных(НачалоПКД, НоваяЗапись.ДнейПКД);
			КонецЕсли;
			
			НаборЗаписей.ДополнительныеСвойства.Вставить("УжеРасчитаннаяКомпозиция", текДанные.Композиция);
			
		    НаборЗаписей.Записать();
			
		КонецЕсли;
	
	КонецЦикла;
	
	
КонецФункции // ПолучитКоличествоДнейКомпозиции()
