
#Область Общее

Функция ИнформацияОПриложении()
	
	Результат = Новый Структура;
	
	//Результат.Вставить("КодПриложения"			, "app.5c924250175064.25846993");              //Приложение бэкофиса
	//Результат.Вставить("СекретныйКодПриложения"	, "WMybJCuyBXXON42V6VyVfT8PF65oMYDBCQz3QP6y7RDc9o5qSe");
	
	//Результат.Вставить("КодПриложения"			, "app.552d288cc83c88.78059741");             //Синхронизация
	//Результат.Вставить("СекретныйКодПриложения"	, "a49b3d28c435cec6ea7d8ba53cd9c9be");
	
	Результат.Вставить("КодПриложения"			, "app.60671a49a3ea13.12951119");             
	Результат.Вставить("СекретныйКодПриложения"	, "jmfeaApP01e61AfIhpgKDnPgD1BimrowXtFR7slj39IERWslbV");
	
	Результат.Вставить("АдресСайта"				, "https://oauth.bitrix.info");
	Результат.Вставить("ОбластьВидимости"		, "user,department,log,sonet_group");
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьАдресДоПриложенияБитрикс24() Экспорт
	
	Возврат "https://integration-ms1.bitrix.info/app/1ctotal/v4/index.php";    
	
КонецФункции

Функция ПолучитьАдресДоРоботаПриложенияБитрикс24() Экспорт

	Возврат "https://integration-ms1.bitrix.info/app/1ctotal/v4/handler.php";
	
КонецФункции

Функция ПолучитьАдресДоХэндлераСработкиОфлайнСобытияПриложенияБитрикс24() Экспорт
	
	Возврат "https://integration-ms1.bitrix.info/app/1ctotal/v4/event.php"; 

КонецФункции


Функция ПолучитьДанныеИзВнешнегоАдреса(АдресРесурса, ИмяФайла = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Соединение = УстановитьСоединениеССервером();
	Если Соединение = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.АдресРесурса = АдресРесурса;
	
	Попытка   
		Результат = Соединение.Получить(HTTPЗапрос, ИмяФайла);
	Исключение
		ОбщегоНазначения.СообщитьПользователю(ИнформацияОбОшибке().Описание);
	КонецПопытки;
	
	Возврат Результат;	
	
КонецФункции

Функция ОтправкаДанныхНаПортал(СтруктураНастроек, АдресРесурса, ТелоЗапроса, ПредОбработкаОшибок = Истина) Экспорт
	
	Если Лев(ТелоЗапроса, 1) <> "&" И СтрДлина(ТелоЗапроса) > 0 Тогда ТелоЗапроса = "&" + ТелоЗапроса КонецЕсли;
	
	Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		
		Соединение = УстановитьСоединениеССервером(СтруктураНастроек.НастройкиПодключения);
		Если Соединение = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ПараметрВременногоТокена 	= "FirstParam=first&auth=" + СтруктураНастроек.НастройкиПодключенияАвторизации.ВременныйТокен;
		                                        
		ПараметрКоннектора = "&" + СтруктураНастроек.ПараметрКоннектора; 
		
		ПустойПараметр = "&LastParam=last";    // 1С может резать символы, поэтому добавляем параметр, который можно спокойно резать.
		
		ТелоHTTPЗапроса = ПараметрВременногоТокена + ПараметрКоннектора + ТелоЗапроса + ПустойПараметр;
		
		HTTPЗапрос = Новый HTTPЗапрос;
		HTTPЗапрос.АдресРесурса = АдресРесурса;
			
	Иначе
	
		Соединение = УстановитьСоединениеССервером();
		Если Соединение = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		АдресПолучателяЗапроса  = Лев(СтруктураНастроек.НастройкиПодключения.АдресСайта, СтрДлина(СтруктураНастроек.НастройкиПодключения.АдресСайта)-6);
		ПараметрТокен		= "auth=" + СтруктураНастроек.НастройкиПодключенияАвторизации.ВременныйТокен;
		ПараметрКоннектора 	= СтруктураНастроек.ПараметрКоннектора; 
		
		ПараметрТело  		= "dataexchange="+ЗакодироватьСтрокуСервер(АдресПолучателяЗапроса + АдресРесурса + "?" + ПараметрТокен + "&" + ПараметрКоннектора + ТелоЗапроса);
		
		ПараметрНачало 		= "FirstParam=first";
		ПараметрКонец 		= "LastParam=last";

		ТелоHTTPЗапроса = ПараметрНачало + "&" + ПараметрТело + "&" + ПараметрКонец;
		
		HTTPЗапрос = Новый HTTPЗапрос;
		HTTPЗапрос.АдресРесурса = "";
	
	КонецЕсли;	
	
	HTTPЗапрос.Заголовки.Вставить("Content-Type"	,"application/x-www-form-urlencoded; charset=utf-8");
	HTTPЗапрос.Заголовки.Вставить("Content-Length"	, Формат(СтрДлина(ТелоHTTPЗапроса), "ЧГ=0"));
	
	HTTPЗапрос.УстановитьТелоИзСтроки(ТелоHTTPЗапроса);
	
	ТелоЗапросаДляЛога = РаскодироватьСтрокуСервер(РаскодироватьСтрокуСервер(ТелоHTTPЗапроса));
	Если СтруктураНастроек.Операция = "КартинкиФайлыПредложений" ИЛИ СтруктураНастроек.Операция = "КартинкиФайлыТоваров" Тогда
		ТелоЗапросаДляЛога = Лев(ТелоЗапросаДляЛога, 300) + "..."; 
	КонецЕсли;  
	
	Попытка   
		
		Если СтруктураНастроек.ОбщиеНастройки.РежимОтладки Тогда    
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Отправка запроса по методу: " + СтруктураНастроек.Портал + АдресРесурса);  
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Тело запроса HTTP запроса: " + ТелоЗапросаДляЛога);
			
		КонецЕсли;
		
		Ответ 			= Соединение.ОтправитьДляОбработки(HTTPЗапрос);	
		ОтветСтрокой 	= Ответ.ПолучитьТелоКакСтроку();            
		
		ОтветСПорталаДляЛога = РаскодироватьJSON(ОтветСтрокой); 
		Если СтруктураНастроек.Операция = "КартинкиФайлыПредложений" ИЛИ СтруктураНастроек.Операция = "КартинкиФайлыТоваров" Тогда
			ОтветСПорталаДляЛога = Лев(ОтветСПорталаДляЛога, 300) + "..."; 
		КонецЕсли;   
		
		Если СтруктураНастроек.ОбщиеНастройки.РежимОтладки Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Ответ с портала: " + ОтветСПорталаДляЛога);
		КонецЕсли;
		
		Если Ответ.КодСостояния = 403 Тогда
			
			Если СтрНайти(ОтветСтрокой, "WRONG_LICENSE") > 0 Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ПолучениеОшибкиПоЛицензииБитрикс24());
			Иначе
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Ключ, который указывали в модуле обмена 1С нужно получать под администратором портала");
			КонецЕсли;
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		Если СтрНайти(ОтветСтрокой, "Unable to get application by token") > 0 Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ОтветСтрокой);
			Возврат Неопределено;
		КонецЕсли;
		
		Если Ответ.КодСостояния = 503 Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Превышено количество запросов в секунду. Синхронизация встанет в паузу на 10 секунд");
			Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(10);    // для того, чтобы почистилась очередь.
			ОчередьЗапросовЗабита = Истина;	
		Иначе
			ОчередьЗапросовЗабита = Ложь;	
		КонецЕсли;
		
		ТокенУстарел = ПроверитьНаАктуальностьВременныйТокен(СтруктураНастроек, ОтветСтрокой);
		
		Если ТокенУстарел ИЛИ ОчередьЗапросовЗабита Тогда
			Возврат ОтправкаДанныхНаПортал(СтруктураНастроек, АдресРесурса, ТелоЗапроса);
		КонецЕсли;
		
	Исключение
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ИнформацияОбОшибке().Описание);
		
		Возврат Неопределено;
	КонецПопытки;
	
	Попытка
		
		Если ОтветСтрокой = "" Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		лСтруктураОтвета = ПрочитатьJSONНаСервере(ОтветСтрокой, Истина);
		
		Если НЕ ПредОбработкаОшибок Тогда
			Возврат лСтруктураОтвета;  
		КонецЕсли;
		
		error 				= лСтруктураОтвета.Получить("error");
		error_description 	= лСтруктураОтвета.Получить("error_description");
		
		Если error_description = "Unable to set event handler: Handler already binded" ИЛИ error_description = "Unable to set placement handler: Handler already binded" Тогда
			Возврат лСтруктураОтвета;
		КонецЕсли;
		
		Если error = "ERROR_TRANSFORMATION" Тогда
			Возврат Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(error) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Ошибки при отправлении на портал: " + Строка(error));
			Если ЗначениеЗаполнено(error_description) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Ошибки при отправлении на портал: " + Строка(error_description));
			КонецЕсли;	
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ТелоЗапросаДляЛога);
			
			Возврат Неопределено;
			
		КонецЕсли;
		
	Исключение           
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось разобрать JSON");
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Пришедший JSON: " + ОтветСПорталаДляЛога);
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Отправленный запрос на портал: " + ТелоЗапросаДляЛога);
		
		ЗаписатьНовыйТокен(СтруктураНастроек.НастройкаПодключения, СтруктураНастроек.НастройкиПодключенияАвторизации.НовыйТокен);
		
		Возврат Неопределено;
		
	КонецПопытки;
	
	ЗаписатьНовыйТокен(СтруктураНастроек.НастройкаПодключения, СтруктураНастроек.НастройкиПодключенияАвторизации.НовыйТокен);
	
	Возврат лСтруктураОтвета;
	
КонецФункции  

Функция ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоЗапроса) Экспорт
	
	Если Лев(ТелоЗапроса, 1) <> "&" И СтрДлина(ТелоЗапроса) > 0 Тогда ТелоЗапроса = "&" + ТелоЗапроса КонецЕсли;
	
	Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
			
		Соединение = УстановитьСоединениеССервером(СтруктураНастроек.НастройкиПодключения);
		Если Соединение = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ПараметрВременногоТокена = "FirstParam=first&auth="		+ СтруктураНастроек.НастройкиПодключенияАвторизации.ВременныйТокен;
		
		ПараметрКоннектора = "&" + СтруктураНастроек.ПараметрКоннектора; 
		
		ПустойПараметр = "&LastParam=last";    // 1С может резать символы, поэтому добавляем параметр, который можно спокойно резать.
		
		HTTPЗапрос = Новый HTTPЗапрос;
		HTTPЗапрос.АдресРесурса = "/rest/batch";
		
		ТелоHTTPЗапроса = ПараметрВременногоТокена + ПараметрКоннектора + ТелоЗапроса + ПустойПараметр;
		
	Иначе
		
		Соединение = УстановитьСоединениеССервером();
		Если Соединение = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		АдресПолучателяЗапроса  = Лев(СтруктураНастроек.НастройкиПодключения.АдресСайта, СтрДлина(СтруктураНастроек.НастройкиПодключения.АдресСайта)-6);
		ПараметрТокен		= "auth=" + СтруктураНастроек.НастройкиПодключенияАвторизации.ВременныйТокен;
		ПараметрКоннектора	= СтруктураНастроек.ПараметрКоннектора; 
		
		ПараметрТело  		= "dataexchange="+ЗакодироватьСтрокуСервер(АдресПолучателяЗапроса + "/rest/batch" + "?" + ПараметрТокен + "&" + ПараметрКоннектора + ТелоЗапроса);
		ПараметрНачало 		= "FirstParam=first";
		ПараметрКонец 		= "LastParam=last";

		ТелоHTTPЗапроса = ПараметрНачало + "&" +ПараметрТело + "&" + ПараметрКонец;
		
		HTTPЗапрос = Новый HTTPЗапрос;
		HTTPЗапрос.АдресРесурса = "";
		
	КонецЕсли;
	
	HTTPЗапрос.Заголовки.Вставить("Content-Type"		,"application/x-www-form-urlencoded; charset=utf-8");
	HTTPЗапрос.Заголовки.Вставить("Content-Length"		, Формат(СтрДлина(ТелоHTTPЗапроса), "ЧГ=0"));
	
	HTTPЗапрос.УстановитьТелоИзСтроки(ТелоHTTPЗапроса);
	
	ТелоЗапросаДляЛога = РаскодироватьСтрокуСервер(РаскодироватьСтрокуСервер(ТелоHTTPЗапроса));
	Если СтруктураНастроек.Операция = "КартинкиФайлыПредложений" ИЛИ СтруктураНастроек.Операция = "КартинкиФайлыТоваров" Тогда
		ТелоЗапросаДляЛога = Лев(ТелоЗапросаДляЛога, 300) + "..."; 
	КонецЕсли;    
	
	Попытка   
		
		Если СтруктураНастроек.ОбщиеНастройки.РежимОтладки Тогда   
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Тело запроса HTTP запроса: " + ТелоЗапросаДляЛога);
		КонецЕсли;
		
		Ответ 			= Соединение.ОтправитьДляОбработки(HTTPЗапрос);	
		ОтветСтрокой 	= Ответ.ПолучитьТелоКакСтроку();
		
		ОтветСПорталаДляЛога = РаскодироватьJSON(ОтветСтрокой);
		Если СтруктураНастроек.Операция = "КартинкиФайлыПредложений" ИЛИ СтруктураНастроек.Операция = "КартинкиФайлыТоваров" Тогда
			ОтветСПорталаДляЛога = Лев(ОтветСПорталаДляЛога, 300) + "..."; 
		КонецЕсли;         
		
		Если СтруктураНастроек.ОбщиеНастройки.РежимОтладки Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Ответ с портала: " + ОтветСПорталаДляЛога);
		КонецЕсли;
		
		Если Ответ.КодСостояния = 403 Тогда
			
			Если СтрНайти(ОтветСтрокой, "WRONG_LICENSE") > 0 Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ПолучениеОшибкиПоЛицензииБитрикс24());
			Иначе
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Ключ, который указывали в модуле обмена 1С нужно получать под администратором портала");
			КонецЕсли;
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		Если Ответ.КодСостояния = 503 Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Превышено количество запросов в секунду. Синхронизация встанет в паузу на 10 секунд");
			Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(10);    // для того, чтобы почистилась очередь.
			ОчередьЗапросовЗабита = Истина;	
		Иначе
			ОчередьЗапросовЗабита = Ложь;	
		КонецЕсли;
		
		ТокенУстарел = ПроверитьНаАктуальностьВременныйТокен(СтруктураНастроек, ОтветСтрокой);
		
		Если ТокенУстарел ИЛИ ОчередьЗапросовЗабита Тогда
			Возврат ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоЗапроса);
		КонецЕсли;
		
	Исключение
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ИнформацияОбОшибке().Описание);
		Возврат Неопределено;
	КонецПопытки;
	
	лСоответствиеОтвета = Неопределено;
	
	Попытка
		
		Если ОтветСтрокой = "" Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		лСоответствиеОтвета = ПрочитатьJSONНаСервере(ОтветСтрокой, Истина);
		
		Если лСоответствиеОтвета.Получить("result") = Неопределено Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось получить результат о выполнении");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Пришедший JSON: " + ОтветСПорталаДляЛога);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Отправленный запрос на портал: " + РаскодироватьСтрокуСервер(ТелоЗапросаДляЛога));
			
			ЗаписатьНовыйТокен(СтруктураНастроек.НастройкаПодключения, СтруктураНастроек.НастройкиПодключенияАвторизации.НовыйТокен);
			
			Возврат Неопределено;
		КонецЕсли;
		
	Исключение                     
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось разобрать JSON");
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Пришедший JSON: " + ОтветСПорталаДляЛога);
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Отправленный запрос на портал: " + РаскодироватьСтрокуСервер(ТелоЗапросаДляЛога));
		
		ЗаписатьНовыйТокен(СтруктураНастроек.НастройкаПодключения, СтруктураНастроек.НастройкиПодключенияАвторизации.НовыйТокен);
		
		Возврат Неопределено;
		
	КонецПопытки;
	
	ЗаписатьНовыйТокен(СтруктураНастроек.НастройкаПодключения, СтруктураНастроек.НастройкиПодключенияАвторизации.НовыйТокен);
	
	Возврат лСоответствиеОтвета;
	
КонецФункции    

Функция ОтправитьЗапросаНаВнешнийСервис(АдресЗапросаНаВнешнийСервис) Экспорт
	
	Результат = Истина;
	
	Попытка
		
		Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
			
			НастройкиПодключения = Новый Структура;
			РазобратьАдресСайта(АдресЗапросаНаВнешнийСервис, НастройкиПодключения);
			
			Соединение = УстановитьСоединениеССервером(НастройкиПодключения);
			Если Соединение = Неопределено Тогда
				Возврат Неопределено;
			КонецЕсли;
			
			HTTPЗапрос = Новый HTTPЗапрос;
			HTTPЗапрос.АдресРесурса = НастройкиПодключения.АдресСкрипта;
			
		Иначе
			
			Соединение = УстановитьСоединениеССервером();
			Если Соединение = Неопределено Тогда
				Возврат Неопределено;
			КонецЕсли;
			
			ПараметрТело  		= "dataexchange="+ЗакодироватьСтрокуСервер(АдресЗапросаНаВнешнийСервис);
			
			ПараметрНачало 		= "FirstParam=first";
			ПараметрКонец 		= "LastParam=last";
			
			ТелоHTTPЗапроса = ПараметрНачало + "&" + ПараметрТело + "&" + ПараметрКонец;
			
			HTTPЗапрос = Новый HTTPЗапрос;
			HTTPЗапрос.АдресРесурса = "";
			
			HTTPЗапрос.УстановитьТелоИзСтроки(ТелоHTTPЗапроса);
			
		КонецЕсли;	
		
		HTTPЗапрос.Заголовки.Вставить("Content-Type"	,"application/x-www-form-urlencoded; charset=utf-8");
		
	Исключение
		Соединение = Неопределено;
		Результат = Ложь;
	КонецПопытки;
	
	Возврат Результат;	
	
КонецФункции

Функция ПроверитьНаАктуальностьВременныйТокен(СтруктураНастроек, ОтветСтрокой)
	
	Если СтрНайти(НРег(ОтветСтрокой), "expired_token") > 0 Тогда
		
		Если ПолучитьТокенДляСоединения(СтруктураНастроек) Тогда
			Возврат Истина;
		Иначе
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ОтветСтрокой);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось получить временный ключ соединения. Выгрузка пакета невозможна.");
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтрНайти(НРег(ОтветСтрокой), "invalid_token") > 0 Тогда
		
		Если ПолучитьТокенДляСоединения(СтруктураНастроек) Тогда
			Возврат Истина;
		Иначе
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ОтветСтрокой);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось получить временный ключ соединения. Выгрузка пакета невозможна.");
		КонецЕсли;
		
	КонецЕсли;
	
	
	Если СтрНайти(НРег(ОтветСтрокой), "The access token provided has expired") > 0 Тогда
		
		Если ПолучитьТокенДляСоединения(СтруктураНастроек) Тогда
			Возврат Истина;
		Иначе
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ОтветСтрокой);
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось получить временный ключ соединения. Выгрузка пакета невозможна.");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучениеОшибкиПоЛицензииБитрикс24()
	
	Возврат "Данный функционал недоступен на текущем тарифном плане.";
	
КонецФункции

Функция ПрочитатьJSONНаСервере(JSON, ПрочитатьВСоответствие) Экспорт
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(JSON);
	
	Возврат ПрочитатьJSON(ЧтениеJSON, ПрочитатьВСоответствие);
	
КонецФункции

Функция УстановитьСоединениеССервером(НастройкиСоединения = Неопределено) Экспорт   
	
	Если НастройкиСоединения = Неопределено Тогда
		НастройкиСоединения = Новый Структура;
		РазобратьАдресСайта("https://1cproxy.bitrix24.ru/", НастройкиСоединения);
	КонецЕсли;
	
	Соединение = Неопределено;
	
	Попытка
		
		Если НастройкиСоединения.Сервер = "" Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Если НЕ НастройкиСоединения.ЗащищенноеСоединение Тогда
			ssl = Неопределено;
		Иначе
			
			ssl = Б24_К_ОбщегоНазначенияВызовСервера.НовоеЗащищенноеСоединение();   
			
		КонецЕсли;
		
		Прокси = Неопределено;
 		Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси(?(НастройкиСоединения.ЗащищенноеСоединение, "HTTPS", "HTTP"));	
	
		Соединение = Новый HTTPСоединение(НастройкиСоединения.Сервер, НастройкиСоединения.Порт,,, Прокси, 43200, ssl);
		
	Исключение
		
		лТекстОшибки = НСтр("ru = 'Не удалось установить соединение с сервером'") + НастройкиСоединения.Сервер + ":" + Строка(НастройкиСоединения.Порт) 
		+ НСтр("ru = '.Проверьте правильность токена.'");
		
		ОбщегоНазначения.СообщитьПользователю(лТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Соединение = Неопределено;
		
	КонецПопытки;
	
	Возврат Соединение;
	
КонецФункции

Функция РазобратьАдресСайта(Знач АдресСайта, НастройкиПодключения)Экспорт 
	
	АдресСайта = СокрЛП(АдресСайта); 
	
	Сервер		 		 = ""; 
	Порт				 = 0;
	АдресСкрипта 		 = "";
	ЗащищенноеСоединение = Ложь;
	
	Если НЕ ПустаяСтрока(АдресСайта) Тогда
		
		АдресСайта = СтрЗаменить(АдресСайта, "\", "/");
		АдресСайта = СтрЗаменить(АдресСайта, " ", "");
		
		Если НРег(Лев(АдресСайта, 7)) = "http://" Тогда
			
			АдресСайта = Сред(АдресСайта, 8);
			
		ИначеЕсли НРег(Лев(АдресСайта, 8)) = "https://" Тогда
			
			АдресСайта = Сред(АдресСайта, 9); 
			ЗащищенноеСоединение = Истина;
			
		КонецЕсли;
		
		ПозицияСлэша = Найти(АдресСайта, "/");
		
		Если ПозицияСлэша > 0 Тогда
			
			Сервер 		 = Лев(АдресСайта, ПозицияСлэша - 1);	
			АдресСкрипта = Прав(АдресСайта, СтрДлина(АдресСайта) - ПозицияСлэша);
			
		Иначе
			
			Сервер 		 = АдресСайта;
			АдресСкрипта = "";
			
		КонецЕсли;
		
		ПозицияДвоеточия = Найти(Сервер, ":");
		ПортСтрока = "0";
		
		Если ПозицияДвоеточия > 0 Тогда
			
			СерверСПортом = Сервер;
			Сервер		  = Лев(СерверСПортом, ПозицияДвоеточия - 1);
			ПортСтрока 	  = Прав(СерверСПортом, СтрДлина(СерверСПортом) - ПозицияДвоеточия);
			
		КонецЕсли;
		
		Попытка
			
			Порт = Число(ПортСтрока);
			
		Исключение
			
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось получить номер порта ('") + ПортСтрока + ")"
			+ Символы.ПС + НСтр("ru = 'Проверьте правильность ввода адреса сайта.'"));
			
			Возврат Ложь;
			
		КонецПопытки;
		
		Если Порт = 0 Тогда
			
			Порт = ?(ЗащищенноеСоединение, 443, 80);
			
		КонецЕсли;
		
	КонецЕсли;
	
	НастройкиПодключения.Вставить("Сервер"	  				, Сервер); 
	НастройкиПодключения.Вставить("Порт"		   			, Порт);
	НастройкиПодключения.Вставить("АдресСкрипта"			, АдресСкрипта);
	НастройкиПодключения.Вставить("ЗащищенноеСоединение"	, ЗащищенноеСоединение);
	
	Если НЕ НастройкиПодключения.Свойство("АдресСайта") Тогда
		АдресСайта = ?(ЗащищенноеСоединение, "https://", "http://") + Сервер;
		НастройкиПодключения.Вставить("АдресСайта", АдресСайта);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьНастройкиПодключения(СтруктураНастроек) Экспорт
	
	НастройкиПодключения = Новый Структура;
	лАдрес = СтруктураНастроек.НастройкиПодключения.АдресСайта;	

	НастройкиПодключения.Вставить("АдресСайта", лАдрес);
	
	Если НЕ РазобратьАдресСайта(лАдрес, НастройкиПодключения) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат НастройкиПодключения;
	
КонецФункции

Функция ДобавлениеНастроекАвторизации(СтруктураНастроек) Экспорт
	
	Если НЕ ПолучитьНастройкиАвторизации(СтруктураНастроек) Тогда
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Ошибка при получении параметров подключения к сайту авторизации. Выгрузка отчетов невозможна.");
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьНастройкиАвторизации(ОсновныеНастройки)
	
	НастройкиПодключения = Новый Структура;
	
	ИнформацияОПриложении = ИнформацияОПриложении();
	
	НастройкиПодключения.Вставить("КодПриложения"			, ИнформацияОПриложении.КодПриложения);
	НастройкиПодключения.Вставить("СекретныйКодПриложения"	, ИнформацияОПриложении.СекретныйКодПриложения);
	НастройкиПодключения.Вставить("ОбластьВидимости"		, ИнформацияОПриложении.ОбластьВидимости);
	НастройкиПодключения.Вставить("АдресСайта"				, ИнформацияОПриложении.АдресСайта);
	
	НастройкиПодключения.Вставить("Токен"					, "");
	НастройкиПодключения.Вставить("ВременныйТокен"			, "");
	НастройкиПодключения.Вставить("НовыйТокен"				, "");
	
	Если НЕ РазобратьАдресСайта(ИнформацияОПриложении.АдресСайта, НастройкиПодключения) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	ОсновныеНастройки.Вставить("НастройкиПодключенияАвторизации", НастройкиПодключения);
	
	
	НастройкиПодключения = Новый Структура;
	НастройкиПодключения.Вставить("АдресСайта", "");
	ОсновныеНастройки.Вставить("НастройкиПодключения", НастройкиПодключения);
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, Метод, ИмяКолонкиИд, ТелоЗапроса = Неопределено, ПодчиненныйКлючРезультата = Неопределено) Экспорт
	
	Результат = Новый Массив;
	
	Ид = 0;
	Если Лев(ТелоЗапроса, 1) <> "&" И СтрДлина(ТелоЗапроса) > 0 Тогда ТелоЗапроса = "&" + ТелоЗапроса КонецЕсли;
	
	Пока Истина Цикл
		
		ПараметрыЗапроса = "&start=-1&order["+ИмяКолонкиИд+"]=ASC&filter[>"+ИмяКолонкиИд+"]="+Строка(Ид) + ТелоЗапроса;
		
		ДанныеПортала = ОтправкаДанныхНаПортал(СтруктураНастроек, Метод, ПараметрыЗапроса); 
		Если ДанныеПортала = Неопределено Тогда
			Возврат Результат;
		КонецЕсли;

		result = ДанныеПортала.Получить("result");
		Если result = Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
		
		Если ПодчиненныйКлючРезультата = Неопределено Тогда
			МассивДанных = result;	
		Иначе
			МассивДанных = result.Получить(ПодчиненныйКлючРезультата);	
		КонецЕсли;
		
		Если типЗнч(МассивДанных) <> Тип("Массив") Тогда
			Возврат Результат;
		КонецЕсли;
		
		Если МассивДанных.Количество() = 0 Тогда
			Возврат Результат;
		КонецЕсли;
		
		Для каждого ТекЭлемент Из МассивДанных Цикл
			Результат.Добавить(ТекЭлемент);
			Ид = Формат(ТекЭлемент.Получить(ИмяКолонкиИд), "ЧГ=0"); 
		КонецЦикла;
	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьМассивСтатусовCRM(СтруктураНастроек,  ТелоHTTPЗапроса = "") Экспорт

	Результат = Новый Массив;
	
	лТелоHTTPЗапроса = ТелоHTTPЗапроса;
	СтруктураОтвета = ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.status.list", лТелоHTTPЗапроса); 
	Если СтруктураОтвета = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	Если result = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;

	Для каждого ТекЭлемент Из result Цикл
		Результат.Добавить(ТекЭлемент);	
	КонецЦикла;
	
	Следующие = СтруктураОтвета.Получить("next"); 
	Пока ЗначениеЗаполнено(Следующие) Цикл
		
		Следующие = Формат(Следующие, "ЧГ=0");
		
		лТелоHTTPЗапроса = ТелоHTTPЗапроса + "&start="+Следующие;	
		СтруктураОтвета = ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.status.list", лТелоHTTPЗапроса); 
		Если СтруктураОтвета = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		result = СтруктураОтвета.Получить("result");
		Если result = Неопределено Тогда
			Прервать;
		КонецЕсли;

		Для каждого ТекЭлемент Из result Цикл
			Результат.Добавить(ТекЭлемент);	
		КонецЦикла;
		
		Следующие = СтруктураОтвета.Получить("next");
		
	КонецЦикла;

	Возврат Результат;
	
КонецФункции  

Функция РеалтаймИспользуется(НастройкаПодключения) Экспорт
	
	РеалТаймС = Истина;
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп");   
		НастройкиС = Модуль.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);         
		РеалТаймС = НастройкиС.СпособСинхронизацииДанных = "ВРежимеРеальногоВремени";
	КонецЕсли;
	
	РеалТаймСМ = Истина;
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп");   
		НастройкиСМ	= Модуль.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);         
		РеалТаймСМ = НастройкиСМ.СпособСинхронизацииДанных = "ВРежимеРеальногоВремени";
	КонецЕсли;
	
	РеалТаймСУ = Истина;
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("Б24_СУ_ОбщегоНазначенияВызовСервераПовтИсп");   
		НастройкиСУХЗ = Модуль.ПолучитьНастройкуПоНастройкеПодключения(НастройкаПодключения);         
		НастройкиСУ = НастройкиСУХЗ.Настройки.Получить();
		РеалТаймСУ = НастройкиСУ.НастройкииЗаказов.РезервироватьТовары;
	КонецЕсли;
				
	Результат = РеалТаймС ИЛИ РеалТаймСМ ИЛИ РеалТаймСУ;	
	                                                                        
	Возврат Результат;                                             
	
КонецФункции

#КонецОбласти


#Область РаботаСТокенами

Функция ПолучитьТокенДляСоединения(СтруктураНастроек, СохранятьТокен = Истина, Токен = "") Экспорт
	
	Если Токен = "" Тогда
		лТокен = ПолучитьТокен(СтруктураНастроек.НастройкаПодключения);	
	Иначе
		лТокен = Токен;
	КонецЕсли;
	
	Соединение = УстановитьСоединениеССервером(СтруктураНастроек.НастройкиПодключенияАвторизации);
	
	Если Соединение = Неопределено Тогда
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Авторизация не выполнена");
		
		Возврат Ложь;
		
	КонецЕсли;
	
	ПараметрТело  		= "grant_type=refresh_token&client_id=" + СтруктураНастроек.НастройкиПодключенияАвторизации.КодПриложения + "&client_secret=" + СтруктураНастроек.НастройкиПодключенияАвторизации.СекретныйКодПриложения + "&refresh_token=" + лТокен;
	ПараметрНачало 		= "FirstParam=first";
	ПараметрКонец 		= "LastParam=last";

	ТелоHTTPЗапроса = ПараметрНачало + "&" +ПараметрТело + "&" + ПараметрКонец;
	
	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.АдресРесурса = "/oauth/token/";
	
	HTTPЗапрос.Заголовки.Вставить("Content-Type"	,"application/x-www-form-urlencoded; charset=utf-8");
	HTTPЗапрос.Заголовки.Вставить("Content-Length"	, Формат(СтрДлина(ТелоHTTPЗапроса), "ЧГ=0"));
	
	HTTPЗапрос.УстановитьТелоИзСтроки(ТелоHTTPЗапроса);
	
	Попытка   
		
		HTTPОтвет = Соединение.ОтправитьДляОбработки(HTTPЗапрос);	
		
		ОтветСтрокой 	= HTTPОтвет.ПолучитьТелоКакСтроку();
		
		сОтвета = ПрочитатьJSONНаСервере(ОтветСтрокой, Ложь);
		
		Если сОтвета.Свойство("error") = истина Тогда
			
			Если сОтвета.error = "invalid_grant" Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось произвести авторизацию. Некорректен постоянный ключ авторизации.");
			КонецЕсли;
			
			Возврат Ложь;
			
		Иначе
			
			//ОбщиеНастройки.НастройкиПодключения.АдресСайта = СокрЛП(сОтвета.client_endpoint);   //костыль
			
			Если НЕ ЗначениеЗаполнено(СтруктураНастроек.Портал) Тогда
				СтруктураНастроек.НастройкиПодключения.АдресСайта = СокрЛП(сОтвета.client_endpoint);
			Иначе
			
				Если Лев(сОтвета.client_endpoint,8) = "https://" Тогда
					СтруктураНастроек.НастройкиПодключения.АдресСайта = СокрЛП(Лев(сОтвета.client_endpoint,8) + СтруктураНастроек.Портал + "/rest/");
				Иначе
					СтруктураНастроек.НастройкиПодключения.АдресСайта = СокрЛП(Лев(сОтвета.client_endpoint,7) + СтруктураНастроек.Портал + "/rest/");
				КонецЕсли;
				
			КонецЕсли;
			
			СтруктураНастроек.НастройкиПодключенияАвторизации.ВременныйТокен 	= СокрЛП(сОтвета.access_token);
			СтруктураНастроек.НастройкиПодключенияАвторизации.НовыйТокен 		= СокрЛП(сОтвета.refresh_token);
			
			Если СохранятьТокен Тогда
				ЗаписатьНовыйТокен(СтруктураНастроек.НастройкаПодключения, СокрЛП(сОтвета.refresh_token));
			КонецЕсли;
		
		КонецЕсли;
		
	Исключение
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ОтветСтрокой);
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось произвести авторизацию. Возможно неверно указан постоянный ключ.");
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Процедура ЗаписатьНовыйТокен(НастройкаПодключения, НовыйТокен) Экспорт

	ТекущаяДата = ТекущаяДатаСеанса();
	
	ДанныеТокена = РегистрыСведений.Б24_К_ТокеныПодключений.Получить(Новый Структура("НастройкаПодключения",НастройкаПодключения));
	Если ДанныеТокена.ВремяПолученияТокена < ТекущаяДата - 5*60 Тогда 
	
		НоваяЗапись = РегистрыСведений.Б24_К_ТокеныПодключений.СоздатьМенеджерЗаписи();
		НоваяЗапись.НастройкаПодключения 	= НастройкаПодключения;
		НоваяЗапись.Токен 					= НовыйТокен; 
		
		НоваяЗапись.ВремяПолученияТокена 	= ТекущаяДата; 
	
		НоваяЗапись.Записать(Истина);
		
	КонецЕсли;	
	
КонецПроцедуры

Функция ПолучитьТокен(НастройкаПодключения) Экспорт
	
	Возврат РегистрыСведений.Б24_К_ТокеныПодключений.Получить(Новый Структура("НастройкаПодключения", НастройкаПодключения)).Токен;
	
КонецФункции

#КонецОбласти


#Область РаботаСКодировками

Функция РаскодироватьСтрокуСервер(ТелоHTTPЗапроса) Экспорт
	
	Возврат РаскодироватьСтроку(ТелоHTTPЗапроса,СпособКодированияСтроки.URLВКодировкеURL);	
	
КонецФункции

Функция ЗакодироватьСтрокуСервер(ЗначениеСтроки) Экспорт
	
	Возврат КодироватьСтроку(XMLСтрока(?(ТипЗнч(ЗначениеСтроки) = Тип("Булево"), Число(ЗначениеСтроки), ЗначениеСтроки)), СпособКодированияСтроки.КодировкаURL, "UTF8");
	
КонецФункции

Функция РаскодироватьJSON(URL) Экспорт
	
	Результат = URL; 
	
	СписокСимволов = Новый СписокЗначений;
	СписокСимволов.Добавить("\u0430", "а");
	СписокСимволов.Добавить("\u0431", "б");
	СписокСимволов.Добавить("\u0432", "в");
	СписокСимволов.Добавить("\u0433", "г");
	СписокСимволов.Добавить("\u0434", "д");
	СписокСимволов.Добавить("\u0435", "е");
	СписокСимволов.Добавить("\u0451", Символ(1105));
	СписокСимволов.Добавить("\u0436", "ж");
	СписокСимволов.Добавить("\u0437", "з");
	СписокСимволов.Добавить("\u0438", "и");
	СписокСимволов.Добавить("\u0439", "й");
	СписокСимволов.Добавить("\u043a", "к");
	СписокСимволов.Добавить("\u043b", "л");
	СписокСимволов.Добавить("\u043c", "м");
	СписокСимволов.Добавить("\u043d", "н");
	СписокСимволов.Добавить("\u043e", "о");
	СписокСимволов.Добавить("\u043f", "п");
	СписокСимволов.Добавить("\u0440", "р");
	СписокСимволов.Добавить("\u0441", "с");
	СписокСимволов.Добавить("\u0442", "т");
	СписокСимволов.Добавить("\u0443", "у");
	СписокСимволов.Добавить("\u0444", "ф");
	СписокСимволов.Добавить("\u0445", "х");
	СписокСимволов.Добавить("\u0446", "ц");
	СписокСимволов.Добавить("\u0447", "ч");
	СписокСимволов.Добавить("\u0448", "ш");
	СписокСимволов.Добавить("\u0449", "щ");
	СписокСимволов.Добавить("\u044a", "ъ");
	СписокСимволов.Добавить("\u044b", "ы");
	СписокСимволов.Добавить("\u044c", "ь");
	СписокСимволов.Добавить("\u044d", "э");
	СписокСимволов.Добавить("\u044e", "ю");
	СписокСимволов.Добавить("\u044f", "я");
	
	СписокСимволов.Добавить("\u0410", "А");
	СписокСимволов.Добавить("\u0411", "Б");
	СписокСимволов.Добавить("\u0412", "В");
	СписокСимволов.Добавить("\u0413", "Г");
	СписокСимволов.Добавить("\u0414", "Д");
	СписокСимволов.Добавить("\u0415", "Е");
	СписокСимволов.Добавить("\u0401", Символ(1025));
	СписокСимволов.Добавить("\u0416", "Ж");
	СписокСимволов.Добавить("\u0417", "З");
	СписокСимволов.Добавить("\u0418", "И");
	СписокСимволов.Добавить("\u0419", "Й");
	СписокСимволов.Добавить("\u041a", "К");
	СписокСимволов.Добавить("\u041b", "Л");
	СписокСимволов.Добавить("\u041c", "М");
	СписокСимволов.Добавить("\u041d", "Н");
	СписокСимволов.Добавить("\u041e", "О");
	СписокСимволов.Добавить("\u041f", "П");
	СписокСимволов.Добавить("\u0420", "Р");
	СписокСимволов.Добавить("\u0421", "С");
	СписокСимволов.Добавить("\u0422", "Т");
	СписокСимволов.Добавить("\u0423", "У");
	СписокСимволов.Добавить("\u0424", "Ф");
	СписокСимволов.Добавить("\u0425", "Х");
	СписокСимволов.Добавить("\u0426", "Ц");
	СписокСимволов.Добавить("\u0427", "Ч");
	СписокСимволов.Добавить("\u0428", "Ш");
	СписокСимволов.Добавить("\u0429", "Щ");
	СписокСимволов.Добавить("\u042a", "Ъ");
	СписокСимволов.Добавить("\u042b", "Ы");
	СписокСимволов.Добавить("\u042c", "Ь");
	СписокСимволов.Добавить("\u042d", "Э");
	СписокСимволов.Добавить("\u042e", "Ю");
	СписокСимволов.Добавить("\u042f", "Я");
	
	
	СписокСимволов.Добавить("\u0022", "'");
	
	СписокСимволов.Добавить("\u003E", ">");
	СписокСимволов.Добавить("\u003е", ">");
	
	СписокСимволов.Добавить("\u003C", "<");
	СписокСимволов.Добавить("\u003c", "<");
	
	Для Каждого текЭлемент Из СписокСимволов Цикл
		
		Результат = СтрЗаменить(Результат,текЭлемент.Значение, текЭлемент.Представление);	
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


#Область ОбластьLongPulling

Функция ПолучитьДанныеLongPulling(СтруктураНастроек, НастройкиПодключенияLongPulling, РазобранныйАдресХостинга, ПараметрыДляПродолженияМониторинга) Экспорт
	
	ДанныеСБитрикс24 =  Неопределено;
	
	clientId = "";
	НастройкиПодключенияLongPulling.Свойство("clientId", clientId);
	clientId = ?(ЗначениеЗаполнено(clientId), "&clientId=" + clientId, "");
	
	Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		
		Соединение = УстановитьСоединениеССервером(РазобранныйАдресХостинга);
		АдресРесурса = РазобранныйАдресХостинга.АдресСкрипта + "?CHANNEL_ID=" + НастройкиПодключенияLongPulling.ИдКанала + clientId + ПараметрыДляПродолженияМониторинга;;
		
	Иначе
		
		Соединение = УстановитьСоединениеССервером();
		
		АдресСайта = ?(РазобранныйАдресХостинга.ЗащищенноеСоединение, "https://", "http://") + РазобранныйАдресХостинга.Сервер;
		АдресРесурса = "?dataexchange="+ ЗакодироватьСтрокуСервер(АдресСайта + "/" + РазобранныйАдресХостинга.АдресСкрипта + "?CHANNEL_ID=" + НастройкиПодключенияLongPulling.ИдКанала + clientId + ПараметрыДляПродолженияМониторинга);
		
	КонецЕсли;
	
	Попытка   
		
		Если Соединение = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Отправка запроса по методу:" + АдресРесурса);
		
		HTTPЗапрос = Новый HTTPЗапрос;
		HTTPЗапрос.АдресРесурса = АдресРесурса;		
		
		Ответ = Соединение.Получить(HTTPЗапрос);	
		ОтветСтрокой 	= Ответ.ПолучитьТелоКакСтроку();
		ОтветСПорталаДляЛога = РаскодироватьJSON(ОтветСтрокой);
		
		Если СтрНайти(НРег(ОтветСтрокой), "expired_token") ИЛИ СтрНайти(НРег(ОтветСтрокой), "The access token provided has expired") > 0 Тогда
			
			Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(3);
			
			СтруктураНастроек = Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(СтруктураНастроек.НастройкаПодключения, Истина);
			ДанныеСБитрикс24 = ПолучитьДанныеLongPulling(СтруктураНастроек, НастройкиПодключенияLongPulling, РазобранныйАдресХостинга, ПараметрыДляПродолженияМониторинга);
			
		Иначе
			ДанныеСБитрикс24 = ОтветСтрокой;
		КонецЕсли;	
		
	Исключение
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не удалось обработать запрос к серверу Long Pulling для загрузки данных в режиме в реального времени.");
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Б24_К_ОбщегоНазначенияВызовСервера.Таймаут(3);
		СтруктураНастроек = Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(СтруктураНастроек.НастройкаПодключения, Истина);
		ДанныеСБитрикс24 = ПолучитьДанныеLongPulling(СтруктураНастроек, НастройкиПодключенияLongPulling, РазобранныйАдресХостинга, ПараметрыДляПродолженияМониторинга);
		
	КонецПопытки;
	
	Возврат ДанныеСБитрикс24;
	
КонецФункции

#КонецОбласти


#Область РаботаШаблонамиКарточек

Процедура ДобавитьЭлементВШаблонКарточки(СтруктураНастроек, МетодЧтения, МетодЗаписи, ИдентификаторЭлементаКарточки) Экспорт
	
	ТелоHTTPЗапроса = "&scope=C";
	СтруктураОтвета = ОтправкаДанныхНаПортал(СтруктураНастроек, МетодЧтения, ТелоHTTPЗапроса); 
	
	Если СтруктураОтвета <> Неопределено Тогда
		result = СтруктураОтвета.Получить("result");
		Если result <> Неопределено Тогда
			СформироватьЗапросДобавленияЭлементаШаблону(СтруктураНастроек, ИдентификаторЭлементаКарточки, result, ТелоHTTPЗапроса);
			ОтправкаДанныхНаПортал(СтруктураНастроек, МетодЗаписи, ТелоHTTPЗапроса); 
		КонецЕсли;
	КонецЕсли;
	
	мПользователи = Новый Массив;
#Область ПолучениеМассиваИдентификаторовПользователей
	Метод	= "/rest/user.get.json";
	Фильтр 	= "&ACTIVE=Y";
	
	СтруктураОтвета = ОтправкаДанныхНаПортал(СтруктураНастроек, Метод, Фильтр); 
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОтвета.Получить("result") <> неопределено Тогда
		
		Для каждого ТекЭлемент Из СтруктураОтвета.Получить("result") Цикл
			мПользователи.Добавить(Формат(ТекЭлемент.Получить("ID"), "ЧГ=0"));
		КонецЦикла;
	
	КонецЕсли;
	
	Следующие = СтруктураОтвета.Получить("next"); 
	
	Пока ЗначениеЗаполнено(Следующие) Цикл
		
		Следующие = Формат(Следующие, "ЧГ=0");

		ТелоHTTPЗапроса = "&start="+Следующие + Фильтр;	
		
		СтруктураОтвета = ОтправкаДанныхНаПортал(СтруктураНастроек, Метод, ТелоHTTPЗапроса); 
		
		Если СтруктураОтвета = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Если СтруктураОтвета.Получить("result") <> Неопределено Тогда
			
			Для каждого ТекЭлемент Из СтруктураОтвета.Получить("result") Цикл
				мПользователи.Добавить(Формат(ТекЭлемент.Получить("ID"), "ЧГ=0"));
			КонецЦикла;
		КонецЕсли;
		
	   Следующие = СтруктураОтвета.Получить("next");
	   
	КонецЦикла;
#КонецОбласти


	МетодЧтенияБАТЧ = Прав(МетодЧтения,СтрДлина(МетодЧтения)-6);
	МетодЗаписиБАТЧ = Прав(МетодЗаписи,СтрДлина(МетодЗаписи)-6);

	Итератор = 0;
	ТелоHTTPЗапросаЧтения = "";
	
	Для каждого ТекИдПользователя Из мПользователи Цикл
		Итератор = Итератор + 1;	
		
		ТелоHTTPЗапросаЧтения = ТелоHTTPЗапросаЧтения + "&" + "cmd["+ТекИдПользователя+"]=" +  ЗакодироватьСтрокуСервер(МетодЧтенияБАТЧ + "?scope=P&userId="+ТекИдПользователя);
		
		Если Итератор = 50 Тогда
			
			ОтправкаЗапросаНаДобавлениеЭлементаШаблону(СтруктураНастроек, ИдентификаторЭлементаКарточки, МетодЗаписиБАТЧ, ТелоHTTPЗапросаЧтения);	
			
			Итератор = 0; 			
			ТелоHTTPЗапросаЧтения = "";
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТелоHTTPЗапросаЧтения) Тогда
		ОтправкаЗапросаНаДобавлениеЭлементаШаблону(СтруктураНастроек, ИдентификаторЭлементаКарточки, МетодЗаписиБАТЧ, ТелоHTTPЗапросаЧтения)	
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьЗапросДобавленияЭлементаШаблону(СтруктураНастроек, ИдентификаторЭлементаКарточки, result,ТелоЗапроса)
	
	Итератор = 0;
	Для каждого ТекЭлемент Из result Цикл
		стрИтератор = Формат(Итератор, "ЧН=; ЧГ=0");
		
		ИмяСекции = ТекЭлемент.Получить("name");
		
		ТелоЗапроса = ТелоЗапроса + "&data["+стрИтератор+"][name]="+ЗакодироватьСтрокуСервер(ИмяСекции);
		ТелоЗапроса = ТелоЗапроса + "&data["+стрИтератор+"][title]="+ЗакодироватьСтрокуСервер(ТекЭлемент.Получить("title"));
		ТелоЗапроса = ТелоЗапроса + "&data["+стрИтератор+"][type]="+ЗакодироватьСтрокуСервер(ТекЭлемент.Получить("type"));
		
		elements = ТекЭлемент.Получить("elements");
		Если elements <> Неопределено Тогда
			ИтераторЭлементов = 0;
			Для каждого ТекЭлементЭлемента Из elements Цикл
				стрИтераторЭлементов = Формат(ИтераторЭлементов, "ЧН=; ЧГ=0");
				
				ИмяЭлемента = ТекЭлементЭлемента.Получить("name");
				
				Если ИмяЭлемента = "UF_CRM_3717202509821" И ИмяСекции <> "main" Тогда
					Продолжить;
				КонецЕсли;
				
				ТелоЗапроса = ТелоЗапроса + "&data["+стрИтератор+"][elements]["+стрИтераторЭлементов+"][name]="+ЗакодироватьСтрокуСервер(ИмяЭлемента);
				
				ИтераторЭлементов = ИтераторЭлементов + 1;
			КонецЦикла;
			
			Если ИмяСекции = "main" Тогда
				стрИтераторЭлементов = Формат(ИтераторЭлементов, "ЧН=; ЧГ=0");
				ТелоЗапроса = ТелоЗапроса + "&data["+стрИтератор+"][elements]["+стрИтераторЭлементов+"][name]="+ИдентификаторЭлементаКарточки;
			КонецЕсли;
			
		КонецЕсли;
		
		Итератор = Итератор + 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОтправкаЗапросаНаДобавлениеЭлементаШаблону(СтруктураНастроек, ИдентификаторЭлементаКарточки, МетодЗаписиБАТЧ, ТелоHTTPЗапросаЧтения)
	
	СтруктураОтвета = ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапросаЧтения); 
	
	Если СтруктураОтвета <> Неопределено Тогда
		
		ТелоHTTPЗапросаЗаписи = "";
		
		result = СтруктураОтвета.Получить("result");
		Если result <> Неопределено Тогда
			
			result2 = result.Получить("result");
			Если result2 <> Неопределено Тогда
				
				Для каждого ТекДанныеПользователя Из result2 Цикл
					Если ТипЗнч(ТекДанныеПользователя) = Тип("КлючИЗначение") Тогда
						Если ТекДанныеПользователя.Значение <> Неопределено Тогда
							
							ЗапросПользователя = "?scope=P&userId="+ТекДанныеПользователя.Ключ;
							СформироватьЗапросДобавленияЭлементаШаблону(СтруктураНастроек, ИдентификаторЭлементаКарточки, ТекДанныеПользователя.Значение, ЗапросПользователя);
							
							ТелоHTTPЗапросаЗаписи = ТелоHTTPЗапросаЗаписи + "&" + "cmd[]=" +  ЗакодироватьСтрокуСервер(МетодЗаписиБАТЧ + ЗапросПользователя);
							
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТелоHTTPЗапросаЗаписи) Тогда
			ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапросаЗаписи); 
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область РаботаСТааймЛайн

Функция ВыгрузитьПечатнуюФормуВТаймЛайн(НастройкаПодключения, Объект, НазваниеПечатнойФормы, ПечатнаяФорма) Экспорт
	
УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура;	
	Результат.Вставить("Выгружено"	, Ложь);	  
	Результат.Вставить("ДанныеФайла", Неопределено);	
	Результат.Вставить("ИдДокумента", "");	
	Результат.Вставить("АдресPDF"	, "");	
	
	СтруктураНастроек 	= Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Истина);  
	
	Если СтруктураНастроек = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю("Не удалось подключиться");
		Возврат Результат;	
	КонецЕсли;
	
	ИнформацияФайлеPDF 	= ПолучитьДвоичноеСодержимоеПечатнойФормы(СтруктураНастроек, ПечатнаяФорма, ТипФайлаТабличногоДокумента.PDF);	
	ИнформацияФайлеDOCX = ПолучитьДвоичноеСодержимоеПечатнойФормы(СтруктураНастроек, ПечатнаяФорма, ТипФайлаТабличногоДокумента.DOCX);	
	
	Если ИнформацияФайлеPDF.ДвоичноеСодержимоеОтчета = Неопределено Тогда
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось получить двоичные данные печатной формы в формате PDF. Возможно нет прав на запись печатной формы во временные файлы");
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ИнформацияФайлеPDF.ОписаниеОшибки);
		
	КонецЕсли;
	
	ДанныеPDF = ЗакодироватьСтрокуСервер(ИнформацияФайлеPDF.ДвоичноеСодержимоеОтчета);
	ДанныеDOCX = ЗакодироватьСтрокуСервер(ИнформацияФайлеDOCX.ДвоичноеСодержимоеОтчета);
	
	Результат.ДанныеФайла = ДанныеPDF; 	
	
	НазваниеСообщения = ЗакодироватьСтрокуСервер(НазваниеПечатнойФормы);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Портал"				, СтруктураНастроек.Портал);
	Запрос.УстановитьПараметр("ТипыДанных"			, ПолучитьТипыДанныхДел());
	Запрос.УстановитьПараметр("ТипОбъекта"			, Объект.Метаданные().ПолноеИмя());
	Запрос.УстановитьПараметр("ИдентификаторОбъекта", XMLСтрока(Объект));
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыДел.Портал КАК Портал,
	|	Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор КАК ИдентификаторОбъекта,
	|	Б24_К_ИдентификаторыДел.ТипДанных КАК ТипДанных,
	|	Б24_К_ИдентификаторыДел.Идентификатор КАК ИдентификаторДела
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыДел КАК Б24_К_ИдентификаторыДел
	|ГДЕ
	|	Б24_К_ИдентификаторыДел.Портал = &Портал
	|	И Б24_К_ИдентификаторыДел.ТипДанных В(&ТипыДанных)
	|	И Б24_К_ИдентификаторыДел.ТипОбъекта = &ТипОбъекта
	|	И Б24_К_ИдентификаторыДел.ИдентификаторОбъекта = &ИдентификаторОбъекта";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ТипДанных = Выборка.ТипДанных;
			
			ТипВладельца = ПолучитьТипВладельцаТаймЛайнПоТипуДанных(ТипДанных, Выборка.ИдентификаторДела);
			Если ТипВладельца = "" Тогда  
				
				ДанныеИдентификатора = СтрРазделить(Выборка.ИдентификаторОбъекта, "_");	
				Если ДанныеИдентификатора.Количество() > 1 Тогда
					ТипВладельца = ДанныеИдентификатора[0];	 
				Иначе
					Возврат Результат;	
				КонецЕсли;
				
				ИдБитрикс24 = ДанныеИдентификатора[1]; 
			
			Иначе
				ИдБитрикс24 	= Выборка.ИдентификаторОбъекта;
			КонецЕсли;
			
			ТелоHTTPЗапроса = "&fields[title]="+НазваниеСообщения+"&fields[number]=2&fields[entityTypeId]="+ТипВладельца+"&fields[entityId]="+Формат(ИдБитрикс24, "ЧГ=0") + "&fields[region]=ru";
			
			Если ДанныеPDF <> Неопределено Тогда
				ТелоHTTPЗапроса = ТелоHTTPЗапроса +"&fields[pdfContent]="+ДанныеPDF;  
			КонецЕсли;
			ТелоHTTPЗапроса = ТелоHTTPЗапроса +"&fields[fileContent]="+ДанныеDOCX; 
			
			ДанныеСПортала 	= ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.documentgenerator.document.upload", ТелоHTTPЗапроса); 
			
			Если НЕ ЗначениеЗаполнено(ДанныеСПортала) Тогда
			ИначеЕсли ДанныеСПортала = Истина Тогда		
				Результат.Выгружено = Истина;	
			Иначе	
				
				result = ДанныеСПортала.Получить("result"); 	
				Если result <> Неопределено Тогда  
					document = result.Получить("document");
					Если document <> Неопределено Тогда        
						id = document.Получить("id");
						Если id <> Неопределено Тогда
							Результат.Выгружено 	= Истина;	
							Результат.ИдДокумента 	= Формат(id,"ЧГ=0");	
							Результат.АдресPDF 		= document.Получить("pdfUrlMachine");	
						КонецЕсли;     
					КонецЕсли;

				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьДвоичноеСодержимоеПечатнойФормы(СтруктураНастроек, ПечатнаяФорма, ФорматПечатнойФормы) Экспорт
	
	Если ФорматПечатнойФормы = ТипФайлаТабличногоДокумента.DOCX Тогда
		РасширениеФайла = ".docx";	
	ИначеЕсли ФорматПечатнойФормы = ТипФайлаТабличногоДокумента.PDF Тогда
		РасширениеФайла = ".pdf";
	ИначеЕсли ФорматПечатнойФормы = ТипФайлаТабличногоДокумента.XLSX Тогда
		РасширениеФайла = ".xlsx";	
	ИначеЕсли ФорматПечатнойФормы = ТипФайлаТабличногоДокумента.XLS Тогда
		РасширениеФайла = ".xls";	
	ИначеЕсли ФорматПечатнойФормы = ТипФайлаТабличногоДокумента.ODS Тогда
		РасширениеФайла = ".ods";	
	Иначе
		РасширениеФайла = ".txt";	
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ДвоичноеСодержимоеОтчета"	, Неопределено);	
	Результат.Вставить("ОписаниеОшибки"				, "");	
	
	ИмяФайлаПечатнойФормы = ПолучитьИмяВременногоФайла(РасширениеФайла);  
	
	ПечатнаяФорма.АвтоМасштаб = Истина;
	
	ПоложениеСтраницы = "Портрет";
	Если СтрНайти(ПечатнаяФорма.ИмяПараметровПечати, "УниверсальныйПередаточныйДокумент") Тогда
		ПоложениеСтраницы = "Ландшафт";
	ИначеЕсли СтрНайти(ПечатнаяФорма.ИмяПараметровПечати, "УПД") Тогда
		ПоложениеСтраницы = "Ландшафт";
	ИначеЕсли СтрНайти(ПечатнаяФорма.ИмяПараметровПечати, "ТОРГ12") Тогда
		ПоложениеСтраницы = "Ландшафт";
	ИначеЕсли СтрНайти(ПечатнаяФорма.ИмяПараметровПечати, "ТТН") Тогда
		ПоложениеСтраницы = "Ландшафт";
	ИначеЕсли СтрНайти(ПечатнаяФорма.ИмяПараметровПечати, "СчетФактура") Тогда
		ПоложениеСтраницы = "Ландшафт";
	КонецЕсли;
	
	Если ПоложениеСтраницы = "Портрет" Тогда
		ПечатнаяФорма.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	Иначе
		ПечатнаяФорма.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	КонецЕсли;
	
	Попытка
		ПечатнаяФорма.Записать(ИмяФайлаПечатнойФормы, ФорматПечатнойФормы);
	Исключение
		Результат.ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат Результат;	
	КонецПопытки;
	
	Результат.ДвоичноеСодержимоеОтчета = Base64Строка(Новый ДвоичныеДанные(ИмяФайлаПечатнойФормы));
	
	Попытка
		УдалитьФайлы(ИмяФайлаПечатнойФормы);
	Исключение
	КонецПопытки;
	
	Возврат Результат;	
	
КонецФункции


Функция ПолучитьТипыДанныхДел() Экспорт
	
	ТипыДанныхДляОбменаСПорталом = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();	
	
	ТипыОбъектов = Новый Массив;
	ТипыОбъектов.Добавить(ТипыДанныхДляОбменаСПорталом.ДелоКомпании);
	ТипыОбъектов.Добавить(ТипыДанныхДляОбменаСПорталом.ДелоКонтакта);
	ТипыОбъектов.Добавить(ТипыДанныхДляОбменаСПорталом.ДелоЛида);
	ТипыОбъектов.Добавить(ТипыДанныхДляОбменаСПорталом.ДелоСделки);
	ТипыОбъектов.Добавить(ТипыДанныхДляОбменаСПорталом.ДелоСчета);
	ТипыОбъектов.Добавить(ТипыДанныхДляОбменаСПорталом.ДелоСмартПроцесса);      
	
	Возврат ТипыОбъектов;
	
КонецФункции

Функция ПолучитьТипВладельцаТаймЛайнПоТипуДанных(ТипДанных, ИдентификаторДела = "") Экспорт
	
	ТипыДанныхДляОбменаСПорталом = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();
	
	ТипВладельца = Неопределено;
	
	Если ТипДанных = ТипыДанныхДляОбменаСПорталом.Лид ИЛИ ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоЛида Тогда
		ТипВладельца = "1";
	ИначеЕсли ТипДанных = ТипыДанныхДляОбменаСПорталом.Проект ИЛИ ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоСделки Тогда
		ТипВладельца = "2";
	ИначеЕсли ТипДанных = ТипыДанныхДляОбменаСПорталом.Контакт ИЛИ ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоКонтакта Тогда
		ТипВладельца = "3";
	ИначеЕсли ТипДанных = ТипыДанныхДляОбменаСПорталом.Компания ИЛИ ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоКомпании Тогда
		ТипВладельца = "4";   
	ИначеЕсли ТипДанных = ТипыДанныхДляОбменаСПорталом.Счет2 ИЛИ ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоСчета Тогда
		ТипВладельца = "31";   
	ИначеЕсли ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоСмартПроцесса Тогда 
		
		РазделенныйИдентификатор = СтрРазделить(ИдентификаторДела, "_"); 
		Если РазделенныйИдентификатор.Количество() > 1 Тогда
			ТипВладельца = РазделенныйИдентификатор[0];   
		Иначе
			ТипВладельца = "";     
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат ТипВладельца;
	
КонецФункции

Функция ПолучитьТипДанныхДелаПоТипуВладельца(ТипВладельца) Экспорт
	
	ТипыДанныхДляОбменаСПорталом = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();
	
	ТипДанных = Неопределено;
	
	Если ТипВладельца = "1" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоЛида;	
	ИначеЕсли ТипВладельца = "2" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоСделки;	
	ИначеЕсли ТипВладельца = "3" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоКонтакта;	
	ИначеЕсли ТипВладельца = "4" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоКомпании;
	ИначеЕсли ТипВладельца = "31" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоСчета;
	Иначе
		ТипДанных = ТипыДанныхДляОбменаСПорталом.ДелоСмартПроцесса;
	КонецЕсли;
	
	Возврат ТипДанных;
	
КонецФункции

Функция ПолучитьПредопределенныеТипыВладельцев() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("1");	
	Результат.Добавить("2");	
	Результат.Добавить("3");	
	Результат.Добавить("4");	
	Результат.Добавить("31");	

	Возврат Результат;

КонецФункции

Процедура ДобавитьЗаписьВТаймЛайн(СтруктураНастроек, ТипВладельца, ИдентификаторВладельца, Статус, СообщениеДела, Объект = Неопределено) Экспорт

	Поля = Новый Структура;
	Поля.Вставить("OWNER_ID"			, ИдентификаторВладельца);
	Поля.Вставить("OWNER_TYPE_ID"		, ТипВладельца);
	Поля.Вставить("COMPLETED"			, "Y");
	Поля.Вставить("SUBJECT"				, ?(Объект = Неопределено,"Журнал регистрации 1С", Строка(Объект)));
	Поля.Вставить("PROVIDER_ID"			, "REST_APP");
	Поля.Вставить("DESCRIPTION_TYPE"	, "3");
	
	Если Статус = "ОК" Тогда
		Поля.Вставить("PROVIDER_TYPE_ID"	, "1CTOTAL_GREEN");      //1CTOTAL - синий, 1CTOTAL_RED - красный, 1CTOTAL_GREEN - зеленый 
		Поля.Вставить("DESCRIPTION"			, СообщениеДела);         
	Иначе
		Поля.Вставить("PROVIDER_TYPE_ID"	, "1CTOTAL_RED");        //1CTOTAL - синий, 1CTOTAL_RED - красный, 1CTOTAL_GREEN - зеленый  
		Поля.Вставить("DESCRIPTION"			, СообщениеДела);  
	КонецЕсли;
	
	ТелоЗапроса = "";
	Если ЗначениеЗаполнено(Объект) Тогда
		ИдентификаторПодключения = СтрЗаменить(СтруктураНастроек.ИдентификаторПодключения, "_SALE", "");
		ТелоЗапроса = "fields[PROVIDER_PARAMS][idAdress]=" + ИдентификаторПодключения + "&fields[PROVIDER_PARAMS][relAdress]=" + ЗакодироватьСтрокуСервер(ПолучитьНавигационнуюСсылку(Объект));
	Иначе
		ИдентификаторПодключения = СтрЗаменить(СтруктураНастроек.ИдентификаторПодключения, "_SALE", "");
		ТелоЗапроса = "fields[PROVIDER_PARAMS][idAdress]=" + ИдентификаторПодключения + "&fields[PROVIDER_PARAMS][relAdress]=" + ЗакодироватьСтрокуСервер("e1cib/list/РегистрСведений.Б24_К_Логирование");
	КонецЕсли;
	
	Для каждого ТекПоле Из Поля Цикл
		ТелоЗапроса = ТелоЗапроса + "&fields["+ ТекПоле.Ключ + "]=" + ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
	КонецЦикла;                                                                     
	
	СтруктураОтвета = ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.activity.add", ТелоЗапроса); 
	Если СтруктураОтвета <> Неопределено Тогда
		result = СтруктураОтвета.Получить("result");
		Если result = Неопределено Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось отправить сообщение у объекта " + ТипВладельца+" с ид:" + ИдентификаторВладельца);
		КонецЕсли;
	Иначе
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось отправить сообщение у объекта " + ТипВладельца+" с ид:" + ИдентификаторВладельца);
	КонецЕсли;
	
КонецПроцедуры      

#КонецОбласти
