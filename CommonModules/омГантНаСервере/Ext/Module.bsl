Функция ПолучитьДанныеДляДиаграммы(стПериоды, СтруктураФильтры, ТипДиаграммы) Экспорт

	СтруктураПараметров = омГантНастройкаСервер.ПолучитьСтруктуруПараметров();
	Если НЕ СтруктураФильтры.Свойство("ОписаниеТиповДанных") Тогда
	 	СтруктураФильтры.Вставить("ОписаниеТиповДанных", СтруктураПараметров.ОписаниеТиповДанных);
	КонецЕсли;

	Если НЕ СтруктураФильтры.Свойство("ТипИнтервала") Тогда
	 	СтруктураФильтры.Вставить("ТипИнтервала", "день");
	КонецЕсли;

	Если НЕ СтруктураФильтры.Свойство("СпосовВывода") Тогда
	 	СтруктураФильтры.Вставить("СпосовВывода", "на экран");
	КонецЕсли;

	Если ТипДиаграммы = "ПланРаботы" Тогда
		СтруктураДанные 	= ПолучитьТаблицуДляДиаграммыПланРаботы(стПериоды, СтруктураФильтры);
		ТаблицыДляДиаграммы = ЗаполнитьСтандартныеТаблицы(ПолучитьДанныеДляДиаграммыПланРаботы(СтруктураДанные, СтруктураФильтры), СтруктураПараметров, СтруктураФильтры);
	ИначеЕсли ТипДиаграммы = "ПланРаботыКонструктор" Тогда
		СтруктураДанные 	= ПолучитьТаблицуДляДиаграммыПланРаботы(стПериоды, СтруктураФильтры, Истина);
		ТаблицыДляДиаграммы = ЗаполнитьСтандартныеТаблицы(ПолучитьДанныеДляДиаграммыПланРаботы(СтруктураДанные, СтруктураФильтры, Истина), СтруктураПараметров, СтруктураФильтры);
	КонецЕсли;
	
    Возврат Новый Структура("ТаблицыДляДиаграммы, СтруктураФильтры", ТаблицыДляДиаграммы, СтруктураФильтры); 
КонецФункции // ПолучитьДаннеДляДиаграммы() 

Функция ЗаполнитьСтандартныеТаблицы(тзДанныеДляДиаграммыПланРаботы, СтруктураПараметров, СтруктураФильтры)

	Если СтруктураФильтры.СпосовВывода = "на печать" Тогда
		ЗаполнитьИнтервалы();
	КонецЕсли;
	
	ТаблицаНастроек = омГантНастройкаСервер.ПолучитьТаблицуНастроекГанта();
	
	СтруктурыДляЗаполненияДанных 	= омГантНастройкаСервер.ПолучитьСтруктурыДляЗаполненияДанных();
	ТаблицыДляДиаграммы 			= омГантНастройкаСервер.ПолучитьТаблицыДляДиаграммы(СтруктураПараметров);
	
	ОписаниеСтрока30 	= Новый ОписаниеТипов("Строка"	, , , , Новый КвалификаторыСтроки(30));
	ОписаниеЧисло6	 	= Новый ОписаниеТипов("Число"	, , ,Новый КвалификаторыЧисла(6, 0, ДопустимыйЗнак.Неотрицательный));
	
	ТаблицаСоответствия = Новый ТаблицаЗначений; 
	ТаблицаСоответствия.Колонки.Добавить("ТипЗначения",		ОписаниеСтрока30);
	ТаблицаСоответствия.Колонки.Добавить("НомерЗначения",	ОписаниеЧисло6);
	
	ТаблицаСоответствияРодителя = Новый ТаблицаЗначений; 
	ТаблицаСоответствияРодителя.Колонки.Добавить("НовыйНомерЗначения",		ОписаниеЧисло6);
	ТаблицаСоответствияРодителя.Колонки.Добавить("НомерЗначения",			ОписаниеЧисло6);
	
	СтруктураВидовЗначения 	= ОбщегоНазначения.СкопироватьРекурсивно(СтруктурыДляЗаполненияДанных.СтруктураВидовЗначения); 
	СтруктураВыводЭлемента 	= ОбщегоНазначения.СкопироватьРекурсивно(СтруктурыДляЗаполненияДанных.СтруктураВыводЭлемента);
	СтруктураВыводЗначения 	= ОбщегоНазначения.СкопироватьРекурсивно(СтруктурыДляЗаполненияДанных.СтруктураВыводЗначения);
	
	Для каждого ТекущиеДанные Из тзДанныеДляДиаграммыПланРаботы Цикл
		
		НомерВидаЭлемента 		= ДобавитьВидЗначения(СтруктураВидовЗначения, ТаблицаНастроек, ТекущиеДанные, ТаблицаСоответствия, ТаблицыДляДиаграммы.тзВидовЭлементов);	
		
		НомерЭлемента 			= ДобавитьВыводЭлемента(СтруктураВыводЭлемента, ТекущиеДанные, ТаблицыДляДиаграммы.тзВыводЭлемента, СтруктураФильтры, НомерВидаЭлемента, ТаблицаСоответствияРодителя);	
		
		ДобавитьВыводЗначения(СтруктураВыводЗначения, ТекущиеДанные, ТаблицыДляДиаграммы, СтруктураФильтры, НомерЭлемента, НомерВидаЭлемента);	
	
	КонецЦикла;

	
	стОтбор = Новый Структура("РодительЭлемента", );
	Для каждого х Из ТаблицаСоответствияРодителя Цикл
		стОтбор.РодительЭлемента = х.НомерЗначения;
		НайденыеСтроки = ТаблицыДляДиаграммы.тзВыводЭлемента.НайтиСтроки(стОтбор);
		
		Для каждого СтрокаСоответствия Из НайденыеСтроки Цикл
			СтрокаСоответствия.РодительЭлемента = х.НовыйНомерЗначения;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицыДляДиаграммы;

КонецФункции

Процедура ДобавитьВыводЗначения(СтруктураВыводЗначения, ТекущиеДанные, ТаблицыДляДиаграммы, СтруктураФильтры, НомерЭлемента, НомерВидаЭлемента)

	Если СтруктураФильтры.СпосовВывода = "на печать" Тогда
		стДанныеИнтервала = ЗаполнитьИнтервалыЗначения(ТекущиеДанные, ТаблицыДляДиаграммы);
		//СтруктураВыводЗначения.Вставить("НомерИнтервалаНачала",			0);
		//СтруктураВыводЗначения.Вставить("ПродолжительностьИнтервала",	0);
	Иначе
		СтруктураВыводЗначения.ДатаНачала 		= ТекущиеДанные.ДатаНачала;	
		СтруктураВыводЗначения.ДатаОкончания 	= ТекущиеДанные.ДатаОкончания;	
	КонецЕсли; 
	
	тзВыводЗначения = ТаблицыДляДиаграммы.тзВыводЗначения;
	
	СтруктураВыводЗначения.НомерЭлемента 			= НомерЭлемента;
	СтруктураВыводЗначения.ПредставлениеЗначения 	= ТекущиеДанные.Представление;
	СтруктураВыводЗначения.СсылкаНаОбъект 			= ТекущиеДанные.СсылкаНаОбъект;
	СтруктураВыводЗначения.НомерВидаЭлемента 		= НомерВидаЭлемента;
	
	ЗаполнитьЗначенияСвойств(тзВыводЗначения.Добавить(), СтруктураВыводЗначения);
	
КонецПроцедуры // ДобавитьВидЗначения

Функция ЗаполнитьИнтервалыЗначения(ТекущиеДанные, ТаблицыДляДиаграммы)

	

КонецФункции // ЗаполнитьИнтервалыЗначения()

Функция ДобавитьВыводЭлемента(СтруктураВыводЭлемента, ТекущиеДанные, тзВыводЭлемента, СтруктураФильтры, НомерВидаЭлемента, ТаблицаСоответствияРодителя)
	
	НайденаяСтрока = тзВыводЭлемента.Найти(ТекущиеДанные.СсылкаНаОбъект, "СсылкаНаОбъект");
	
	Если НЕ НайденаяСтрока = Неопределено Тогда
		Возврат НайденаяСтрока.НомерЭлемента;
	КонецЕсли;
	
	СтруктураВыводЭлемента.НомерЭлемента 			= тзВыводЭлемента.Количество() + 1;
	СтруктураВыводЭлемента.ПредставлениеЭлемента 	= ТекущиеДанные.Представление;
	СтруктураВыводЭлемента.НомерВидаЭлемента 		= НомерВидаЭлемента;
	СтруктураВыводЭлемента.РодительЭлемента			= ТекущиеДанные.Родитель;
	СтруктураВыводЭлемента.СсылкаНаОбъект 			= ТекущиеДанные.СсылкаНаОбъект;
	
	ЗаполнитьЗначенияСвойств(тзВыводЭлемента.Добавить(), СтруктураВыводЭлемента); 
	
	НС = ТаблицаСоответствияРодителя.Добавить();
	НС.НовыйНомерЗначения 	= СтруктураВыводЭлемента.НомерЭлемента;
	НС.НомерЗначения		= ТекущиеДанные.НомерПоПорядку;
	
    Возврат СтруктураВыводЭлемента.НомерЭлемента; 
КонецФункции // ДобавитьВидЗначения

Процедура ЗаполнитьИнтервалы()

	

КонецПроцедуры  

Функция ДобавитьВидЗначения(СтруктураВидовЗначения, ТаблицаНастроек, ТекущиеДанные, ТаблицаСоответствия, тзВидовЭлементов)

	НайденаяСтрокаНастройки = ТаблицаНастроек.Найти(ТекущиеДанные.ТипЗначения, "Объект");
	
	Если НайденаяСтрокаНастройки = Неопределено Тогда
		ТипЗначения = "ПоУмолчанию";
	Иначе
		ТипЗначения = ТекущиеДанные.ТипЗначения;
	КонецЕсли;
	
	 НайденаяСтрока = ТаблицаСоответствия.Найти(ТекущиеДанные.ТипЗначения, "ТипЗначения");
	
	Если НЕ НайденаяСтрока  = Неопределено Тогда
		Возврат НайденаяСтрока.НомерЗначения;
	КонецЕсли;
	
	
	НС 					= ТаблицаСоответствия.Добавить();
	НС.ТипЗначения 		= ТекущиеДанные.ТипЗначения;
	НС.НомерЗначения 	= ТаблицаСоответствия.Количество();
	
	СтруктураВидовЗначения.НомерВидаЭлемента = НС.НомерЗначения;

	Если НЕ ТипЗначения = "ПоУмолчанию" Тогда
		СтруктураВидовЗначения.ЦветФона 					= НайденаяСтрокаНастройки.Цвет;
		СтруктураВидовЗначения.Представление 				= НайденаяСтрокаНастройки.ПредставлениеЗначения;
		СтруктураВидовЗначения.ПараметрыШрифта.РазмерШрифта = НайденаяСтрокаНастройки.РазмерШрифта;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(тзВидовЭлементов.Добавить(), СтруктураВидовЗначения);
	
	Возврат НС.НомерЗначения;
	
КонецФункции // ДобавитьВидЗначения

Функция ПолучитьДанныеДляДиаграммыПланРаботы(СтруктураДанные, СтруктураФильтры, Конструктор = Ложь)
	Если СтруктураФильтры.Свойство("Группировки") Тогда
		МассивГруппировки = СтруктураФильтры.Группировки;
	Иначе
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	Если МассивГруппировки.Количество() = 0 Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли; 
	
	ОписаниеТиповДанных = СтруктураФильтры.ОписаниеТиповДанных;

	ОписаниеЧисло6	 	= Новый ОписаниеТипов("Число"	, , ,Новый КвалификаторыЧисла(6, 0, ДопустимыйЗнак.Неотрицательный));
	ОписаниеСтрока30 	= Новый ОписаниеТипов("Строка"	, , , , Новый КвалификаторыСтроки(30));
	ОписаниеДатаВремя 	= Новый ОписаниеТипов("Дата"	, , , , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
	
	тзДанныеДляДиаграммыПланРаботы = Новый ТаблицаЗначений;
	тзДанныеДляДиаграммыПланРаботы.Колонки.Добавить("СсылкаНаОбъект", 	ОписаниеТиповДанных);//);
	тзДанныеДляДиаграммыПланРаботы.Колонки.Добавить("Представление", 	ОписаниеСтрока30);
	тзДанныеДляДиаграммыПланРаботы.Колонки.Добавить("Родитель", 		ОписаниеЧисло6);
	тзДанныеДляДиаграммыПланРаботы.Колонки.Добавить("НомерПоПорядку", 	ОписаниеЧисло6);
	тзДанныеДляДиаграммыПланРаботы.Колонки.Добавить("ДатаНачала", 		ОписаниеДатаВремя);
	тзДанныеДляДиаграммыПланРаботы.Колонки.Добавить("ДатаОкончания", 	ОписаниеДатаВремя);
	тзДанныеДляДиаграммыПланРаботы.Колонки.Добавить("ТипЗначения", 		ОписаниеСтрока30);
	
	тзДанныеДляДиаграммыПланРаботы.Индексы.Добавить("Родитель, СсылкаНаОбъект");
	
	НомерПоПорядку			= 1;
    МаксИндексГруппировок	= МассивГруппировки.Количество() - 1;
									
	СтруктураОтбор = Новый Структура("Родитель, СсылкаНаОбъект", 0);								
	ТекущаяГруппировка = МассивГруппировки[0];
	СтруктураОтбора= Новый Структура();
	стПараметры = Новый Структура("тзДанныеДляДиаграммыПланРаботы, СтруктураДанные, МассивГруппировки, МаксИндексГруппировок, СтруктураОтбора"
									, тзДанныеДляДиаграммыПланРаботы, СтруктураДанные, МассивГруппировки, МаксИндексГруппировок, СтруктураОтбора);

	стПараметры.Вставить("Конструктор", 	Конструктор);
	
	ЗаполнитьГруппировкуПланРаботы(стПараметры, ТекущаяГруппировка, 0, НомерПоПорядку, СтруктураОтбор, стПараметры.СтруктураДанные.тзИзделияКомплект, 0);
	
	РасчитатьДатыРодителей(тзДанныеДляДиаграммыПланРаботы, МаксИндексГруппировок);
	
	Возврат тзДанныеДляДиаграммыПланРаботы;
КонецФункции

Процедура РасчитатьДатыРодителей(тзДанныеДляДиаграммыПланРаботы, МаксИндексГруппировок)
	
	стОтбор = Новый Структура("Родитель", 0);
	
	НайденыеСтроки = тзДанныеДляДиаграммыПланРаботы.НайтиСтроки(стОтбор);
	ПустаяДата 	= Дата(1, 1, 1);
	
	Для каждого ТекущаяСтрока Из НайденыеСтроки Цикл
		
		Если НЕ (ТекущаяСтрока.ДатаНачала = ПустаяДата ИЛИ ТекущаяСтрока.ДатаОкончания = ПустаяДата) Тогда
			Продолжить;	
		КонецЕсли;
		
		стОтбор.Родитель = ТекущаяСтрока.НомерПоПорядку;
	    стДаты = ПолучитьДатыПериода(стОтбор, тзДанныеДляДиаграммыПланРаботы, ПустаяДата);
		ТекущаяСтрока.ДатаНачала	= стДаты.ДатаНачала;
		ТекущаяСтрока.ДатаОкончания = стДаты.ДатаОкончания;
		
	КонецЦикла;

КонецПроцедуры // РасчитатьДатыРодителей(тзДанныеДляДиаграммыПланРаботы)

Функция ПолучитьДатыПериода(стОтбор, тзДанныеДляДиаграммыПланРаботы, ПустаяДата)
    стДаты = Новый Структура("ДатаНачала, ДатаОкончания",ПустаяДата, ПустаяДата);
	
	НайденыеСтроки = тзДанныеДляДиаграммыПланРаботы.НайтиСтроки(стОтбор);
	Для каждого ТекущаяСтрока Из НайденыеСтроки Цикл 
		Если ТекущаяСтрока.ДатаНачала = ПустаяДата ИЛИ ТекущаяСтрока.ДатаОкончания = ПустаяДата Тогда
			стОтбор.Родитель = ТекущаяСтрока.НомерПоПорядку;
			стДатыПодчиненные = ПолучитьДатыПериода(стОтбор, тзДанныеДляДиаграммыПланРаботы, ПустаяДата);
			ТекущаяСтрока.ДатаНачала	= стДатыПодчиненные.ДатаНачала;
			ТекущаяСтрока.ДатаОкончания = стДатыПодчиненные.ДатаОкончания;
		КонецЕсли; 
		
		Если стДаты.ДатаНачала = ПустаяДата Тогда
			стДаты.ДатаНачала 	= ТекущаяСтрока.ДатаНачала;	
		Иначе	
			стДаты.ДатаНачала 	= ?(стДаты.ДатаНачала 		< ТекущаяСтрока.ДатаНачала, 	стДаты.ДатаНачала, 		ТекущаяСтрока.ДатаНачала);
		КонецЕсли;
		
		стДаты.ДатаОкончания 	= ?(стДаты.ДатаОкончания 	> ТекущаяСтрока.ДатаОкончания, 	стДаты.ДатаОкончания, 	ТекущаяСтрока.ДатаОкончания);
	КонецЦикла;
	
	Возврат стДаты;
КонецФункции

Процедура ЗаполнитьГруппировкуПланРаботы(стПараметры, Знач ТекущаяГруппировка, Знач УровеньГруппировки, НомерПоПорядку, Знач СтруктураОтбор, Знач тзИзделияКомплект, Знач Родитель)
	
	Для каждого ТекущиеДанные Из тзИзделияКомплект Цикл
		ТекущиеЗначение 				= ТекущиеДанные[ТекущаяГруппировка];
		СтруктураОтбор.СсылкаНаОбъект 	= ТекущиеЗначение;
		СтруктураОтбор.Родитель 		= Родитель;
		
		НайденыеСтроки = стПараметры.тзДанныеДляДиаграммыПланРаботы.НайтиСтроки(СтруктураОтбор);
		
		Если НайденыеСтроки.Количество() = 0 Тогда
			
			Если УровеньГруппировки = стПараметры.МаксИндексГруппировок Тогда
				ЗаполнитьДатыГруппировкиПланРаботы(стПараметры.тзДанныеДляДиаграммыПланРаботы, ТекущиеЗначение, Родитель, НомерПоПорядку, стПараметры, ТекущиеДанные);
			Иначе	
				НС 					= стПараметры.тзДанныеДляДиаграммыПланРаботы.Добавить();
				НС.СсылкаНаОбъект	= ТекущиеЗначение;
				НС.Родитель 		= Родитель;
				НС.НомерПоПорядку 	= НомерПоПорядку;
				НС.ТипЗначения 		= ТекущаяГруппировка;
				НС.Представление	= ПолучитьПредставление(ТекущиеДанные, ТекущаяГруппировка);
				
				НомерПоПорядку 			= НомерПоПорядку + 1;
				ПодчиненыйУровень 		= УровеньГруппировки + 1;
				ПодчиненнаяГруппировка 	= стПараметры.МассивГруппировки[ПодчиненыйУровень];
				
				Если НЕ стПараметры.СтруктураОтбора.Свойство(ТекущаяГруппировка) Тогда
					стПараметры.СтруктураОтбора.Вставить(ТекущаяГруппировка);
				КонецЕсли;
				
				стПараметры.СтруктураОтбора[ТекущаяГруппировка] = ТекущиеЗначение;
				
				НайденыеСтроки = стПараметры.СтруктураДанные.тзИзделияКомплект.НайтиСтроки(стПараметры.СтруктураОтбора);
				
				ЗаполнитьГруппировкуПланРаботы(стПараметры, ПодчиненнаяГруппировка, ПодчиненыйУровень, НомерПоПорядку, СтруктураОтбор, НайденыеСтроки, НомерПоПорядку - 1);	
			КонецЕсли;	
			
		КонецЕсли;
		
		Если Родитель = 0 Тогда
			стПараметры.СтруктураОтбора = Новый Структура;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры 

Функция ПолучитьПредставление(ТекущиеДанные, ТекущаяГруппировка)

	Если ТекущаяГруппировка = "Комплект" Тогда
		Возврат ТекущиеДанные.НаименованиеКомплекта;
	ИначеЕсли ТекущаяГруппировка = "Конструктор" Тогда
		Возврат ТекущиеДанные.КонструкторНаименование;
	ИначеЕсли ТекущаяГруппировка = "ОсновноеПомещение" Тогда
		Возврат ТекущиеДанные.ОсновноеПомещениеНаименование;
	ИначеЕсли ТекущаяГруппировка = "Проект" Тогда
		Возврат Формат(ТекущиеДанные.СделкаНомер, "ЧЦ=3; ЧВН=") + " (" + ТекущиеДанные.СделкаПрефикс + ") " + ТекущиеДанные.СделкаНаименование;
	ИначеЕсли ТекущаяГруппировка = "Дизайнер" Тогда
		Возврат ТекущиеДанные.ДизайнерНаименование;
	ИначеЕсли ТекущаяГруппировка = "ПриложениеКДоговору" Тогда
		Возврат ТекущиеДанные.НаименованиеПриложениеКДоговору;
	КонецЕсли;
	
    Возврат "";
КонецФункции // ПолучитьПредставление(ТекущиеДанные, ТекущаяГруппировка)
 
Процедура ЗаполнитьДатыГруппировкиПланРаботы(тзДанныеДляДиаграммыПланРаботы, ТекущиеЗначение, Родитель, НомерПоПорядку, стПараметры, ТекущиеДанные)

    стОтбор 	= Новый Структура("Комплект", ТекущиеЗначение);
	ПустаяДата 	= Дата(1, 1, 1);
	
	НайденыеСтроки = стПараметры.СтруктураДанные.тзДатыКомплекта.НайтиСтроки(стОтбор);
	
	Если НайденыеСтроки.Количество() > 0 Тогда
		
		Представление	= ПолучитьПредставление(ТекущиеДанные, "Комплект");
		
		НайденаяСтрока = НайденыеСтроки[0];
		
		Если НЕ (НайденаяСтрока.НачалоТЗ = ПустаяДата ИЛИ НайденаяСтрока.КрайТЗ = ПустаяДата) Тогда
			НС 					= стПараметры.тзДанныеДляДиаграммыПланРаботы.Добавить();
			НС.СсылкаНаОбъект	= ТекущиеЗначение;
			НС.Родитель 		= Родитель;
			НС.НомерПоПорядку 	= НомерПоПорядку;
			НС.ДатаНачала 		= НайденаяСтрока.НачалоТЗ;
			НС.ДатаОкончания 	= КонецДня(НайденаяСтрока.КрайТЗ);
			НС.ТипЗначения 		= "ТЗ";
			
			НомерПоПорядку 		= НомерПоПорядку + 1;
			НС.Представление	= Представление;
			
		КонецЕсли;
		
		Если НЕ (НайденаяСтрока.НачалоПКД = ПустаяДата ИЛИ НайденаяСтрока.КрайПКД = ПустаяДата) Тогда
			НС 					= стПараметры.тзДанныеДляДиаграммыПланРаботы.Добавить();
			НС.СсылкаНаОбъект	= ТекущиеЗначение;
			НС.Родитель 		= Родитель;
			НС.НомерПоПорядку 	= НомерПоПорядку;
			НС.ДатаНачала 		= НайденаяСтрока.НачалоПКД;
			НС.ДатаОкончания 	= КонецДня(НайденаяСтрока.КрайПКД);
			НС.ТипЗначения 		= "ПКД";
			
			НомерПоПорядку 		= НомерПоПорядку + 1;
			НС.Представление	= Представление;
			
		КонецЕсли;
		
		Если НЕ (НайденаяСтрока.НачалоРКД = ПустаяДата ИЛИ НайденаяСтрока.КрайРКД = ПустаяДата) Тогда
			НС 					= стПараметры.тзДанныеДляДиаграммыПланРаботы.Добавить();
			НС.СсылкаНаОбъект	= ТекущиеЗначение;
			НС.Родитель 		= Родитель;
			НС.НомерПоПорядку 	= НомерПоПорядку;
			НС.ДатаНачала 		= НайденаяСтрока.НачалоРКД;
			НС.ДатаОкончания 	= КонецДня(НайденаяСтрока.КрайРКД);
			НС.ТипЗначения 		= "РКД";
			
			НомерПоПорядку 		= НомерПоПорядку + 1;
			НС.Представление	= Представление;
			
		КонецЕсли; 
		
	
	КонецЕсли;
	
	Если НЕ стПараметры.Конструктор Тогда
		
		НайденыеСтроки = стПараметры.СтруктураДанные.тзДатыКомплектаМонтаж.НайтиСтроки(стОтбор);
		Если НайденыеСтроки.Количество() > 0 Тогда
			
			НайденаяСтрока = НайденыеСтроки[0];
			
			Если НЕ (НайденаяСтрока.НачалоМонтажа = ПустаяДата ИЛИ НайденаяСтрока.КрайМонтажа = ПустаяДата) Тогда
				НС 					= стПараметры.тзДанныеДляДиаграммыПланРаботы.Добавить();
				НС.СсылкаНаОбъект	= ТекущиеЗначение;
				НС.Родитель 		= Родитель;
				НС.НомерПоПорядку 	= НомерПоПорядку;
				НС.ДатаНачала 		= НайденаяСтрока.НачалоМонтажа;
				НС.ДатаОкончания 	= КонецДня(НайденаяСтрока.КрайМонтажа);
				НС.ТипЗначения 		= "Монтаж";
				
				НомерПоПорядку 		= НомерПоПорядку + 1;
				НС.Представление	= Представление;
				
			КонецЕсли;
			
		КонецЕсли;	
		
		НайденыеСтроки = стПараметры.СтруктураДанные.тзДатыКомплектаПроизводство.НайтиСтроки(стОтбор);
		Если НайденыеСтроки.Количество() > 0 Тогда
			
			НайденаяСтрока = НайденыеСтроки[0];
			
			Если НЕ (НайденаяСтрока.НачалоПроизводства = ПустаяДата ИЛИ НайденаяСтрока.КрайПроизводства = ПустаяДата) Тогда
				НС 					= стПараметры.тзДанныеДляДиаграммыПланРаботы.Добавить();
				НС.СсылкаНаОбъект	= ТекущиеЗначение;
				НС.Родитель 		= Родитель;
				НС.НомерПоПорядку 	= НомерПоПорядку;
				НС.ДатаНачала 		= НайденаяСтрока.НачалоПроизводства;
				НС.ДатаОкончания 	= КонецДня(НайденаяСтрока.КрайПроизводства);
				НС.ТипЗначения 		= "Производство";
				
				НомерПоПорядку 		= НомерПоПорядку + 1;
				НС.Представление	= Представление;
				
			КонецЕсли;
			
		КонецЕсли;	
	КонецЕсли;	


КонецПроцедуры

Функция ПолучитьТаблицуДляДиаграммыПланРаботы(стПериоды, стФильтры, Конструктор = Ложь)
	
	мСделка 	= Неопределено;
	мКонструктор = Неопределено;
	
	Если стФильтры.Свойство("Проект") Тогда
		мСделка = стФильтры.Проект;	
	КонецЕсли;
	
	Если стФильтры.Свойство("Конструктор") Тогда
		мКонструктор = стФильтры.Конструктор;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплекта.КрайПКД КАК КрайПКД,
		|	ДатыКомплекта.КрайРКД КАК КрайРКД,
		|	ДатыКомплекта.КрайТЗ КАК КрайТЗ,
		|	ДатыКомплекта.НачалоПКД КАК НачалоПКД,
		|	ДатыКомплекта.НачалоРКД КАК НачалоРКД,
		|	ДатыКомплекта.НачалоТЗ КАК НачалоТЗ,
		|	ДатыКомплекта.Комплект КАК Комплект
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Конструктор = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ДатыКомплекта.Комплект.Конструктор В (&Конструктор)
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Проект = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ДатыКомплекта.Проект В (&Проект)
		|		КОНЕЦ
		|	И НЕ ДатыКомплекта.Проект.НеВыводитьНаДиаграмму
		|	И (ДатыКомплекта.НачалоТЗ <= &ДатаОкончания
		|				И ДатыКомплекта.КрайТЗ >= &ДатаНачала
		|			ИЛИ ДатыКомплекта.НачалоРКД <= &ДатаОкончания
		|				И ДатыКомплекта.КрайРКД >= &ДатаНачала
		|			ИЛИ ДатыКомплекта.НачалоПКД <= &ДатаОкончания
		|				И ДатыКомплекта.КрайПКД >= &ДатаНачала)";
	
	Запрос.УстановитьПараметр("ДатаНачала", 	стПериоды.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	стПериоды.ДатаОкончания);
	Запрос.УстановитьПараметр("Проект", 		?(ЗначениеЗаполнено(мСделка), 		мСделка, 		Неопределено));
	Запрос.УстановитьПараметр("Конструктор", 	?(ЗначениеЗаполнено(мКонструктор), 	мКонструктор, 	Неопределено));

	Если Конструктор Тогда
		тзДатыКомплекта = Запрос.Выполнить().Выгрузить();
		тзДатыКомплекта.Индексы.Добавить("Комплект");
		
		тзДатыКомплектаМонтаж 		= Новый ТаблицаЗначений;
		тзДатыКомплектаПроизводство = Новый ТаблицаЗначений;
	Иначе
		
		Запрос.Текст = Запрос.Текст + "
	
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыКомплектаМонтаж.Комплект КАК Комплект,
		|	ДатыКомплектаМонтаж.КрайМонтажа КАК КрайМонтажа,
		|	ДатыКомплектаМонтаж.НачалоМонтажа КАК НачалоМонтажа
		|ИЗ
		|	РегистрСведений.ДатыКомплектаМонтаж КАК ДатыКомплектаМонтаж
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Конструктор = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ДатыКомплектаМонтаж.Комплект.Конструктор В (&Конструктор)
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Проект = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ДатыКомплектаМонтаж.Проект В (&Проект)
		|		КОНЕЦ
        |   И НЕ ДатыКомплектаМонтаж.Проект.НеВыводитьНаДиаграмму
		|	И ДатыКомплектаМонтаж.КрайМонтажа >= &ДатаНачала
		|	И ДатыКомплектаМонтаж.НачалоМонтажа <= &ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыКомплектаПроизводство.Комплект КАК Комплект,
		|	ДатыКомплектаПроизводство.КрайПроизводства КАК КрайПроизводства,
		|	ДатыКомплектаПроизводство.НачалоПроизводства КАК НачалоПроизводства
		|ИЗ
		|	РегистрСведений.ДатыКомплектаПроизводство КАК ДатыКомплектаПроизводство
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Конструктор = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ДатыКомплектаПроизводство.Комплект.Конструктор В (&Конструктор)
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Проект = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ДатыКомплектаПроизводство.Проект В (&Проект)
		|		КОНЕЦ
        |   И НЕ ДатыКомплектаПроизводство.Проект.НеВыводитьНаДиаграмму
		|	И ДатыКомплектаПроизводство.КрайПроизводства >= &ДатаНачала
		|	И ДатыКомплектаПроизводство.НачалоПроизводства <= &ДатаОкончания";
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		тзДатыКомплекта 			= РезультатЗапроса[0].Выгрузить();
		тзДатыКомплекта.Индексы.Добавить("Комплект");
		
		тзДатыКомплектаМонтаж 		= РезультатЗапроса[1].Выгрузить();
		тзДатыКомплектаМонтаж.Индексы.Добавить("Комплект");
		
		тзДатыКомплектаПроизводство = РезультатЗапроса[2].Выгрузить();
		тзДатыКомплектаПроизводство.Индексы.Добавить("Комплект");
		
	КонецЕсли;
	
	мКомплекты = Новый Массив;
	
	ДобавитьКомплект(тзДатыКомплекта, 				мКомплекты);
	ДобавитьКомплект(тзДатыКомплектаМонтаж, 		мКомплекты);
	ДобавитьКомплект(тзДатыКомплектаПроизводство, 	мКомплекты);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплектИзделияЭлемент.Ссылка КАК Комплект,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Конструктор КАК Конструктор,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.ОсновноеПомещение КАК ОсновноеПомещение,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Проект КАК Проект,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Наименование КАК НаименованиеКомплекта,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Проект.Наименование КАК СделкаНаименование,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Проект.Номер КАК СделкаНомер,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Проект.Префикс КАК СделкаПрефикс,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Проект.Дизайнер КАК Дизайнер,
		|	ИзделияЗаказчикаИзделияЭлемент.Ссылка КАК ИзделиеЗаказчика,
		|	ПриложенияКДоговорамИзделияЗаказчика.Ссылка.Ссылка КАК ПриложениеКДоговору,
		|	ПриложенияКДоговорамИзделияЗаказчика.Ссылка.Наименование КАК НаименованиеПриложениеКДоговору,
		|	ИзделияЗаказчикаИзделияЭлемент.Ссылка.ПолноеНаименование КАК НаименованиеИзделиеЗаказчика,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Конструктор.Наименование КАК КонструкторНаименование,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.ОсновноеПомещение.Наименование КАК ОсновноеПомещениеНаименование,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Проект.Дизайнер.Наименование КАК ДизайнерНаименование,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.ГабаритныйЧертеж КАК ГабаритныйЧертеж,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.ГабаритныйЧертеж.Наименование КАК ОбъектЗамераНаименование
		|ИЗ
		|	Справочник.ИзделияКомплект.ИзделияЭлемент КАК ИзделияКомплектИзделияЭлемент
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИзделияЗаказчика.ИзделияЭлемент КАК ИзделияЗаказчикаИзделияЭлемент
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПриложенияКДоговорам.ИзделияЗаказчика КАК ПриложенияКДоговорамИзделияЗаказчика
		|			ПО ИзделияЗаказчикаИзделияЭлемент.Ссылка = ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика
		|		ПО ИзделияКомплектИзделияЭлемент.ИзделиеЭлемент = ИзделияЗаказчикаИзделияЭлемент.ИзделиеЭлемент
		|ГДЕ
		|	ИзделияКомплектИзделияЭлемент.Ссылка В(&мКомплекты)";
	
	Запрос.УстановитьПараметр("мКомплекты", мКомплекты);
	
	тзИзделияКомплект = Запрос.Выполнить().Выгрузить(); 
	
	
	Возврат Новый Структура("тзИзделияКомплект, тзДатыКомплекта, тзДатыКомплектаМонтаж, тзДатыКомплектаПроизводство"
							,тзИзделияКомплект, тзДатыКомплекта, тзДатыКомплектаМонтаж, тзДатыКомплектаПроизводство);


КонецФункции // ПолучитьДаннеДляДиаграммы()

Процедура ДобавитьКомплект(тз, мКомплекты)
	Для каждого х Из тз Цикл
		Если мКомплекты.Найти(х.Комплект) = Неопределено Тогда
			мКомплекты.Добавить(х.Комплект);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
 
Процедура ВывестиДиаграмуГанта(ТаблицыДляДиаграммы, СтруктураФильтры, ПолеДиаграмма) Экспорт
	Если СтруктураФильтры.СпосовВывода = "на печать" Тогда
		ВывестиГантаНаПечать(ТаблицыДляДиаграммы, ПолеДиаграмма);
	Иначе
		ВывестиГантаНаЭкран(ТаблицыДляДиаграммы, ПолеДиаграмма);
	КонецЕсли;
КонецПроцедуры 

Процедура ВывестиГантаНаЭкран(ТаблицыДляДиаграммы, ПолеДиаграмма)
	
	ПолеДиаграмма.Обновление = Ложь;
	ПолеДиаграмма.Очистить();
	
	тзВыводЗначения		= ТаблицыДляДиаграммы.тзВыводЗначения;
	тзВидовЭлементов 	= ТаблицыДляДиаграммы.тзВидовЭлементов;
	тзВыводЭлемента 	= ТаблицыДляДиаграммы.тзВыводЭлемента;
	тзВыводЭлемента.Сортировать("ПредставлениеЭлемента");
	
	стОтбор 			= Новый Структура("РодительЭлемента", 0);
	стОтборИнтервалы 	= Новый Структура("НомерЭлемента");
	
	НайденыеСтроки = тзВыводЭлемента.НайтиСтроки(стОтбор);
	
	ОписаниеСтрока30 	= Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(30));

	ТаблицаСерии = Новый ТаблицаЗначений;
	ТаблицаСерии.Колонки.Добавить("НаименованиеСерии", ОписаниеСтрока30);
	ТаблицаСерии.Колонки.Добавить("Серия");
	
	Уровень = 0;
	
	Для каждого ТекущиеДанные Из НайденыеСтроки Цикл
		
		стОформление 	= ПолучитьОформлениеДанных(ТекущиеДанные.НомерВидаЭлемента, тзВидовЭлементов);
		
		Точка 				= ПолеДиаграмма.Точки.Добавить();
		Точка.Текст 		= ТекущиеДанные.ПредставлениеЭлемента;
		Точка.ЦветФона 		= стОформление.ЦветФона; 
		Точка.Шрифт 		= омТабличныйДокументКлиентСервер.ИзменитьПараметрШрифта(Точка.Шрифт,,стОформление.Размер);
		Точка.Значение 		= ТекущиеДанные.СсылкаНаОбъект;
		
		Серия 				= ПолучитьСерию(ТаблицаСерии, ПолеДиаграмма, ТекущиеДанные.Серия);
		Значение 			= ПолеДиаграмма.ПолучитьЗначение(Точка, Серия);
		
		стОтборИнтервалы.НомерЭлемента = ТекущиеДанные.НомерЭлемента;
		ВывестиИнтервалы(Значение, тзВыводЗначения.НайтиСтроки(стОтборИнтервалы), тзВидовЭлементов, Точка.Текст);
		
		стОтбор.РодительЭлемента = ТекущиеДанные.НомерЭлемента;
		ВывестиПодчиныеДанные(ПолеДиаграмма, стОтбор, тзВыводЭлемента, тзВыводЗначения, тзВидовЭлементов, ТекущиеДанные.СсылкаНаОбъект, стОтборИнтервалы, ТаблицаСерии, Уровень + 1);
		
	КонецЦикла;
	
	ПолеДиаграмма.Обновление = Истина;

КонецПроцедуры  

Процедура ВывестиПодчиныеДанные(ПолеДиаграмма, стОтбор, тзВыводЭлемента, тзВыводЗначения, тзВидовЭлементов, ТочкаРодитель, стОтборИнтервалы, ТаблицаСерии, Знач Уровень)
	НайденыеСтроки = тзВыводЭлемента.НайтиСтроки(стОтбор);

	Для каждого ТекущиеДанные Из НайденыеСтроки Цикл
		
		стОформление 	= ПолучитьОформлениеДанных(ТекущиеДанные.НомерВидаЭлемента, тзВидовЭлементов);
		
		Точка 				= ПолеДиаграмма.УстановитьТочку(ТекущиеДанные.СсылкаНаОбъект, ТочкаРодитель);
		Точка.Текст 		= ТекущиеДанные.ПредставлениеЭлемента;
		Точка.ЦветФона 		= стОформление.ЦветФона; 
		Точка.Шрифт 		= омТабличныйДокументКлиентСервер.ИзменитьПараметрШрифта(Точка.Шрифт,,стОформление.Размер);
		Серия 				= ПолучитьСерию(ТаблицаСерии, ПолеДиаграмма, ТекущиеДанные.Серия);
		Значение 			= ПолеДиаграмма.ПолучитьЗначение(Точка, Серия);
		
		
		стОтборИнтервалы.НомерЭлемента = ТекущиеДанные.НомерЭлемента;
		ВывестиИнтервалы(Значение, тзВыводЗначения.НайтиСтроки(стОтборИнтервалы), тзВидовЭлементов);
		
		стОтбор.РодительЭлемента = ТекущиеДанные.НомерЭлемента;
		ВывестиПодчиныеДанные(ПолеДиаграмма, стОтбор, тзВыводЭлемента, тзВыводЗначения, тзВидовЭлементов, ТекущиеДанные.СсылкаНаОбъект, стОтборИнтервалы, ТаблицаСерии, Уровень + 1);
		
	КонецЦикла;

КонецПроцедуры

Процедура ВывестиИнтервалы(Значение, НайденыеВыводЗначения, тзВидовЭлементов, Текст = "")

	Для каждого ТекущийИнтервал Из НайденыеВыводЗначения Цикл
		стОформление 	= ПолучитьОформлениеДанных(ТекущийИнтервал.НомерВидаЭлемента, тзВидовЭлементов);
		Интервал				= Значение.Добавить();
		Интервал.Текст 			= Текст;
		Интервал.Расшифровка 	= Новый Структура("Значение", Значение.Точка.Значение);
		Интервал.Цвет 			= стОформление.ЦветФона;
		Интервал.Начало			= ТекущийИнтервал.ДатаНачала;
		Интервал.Конец			= ТекущийИнтервал.ДатаОкончания;
	КонецЦикла;	       

КонецПроцедуры

Функция ПолучитьОформлениеДанных(НомерВидаЭлемента, тзВидовЭлементов)

    НайденаяСтрока = тзВидовЭлементов.Найти(НомерВидаЭлемента, "НомерВидаЭлемента");
	
	Возврат Новый Структура("ЦветФона, Размер", НайденаяСтрока.ЦветФона, НайденаяСтрока.ПараметрыШрифта.РазмерШрифта); 

КонецФункции // ПолучитьОформлениеДанных(ТекущиеДанные.НомерВидаЭлемента, тзВидовЭлементов)()

Функция ПолучитьСерию(ТаблицаСерии, ПолеДиаграмма, НаименованиеСерии)
	
	НайденаяСерия = ТаблицаСерии.Найти(НаименованиеСерии, "НаименованиеСерии");
	
	Если НайденаяСерия = Неопределено Тогда
		
		НайденаяСерия = ТаблицаСерии.Добавить();
		НайденаяСерия.НаименованиеСерии = НаименованиеСерии;
		НайденаяСерия.Серия = ПолеДиаграмма.УстановитьСерию(НаименованиеСерии);
	КонецЕсли;
	
    Возврат НайденаяСерия.Серия;
КонецФункции // ПолучитьСерию(ТаблицаСерии, ПолеДиаграмма, Серия)

Процедура ВывестиГантаНаПечать(ТаблицыДляДиаграммы, ПолеДиаграмма)

	

КонецПроцедуры
 

Функция ПолучитьДатыПроекта(Проект, Помещение = Неопределено, ОбъеденятьВКомпозиции = Истина) Экспорт   	

	Запрос = Новый Запрос;
	
	Запрос.Текст = ?(ОбъеденятьВКомпозиции, ТекстЗапросаОбъеденятьВКомпозицию(), ТекстЗапросаТолькоКомплекты());
	
	Запрос.УстановитьПараметр("Помещение", 	Помещение);
	Запрос.УстановитьПараметр("Проект", 	Проект);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьДатыПроекта()

Функция ТекстЗапросаОбъеденятьВКомпозицию()

    Возврат "ВЫБРАТЬ
            |	ВЫБОР
            |		КОГДА ИзделияКомплект.Родитель.Композиция ЕСТЬ NULL
            |				ИЛИ ИзделияКомплект.Родитель.Композиция = ЗНАЧЕНИЕ(Справочник.Композиции.ПустаяСсылка)
            |			ТОГДА ИзделияКомплект.Ссылка
            |		ИНАЧЕ ИзделияКомплект.Родитель.Композиция
            |	КОНЕЦ КАК Комплект
            |ПОМЕСТИТЬ втКомплекты
            |ИЗ
            |	Справочник.ИзделияКомплект КАК ИзделияКомплект
            |ГДЕ
            |	ИзделияКомплект.Проект = &Проект
            |;
            |
            |////////////////////////////////////////////////////////////////////////////////
            |ВЫБРАТЬ
            |	втКомплекты.Комплект.Ссылка КАК Ссылка,
            |	втКомплекты.Комплект.НаименованиеКомплекта КАК Наименование,
            |	втКомплекты.Комплект.ОсновноеПомещение КАК ОсновноеПомещение,
            |	втКомплекты.Комплект.ОсновноеПомещение.Наименование КАК ОсновноеПомещениеНаименование
            |ПОМЕСТИТЬ втКомплектПомещения
            |ИЗ
            |	втКомплекты КАК втКомплекты
            |ГДЕ
            |	НЕ втКомплекты.Комплект.ЭтоГруппа
            |;
            |
            |////////////////////////////////////////////////////////////////////////////////
            |ВЫБРАТЬ
            |	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаНачалоМонтажа, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоМонтажа,
            |	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаНачалоПроектирования, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоПроектирования,
            |	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаНачалоПроизводства, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоПроизводства,
            |	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаНачалоСогласования, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоСогласования,
            |	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаОкончаниеМонтажа, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеМонтажа,
            |	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаОкончаниеПроектирования, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеПроектирования,
            |	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаОкончаниеПроизводства, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеПроизводства,
            |	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаОкончаниеСогласования, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеСогласования,
            |	ИзделияКомплект.ОсновноеПомещение КАК Помещение,
            |	ИзделияКомплект.ОсновноеПомещениеНаименование КАК ПомещениеНаименование,
            |	ИзделияКомплект.Наименование КАК НаименованиеКомплект,
            |	ИзделияКомплект.Ссылка КАК Комплект
            |ИЗ
            |	втКомплектПомещения КАК ИзделияКомплект
            |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних(, ) КАК ДанныеКомплекта
            |		ПО (ДанныеКомплекта.Комплект = ИзделияКомплект.Ссылка)
            |ГДЕ
            |	ВЫБОР
            |			КОГДА &Помещение = НЕОПРЕДЕЛЕНО
            |				ТОГДА ИСТИНА
            |			ИНАЧЕ ИзделияКомплект.ОсновноеПомещение = &Помещение
            |		КОНЕЦ
            |
            |УПОРЯДОЧИТЬ ПО
            |	ИзделияКомплект.ОсновноеПомещение.Наименование,
            |	ИзделияКомплект.Наименование";

КонецФункции // ТекстЗапросаОбъеденятьВКомпозицию()

Функция ТекстЗапросаТолькоКомплекты()

    Возврат "ВЫБРАТЬ
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаНачалоМонтажа, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоМонтажа,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаНачалоПроектирования, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоПроектирования,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаНачалоПроизводства, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоПроизводства,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаНачалоСогласования, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоСогласования,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаОкончаниеМонтажа, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеМонтажа,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаОкончаниеПроектирования, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеПроектирования,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаОкончаниеПроизводства, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеПроизводства,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаОкончаниеСогласования, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеСогласования,
		|	ИзделияКомплект.ОсновноеПомещение КАК Помещение,
		|	ИзделияКомплект.ОсновноеПомещение.Наименование КАК ПомещениеНаименование,
		|	ИзделияКомплект.Наименование КАК НаименованиеКомплект,
		|	ИзделияКомплект.Ссылка КАК Комплект
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних(, ) КАК ДанныеКомплекта
		|		ПО (ДанныеКомплекта.Комплект = ИзделияКомплект.Ссылка)
		|ГДЕ
		|	ИзделияКомплект.Проект = &Проект
		|	И НЕ ИзделияКомплект.ЭтоГруппа
		|	И ВЫБОР
		|			КОГДА &Помещение = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ИзделияКомплект.ОсновноеПомещение = &Помещение
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИзделияКомплект.ОсновноеПомещение.Наименование,
		|	ИзделияКомплект.Наименование";

КонецФункции // ТекстЗапросаТолькоКомплекты()


Функция ПолучитьИнтервалыПомещения(тзДатыПроекта) Экспорт
	
	ПустаяДата = Дата(1,1,1);

	тзИнтервалыПомещения = Новый ТаблицаЗначений;
	тзИнтервалыПомещения.Колонки.Добавить("Помещение", 					Новый ОписаниеТипов("СправочникСсылка.Помещения"));
	тзИнтервалыПомещения.Колонки.Добавить("ПомещениеНаименование", 		Новый ОписаниеТипов("Строка"));
	тзИнтервалыПомещения.Колонки.Добавить("НачалоПроектирования", 		Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("ОкончаниеПроектирования", 	Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("НачалоСогласования", 		Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("ОкончаниеСогласования", 		Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("НачалоПроизводства", 		Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("ОкончаниеПроизводства", 		Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("НачалоМонтажа", 				Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("ОкончаниеМонтажа", 			Новый ОписаниеТипов("Дата"));
    тзИнтервалыПомещения.Индексы.Добавить("Помещение");
	
	Для каждого х Из тзДатыПроекта Цикл
		
		НайденаяСтрока = тзИнтервалыПомещения.Найти(х.Помещение);
		
		Если НайденаяСтрока = Неопределено Тогда
		
			ЗаполнитьЗначенияСвойств(тзИнтервалыПомещения.Добавить(), х);
		    Продолжить;
	
		КонецЕсли;
		
		АнализДатыНачала("НачалоПроектирования", 		ПустаяДата, х, НайденаяСтрока);
		АнализДатыНачала("НачалоСогласования", 			ПустаяДата, х, НайденаяСтрока);
		АнализДатыНачала("НачалоПроизводства", 			ПустаяДата, х, НайденаяСтрока);
		АнализДатыНачала("НачалоМонтажа", 				ПустаяДата, х, НайденаяСтрока);
		
		АнализДатыОкончания("ОкончаниеПроектирования", 	х, НайденаяСтрока);
		АнализДатыОкончания("ОкончаниеСогласования", 	х, НайденаяСтрока);
		АнализДатыОкончания("ОкончаниеПроизводства", 	х, НайденаяСтрока);
		АнализДатыОкончания("ОкончаниеМонтажа", 		х, НайденаяСтрока);
		
		
	КонецЦикла;
	
	Возврат тзИнтервалыПомещения; 

КонецФункции 

Процедура АнализДатыОкончания(ИмяКолонки, СтрокаДатыПроекта, СтрокаИнтервалыПомещения)
	
	Если СтрокаИнтервалыПомещения[ИмяКолонки] < СтрокаДатыПроекта[ИмяКолонки] Тогда
		
		СтрокаИнтервалыПомещения[ИмяКолонки] = СтрокаДатыПроекта[ИмяКолонки];
		
	КонецЕсли;
	
КонецПроцедуры

Процедура АнализДатыНачала(ИмяКолонки, ПустаяДата, СтрокаДатыПроекта, СтрокаИнтервалыПомещения)
	
	Если СтрокаДатыПроекта[ИмяКолонки] = ПустаяДата Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрокаИнтервалыПомещения[ИмяКолонки] = ПустаяДата Тогда
		
		СтрокаИнтервалыПомещения[ИмяКолонки] = СтрокаДатыПроекта[ИмяКолонки];
		
	ИначеЕсли СтрокаИнтервалыПомещения[ИмяКолонки] > СтрокаДатыПроекта[ИмяКолонки] Тогда
		
		СтрокаИнтервалыПомещения[ИмяКолонки] = СтрокаДатыПроекта[ИмяКолонки];
		
	КонецЕсли;
	
КонецПроцедуры

 
