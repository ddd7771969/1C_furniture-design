///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция КлассификаторПолномочий() Экспорт
	
	Архив = Справочники.МашиночитаемыеДоверенности.ПолучитьМакет("КлассификаторПолномочий");
	ИмяАрхива = ПолучитьИмяВременногоФайла("zip");
	Архив.Записать(ИмяАрхива);
		
	КаталогОбновлений = ФайловаяСистема.СоздатьВременныйКаталог(
		Строка(Новый УникальныйИдентификатор));
	
	ЧтениеZipФайла = Новый ЧтениеZipФайла(ИмяАрхива);
	Для Каждого ЭлементАрхива Из ЧтениеZipФайла.Элементы Цикл
		ИмяФайла = ЭлементАрхива.Имя;
		ЧтениеZipФайла.Извлечь(ЭлементАрхива, КаталогОбновлений);
	КонецЦикла;
	ЧтениеZipФайла.Закрыть();
	
	ФайлДанных = Новый ТекстовыйДокумент;
	ФайлДанных.Прочитать(КаталогОбновлений + ИмяФайла, "UTF-8");
			
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ФайлДанных.ПолучитьТекст());
	Результат = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	
	ФайловаяСистема.УдалитьВременныйКаталог(КаталогОбновлений);
	ФайловаяСистема.УдалитьВременныйФайл(ИмяАрхива);
	
	Возврат Результат;
	
КонецФункции

Функция КлассификаторОграничений() Экспорт
	
	Архив = Справочники.МашиночитаемыеДоверенности.ПолучитьМакет("Ограничения");
	ИмяАрхива = ПолучитьИмяВременногоФайла("zip");
	Архив.Записать(ИмяАрхива);
		
	КаталогОбновлений = ФайловаяСистема.СоздатьВременныйКаталог(
		Строка(Новый УникальныйИдентификатор));
	
	ЧтениеZipФайла = Новый ЧтениеZipФайла(ИмяАрхива);
	Для Каждого ЭлементАрхива Из ЧтениеZipФайла.Элементы Цикл
		ИмяФайла = ЭлементАрхива.Имя;
		ЧтениеZipФайла.Извлечь(ЭлементАрхива, КаталогОбновлений);
	КонецЦикла;
	ЧтениеZipФайла.Закрыть();
	
	ФайлДанных = Новый ТекстовыйДокумент;
	ФайлДанных.Прочитать(КаталогОбновлений + ИмяФайла, "windows-1251");
		
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Код");
	Таблица.Колонки.Добавить("Наименование");
	Таблица.Колонки.Добавить("ОписаниеТипа");
	Таблица.Колонки.Добавить("ФЛК");
	Таблица.Колонки.Добавить("Описание");
	Таблица.Колонки.Добавить("ДействуетС");
	Таблица.Колонки.Добавить("Отозвано");
	Таблица.Колонки.Добавить("ЭтоТекстовоеЗначение");
	Таблица.Колонки.Добавить("КоличествоСимволов");
	
	Для НомерСтроки = 2 По ФайлДанных.КоличествоСтрок() Цикл
		
		СтрокаДанных = ФайлДанных.ПолучитьСтроку(НомерСтроки);
		СтрокаДанныхМассив = СтрРазделить(СтрокаДанных, ";", Истина);
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.Код = СтрокаДанныхМассив[0];
		НоваяСтрока.Наименование = СтрокаДанныхМассив[1];
		НоваяСтрока.Описание = СтрокаДанныхМассив[2];
		Формат = СтрокаДанныхМассив[3];
		НоваяСтрока.ФЛК = СтрокаДанныхМассив[4];
		НоваяСтрока.ДействуетС = ОбщегоНазначенияКлиентСервер.СтрокаВДату(СтрокаДанныхМассив[5]);
		НоваяСтрока.Отозвано = ?(СтрокаДанныхМассив[6] = "Да", Истина, Ложь);
		НоваяСтрока.ЭтоТекстовоеЗначение = ?(СтрокаДанныхМассив[7] = "Да", Ложь, Истина);
		
		КоличествоСимволов = Неопределено;
		ЛеваяСкобка = СтрНайти(Формат, "(");
		Если ЛеваяСкобка <> 0 Тогда
			ПраваяСкобка = СтрНайти(Формат, ")");
			КоличествоСимволов = Число(Сред(Формат, ЛеваяСкобка + 1, ПраваяСкобка - ЛеваяСкобка - 1));
		КонецЕсли;
		
		Если СтрНачинаетсяС(НРег(Формат), "integer") Тогда
			Если КоличествоСимволов = Неопределено Тогда
				КоличествоСимволов = 15;
			КонецЕсли;
			ОписаниеТипов = ОбщегоНазначения.ОписаниеТипаЧисло(КоличествоСимволов);
		Иначе
			Если КоличествоСимволов = Неопределено Тогда
				КоличествоСимволов = 255;
			КонецЕсли;
			ОписаниеТипов = ОбщегоНазначения.ОписаниеТипаСтрока(КоличествоСимволов);
		КонецЕсли;
		
		НоваяСтрока.ОписаниеТипа = ОписаниеТипов;

	КонецЦикла;
	
	ФайловаяСистема.УдалитьВременныйКаталог(КаталогОбновлений);
	ФайловаяСистема.УдалитьВременныйФайл(ИмяАрхива);
	
	Возврат Таблица;
	
КонецФункции

Функция ЗначенияОграничений() Экспорт
	
	Архив = Справочники.МашиночитаемыеДоверенности.ПолучитьМакет("ЗначенияОграничений");
	ИмяАрхива = ПолучитьИмяВременногоФайла("zip");
	Архив.Записать(ИмяАрхива);
		
	КаталогОбновлений = ФайловаяСистема.СоздатьВременныйКаталог(
		Строка(Новый УникальныйИдентификатор));
	
	ЧтениеZipФайла = Новый ЧтениеZipФайла(ИмяАрхива);
	Для Каждого ЭлементАрхива Из ЧтениеZipФайла.Элементы Цикл
		ИмяФайла = ЭлементАрхива.Имя;
		ЧтениеZipФайла.Извлечь(ЭлементАрхива, КаталогОбновлений);
	КонецЦикла;
	ЧтениеZipФайла.Закрыть();
	
	ФайлДанных = Новый ТекстовыйДокумент;
	ФайлДанных.Прочитать(КаталогОбновлений + ИмяФайла, "windows-1251");
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Код");
	Таблица.Колонки.Добавить("Наименование");
	Таблица.Колонки.Добавить("КодОграничения");
	Таблица.Колонки.Добавить("ДействуетС");
	Таблица.Колонки.Добавить("Отозвано");
	
	Для НомерСтроки = 2 По ФайлДанных.КоличествоСтрок() Цикл
		
		СтрокаДанных = ФайлДанных.ПолучитьСтроку(НомерСтроки);
		СтрокаДанныхМассив = СтрРазделить(СтрокаДанных, ";", Истина);
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.Код = СтрокаДанныхМассив[0];
		НоваяСтрока.Наименование = СтрокаДанныхМассив[1];
		НоваяСтрока.КодОграничения = СтрокаДанныхМассив[2];
		НоваяСтрока.ДействуетС = ОбщегоНазначенияКлиентСервер.СтрокаВДату(СтрокаДанныхМассив[3]);
		НоваяСтрока.Отозвано = ?(СтрокаДанныхМассив[4] = "Да", Истина, Ложь);
		
	КонецЦикла;
	
	ФайловаяСистема.УдалитьВременныйКаталог(КаталогОбновлений);
	ФайловаяСистема.УдалитьВременныйФайл(ИмяАрхива);
	
	Возврат Таблица;
	
КонецФункции

#КонецОбласти