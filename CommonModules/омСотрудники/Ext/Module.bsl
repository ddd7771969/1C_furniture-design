
Функция ПолучитьТекущегоСотрудника() Экспорт

	Возврат ПараметрыСеанса.ТекущийПользователь.Сотрудник;
	
КонецФункции // ПолучитьТекущегоСотрудника()

Функция ПолучитьТекущегоПользователя() Экспорт

	Возврат ПараметрыСеанса.ТекущийПользователь;

КонецФункции // ПолучитьТекущегоПользователя()


// Если ВозвратВТаблице истина, то всегда возвращается таблица значений,
//если ложь, 
//	то тогда если мСотрудники массив возвращается массив структур,
//	если элемент справочника, то структура
Функция ПолучитьКонтактыСотрудников(мСотрудники, ВозвратВТаблице = Ложь) Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиКонтактнаяИнформация.Ссылка КАК Сотрудник,
		|	СотрудникиКонтактнаяИнформация.АдресЭП КАК АдресЭП,
		|	СотрудникиКонтактнаяИнформация.НомерТелефона КАК НомерТелефона
		|ИЗ
		|	Справочник.Сотрудники.КонтактнаяИнформация КАК СотрудникиКонтактнаяИнформация
		|ГДЕ
		|	СотрудникиКонтактнаяИнформация.Ссылка В(&мСотрудники)
		|	И СотрудникиКонтактнаяИнформация.Тип В(&Тип)";
	
	Запрос.УстановитьПараметр("мСотрудники", мСотрудники);
	Запрос.УстановитьПараметр("Тип", ПолучитьТипыКонтактнойИнформации());
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если ВозвратВТаблице Тогда
	
		ДанныеВозврата = Новый ТаблицаЗначений;
		
		ДанныеВозврата.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		ДанныеВозврата.Колонки.Добавить("АдресЭП", Новый ОписаниеТипов("Строка"));
		ДанныеВозврата.Колонки.Добавить("НомерТелефона", Новый ОписаниеТипов("Строка"));
	
	Иначе
		Если ТипЗнч(мСотрудники) = Тип("Массив") Тогда
			ВозвратВМассиве = Истина;
			
			ДанныеВозврата = Новый Массив;
		Иначе
			ВозвратВМассиве = Ложь;
			
			ДанныеВозврата = Новый Структура("АдресЭП,НомерТелефона","","");
		КонецЕсли;
	КонецЕсли;
	
	
	Пока Выборка.Следующий() Цикл
		Если ВозвратВТаблице Тогда
			НайденаяСтрока = ДанныеВозврата.Найти(Выборка.Сотрудник, "Сотрудник");
			
			Если НайденаяСтрока = Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ДанныеВозврата.Добавить(), Выборка);
			Иначе	
				Если НЕ ПустаяСтрока(Выборка.АдресЭП) Тогда
					НайденаяСтрока.АдресЭП 			= Выборка.АдресЭП;
				КонецЕсли;	
				Если НЕ ПустаяСтрока(Выборка.НомерТелефона) Тогда
					НайденаяСтрока.НомерТелефона 	= Выборка.НомерТелефона;
				КонецЕсли;	
			КонецЕсли;

		Иначе
			
			Если ВозвратВМассиве Тогда
				СотрудникУжеСуществует = Ложь;
				
				Для каждого текСтруктура Из ДанныеВозврата Цикл
					Если текСтруктура.Сотрудник = Выборка.Сотрудник Тогда
						Если НЕ ПустаяСтрока(Выборка.АдресЭП) Тогда
							текСтруктура.АдресЭП 			= Выборка.АдресЭП;
						КонецЕсли;	
						Если НЕ ПустаяСтрока(Выборка.НомерТелефона) Тогда
							текСтруктура.НомерТелефона 	= Выборка.НомерТелефона;
						КонецЕсли;	
					КонецЕсли;
					СотрудникУжеСуществует = Истина;
				КонецЦикла; 
				
				Если НЕ СотрудникУжеСуществует Тогда
					стДанные = Новый Структура("АдресЭП,НомерТелефона,Сотрудник","","");
					ЗаполнитьЗначенияСвойств(стДанные, Выборка);
					ДанныеВозврата.Добавить(стДанные);
				КонецЕсли;
			Иначе
				Если НЕ ПустаяСтрока(Выборка.АдресЭП) Тогда
					ДанныеВозврата.АдресЭП 			= Выборка.АдресЭП;
				КонецЕсли;	
				Если НЕ ПустаяСтрока(Выборка.НомерТелефона) Тогда
					ДанныеВозврата.НомерТелефона 	= Выборка.НомерТелефона;
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДанныеВозврата; 
КонецФункции // ПолучитьКонтактыСотрудников()

Функция ПолучитьТипыКонтактнойИнформации() Экспорт

	мТипыИнформации = Новый Массив(2);
	
	мТипыИнформации[0] = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
	мТипыИнформации[1] = Перечисления.ТипыКонтактнойИнформации.Телефон;
	
	Возврат мТипыИнформации; 	

КонецФункции // ПолучитьТипыКонтактнойИнформации()

//ТекущийОбъект Композиция, ИзделиеКомплект
Функция СотрудникиОбъекта(ТекущийОбъект, ВозвращатьПользователей = Истина) Экспорт
	
	ПустойСотрудник = ?(ВозвращатьПользователей, Справочники.Пользователи.ПустаяСсылка(), Справочники.Сотрудники.ПустаяСсылка());
	
	стСотрудники = Новый Структура("Конструктор, Менеджер, ВедущийКонструктор, Дизайнер"
		, ПустойСотрудник, ПустойСотрудник, ПустойСотрудник, ПустойСотрудник);
	
	СотрудникиКомплекты(ТекущийОбъект, стСотрудники);
	
	Если ВозвращатьПользователей Тогда
		
		мСотрудники = Новый Массив;
		
		Если ЗначениеЗаполнено(стСотрудники.Конструктор) Тогда
			мСотрудники.Добавить(стСотрудники.Конструктор);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(стСотрудники.Менеджер) Тогда
			мСотрудники.Добавить(стСотрудники.Менеджер);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(стСотрудники.ВедущийКонструктор) Тогда
			мСотрудники.Добавить(стСотрудники.ВедущийКонструктор);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(стСотрудники.Дизайнер) Тогда
			мСотрудники.Добавить(стСотрудники.Дизайнер);
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Пользователи.Сотрудник КАК Сотрудник,
			|	Пользователи.Ссылка КАК Пользователь
			|ИЗ
			|	Справочник.Пользователи КАК Пользователи
			|ГДЕ
			|	Пользователи.Сотрудник В(&мСотрудники)";
		
		Запрос.УстановитьПараметр("мСотрудники", мСотрудники);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Для каждого текЭлемент Из стСотрудники Цикл
				
				Если текЭлемент.Значение = Выборка.Сотрудник Тогда
					
					стСотрудники[текЭлемент.Ключ] = Выборка.Пользователь;
					Прервать;
					
				КонецЕсли;	
				
			КонецЦикла;
			
		КонецЦикла;
		
		
	КонецЕсли;
	
	
	Возврат стСотрудники;
	
КонецФункции // СотрудникиОбъекта()

Функция СотрудникиКомплекты(Комплект, стСотрудники)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ДанныеКомплектаСрезПоследних.Конструктор, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)) КАК Конструктор,
	|	ИзделияКомплект.Проект.ВедущийКонструктор КАК ВедущийКонструктор,
	|	ИзделияКомплект.Проект.Дизайнер КАК Дизайнер,
	|	ИзделияКомплект.Проект.Менеджер КАК Менеджер
	|ИЗ
	|	Справочник.ИзделияКомплект КАК ИзделияКомплект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект = &Комплект) КАК ДанныеКомплектаСрезПоследних
	|		ПО (ДанныеКомплектаСрезПоследних.Комплект = ИзделияКомплект.Ссылка)
	|ГДЕ
	|	ИзделияКомплект.Ссылка = &Комплект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзменениеСотрудникаСрезПоследних.Менеджер КАК Менеджер,
	|	ИзменениеСотрудникаСрезПоследних.ВедущийКонструктор КАК ВедущийКонструктор,
	|	ИзменениеСотрудникаСрезПоследних.Дизайнер КАК Дизайнер
	|ИЗ
	|	РегистрСведений.ИзменениеСотрудника.СрезПоследних(, ОбъектИзменения = &Комплект) КАК ИзменениеСотрудникаСрезПоследних";
	
	Запрос.УстановитьПараметр("Комплект", Комплект);
	
	Если ТипЗнч(Комплект) = Тип("СправочникСсылка.Композиции") Тогда
	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.ИзделияКомплект", "Справочник.Композиции");	
	
	КонецЕсли;
	
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[0].Выбрать();
	тз = Результат[1].Выгрузить();
	
	Пока Выборка.Следующий() Цикл
		
		Если тз.Количество() = 0 Тогда
			
			стСотрудники.Конструктор 		= Выборка.Конструктор;
			стСотрудники.ВедущийКонструктор	= Выборка.ВедущийКонструктор;
			стСотрудники.Менеджер 			= Выборка.Менеджер;
			стСотрудники.Дизайнер 			= Выборка.Дизайнер;
			
		Иначе
			
			стСотрудники.ВедущийКонструктор	= ?(ЗначениеЗаполнено(тз[0].ВедущийКонструктор), 	тз[0].ВедущийКонструктор, 		Выборка.ВедущийКонструктор);
			стСотрудники.Менеджер 			= ?(ЗначениеЗаполнено(тз[0].Менеджер), 				тз[0].Менеджер, 				Выборка.Менеджер);
			стСотрудники.Дизайнер 			= ?(ЗначениеЗаполнено(тз[0].Дизайнер), 				тз[0].Дизайнер, 				Выборка.Дизайнер);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат стСотрудники;
	
КонецФункции // СотрудникиКомпозиции(ТекущийОбъект, стСотрудники)

Функция ПолучитьКонструктораРазработки(Задача) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеКомплекта.Конструктор КАК Конструктор
	|ИЗ
	|	Задача.Задачи_ПКД КАК Задачи_ПКД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних КАК ДанныеКомплекта
	|		ПО Задачи_ПКД.ОбъектТЗ = ДанныеКомплекта.Комплект
	|ГДЕ
	|	Задачи_ПКД.Ссылка = &Задача";
	
	Если ТипЗнч(Задача) = Тип("ЗадачаСсылка.Задачи_РКД") Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Задачи_ПКД", "Задачи_РКД");	
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Задача", Задача);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат омСотрудники.ПолучитьПользователяПоСотруднтку(Выборка.Конструктор);
	КонецЦикла;
	
	Возврат Справочники.Пользователи.ПустаяСсылка();
	
КонецФункции // ПолучитьКонструктораРазработки()

Функция ПолучитьКонструктораРазработкиЗадач(мЗадачи) Экспорт
	
	Если НЕ ТипЗнч(мЗадачи) = Тип("Массив") Тогда
	
		Возврат Новый ТаблицаЗначений;	
	
	КонецЕсли;
	
	Если мЗадачи.Количество() = 0 Тогда
	
		Возврат Новый ТаблицаЗначений;	
	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеКомплекта.Конструктор КАК Конструктор,
		|	Задачи_ПКД.Ссылка КАК Задача
		|ИЗ
		|	Задача.Задачи_ПКД КАК Задачи_ПКД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта КАК ДанныеКомплекта
		|		ПО Задачи_ПКД.ОбъектТЗ = ДанныеКомплекта.Комплект
		|ГДЕ
		|	Задачи_ПКД.Ссылка В(&мЗадачи)";
	
	Если ТипЗнч(мЗадачи[0]) = Тип("ЗадачаСсылка.Задачи_РКД") Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Задачи_ПКД", "Задачи_РКД");	
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("мЗадачи", мЗадачи);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьКонструктораРазработкиЗадач(Задачи)

Функция ПолучитьСотруднткаПоПользователю(Пользователь) Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.Сотрудник КАК Сотрудник
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Ссылка = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Сотрудник;
	КонецЦикла;
	
	
КонецФункции // ПолучитьПользователяПоСотруднику()

Функция ПолучитьПользователяПоСотруднтку(Сотрудник) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
		
		Возврат Справочники.Пользователи.ПустаяСсылка();	
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Сотрудник = &Сотрудник";
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка;
	КонецЦикла;
	
КонецФункции // ПолучитьПользователяПоСотруднтку(Сотрудник)
