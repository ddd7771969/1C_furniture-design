
Функция ПервыйУровень(СсылкаРодитель, Композиция = Неопределено) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Композиция КАК Композиция
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|ГДЕ
		|	ИзделияКомплект.Ссылка = &Ссылка
		|	И НЕ ИзделияКомплект.Родитель.Наименование ЕСТЬ NULL
		|	И ИзделияКомплект.Родитель.Родитель.Наименование ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаРодитель);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Композиция = Выборка.Композиция;
		Возврат Истина;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции // ПервыйУровень()

Функция ПолучитьДанныеДляКода(ОсновноеИзделие, Проект) Экспорт
	
	Запрос = Новый Запрос;
	КоличествоВПакете = 0;

	Если ЗначениеЗаполнено(Проект) Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Проекты.Номер КАК НомерПроекта
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	Проекты.Ссылка = &Проект";
		
		Запрос.УстановитьПараметр("Проект", Проект);
		
		КоличествоВПакете = КоличествоВПакете + 1;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОсновноеИзделие) Тогда
		
		Если ЗначениеЗаполнено(Проект) Тогда
			Запрос.Текст = Запрос.Текст + "
			|;
			|";
		КонецЕсли; 
		
		Запрос.Текст = 	Запрос.Текст + 
		"ВЫБРАТЬ
		|	ИзделияЭлемент.Помещения.НомерПомещения КАК НомерПомещения,
		|	ИзделияЭлемент.НомерПоПорядку КАК НомерИзделия
		|ИЗ
		|	Справочник.ИзделияЭлемент КАК ИзделияЭлемент
		|ГДЕ
		|	ИзделияЭлемент.Ссылка = &ОсновноеИзделие";
		Запрос.УстановитьПараметр("ОсновноеИзделие", ОсновноеИзделие);

		КоличествоВПакете = КоличествоВПакете + 1;
	КонецЕсли;
	
	стВозврата = Новый Структура("НомерПроекта,НомерПомещения,НомерИзделия","","",0);
	
	Если КоличествоВПакете = 1 Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(стВозврата, Выборка);
		КонецЦикла;
	Иначе
		Пакеты = Запрос.ВыполнитьПакет();
		
		Выборка = Пакеты[0].Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(стВозврата, Выборка);
		КонецЦикла;
		
		Выборка = Пакеты[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(стВозврата, Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	стВозврата.НомерИзделия = Формат(стВозврата.НомерИзделия, "ЧЦ=3; ЧВН=; ЧГ=0");
	
    Возврат стВозврата;
	
КонецФункции // ПолучитьДанныеДляКода()

Функция ПолучитьДанныеДляНомераКомплекта(ИзделиеКомплект) Экспорт

	ОсновноеИзделие = Неопределено;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплектИзделияЭлемент.ИзделиеЭлемент КАК ИзделиеЭлемент,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Проект КАК Проект
		|ИЗ
		|	Справочник.ИзделияКомплект.ИзделияЭлемент КАК ИзделияКомплектИзделияЭлемент
		|ГДЕ
		|	ИзделияКомплектИзделияЭлемент.Ссылка = &ИзделиеКомплект";
	
	Запрос.УстановитьПараметр("ИзделиеКомплект", ИзделиеКомплект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОсновноеИзделие = Выборка.ИзделиеЭлемент;
		Проект 			= Выборка.Проект;
	КонецЦикла;
	
	Если ОсновноеИзделие = Неопределено Тогда
		Возврат Неопределено;	
	КонецЕсли; 
	
	Возврат ПолучитьДанныеДляКода(ОсновноеИзделие, Проект);
	
КонецФункции // ПолучитьДанныпеДляНомераКомплекта(ИзделиеКомплект)

Процедура УстановитьКодПДМКодКомплекта(КодКомплект, Код_в_PDM) Экспорт

	Если ТипЗнч(КодКомплект) = Тип("Строка") Тогда
		ИзделиеКомплект = Справочники.ИзделияКомплект.НайтиПоКоду(КодКомплект);
	Иначе
		ИзделиеКомплект = КодКомплект;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИзделиеКомплект) Тогда
		
		ПроектСсылка = ИзделиеКомплект.Проект;
		
		МенеджерЗаписи = РегистрыСведений.ДатыКомплекта.СоздатьМенеджерЗаписи(); 
		
		МенеджерЗаписи.Проект 		= ПроектСсылка;
		МенеджерЗаписи.Комплект 	= ИзделиеКомплект;
		
		МенеджерЗаписи.Прочитать();
		
		МенеджерЗаписи.Проект 		= ПроектСсылка;
		МенеджерЗаписи.Комплект 	= ИзделиеКомплект;
		МенеджерЗаписи.Код_в_PDM 	= Код_в_PDM;
		
		МенеджерЗаписи.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

