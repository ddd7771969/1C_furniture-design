Функция ПолучитьОбъекты(Сделка = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Сделка) Тогда
		мВозврата = Новый Массив(1);
		мВозврата[0] = Сделка;
		
		Возврат мВозврата;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Проекты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	(Проекты.ПометкаУдаления = ЛОЖЬ
		|				И Проекты.СтатусОбъекта.Завершен = ЛОЖЬ
		|			ИЛИ Проекты.СтатусОбъекта.Завершен ЕСТЬ NULL)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Проекты.Наименование";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции // ПолучитьОбъекты()

Процедура ЗаполнитьФактическиеДатыЭтапов(тзЭтапПлан,ИзделияЗаказчика,стЭтапы)
	
	
	мЭтапыПоследовательно = РегистрыСведений.ПоследовательностьЭтаповКанбан.ПолучитьЭтапыСтадии();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнформацияКарточкиКанбан.СсылкаНаОбъект КАК Комплект,
		|	ИсторияДвижениеКарточки.Период КАК Период,
		|	ИсторияДвижениеКарточки.НачальныйЭтап КАК НачальныйЭтап,
		|	ИсторияДвижениеКарточки.ТекущийЭтап КАК ТекущийЭтап
		|ИЗ
		|	РегистрСведений.ИнформацияКарточкиКанбан КАК ИнформацияКарточкиКанбан
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияДвижениеКарточки КАК ИсторияДвижениеКарточки
		|		ПО ИнформацияКарточкиКанбан.КарточкаКанбан = ИсторияДвижениеКарточки.КарточкаКанбана
		|ГДЕ
		|	ИнформацияКарточкиКанбан.СсылкаНаОбъект В(&СсылкаНаОбъект)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	Запрос.УстановитьПараметр("СсылкаНаОбъект", омТЧ_НаКлиентеСервере.КолонкуВМассив(ИзделияЗаказчика,"Комплект"));
	
	тзПериодыЭтапов = Запрос.Выполнить().Выгрузить();
	тзПериодыЭтапов.Индексы.Добавить("Комплект,НачальныйЭтап");
	тзПериодыЭтапов.Индексы.Добавить("Комплект,ТекущийЭтап");
	
	мНастройки 						= стЭтапы.СоответствиеЭтапов.мНастройки;
	стМассивыДиапозонТиповКомплекта = стЭтапы.стМассивыДиапозонТиповКомплекта;
	
	стОтборНачало 		= Новый Структура("Комплект,ТекущийЭтап", );
	стОтборОкончание 	= Новый Структура("Комплект,НачальныйЭтап", );

	ПустаяДата = Дата(1,1,1);
	
	Для каждого строкаПлана Из тзЭтапПлан Цикл
		НСИзделие = ИзделияЗаказчика.Найти(строкаПлана.ИзделиеЭлемент,"ИзделиеЭлемент");	
		Если НСИзделие = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Комплект 		= НСИзделие.Комплект;
		ТипКомплекта 	= НСИзделие.ТипКомплекта;
		ТекущийЭтап 	= НСИзделие.Этап;
		
		Для Каждого Элемент из стЭтапы.СоответствиеЭтапов Цикл
			Индекс = мНастройки.Найти(Элемент.Ключ);
			Если Индекс = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстовыйИндекс = Строка(Индекс + 1);
			
			Если СтрНайти(стМассивыДиапозонТиповКомплекта[ТипКомплекта],ТекстовыйИндекс) = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПервыйЭтапРаньше(ТекущийЭтап,Элемент.Значение,мЭтапыПоследовательно) > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			стОтборНачало.ТекущийЭтап 	= Элемент.Значение;
			стОтборНачало.Комплект 		= Комплект;
			
			НайденыеСтроки = тзПериодыЭтапов.НайтиСтроки(стОтборНачало);
			
			КоличествоНайденых = НайденыеСтроки.Количество();
			
			строкаПлана["КоличествоПереходов" + ТекстовыйИндекс] = КоличествоНайденых;

			Если КоличествоНайденых = 0 Тогда
				ДатаНачала = ПустаяДата;
			Иначе
				ДатаНачала = НайденыеСтроки[0].Период;
			КонецЕсли;
			
			стОтборОкончание.НачальныйЭтап 	= Элемент.Значение;
			стОтборОкончание.Комплект 		= Комплект;
			
			НайденыеСтроки = тзПериодыЭтапов.НайтиСтроки(стОтборОкончание);
			
			КоличествоНайденых = НайденыеСтроки.Количество();
			
			Если КоличествоНайденых = 0 Тогда
				ДатаОкончания = ПустаяДата;
			Иначе
				ДатаОкончания = НайденыеСтроки[КоличествоНайденых - 1].Период;
			КонецЕсли;
			
			строкаПлана["ДатаФакт" + ТекстовыйИндекс] = Формат(ДатаНачала,"ДФ=dd.MM.yy") + "-" + Формат(ДатаОкончания,"ДФ=dd.MM.yy");
			
		КонецЦикла;	
		
	КонецЦикла;
	
	
	
КонецПроцедуры // ПолучитьРеальныеДатыЭтапов() 

Функция ПервыйЭтапРаньше(ПервыйЭтап,ВторойЭтап,мЭтапыПоследовательно)
	Если ПервыйЭтап = ВторойЭтап Тогда
		Возврат 0;
	КонецЕсли;
	
	Для каждого текЭтап Из мЭтапыПоследовательно Цикл
		Если текЭтап = ПервыйЭтап Тогда
			Возврат -1;
		ИначеЕсли текЭтап = ВторойЭтап Тогда
			Возврат 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат 2;
	
КонецФункции // ПервыйЭтапРаньше(ТекущийЭтап,Элемент.Значение,мЭтапыПоследовательно)

Функция ПолучитьЭтапПлан(ИзделияЗаказчика,Стадии)
	
	стМассивыДиапозонТиповКомплекта = ПолучитьДиапозоныКомплентов();
	
	ВнешОбр = Отчеты.ИзделияПоЭтапам.Создать();
	стЭтапы = ВнешОбр.ПолучитьДанныеНастройки();
	мНастройки = стЭтапы.мНастройки;
	
	тзЭтапПлан = ИнициализироватьТаблицуПлан();
	
	стОтбор = Новый Структура("Этап,Комплект",);
	
	Для Каждого текИзделие из ИзделияЗаказчика Цикл 
		
		Если ПустаяСтрока(текИзделие.ТипКомплекта) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Элемент из стЭтапы Цикл
			Индекс = мНастройки.Найти(Элемент.Ключ);
			Если Индекс = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстовыйИндекс = Строка(Индекс + 1);
			
			Если СтрНайти(стМассивыДиапозонТиповКомплекта[текИзделие.ТипКомплекта],ТекстовыйИндекс) = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			стОтбор.Этап 		= Элемент.Значение;
			стОтбор.Комплект 	= текИзделие.Комплект;
			
			ДобавитьданныеВТЗ(тзЭтапПлан,текИзделие.ИзделиеЭлемент,Стадии,ТекстовыйИндекс,стОтбор); 
		КонецЦикла;	
	КонецЦикла;	 
	
	Возврат Новый Структура("тзЭтапПлан,стМассивыДиапозонТиповКомплекта,СоответствиеЭтапов", тзЭтапПлан,стМассивыДиапозонТиповКомплекта,стЭтапы);
	
КонецФункции // ПолучитьЭтапПлан(ИзделияЗаказчика,Стадии) 

Процедура ДобавитьданныеВТЗ(тзЭтапПлан,ИзделиеЗаказчика,Стадии,ТекстовыйИндекс,стОтбор)
	НайденыеСтроки = Стадии.НайтиСтроки(стОтбор);
	Если НайденыеСтроки.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;
	
	НайденаяСтрока = тзЭтапПлан.Найти(ИзделиеЗаказчика);
	
	Если НайденаяСтрока = Неопределено Тогда
		НайденаяСтрока = тзЭтапПлан.Добавить();
		НайденаяСтрока.ИзделиеЭлемент = ИзделиеЗаказчика;
	КонецЕсли;
	
	НайденаяСтрока["ДатаПлан" + ТекстовыйИндекс] = Формат(НайденыеСтроки[0].ДатаНачала,"ДФ=dd.MM.yy") + "-" + Формат(НайденыеСтроки[0].ДатаОкончания,"ДФ=dd.MM.yy");
КонецПроцедуры // ДобавитьданныеВТЗ(тзЭтапПлан,ИзделияЗаказчика,Стадии,ТекстовыйИндекс,Элемент.Значение)()

Функция ПолучитьДиапозоныКомплентов()
 	стМассивыДиапозонТиповКомплекта = Новый Структура("Комплект,Производство,Монтаж",);
	стМассивыДиапозонТиповКомплекта.Комплект = "1,2,3,4,5,6,7,8,9,10";	
	стМассивыДиапозонТиповКомплекта.Производство = "11,12,13,14,15";	
	стМассивыДиапозонТиповКомплекта.Монтаж = "16,17";	
	
	Возврат стМассивыДиапозонТиповКомплекта;
КонецФункции // ПолучитьДиапозоныКомплентов()

Функция ИнициализироватьТаблицуПлан()
	
	КвалификаторыСтроки = Новый КвалификаторыСтроки(17);
	ОписаниеСтрока = Новый ОписаниеТипов("Строка",,,,КвалификаторыСтроки);
	
	КвалификаторыЧисла = Новый КвалификаторыЧисла(3, 0, ДопустимыйЗнак.Неотрицательный);
	ОписаниеЧисло = Новый ОписаниеТипов("Число",,,КвалификаторыЧисла);
	
	тзЭтапПлан = Новый ТаблицаЗначений;
	тзЭтапПлан.Колонки.Добавить("ИзделиеЭлемент", Новый ОписаниеТипов("СправочникСсылка.ИзделияЭлемент"));
	тзЭтапПлан.Колонки.Добавить("ДатаПлан1",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан2",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан3",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан4",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан5",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан6",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан7",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан8",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан9",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан10", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан11", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан12", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан13", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан14", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан15", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан16", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаПлан17", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт1",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт2",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт3",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт4",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт5",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт6",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт7",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт8",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт9",  ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт10", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт11", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт12", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт13", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт14", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт15", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт16", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("ДатаФакт17", ОписаниеСтрока);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов1",  ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов2",  ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов3",  ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов4",  ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов5",  ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов6",  ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов7",  ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов8",  ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов9",  ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов10", ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов11", ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов12", ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов13", ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов14", ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов15", ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов16", ОписаниеЧисло);
	тзЭтапПлан.Колонки.Добавить("КоличествоПереходов17", ОписаниеЧисло);
	
    Возврат тзЭтапПлан;
КонецФункции // ИнициализироватьТаблицуПлан()

Функция ПолучитьДатуНачалаОкончания(стОтбор,РасписаниеПлан) Экспорт
	стДаты = Новый Структура("ДатаНачала,ДатаОкончания,стСтадия",Дата(1,1,1),Дата(1,1,1),Новый Структура("Стадия,ИзделиеКомплект", ));
	ЗаполнитьЗначенияСвойств(стДаты.стСтадия,стОтбор);
	НаденыеСтроки = РасписаниеПлан.НайтиСтроки(стОтбор);
	Если НаденыеСтроки.Количество() = 1 Тогда
		ЗаполнитьЗначенияСвойств(стДаты,НаденыеСтроки[0]);
	КонецЕсли;
	Возврат стДаты;
КонецФункции // ПолучитьДатуНачалаОкончания(стОтбор,стТаблицыДанных.РасписаниеПлан)

Функция ПолучитьКраткоеПредставлениеДокумента(СсылкаДокумент) Экспорт 
	
	Возврат Строка(СсылкаДокумент);	
	

КонецФункции // ПолучитьКраткоеПредставлениеДокумента()
