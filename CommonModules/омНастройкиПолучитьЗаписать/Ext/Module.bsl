Функция ПолучитьНастройкиТЗКанбан(Пользователь = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		Пользователь = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли; 
	
	стПараметры = ПолучитьСтруктуруТЗКанбан(); 
	ИмяНастройкиПроектыКанбанТЗ = "ПроектыКанбанТЗ";

	
	мПараметры = Новый Массив;
	Для каждого х Из стПараметры Цикл
		мПараметры.Добавить(х.Ключ);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПроекта.ИмяНастройки КАК ИмяНастройки,
		|	НастройкиПроекта.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.НастройкиПроекта КАК НастройкиПроекта
		|ГДЕ
		|	НастройкиПроекта.Пользователь = &Пользователь
		|	И НастройкиПроекта.ИмяНастройки В(&ИмяНастройки)";
	
	Запрос.УстановитьПараметр("ИмяНастройки", мПараметры);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ИмяНастройкиПроектыКанбанТЗ = Выборка.ИмяНастройки Тогда
			стПараметры.ПроектыКанбанТЗ.Добавить(Выборка.Значение);
		Иначе
			стПараметры[Выборка.ИмяНастройки] = Выборка.Значение;
		КонецЕсли;
	КонецЦикла;

	Возврат стПараметры;

КонецФункции // ПолучитьНастройкиТЗКанбан()

Функция ПолучитьСтруктуруТЗКанбан() Экспорт

	стПараметры = Новый Структура();
	
	стПараметры.Вставить("ШиринаКолонкиТЗКанбан", 	240);
	стПараметры.Вставить("РазмерШрифтаТЗКанбан", 	16);
	стПараметры.Вставить("СортировкаТЗКанбан", 		0);
	стПараметры.Вставить("ПроектыКанбанТЗ", 		Новый Массив);
	
	Возврат стПараметры;

КонецФункции // ПолучитьСтруктуруТЗКанбан()

Процедура ЗаписатьНастройкиКанбанТЗ(стПараметры, Пользователь = Неопределено) Экспорт

	Если Пользователь = Неопределено Тогда
		Пользователь = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли; 

	ИмяНастройки = "ПроектыКанбанТЗ";

	мПараметры = Новый Массив;
	Для каждого х Из стПараметры Цикл
		
		Если х.Ключ = ИмяНастройки Тогда
			Продолжить;
		КонецЕсли;
		
		МенеджерЗаписи = РегистрыСведений.НастройкиПроекта.СоздатьМенеджерЗаписи(); 
		МенеджерЗаписи.ИмяНастройки 	= х.Ключ;
		МенеджерЗаписи.НомерНастройки 	= 0;
		МенеджерЗаписи.Пользователь 	= Пользователь;
		МенеджерЗаписи.Значение			= х.Значение;
		
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
	
	НаборЗаписей = РегистрыСведений.НастройкиПроекта.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИмяНастройки.Установить(ИмяНастройки);
	
	НомерНастройки = 0;
	
	Для каждого Значение Из стПараметры[ИмяНастройки] Цикл
		
		НоваяЗапись = НаборЗаписей.Добавить(); 
		НоваяЗапись.Значение 		= Значение;
		НоваяЗапись.НомерНастройки 	= НомерНастройки;
		НоваяЗапись.ИмяНастройки 	= ИмяНастройки;
		НоваяЗапись.Пользователь 	= Пользователь;
		
		НомерНастройки = НомерНастройки + 1;
	
	КонецЦикла;
	
	НаборЗаписей.Записать();

КонецПроцедуры

