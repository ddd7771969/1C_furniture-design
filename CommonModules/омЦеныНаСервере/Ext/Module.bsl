&НаСервере
Функция ПолучитьЦенуНоменклатуры(Номенклатура,ТолькоМаксимальную = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦеныНоменклатурыДокументов.СпособЗаполненияЦены КАК СпособЗаполненияЦены,
		|	МАКСИМУМ(ЦеныНоменклатурыДокументов.Дата) КАК Дата,
		|	ЦеныНоменклатурыДокументов.Документ КАК Регистратор
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатурыДокументов КАК ЦеныНоменклатурыДокументов
		|ГДЕ
		|	ЦеныНоменклатурыДокументов.Номенклатура = &Номенклатура
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦеныНоменклатурыДокументов.СпособЗаполненияЦены,
		|	ЦеныНоменклатурыДокументов.Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.СпособЗаполненияЦены КАК СпособЗаполненияЦены,
		|	ЦеныНоменклатурыДокументов.Цена КАК Цена
		|ИЗ
		|	вт КАК вт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыДокументов КАК ЦеныНоменклатурыДокументов
		|		ПО вт.Регистратор = ЦеныНоменклатурыДокументов.Документ";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если ТолькоМаксимальную Тогда
		МаксимальнаяЦена = 0; 
		Пока Выборка.Следующий() Цикл
			МаксимальнаяЦена = ?(МаксимальнаяЦена > Выборка.Цена,МаксимальнаяЦена,Выборка.Цена);
		КонецЦикла;
		
		Возврат МаксимальнаяЦена;
	Иначе
		мВозврата = Новый Массив;
		Пока Выборка.Следующий() Цикл
			стВозврата = Новый Структура("Цена,СпособЗаполненияЦены", );
			ЗаполнитьЗначенияСвойств(стВозврата,Выборка);
			мВозврата.Добавить(стВозврата);
			
			Возврат мВозврата;
		КонецЦикла;
	КонецЕсли;
	
	
КонецФункции // ПолучитьЦенуНоменклатуры()

&НаСервере
Функция ПолучитьМаксимальнуюЦенуНоменклатуры(мНоменклатура) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(ЦеныНоменклатурыДокументов.Дата) КАК Дата,
		|	ЦеныНоменклатурыДокументов.Документ КАК Документ,
		|	ЦеныНоменклатурыДокументов.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатурыДокументов.СпособЗаполненияЦены КАК СпособЗаполненияЦены
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатурыДокументов КАК ЦеныНоменклатурыДокументов
		|ГДЕ
		|	ЦеныНоменклатурыДокументов.Номенклатура В(&мНоменклатура)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦеныНоменклатурыДокументов.Документ,
		|	ЦеныНоменклатурыДокументов.Номенклатура,
		|	ЦеныНоменклатурыДокументов.СпособЗаполненияЦены
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ЦеныНоменклатурыДокументов.Цена) КАК Цена,
		|	вт.Номенклатура КАК Номенклатура
		|ИЗ
		|	вт КАК вт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыДокументов КАК ЦеныНоменклатурыДокументов
		|		ПО вт.Документ = ЦеныНоменклатурыДокументов.Документ
		|			И вт.Номенклатура = ЦеныНоменклатурыДокументов.Номенклатура
		|
		|СГРУППИРОВАТЬ ПО
		|	вт.Номенклатура";
	
	Запрос.УстановитьПараметр("мНоменклатура", мНоменклатура);
	
	мДанные = ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
	
	Если Тип("Массив") = ТипЗнч(мНоменклатура) Тогда
		Возврат мДанные;	
	КонецЕсли;
	
	Если мДанные.Количество() = 0 Тогда
		Возврат 0;	
	КонецЕсли;
	
	Возврат мДанные[0].Цена;
	
КонецФункции // ПолучитьЦенуНоменклатуры()

&НаСервере
Функция ПолучитьСебестоимостьМатериалов(мНоменклатура, Склад, Знач Дата = Неопределено) Экспорт
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДата();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваровНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровНаСкладахОстатки.СуммаСНДСОстаток / ВЫБОР
		|		КОГДА ОстаткиТоваровНаСкладахОстатки.КоличествоОстаток = 0
		|			ТОГДА 1
		|		ИНАЧЕ ОстаткиТоваровНаСкладахОстатки.КоличествоОстаток
		|	КОНЕЦ КАК Цена
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровНаСкладах.Остатки(
		|			&Дата,
		|			Номенклатура В (&Номенклатура)
		|				И Склад = &Склад) КАК ОстаткиТоваровНаСкладахОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Номенклатура", мНоменклатура);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьСебестоимость()

&НаСервере
Процедура СоздатьЗаписиВРегистреЦен(СсылкаДокумент,тзДанные,СпособыЗаполненияЦен,ДатаДокумента) Экспорт
	
	НаборЗаписей = РегистрыСведений.ЦеныНоменклатурыДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(СсылкаДокумент);
	НаборЗаписей.Записать();
	
	Если тзДанные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	тзТекущиеЦены = ПолучитьЦеныНаДату(ОбщегоНазначения.ВыгрузитьКолонку(тзДанные, "Номенклатура",Истина),ДатаДокумента);
	
	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	
	Для каждого текСтрока Из тзДанные Цикл 
		СоздатьЗапись = Истина;
		Для каждого строкаТекущиеЦены Из тзТекущиеЦены Цикл
			Если строкаТекущиеЦены.Номенклатура = текСтрока.Номенклатура Тогда
				Если строкаТекущиеЦены.Цена = текСтрока.Цена Тогда
					СоздатьЗапись = Ложь;	
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла; 
		Если СоздатьЗапись Тогда
			МенеджерЗаписей = РегистрыСведений.ЦеныНоменклатурыДокументов.СоздатьМенеджерЗаписи();
			МенеджерЗаписей.Документ 				= СсылкаДокумент;
			МенеджерЗаписей.Номенклатура 			= текСтрока.Номенклатура;
			МенеджерЗаписей.СпособЗаполненияЦены 	= СпособыЗаполненияЦен;
			МенеджерЗаписей.Цена 					= текСтрока.Цена;
			МенеджерЗаписей.Автор 					= ТекущийПользователь;
			МенеджерЗаписей.Дата 					= ДатаДокумента;
			МенеджерЗаписей.Записать()
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // СоздатьЗаписиВРегистреЦен() 

&НаСервере
Функция ПолучитьЦеныНаДату(мНоменклатура,Знач ДатаДокумента = Неопределено) Экспорт
	
	Если ДатаДокумента = Неопределено Тогда
		ДатаДокумента = КонецДня(ТекущаяДата());
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦеныНоменклатурыДокументов.Цена КАК Цена,
		|	ЦеныНоменклатурыДокументов.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(ЦеныНоменклатурыДокументов.Дата) КАК Дата
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатурыДокументов КАК ЦеныНоменклатурыДокументов
		|ГДЕ
		|	ЦеныНоменклатурыДокументов.Номенклатура В(&мНоменклатура)
		|	И ЦеныНоменклатурыДокументов.Дата <= &ДатаДокумента
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦеныНоменклатурыДокументов.Номенклатура,
		|	ЦеныНоменклатурыДокументов.Цена";
	
	Запрос.УстановитьПараметр("мНоменклатура", мНоменклатура);
	Запрос.УстановитьПараметр("ДатаДокумента", ДатаДокумента);
	
	Возврат Запрос.Выполнить().Выгрузить();
	

КонецФункции // ПолучитьТекущиеЦены()

&НаСервере
Функция ПолучитьЦеныПредварительнойНоменклатуры(ПредварительнаяНоменклатура) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦеныПредварительнойНоменклатуры.Период КАК Дата,
		|	ЦеныПредварительнойНоменклатуры.Цена КАК Цена,
		|	ЦеныПредварительнойНоменклатуры.Автор КАК Автор
		|ИЗ
		|	РегистрСведений.ЦеныПредварительнойНоменклатуры КАК ЦеныПредварительнойНоменклатуры
		|ГДЕ
		|	ЦеныПредварительнойНоменклатуры.ПредварительнаяНоменклатура = &ПредварительнаяНоменклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
	
	Запрос.УстановитьПараметр("ПредварительнаяНоменклатура", ПредварительнаяНоменклатура);
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
	
КонецФункции // ПолучитьЦеныНоменклатуры()

&НаСервере
Процедура УстановитьЦенуПредварительнаяНоменклатура(ПредварительнаяНоменклатура,Цена,Документ = Неопределено) Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст =                                                                               
		"ВЫБРАТЬ
		|	ЦеныПредварительнойНоменклатурыСрезПоследних.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныПредварительнойНоменклатуры.СрезПоследних(, ПредварительнаяНоменклатура = &ПредварительнаяНоменклатура) КАК ЦеныПредварительнойНоменклатурыСрезПоследних";
	
	Запрос.УстановитьПараметр("ПредварительнаяНоменклатура", ПредварительнаяНоменклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЦенаВРегистре = 0;
	
	Пока Выборка.Следующий() Цикл
		ЦенаВРегистре = Выборка.Цена;
	КонецЦикла;
	
	Если ЦенаВРегистре = Цена Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ЦеныПредварительнойНоменклатуры.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = ТекущаяДата(); 
	МенеджерЗаписи.ПредварительнаяНоменклатура 	= ПредварительнаяНоменклатура;
	МенеджерЗаписи.Автор  						= ?(Документ = Неопределено,ПараметрыСеанса.ТекущийПользователь,Документ);
	МенеджерЗаписи.Цена   						= Цена;
	
	МенеджерЗаписи.Записать();
 	
КонецПроцедуры


&НаСервере
Функция ПолучитьТекущиеЦеныПредварительнойНоменклатуры(ПредварительнаяНоменклатура) Экспорт

	Если ТипЗнч(ПредварительнаяНоменклатура) = Тип("Массив") Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦеныПредварительнойНоменклатурыСрезПоследних.Цена КАК Цена,
		|	ЦеныПредварительнойНоменклатурыСрезПоследних.ПредварительнаяНоменклатура КАК ПредварительнаяНоменклатура
		|ИЗ
		|	РегистрСведений.ЦеныПредварительнойНоменклатуры.СрезПоследних(, ПредварительнаяНоменклатура В (&ПредварительнаяНоменклатура)) КАК ЦеныПредварительнойНоменклатурыСрезПоследних
		|ГДЕ
		|	ЦеныПредварительнойНоменклатурыСрезПоследних.Цена <> 0";
		
		Запрос.УстановитьПараметр("ПредварительнаяНоменклатура", ПредварительнаяНоменклатура);
		
		Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
			
	Иначе
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦеныПредварительнойНоменклатурыСрезПоследних.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныПредварительнойНоменклатуры.СрезПоследних(, ПредварительнаяНоменклатура = &ПредварительнаяНоменклатура) КАК ЦеныПредварительнойНоменклатурыСрезПоследних";
		
		Запрос.УстановитьПараметр("ПредварительнаяНоменклатура", ПредварительнаяНоменклатура);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Возврат Выборка.Цена;
		КонецЦикла;
		
		Возврат 0;
		
	КонецЕсли;	

КонецФункции // ПолучитьТекущиеЦеныПредварительнойНоменклатуры()
