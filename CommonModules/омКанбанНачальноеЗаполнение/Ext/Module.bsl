Процедура ЗаполнитьНачальноеЗаполнениеКанбана(ИмяКанбана, стРазделители, мРесурсы, Комментарий) Экспорт

	стПараметры = ПолучитьДанныеДоски(ИмяКанбана); //ДоскаУИД, КанбанДорожкиУИД, КанбанКолонкиУИД, Разделители
	
	Сотрудник 	= Неопределено;
	Проект		= Неопределено;
	
	Если стПараметры.Разделители.Сотрудник Тогда
		стРазделители.Свойство("Сотрудник", Сотрудник);
	КонецЕсли;
	
	Если стПараметры.Разделители.Проект Тогда
		стРазделители.Свойство("Проект", Проект);
	КонецЕсли; 
	
	ТребуетсяРазделитель = НЕ(Сотрудник = Неопределено И Проект = Неопределено);
	
	ТД = ТекущаяДата();
	Пользователь = ПараметрыСеанса.ТекущийПользователь;
	
	стПараметрыКарточки 			= ПолучитьСтруктуруДляЭлементовКанбана("Карточка");
	
	стПараметрыПерехода 			= ПолучитьСтруктуруДляЭлементовКанбана("ПереходКарточки", Комментарий);
	
	стПараметрыДополнительныеДанные = ПолучитьСтруктуруДляЭлементовКанбана("ДополнительныеДанные");
	стПараметрыДополнительныеДанные.ТипДополнения 	= "Новая";
	стПараметрыДополнительныеДанные.Дополнение 		= Истина;
	
	стПараметрыКарточки.Доска 			= стПараметры.ДоскаУИД;
	стПараметрыПерехода.Доска 			= стПараметры.ДоскаУИД;
	стПараметрыПерехода.УИДКудаКолонка 	= стПараметры.КанбанКолонкиУИД;
	стПараметрыПерехода.УИДКудаДорожка 	= стПараметры.КанбанДорожкиУИД;
	
	Для каждого текущийРесурс Из мРесурсы Цикл
		
		стПараметрыКарточки.Наименование 	= текущийРесурс.Наименование;
		стПараметрыКарточки.Ресурс 			= текущийРесурс.Ссылка;
		УИДКарточка 						= СоздатьКарточкиКанбан(стПараметрыКарточки);
	
		стПараметрыПерехода.УИДКарточка 	= УИДКарточка;
		
		УИДПерехода = СоздатьДвижениеКарточки(стПараметрыПерехода); 
		
		стПараметрыПерехода.НомерВКолонке 	= стПараметрыПерехода.НомерВКолонке + 1;
		
		
		стПараметрыДополнительныеДанные.УИДПерехода = УИДПерехода;
		СоздатьДополнениеКДвижению(стПараметрыДополнительныеДанные);
		
		Если ТребуетсяРазделитель Тогда
			
			МенеджерКанбанРазделители = РегистрыСведений.КанбанРазделители.СоздатьМенеджерЗаписи();
			МенеджерКанбанРазделители.УИДКарточка 	= УИДКарточка;
			МенеджерКанбанРазделители.Проект 		= Проект;
			МенеджерКанбанРазделители.Сотрудник 	= Сотрудник;
			
			МенеджерКанбанРазделители.Записать();
			
		КонецЕсли;

	КонецЦикла;
	

КонецПроцедуры 

Функция ПолучитьРазделитилиИзКода(Код) Экспорт

 	Разделители = Новый Структура("Проект, Сотрудник", Ложь, Ложь);
	
	ДвоичныйКод = ИзДесятичногоВСтроковуюДвоичную(Код);
	
	ДлинаКода = СтрДлина(ДвоичныйКод);
	
	Если Сред(ДвоичныйКод,ДлинаКода, 1) = "1" Тогда
	
		Разделители.Проект = Истина;	
	
	КонецЕсли;
	
	Если Сред(ДвоичныйКод,ДлинаКода - 1, 1) = "1" Тогда
	
		Разделители.Сотрудник = Истина;	
	
	КонецЕсли;
	
	Возврат Разделители;
	

КонецФункции // ПолучитьРазделитилиИзКода()

Функция ИзДесятичногоВСтроковуюДвоичную(Знач ДесятичноеЧисло)
	
	стрВозврата = "";
	
	Пока ДесятичноеЧисло > 0 Цикл
	
		стрВозврата = Строка(ДесятичноеЧисло % 2) + стрВозврата;		
		ДесятичноеЧисло = Цел(ДесятичноеЧисло / 2)
	
	КонецЦикла;
	
	Возврат стрВозврата;

КонецФункции // ИзДесятичногоВСтроковуюДвоичную()

Функция ПолучитьДанныеДоски(ИмяКанбана)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанДоски.УИД КАК УИД,
		|	КанбанДоски.Разделитель КАК Разделитель
		|ПОМЕСТИТЬ втДоска
		|ИЗ
		|	РегистрСведений.КанбанДоски КАК КанбанДоски
		|ГДЕ
		|	КанбанДоски.Наименование = &Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КанбанДорожки.УИД КАК УИД
		|ИЗ
		|	РегистрСведений.КанбанДорожки КАК КанбанДорожки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДоска КАК втДоска
		|		ПО КанбанДорожки.Доска = втДоска.УИД
		|ГДЕ
		|	КанбанДорожки.НомерПоПорядку = 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КанбанКолонки.УИД КАК УИД
		|ИЗ
		|	РегистрСведений.КанбанКолонки КАК КанбанКолонки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДоска КАК втДоска
		|		ПО КанбанКолонки.Доска = втДоска.УИД
		|ГДЕ
		|	КанбанКолонки.НомерПоПорядку = 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДоска.УИД КАК УИД,
		|	втДоска.Разделитель КАК Разделитель
		|ИЗ
		|	втДоска КАК втДоска";
	
	Запрос.УстановитьПараметр("Наименование", ИмяКанбана);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	стПараметры = Новый Структура("ДоскаУИД, КанбанДорожкиУИД, КанбанКолонкиУИД, Разделители", );
	
	Выборка = РезультатЗапроса[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		стПараметры.КанбанДорожкиУИД = Выборка.УИД;
	КонецЦикла;

	Выборка = РезультатЗапроса[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		стПараметры.КанбанКолонкиУИД = Выборка.УИД;
	КонецЦикла; 
	
	Выборка = РезультатЗапроса[3].Выбрать();
	Пока Выборка.Следующий() Цикл
		стПараметры.ДоскаУИД 	= Выборка.УИД;
		стПараметры.Разделители = ПолучитьРазделитилиИзКода(Выборка.Разделитель);
	КонецЦикла; 
	
	Возврат стПараметры;	
	
КонецФункции // ПолучитьДанныеДоски()

Функция СоздатьКарточкиКанбан(стПараметры) Экспорт
	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КанбанКарточки.УИД КАК УИД
		|ИЗ
		|	РегистрСведений.КанбанКарточки КАК КанбанКарточки
		|ГДЕ
		|	КанбанКарточки.Доска = &ДоскаУИД
		|	И КанбанКарточки.Ресурс = &Ресурс";
	
	Запрос.УстановитьПараметр("ДоскаУИД",	стПараметры.Доска);
	Запрос.УстановитьПараметр("Ресурс",	стПараметры.Ресурс);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.УИД;
	КонецЦикла;
	
	
	МенеджерЗаписи = РегистрыСведений.КанбанКарточки.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, стПараметры); 
	МенеджерЗаписи.УИД = Строка(Новый УникальныйИдентификатор());
	
	МенеджерЗаписи.Записать();
	
	Возврат МенеджерЗаписи.УИД;
	
КонецФункции

Функция ПолучитьСтруктуруДляЭлементовКанбана(ТипЭлемента, Комментарий = "") Экспорт

    стПараметры = Новый Структура();

	Если ТипЭлемента = "Карточка" Тогда
		
		стПараметры.Вставить("Доска", 			"");
		стПараметры.Вставить("Комментарий", 	Комментарий);
		стПараметры.Вставить("Наименование", 	"");
		стПараметры.Вставить("Окончание", 		Ложь);
		стПараметры.Вставить("Ресурс", 			Неопределено);
		стПараметры.Вставить("Цвет", 			"");
		стПараметры.Вставить("УИДКарточка",		Новый УникальныйИдентификатор());
		
	ИначеЕсли ТипЭлемента = "ПереходКарточки" Тогда
		
		стПараметры.Вставить("Доска", 			"");
		стПараметры.Вставить("УИДКарточка", 	"");
		стПараметры.Вставить("НомерВКолонке",	0);
		стПараметры.Вставить("Комментарий", 	Комментарий);
		стПараметры.Вставить("Наименование", 	"");
		стПараметры.Вставить("Окончание", 		Ложь);
		стПараметры.Вставить("Пользователь", 	ПараметрыСеанса.ТекущийПользователь);
		стПараметры.Вставить("УИДКудаДорожка",	"");
		стПараметры.Вставить("УИДКудаКолонка",	"");
		стПараметры.Вставить("Период",			ТекущаяДата());
		стПараметры.Вставить("УИДПерехода",		Новый УникальныйИдентификатор());
		
	ИначеЕсли ТипЭлемента = "ДополнительныеДанные" Тогда

		стПараметры.Вставить("УИДПерехода",		"");
		стПараметры.Вставить("ТипДополнения", 	"");
		стПараметры.Вставить("Дополнение", 		Комментарий);

	КонецЕсли;
	
	
	Возврат стПараметры;
	
КонецФункции // ПолучитьСтруктуруДляСозданияКарточки()
 
Функция СоздатьДвижениеКарточки(стПараметры) Экспорт

	Если стПараметры.НомерВКолонке = -1 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЕСТЬNULL(МАКСИМУМ(КанбанПереходКарточкиСрезПоследних.НомерВКолонке), 0) + 1 КАК НомерВКолонке
			|ИЗ
			|	РегистрСведений.КанбанПереходКарточки.СрезПоследних(, ) КАК КанбанПереходКарточкиСрезПоследних
			|ГДЕ
			|	КанбанПереходКарточкиСрезПоследних.УИДКудаКолонка = &УИДКудаКолонка";
		
		Запрос.УстановитьПараметр("УИДКудаКолонка", стПараметры.УИДКудаКолонка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			стПараметры.НомерВКолонке = Выборка.НомерВКолонке;
		КонецЦикла;
		
	КонецЕсли;
	
	
	МенеджерЗаписи = РегистрыСведений.КанбанПереходКарточки.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, стПараметры);
	
	МенеджерЗаписи.Записать();
	
	Если НЕ ПустаяСтрока(стПараметры.Комментарий) Тогда
		
		стПараметрыДополнительныеДанные 				= ПолучитьСтруктуруДляЭлементовКанбана("ДополнительныеДанные", стПараметры.Комментарий);
		стПараметрыДополнительныеДанные.ТипДополнения 	= "Комментарий";
		
		стПараметрыДополнительныеДанные.УИДПерехода = МенеджерЗаписи.УИДПерехода;
		СоздатьДополнениеКДвижению(стПараметрыДополнительныеДанные);
		
	КонецЕсли;
	
	омКанбанДоскиНаСервере.ИнкриментВерсии(стПараметры.Доска);

	Возврат МенеджерЗаписи.УИДПерехода;
	
КонецФункции // СоздатьДвижениеКарточки()

Процедура СоздатьДополнениеКДвижению(стПараметры) Экспорт

	МенеджерЗаписи = РегистрыСведений.КанбанДополнительныеДанныеПерехода.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, стПараметры);
	
	МенеджерЗаписи.Записать();

КонецПроцедуры

