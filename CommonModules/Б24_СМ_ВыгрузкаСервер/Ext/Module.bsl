
#Область ПроцедурыИФункцииФормированияПакетов

///////////////////////////////////////////////////////////////////
//			ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЙ ПАКЕТОВ            //
/////////////////////////////////////////////////////////////////


Процедура ФормированиеПакетовДанных(пСтруктураНастроек, ДанныеЭлемента) Экспорт
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	ТипДанных ="";
	
	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Формирование пакетов данных для выгрузки.");
	
	ТаблицаОбъектовОбменаВыгрузки = ПолучитьТаблицуДляВыгрузкиДанных(СтруктураНастроек, ДанныеЭлемента);
	
	ЗагрузкаТаблицыДанныхВРегистрПакетов(СтруктураНастроек, ТаблицаОбъектовОбменаВыгрузки, ДанныеЭлемента);
	
	пСтруктураНастроек.Логирование.НомерСообщения = СтруктураНастроек.Логирование.НомерСообщения; 	// чтобы лог не затерся.
	
КонецПроцедуры

Процедура ЗагрузкаТаблицыДанныхВРегистрПакетов(СтруктураНастроек, ТаблицаОбъектовОбменаВыгрузки, ДанныеЭлемента)
	
	ТаблицаОбъектовОбменаВыгрузки.Колонки.Добавить("Пакет");
	
	РазбивкаТаблицыДанныхНаПакеты(СтруктураНастроек, ТаблицаОбъектовОбменаВыгрузки, ДанныеЭлемента);
	
	НаборЗаписей = РегистрыСведений.Б24_СМ_ПакетыВыгрузки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НастройкаПодключения.Установить(СтруктураНастроек.НастройкаПодключения);
	НаборЗаписей.Отбор.ТипВладельцаСмартПроцесса.Установить(ДанныеЭлемента.ТипВладельцаСмартПроцесса);
	НаборЗаписей.Отбор.ИдСмартПроцесса.Установить(ДанныеЭлемента.ИдСмартПроцесса);
	НаборЗаписей.Загрузить(ТаблицаОбъектовОбменаВыгрузки);
	НаборЗаписей.Записать();             
	
КонецПроцедуры

Процедура РазбивкаТаблицыДанныхНаПакеты(СтруктураНастроек, Таблица, ДанныеЭлемента)
	
	Если ДанныеЭлемента.Данные.ТабличнаяЧасть1С <> Неопределено Тогда
		КоличествоДанныхВПакете = СтруктураНастроек.КоличествоДанныхВПакете/3;	
	Иначе
		КоличествоДанныхВПакете = СтруктураНастроек.КоличествоДанныхВПакете/2;	
	КонецЕсли;
	
	Итератор 	= 0;
	Пакет 		= 1;
	
	Для каждого ТекСтр Из Таблица Цикл
		
		Если Не ЗначениеЗаполнено(ТекСтр.Объект) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТекСтр.ИдентификаторОбъекта) Тогда //при полной выгрузке пусто
			ТекСтр.ИдентификаторОбъекта = XMLСтрока(ТекСтр.Объект);	
		КонецЕсли;
		
		Итератор = Итератор + 1;
		
		Если Итератор > КоличествоДанныхВПакете Тогда
			Пакет = Пакет + 1;
			Итератор = 1;
		КонецЕсли;
		
		ТекСтр.Пакет = Пакет;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьТаблицуДляВыгрузкиДанных(СтруктураНастроек, ДанныеЭлемента) Экспорт
	
	СтруктураСхемКомпоновки = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьСтруктуруСхемКомпоновки();
	СхемаВыгрузки = СтруктураСхемКомпоновки.МакетСмартПроцессов;
	ИсточникДанных = ДанныеЭлемента.Данные.ТипСущности1С + "." + ДанныеЭлемента.Данные.Сущность1С.Значение; 
	СхемаВыгрузки.НаборыДанных.ОсновнойНаборДанных.Запрос = СтрЗаменить(СхемаВыгрузки.НаборыДанных.ОсновнойНаборДанных.Запрос, "Справочник.Валюты", ИсточникДанных);
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаВыгрузки)); 
	Если ЗначениеЗаполнено(ДанныеЭлемента.Данные.НастройкиКомпоновкиДанных) Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(ДанныеЭлемента.Данные.НастройкиКомпоновкиДанных);
		КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(СхемаВыгрузки.НастройкиПоУмолчанию);
	КонецЕсли;
	
	
	Запрос = Новый Запрос;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаВыгрузки, КомпоновщикНастроек.ПолучитьНастройки(),,,Тип("ГенераторМакетаКомпоновкиДанных"));
	Запрос.Текст = МакетКомпоновкиДанных.НаборыДанных.ОсновнойНаборДанных.Запрос;
	
	Для каждого Параметр Из МакетКомпоновкиДанных.ЗначенияПараметров Цикл
		Запрос.Параметры.Вставить(Параметр.Имя, Параметр.Значение);
	КонецЦикла;
	
	ВсеВыгружаемыеДанные = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НастройкаПодключения"		, СтруктураНастроек.НастройкаПодключения);
	Запрос.УстановитьПараметр("ТипВладельцаСмартПроцесса"	, ДанныеЭлемента.ТипВладельцаСмартПроцесса);
	Запрос.УстановитьПараметр("ИдСмартПроцесса"				, ДанныеЭлемента.ИдСмартПроцесса);
	Запрос.УстановитьПараметр("ВремяЗапускаВМиллисекундах"	, СтруктураНастроек.ВремяЗапускаВМиллисекундах);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СМ_ТаблицаИзменений.НастройкаПодключения КАК НастройкаПодключения,
	|	Б24_СМ_ТаблицаИзменений.ТипВладельцаСмартПроцесса КАК ТипВладельцаСмартПроцесса,
	|	Б24_СМ_ТаблицаИзменений.ИдСмартПроцесса КАК ИдСмартПроцесса,
	|	Б24_СМ_ТаблицаИзменений.ТипСущности1С КАК ТипСущности1С,
	|	Б24_СМ_ТаблицаИзменений.Сущность1С КАК Сущность1С,
	|	Б24_СМ_ТаблицаИзменений.ИдентификаторОбъекта КАК ИдентификаторОбъекта
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрСведений.Б24_СМ_ТаблицаИзменений КАК Б24_СМ_ТаблицаИзменений
	|ГДЕ
	|	Б24_СМ_ТаблицаИзменений.НастройкаПодключения = &НастройкаПодключения
	|	И Б24_СМ_ТаблицаИзменений.ТипВладельцаСмартПроцесса = &ТипВладельцаСмартПроцесса
	|	И Б24_СМ_ТаблицаИзменений.ИдСмартПроцесса = &ИдСмартПроцесса
	|	И Б24_СМ_ТаблицаИзменений.ВремяЗаписиВМиллисекундах <= &ВремяЗапускаВМиллисекундах	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Б24_СМ_ПакетыВыгрузки.НастройкаПодключения,
	|	Б24_СМ_ПакетыВыгрузки.ТипВладельцаСмартПроцесса,
	|	Б24_СМ_ПакетыВыгрузки.ИдСмартПроцесса,
	|	Б24_СМ_ПакетыВыгрузки.ТипСущности1С,
	|	Б24_СМ_ПакетыВыгрузки.Сущность1С,
	|	Б24_СМ_ПакетыВыгрузки.ИдентификаторОбъекта
	|ИЗ
	|	РегистрСведений.Б24_СМ_ПакетыВыгрузки КАК Б24_СМ_ПакетыВыгрузки
	|ГДЕ
	|	Б24_СМ_ПакетыВыгрузки.НастройкаПодключения = &НастройкаПодключения
	|	И Б24_СМ_ПакетыВыгрузки.ТипВладельцаСмартПроцесса = &ТипВладельцаСмартПроцесса
	|	И Б24_СМ_ПакетыВыгрузки.ИдСмартПроцесса = &ИдСмартПроцесса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Данные.НастройкаПодключения КАК НастройкаПодключения,
	|	ВТ_Данные.ТипВладельцаСмартПроцесса КАК ТипВладельцаСмартПроцесса,
	|	ВТ_Данные.ИдСмартПроцесса КАК ИдСмартПроцесса,
	|	ВТ_Данные.ТипСущности1С КАК ТипСущности1С,
	|	ВТ_Данные.Сущность1С КАК Сущность1С,
	|	ВТ_Данные.ИдентификаторОбъекта КАК ИдентификаторОбъекта
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Данные.ИдСмартПроцесса,
	|	ВТ_Данные.ТипСущности1С,
	|	ВТ_Данные.ТипВладельцаСмартПроцесса,
	|	ВТ_Данные.Сущность1С,
	|	ВТ_Данные.НастройкаПодключения,
	|	ВТ_Данные.ИдентификаторОбъекта";
	
	ПакетыДанных = Запрос.Выполнить().Выгрузить();
	
	Б24_СМ_ОбщегоНазначенияВызовСервера.ДобавитьЗаполнитьКолонкуОбъектТаблицы(ДанныеЭлемента.Данные, ПакетыДанных);
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПолнаяВыгрузка"		, СтруктураНастроек.ПолнаяВыгрузка);
	Запрос.УстановитьПараметр("ВсеВыгружаемыеДанные", ВсеВыгружаемыеДанные);
	Запрос.УстановитьПараметр("ПакетыДанных"		, ПакетыДанных);
	
	Запрос.УстановитьПараметр("НастройкаПодключения"		, СтруктураНастроек.НастройкаПодключения);
	Запрос.УстановитьПараметр("ТипВладельцаСмартПроцесса"	, ДанныеЭлемента.ТипВладельцаСмартПроцесса);
	Запрос.УстановитьПараметр("ИдСмартПроцесса"				, ДанныеЭлемента.ИдСмартПроцесса);
	Запрос.УстановитьПараметр("ТипСущности1С"				, ДанныеЭлемента.Данные.ТипСущности1С);
	Запрос.УстановитьПараметр("Сущность1С"					, ДанныеЭлемента.Данные.Сущность1С.Значение);
	Запрос.Текст = "ВЫБРАТЬ
	|	ПакетыДанных.НастройкаПодключения КАК НастройкаПодключения,
	|	ПакетыДанных.ТипВладельцаСмартПроцесса КАК ТипВладельцаСмартПроцесса,
	|	ПакетыДанных.ИдСмартПроцесса КАК ИдСмартПроцесса,
	|	ПакетыДанных.ТипСущности1С КАК ТипСущности1С,
	|	ПакетыДанных.Сущность1С КАК Сущность1С,
	|	ПакетыДанных.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
	|	ПакетыДанных.Объект КАК Объект
	|ПОМЕСТИТЬ ВТ_ПакетыДанных
	|ИЗ
	|	&ПакетыДанных КАК ПакетыДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НастройкаПодключения КАК НастройкаПодключения,
	|	&ТипВладельцаСмартПроцесса КАК ТипВладельцаСмартПроцесса,
	|	&ИдСмартПроцесса КАК ИдСмартПроцесса,
	|	&ТипСущности1С КАК ТипСущности1С,
	|	&Сущность1С КАК Сущность1С,
	|	"""" КАК ИдентификаторОбъекта,
	|	ВсеВыгружаемыеДанные.Объект КАК Объект
	|ПОМЕСТИТЬ ВТ_ВсеВыгружаемыеДанные
	|ИЗ
	|	&ВсеВыгружаемыеДанные КАК ВсеВыгружаемыеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеВыгружаемыеДанные.НастройкаПодключения КАК НастройкаПодключения,
	|	ВсеВыгружаемыеДанные.ТипВладельцаСмартПроцесса КАК ТипВладельцаСмартПроцесса,
	|	ВсеВыгружаемыеДанные.ИдСмартПроцесса КАК ИдСмартПроцесса,
	|	ВсеВыгружаемыеДанные.ТипСущности1С КАК ТипСущности1С,
	|	ВсеВыгружаемыеДанные.Сущность1С КАК Сущность1С,
	|	ВсеВыгружаемыеДанные.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
	|	ВсеВыгружаемыеДанные.Объект КАК Объект
	|ИЗ
	|	ВТ_ВсеВыгружаемыеДанные КАК ВсеВыгружаемыеДанные
	|ГДЕ
	|	&ПолнаяВыгрузка = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПакетыДанных.НастройкаПодключения,
	|	ПакетыДанных.ТипВладельцаСмартПроцесса,
	|	ПакетыДанных.ИдСмартПроцесса,
	|	ПакетыДанных.ТипСущности1С,
	|	ПакетыДанных.Сущность1С,
	|	ПакетыДанных.ИдентификаторОбъекта,
	|	ПакетыДанных.Объект
	|ИЗ
	|	ВТ_ПакетыДанных КАК ПакетыДанных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВсеВыгружаемыеДанные КАК ВсеВыгружаемыеДанные
	|		ПО ПакетыДанных.Объект = ВсеВыгружаемыеДанные.Объект
	|ГДЕ
	|	&ПолнаяВыгрузка = ЛОЖЬ";
				   
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПолучитьПакетИзРегистраПакетов(СтруктураНастроек, ДанныеЭлемента, Пакет) Экспорт
	
	Запрос = Новый Запрос;  
	Запрос.УстановитьПараметр("НастройкаПодключения"		, СтруктураНастроек.НастройкаПодключения);
	Запрос.УстановитьПараметр("ТипВладельцаСмартПроцесса"	, ДанныеЭлемента.ТипВладельцаСмартПроцесса);
	Запрос.УстановитьПараметр("ИдСмартПроцесса"				, ДанныеЭлемента.ИдСмартПроцесса);
	Запрос.УстановитьПараметр("Пакет"						, Пакет);
	Запрос.Текст = "ВЫБРАТЬ 
	|	Б24_СМ_ПакетыВыгрузки.ТипСущности1С КАК ТипСущности1С,
	|	Б24_СМ_ПакетыВыгрузки.Сущность1С КАК Сущность1С,
	|	Б24_СМ_ПакетыВыгрузки.ИдентификаторОбъекта КАК ИдентификаторОбъекта
	|ИЗ
	|	РегистрСведений.Б24_СМ_ПакетыВыгрузки КАК Б24_СМ_ПакетыВыгрузки
	|ГДЕ
	|	Б24_СМ_ПакетыВыгрузки.НастройкаПодключения = &НастройкаПодключения
	|	И Б24_СМ_ПакетыВыгрузки.ТипВладельцаСмартПроцесса = &ТипВладельцаСмартПроцесса
	|	И Б24_СМ_ПакетыВыгрузки.ИдСмартПроцесса = &ИдСмартПроцесса
	|	И Б24_СМ_ПакетыВыгрузки.Пакет = &Пакет
	|
	|УПОРЯДОЧИТЬ ПО
	|	Б24_СМ_ПакетыВыгрузки.Пакет";
	
	мДанных = Запрос.Выполнить().Выгрузить();
	
	Возврат мДанных; 	
	
КонецФункции

Функция ПолучитьКоличествоВыгружаемыхПакетов(СтруктураНастроек, ДанныеЭлемента) Экспорт
	
	Результат = 0;
	
	Запрос = Новый Запрос;    	
	Запрос.УстановитьПараметр("НастройкаПодключения"		, СтруктураНастроек.НастройкаПодключения);
	Запрос.УстановитьПараметр("ТипВладельцаСмартПроцесса"	, ДанныеЭлемента.ТипВладельцаСмартПроцесса);
	Запрос.УстановитьПараметр("ИдСмартПроцесса"				, ДанныеЭлемента.ИдСмартПроцесса);
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	Б24_СМ_ПакетыВыгрузки.Пакет КАК Пакет
	|ИЗ
	|	РегистрСведений.Б24_СМ_ПакетыВыгрузки КАК Б24_СМ_ПакетыВыгрузки
	|ГДЕ
	|	Б24_СМ_ПакетыВыгрузки.НастройкаПодключения = &НастройкаПодключения
	|	И Б24_СМ_ПакетыВыгрузки.ТипВладельцаСмартПроцесса = &ТипВладельцаСмартПроцесса
	|	И Б24_СМ_ПакетыВыгрузки.ИдСмартПроцесса = &ИдСмартПроцесса
	|
	|УПОРЯДОЧИТЬ ПО
	|	Пакет УБЫВ";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		тзнДанных = ВыполненныйЗапрос.Выгрузить();	
		Результат = тзнДанных[0].Пакет;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


Процедура УдалениеРегистрацииПоТипуДанных(СтруктураНастроек, ДанныеЭлемента, МассивДанных) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НастройкаПодключения"		, СтруктураНастроек.НастройкаПодключения);
	Запрос.УстановитьПараметр("ТипВладельцаСмартПроцесса"	, ДанныеЭлемента.ТипВладельцаСмартПроцесса);
	Запрос.УстановитьПараметр("ИдСмартПроцесса"				, ДанныеЭлемента.ИдСмартПроцесса);
	Запрос.УстановитьПараметр("МассивДанных" 				, МассивДанных);
	Запрос.Текст = "ВЫБРАТЬ
	|	МассивДанных.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
	|	МассивДанных.ТипСущности1С КАК ТипСущности1С,
	|	МассивДанных.Сущность1С КАК Сущность1С,
	|	&НастройкаПодключения КАК НастройкаПодключения,
	|	&ТипВладельцаСмартПроцесса КАК ТипВладельцаСмартПроцесса,
	|	&ИдСмартПроцесса КАК ИдСмартПроцесса
	|ПОМЕСТИТЬ ВТ_МассивДанных
	|ИЗ
	|	&МассивДанных КАК МассивДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_СМ_ТаблицаИзменений.НастройкаПодключения КАК НастройкаПодключения,
	|	Б24_СМ_ТаблицаИзменений.ТипВладельцаСмартПроцесса КАК ТипВладельцаСмартПроцесса,
	|	Б24_СМ_ТаблицаИзменений.ИдСмартПроцесса КАК ИдСмартПроцесса,
	|	Б24_СМ_ТаблицаИзменений.ТипСущности1С КАК ТипСущности1С,
	|	Б24_СМ_ТаблицаИзменений.Сущность1С КАК Сущность1С,
	|	Б24_СМ_ТаблицаИзменений.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
	|	Б24_СМ_ТаблицаИзменений.ВремяЗаписиВМиллисекундах КАК ВремяЗаписиВМиллисекундах
	|ИЗ
	|	РегистрСведений.Б24_СМ_ТаблицаИзменений КАК Б24_СМ_ТаблицаИзменений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_МассивДанных КАК ВТ_МассивДанных
	|		ПО Б24_СМ_ТаблицаИзменений.НастройкаПодключения = ВТ_МассивДанных.НастройкаПодключения
	|			И Б24_СМ_ТаблицаИзменений.ТипВладельцаСмартПроцесса = ВТ_МассивДанных.ТипВладельцаСмартПроцесса
	|			И Б24_СМ_ТаблицаИзменений.ИдСмартПроцесса = ВТ_МассивДанных.ИдСмартПроцесса
	|			И Б24_СМ_ТаблицаИзменений.ТипСущности1С = ВТ_МассивДанных.ТипСущности1С
	|			И Б24_СМ_ТаблицаИзменений.Сущность1С = ВТ_МассивДанных.Сущность1С
	|			И Б24_СМ_ТаблицаИзменений.ИдентификаторОбъекта = ВТ_МассивДанных.ИдентификаторОбъекта";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Запись = РегистрыСведений.Б24_СМ_ТаблицаИзменений.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.Прочитать();
		Запись.Удалить();
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалениеЗаписиИзПакетаДанных(СтруктураНастроек, Успешно,  ДанныеЭлемента, Пакет, Данные) Экспорт
	
	Если Успешно Тогда
		
		НаборЗаписей = РегистрыСведений.Б24_СМ_ПакетыВыгрузки.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.НастройкаПодключения.Установить(СтруктураНастроек.НастройкаПодключения);
		НаборЗаписей.Отбор.ТипВладельцаСмартПроцесса.Установить(ДанныеЭлемента.ТипВладельцаСмартПроцесса);
		НаборЗаписей.Отбор.ИдСмартПроцесса.Установить(ДанныеЭлемента.ИдСмартПроцесса);
		НаборЗаписей.Отбор.ТипСущности1С.Установить(ДанныеЭлемента.Данные.ТипСущности1С);
		НаборЗаписей.Отбор.Сущность1С.Установить(ДанныеЭлемента.Данные.Сущность1С.Значение);
		НаборЗаписей.Отбор.ИдентификаторОбъекта.Установить(XMLСтрока(Данные));
		НаборЗаписей.Отбор.Пакет.Установить(Пакет);
		
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ПроцедурыИФункцииФормированияДанныхВыгрузки

Функция СформироватьДанныеПоСмартПроцессу(пСтруктураНастроек, ДанныеЭлемента, МассивДанных, Пакет) Экспорт
	
	мЭлементы 	=  Новый массив;
	
	Если ДанныеЭлемента.Данные.Сущность1С = Неопределено ИЛИ МассивДанных.Количество() = 0 Тогда
		Возврат мЭлементы;
	КонецЕсли;
	
	СтруктураНастроек = Новый Структура;
	Для Каждого ЭлементСтруктуры Из пСтруктураНастроек Цикл
		СтруктураНастроек.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;	
	
	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Формирование запросов для смарт-процесса");
	
	РеквизитыТабличнойЧасти = Б24_СМ_ОбщегоНазначенияВызовСервера.ПолучитьРеквизитыТабличнойЧастиСущности1С(ДанныеЭлемента.Данные, "ДополнительныеРеквизиты");
	
	Массив = Новый Массив;
	Массив.Добавить(ТипЗнч(МассивДанных[0].Объект));
	ОписаниеТиповО = Новый ОписаниеТипов(Массив);
    тзнОбъектовДел = ПолучитьТаблицуОбъектовДел(СтруктураНастроек, ОписаниеТиповО, МассивДанных);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивДанных"				, МассивДанных);	
	Запрос.УстановитьПараметр("Портал"						, СтруктураНастроек.Портал);
	Запрос.УстановитьПараметр("ОбъектыДел"					, тзнОбъектовДел);  
	Запрос.УстановитьПараметр("ТипВладельцаСмартПроцесса"	, ДанныеЭлемента.ТипВладельцаСмартПроцесса);	
	Запрос.УстановитьПараметр("ИдСмартПроцесса"				, ДанныеЭлемента.ИдСмартПроцесса);	
	
	Запрос.УстановитьПараметр("ТипДанныхСчетов"				, СтруктураНастроек.ТипыОбъектовОбмена.Счет2);
	Запрос.УстановитьПараметр("ТипДанныхСделок"				, СтруктураНастроек.ТипыОбъектовОбмена.Сделка);
	Запрос.УстановитьПараметр("ТипДанныхЗаказов"			, СтруктураНастроек.ТипыОбъектовОбмена.Заказ);
	Запрос.УстановитьПараметр("ТипДанныхКомпаний"			, СтруктураНастроек.ТипыОбъектовОбмена.Компания);
	Запрос.УстановитьПараметр("ТипДанныхКонтактов"			, СтруктураНастроек.ТипыОбъектовОбмена.Контакт);	
	Запрос.УстановитьПараметр("ТипДанныхЗССП"				, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваСмартПроцесса);			
	
	Запрос.УстановитьПараметр("ЭтоТовар2"					, СтруктураНастроек.ВерсияТоваров = "v.2");
	Запрос.УстановитьПараметр("ТипДанныхТоваров"			, СтруктураНастроек.ТипыОбъектовОбмена.Товар);
	Запрос.УстановитьПараметр("ТипДанныхТоваров2"			, СтруктураНастроек.ТипыОбъектовОбмена.Товар2);
	Запрос.УстановитьПараметр("ТипДанныхПредложений"		, СтруктураНастроек.ТипыОбъектовОбмена.Предложение);	
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОбъектыДел.ТипДанных КАК ТипДанных,
	               |	ОбъектыДел.Объект КАК Объект,
	               |	ОбъектыДел.ТипОбъекта КАК ТипОбъекта,
	               |	ОбъектыДел.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
	               |	ОбъектыДел.Портал КАК Портал
	               |ПОМЕСТИТЬ ВТ_ОбъектыДел
	               |ИЗ
	               |	&ОбъектыДел КАК ОбъектыДел
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МассивДанных.ТипСущности1С КАК ТипСущности1С,
	               |	МассивДанных.Сущность1С КАК Сущность1С,
	               |	МассивДанных.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
	               |	МассивДанных.Объект КАК Объект,
	               |	&Портал КАК Портал,
	               |	&ТипВладельцаСмартПроцесса КАК ТипВладельцаСмартПроцесса,
	               |	&ИдСмартПроцесса КАК ИдСмартПроцесса
	               |ПОМЕСТИТЬ ВТ_Данные
	               |ИЗ
	               |	&МассивДанных КАК МассивДанных
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ОбъектыДел.Объект КАК Объект,
	               |	Б24_К_ИдентификаторыДел.Идентификатор КАК Идентификатор,
	               |	Б24_К_ИдентификаторыДел.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор
	               |ПОМЕСТИТЬ ВТ_ИдентификаторыДел
	               |ИЗ
	               |	РегистрСведений.Б24_К_ИдентификаторыДел КАК Б24_К_ИдентификаторыДел
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбъектыДел КАК ВТ_ОбъектыДел
	               |		ПО Б24_К_ИдентификаторыДел.Портал = ВТ_ОбъектыДел.Портал
	               |			И Б24_К_ИдентификаторыДел.ТипДанных = ВТ_ОбъектыДел.ТипДанных
	               |			И Б24_К_ИдентификаторыДел.ТипОбъекта = ВТ_ОбъектыДел.ТипОбъекта
	               |			И Б24_К_ИдентификаторыДел.ИдентификаторОбъекта = ВТ_ОбъектыДел.ИдентификаторОбъекта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТ_ОбъектыДел
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Данные.Объект КАК Объект,
	               |	Б24_СМ_ИдентификаторыОбъектов.Идентификатор КАК ИдентификаторБ24,
	               |	ВТ_Данные.ИдентификаторОбъекта КАК ИдентификаторОбъекта1С
	               |ПОМЕСТИТЬ ВТ_ДанныеИД
	               |ИЗ
	               |	ВТ_Данные КАК ВТ_Данные
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Б24_СМ_ИдентификаторыОбъектов КАК Б24_СМ_ИдентификаторыОбъектов
	               |		ПО ВТ_Данные.Портал = Б24_СМ_ИдентификаторыОбъектов.Портал
	               |			И ВТ_Данные.ТипВладельцаСмартПроцесса = Б24_СМ_ИдентификаторыОбъектов.ТипВладельцаСмартПроцесса
	               |			И ВТ_Данные.ИдСмартПроцесса = Б24_СМ_ИдентификаторыОбъектов.ИдСмартПроцесса
	               |			И ВТ_Данные.ТипСущности1С = Б24_СМ_ИдентификаторыОбъектов.ТипСущности1С
	               |			И ВТ_Данные.Сущность1С = Б24_СМ_ИдентификаторыОбъектов.Сущность1С
	               |			И ВТ_Данные.ИдентификаторОбъекта = Б24_СМ_ИдентификаторыОбъектов.ИдентификаторОбъекта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ДанныеИД.Объект КАК Объект,
	               |	ВТ_ДанныеИД.ИдентификаторБ24 КАК ИдентификаторБ24,
	               |	ВТ_ДанныеИД.ИдентификаторОбъекта1С КАК ИдентификаторОбъекта1С,
	               |	ВТ_ИдентификаторыДел.Идентификатор КАК ИдентификаторДела
	               |ИЗ
	               |	ВТ_ДанныеИД КАК ВТ_ДанныеИД
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыДел КАК ВТ_ИдентификаторыДел
	               |		ПО ВТ_ДанныеИД.Объект = ВТ_ИдентификаторыДел.Объект
	               |			И ВТ_ДанныеИД.ИдентификаторБ24 = ВТ_ИдентификаторыДел.ДополнительныйИдентификатор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	               |	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	               |	Б24_К_ИдентификаторыОбъектов.ДополнительныйИдентификатор КАК ДополнительныйИдентификатор,
	               |	ЗначенияСвойствОбъектов.Наименование КАК Наименование
	               |ПОМЕСТИТЬ ВТ_ИдентификаторыЗначенийПользовательскихПолей
	               |ИЗ
	               |	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	               |		ПО Б24_К_ИдентификаторыОбъектов.Объект = ЗначенияСвойствОбъектов.Ссылка
	               |ГДЕ
	               |	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхЗССП
	               |	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ДополнительныйИдентификатор,
	               |	Наименование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Данные.Объект КАК Объект,
	               |	ДополнительныеСведения.Свойство КАК Свойство,
	               |	ДополнительныеСведения.Значение КАК Значение,
	               |	ИСТИНА КАК ЭтоСведение
	               |ПОМЕСТИТЬ ВТ_ДанныеСвойств
	               |ИЗ
	               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Данные КАК ВТ_Данные
	               |		ПО ДополнительныеСведения.Объект = ВТ_Данные.Объект";
	Если РеквизитыТабличнойЧасти.Количество() > 0 Тогда
		Запрос.Текст = Запрос.Текст + "	
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_Данные.Объект,
		|	ОбъектыДополнительныеРеквизиты.Свойство,
		|	ОбъектыДополнительныеРеквизиты.Значение,
		|	ЛОЖЬ
		|ИЗ
		|	"+ДанныеЭлемента.Данные.ТипСущности1С+"."+ДанныеЭлемента.Данные.Сущность1С.Значение+".ДополнительныеРеквизиты КАК ОбъектыДополнительныеРеквизиты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Данные КАК ВТ_Данные
		|		ПО ОбъектыДополнительныеРеквизиты.Ссылка = ВТ_Данные.Объект";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "	
	|;
	|";
	
	Запрос.Текст = Запрос.Текст + "ВЫБРАТЬ
	                              |	ВТ_ДанныеСвойств.Объект КАК Объект,
	                              |	ВТ_ДанныеСвойств.Свойство КАК Свойство,
	                              |	ВТ_ДанныеСвойств.ЭтоСведение КАК ЭтоСведение,
	                              |	ВТ_ДанныеСвойств.Значение КАК Значение,
	                              |	ВТ_ИдентификаторыЗначенийПользовательскихПолей.Идентификатор КАК ИдентификаторЗначенияСвойстваБитрикс24
	                              |ПОМЕСТИТЬ ВТ_ДанныеСвойствСИд
	                              |ИЗ
	                              |	ВТ_ДанныеСвойств КАК ВТ_ДанныеСвойств
	                              |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыЗначенийПользовательскихПолей КАК ВТ_ИдентификаторыЗначенийПользовательскихПолей
	                              |		ПО ВТ_ДанныеСвойств.Значение = ВТ_ИдентификаторыЗначенийПользовательскихПолей.Объект
	                              |
	                              |ИНДЕКСИРОВАТЬ ПО
	                              |	Объект,
	                              |	Свойство
	                              |;
	                              |
	                              |////////////////////////////////////////////////////////////////////////////////
	                              |УНИЧТОЖИТЬ ВТ_ДанныеСвойств
	                              |;
	                              |
	                              |////////////////////////////////////////////////////////////////////////////////
	                              |ВЫБРАТЬ
	                              |	Б24_К_ПользователиБитрикс24.Пользователь1С КАК Объект,
	                              |	Б24_К_ПользователиБитрикс24.Идентификатор КАК Идентификатор
	                              |ПОМЕСТИТЬ ВТ_ИдентификаторыПользователей
	                              |ИЗ
	                              |	РегистрСведений.Б24_К_ПользователиБитрикс24 КАК Б24_К_ПользователиБитрикс24
	                              |ГДЕ
	                              |	Б24_К_ПользователиБитрикс24.Портал = &Портал
	                              |;
	                              |
	                              |////////////////////////////////////////////////////////////////////////////////
	                              |ВЫБРАТЬ
	                              |	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	                              |	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	                              |ПОМЕСТИТЬ ВТ_ИдентификаторыСделокЗаказов
	                              |ИЗ
	                              |	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	                              |ГДЕ
	                              |	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	                              |	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхСделок
	                              |
	                              |ИНДЕКСИРОВАТЬ ПО
	                              |	Объект
	                              |;
	                              |
	                              |////////////////////////////////////////////////////////////////////////////////
	                              |ВЫБРАТЬ
	                              |	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	                              |	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	                              |ПОМЕСТИТЬ ВТ_ИдентификаторыСчетов
	                              |ИЗ
	                              |	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	                              |ГДЕ
	                              |	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	                              |	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхСчетов
	                              |
	                              |ИНДЕКСИРОВАТЬ ПО
	                              |	Объект
	                              |;
	                              |
	                              |////////////////////////////////////////////////////////////////////////////////
	                              |ВЫБРАТЬ
	                              |	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	                              |	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	                              |ПОМЕСТИТЬ ВТ_ИдентификаторыКомпаний
	                              |ИЗ
	                              |	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	                              |ГДЕ
	                              |	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	                              |	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКомпаний
	                              |;
	                              |
	                              |////////////////////////////////////////////////////////////////////////////////
	                              |ВЫБРАТЬ
	                              |	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	                              |	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	                              |ПОМЕСТИТЬ ВТ_ИдентификаторыКонтактов
	                              |ИЗ
	                              |	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	                              |ГДЕ
	                              |	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	                              |	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКонтактов";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СформированныйЗапрос = СформироватьСтруктуруДанныхПоЭлементуСмартПроцесса(СтруктураНастроек, ДанныеЭлемента, МенеджерВременныхТаблиц, Выборка);
		Если ЗначениеЗаполнено(СформированныйЗапрос) Тогда
			мЭлементы.Добавить(СформированныйЗапрос);
		КонецЕсли;
		
	КонецЦикла;
	
	пСтруктураНастроек.КлючиПодчиненныхБатчЗапросов 	= СтруктураНастроек.КлючиПодчиненныхБатчЗапросов; 	
	пСтруктураНастроек.Логирование.НомерСообщения	 	= СтруктураНастроек.Логирование.НомерСообщения; 	
	
	Возврат мЭлементы;	

КонецФункции

Функция СформироватьСтруктуруДанныхПоЭлементуСмартПроцесса(СтруктураНастроек, ДанныеЭлемента, МенеджерВременныхТаблиц, ИнформацияЭлементаСмартПроцесса)
	
	ВыгружатьНаПортал 					= ДанныеЭлемента.Данные.ВыгружатьНаПортал;
	ОбновлятьНаПортале 					= ДанныеЭлемента.Данные.ОбновлятьНаПортале;
	ДанныеНастроекНаправлений 			= ДанныеЭлемента.Данные.ДанныеНастроекНаправлений;
	ДанныеНастроекСтадий 				= ДанныеЭлемента.Данные.ДанныеНастроекСтадий;
	ИнформацияОПолях 					= ДанныеЭлемента.Данные.ТаблицаСоответствийВыгрузки;
	ИнформацияОПоляхТЧ 					= ДанныеЭлемента.Данные.ТаблицаСоответствийВыгрузкиТабЧасть;
	АлгоритмПриВыгрузкеДанныхНаПортал 	= ДанныеЭлемента.Данные.АлгоритмПриВыгрузкеДанныхНаПортал;
	
	ЭтоНовыйЭлемент = НЕ ЗначениеЗаполнено(ИнформацияЭлементаСмартПроцесса.ИдентификаторБ24);
	Если НЕ ЭтоНовыйЭлемент И НЕ ОбновлятьНаПортале Тогда 
		СтруктураНастроек.НеобновляемыеДанныеНаПортале.Добавить(ИнформацияЭлементаСмартПроцесса.Объект);
		Возврат "";
	КонецЕсли;
	
	ИдЭлемента		= ИнформацияЭлементаСмартПроцесса.ИдентификаторБ24;
	КлючЭлемента 	= XMLСтрока(ИнформацияЭлементаСмартПроцесса.Объект); 
	
#Область Шапка
	
	ИдентификаторНаправления = "";
#Область ПолучениеЗначенияНаправления
	
	Если ДанныеНастроекНаправлений <> Неопределено Тогда
		
		ЗначениеНаправления = "НеУказаноСлужебный";
		Если ДанныеНастроекНаправлений.ИсточникНаправленийВ1С = "Реквизит объекта" Тогда
			
			Если ДанныеНастроекНаправлений.Реквизит1С.Количество() > 0 Тогда
				ЗначениеНаправления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИнформацияЭлементаСмартПроцесса.Объект, ДанныеНастроекНаправлений.Реквизит1С[0].Значение);  
			КонецЕсли;
			
		ИначеЕсли ДанныеНастроекНаправлений.ИсточникНаправленийВ1С = "Дополнительный реквизит" ИЛИ ДанныеНастроекНаправлений.ИсточникНаправленийВ1С = "Дополнительное сведение" Тогда
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Объект"		, ИнформацияЭлементаСмартПроцесса.Объект);
			Запрос.УстановитьПараметр("Свойство"	, ДанныеНастроекНаправлений.СвойствоОбъекта);
			Запрос.УстановитьПараметр("ЭтоСведение"	, ?(ДанныеНастроекНаправлений.ИсточникНаправленийВ1С = "Дополнительный реквизит", Ложь, Истина));
			Запрос.Текст = "ВЫБРАТЬ
			|	ВТ_ДанныеСвойствСИд.Значение КАК Значение
			|ИЗ
			|	ВТ_ДанныеСвойствСИд КАК ВТ_ДанныеСвойствСИд
			|ГДЕ
			|	ВТ_ДанныеСвойствСИд.Объект = &Объект
			|	И ВТ_ДанныеСвойствСИд.Свойство = &Свойство
			|	И ВТ_ДанныеСвойствСИд.ЭтоСведение = &ЭтоСведение";
			
			ВыполненныйЗапрос = Запрос.Выполнить();
			Если НЕ ВыполненныйЗапрос.Пустой() Тогда
				Выборка = ВыполненныйЗапрос.Выбрать();
				Пока Выборка.Следующий() Цикл
					ЗначениеНаправления = Выборка.Значение	
				КонецЦикла;
			КонецЕсли;
			
		ИначеЕсли ДанныеНастроекНаправлений.ИсточникНаправленийВ1С = "Произвольный алгоритм" Тогда
			
			Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
			Параметры.СтруктураНастроек = СтруктураНастроек;
			Параметры.Объект1С 			= ИнформацияЭлементаСмартПроцесса.Объект;

			ЗначениеНаправления = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(ДанныеНастроекНаправлений.Алгоритм, Параметры, "Функция");		
			
		КонецЕсли;

		Если ЗначениеНаправления <> "НеУказаноСлужебный" Тогда

			НайденнаяСтрока = ДанныеНастроекНаправлений.СоответствияНаправлений.Найти(ЗначениеНаправления, "Значение1С");
			Если НайденнаяСтрока <> Неопределено Тогда
				ИдентификаторНаправления = НайденнаяСтрока.ИдНаправления; 	
			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;
#КонецОбласти
		
	ИдентификаторСтадии = "";
#Область ПолучениеЗначенияСтадии
	Если ДанныеНастроекСтадий <> Неопределено Тогда
		
		ЗначениеСтадии = "НеУказаноСлужебный";

		Если ДанныеНастроекСтадий.ИсточникСтадийВ1С = "Реквизит объекта" Тогда
			
			Если ДанныеНастроекСтадий.Реквизит1С.Количество() > 0 Тогда
				ЗначениеСтадии = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИнформацияЭлементаСмартПроцесса.Объект, ДанныеНастроекСтадий.Реквизит1С[0].Значение);  
			КонецЕсли;
			
		ИначеЕсли ДанныеНастроекСтадий.ИсточникСтадийВ1С = "Дополнительный реквизит" ИЛИ ДанныеНастроекСтадий.ИсточникСтадийВ1С = "Дополнительное сведение" Тогда
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Объект"		, ИнформацияЭлементаСмартПроцесса.Объект);
			Запрос.УстановитьПараметр("Свойство"	, ДанныеНастроекСтадий.СвойствоОбъекта);
			Запрос.УстановитьПараметр("ЭтоСведение"	, ?(ДанныеНастроекСтадий.ИсточникСтадийВ1С = "Дополнительный реквизит", Ложь, Истина));
			Запрос.Текст = "ВЫБРАТЬ
			|	ВТ_ДанныеСвойствСИд.Значение КАК Значение
			|ИЗ
			|	ВТ_ДанныеСвойствСИд КАК ВТ_ДанныеСвойствСИд
			|ГДЕ
			|	ВТ_ДанныеСвойствСИд.Объект = &Объект
			|	И ВТ_ДанныеСвойствСИд.Свойство = &Свойство
			|	И ВТ_ДанныеСвойствСИд.ЭтоСведение = &ЭтоСведение";
			
			ВыполненныйЗапрос = Запрос.Выполнить();
			Если НЕ ВыполненныйЗапрос.Пустой() Тогда
				Выборка = ВыполненныйЗапрос.Выбрать();
				Пока Выборка.Следующий() Цикл
					ЗначениеСтадии = Выборка.Значение	
				КонецЦикла;
			КонецЕсли;
			
		ИначеЕсли ДанныеНастроекСтадий.ИсточникСтадийВ1С = "Произвольный алгоритм" Тогда
			
			Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
			Параметры.СтруктураНастроек = СтруктураНастроек;
			Параметры.Объект1С 			= ИнформацияЭлементаСмартПроцесса.Объект;

			ЗначениеСтадии = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(ДанныеНастроекСтадий.Алгоритм, Параметры, "Функция");		

		КонецЕсли;
		
		Если ЗначениеСтадии <> "НеУказаноСлужебный" Тогда

			Если ИдентификаторНаправления = "" Тогда
				НайденнаяСтрока = ДанныеНастроекСтадий.СоответствияСтадийНаправлений.Найти(ЗначениеСтадии, "Значение1С");
				Если НайденнаяСтрока <> Неопределено Тогда
					ИдентификаторСтадии = НайденнаяСтрока.ИдСтадии; 	
				КонецЕсли;
			Иначе
				НайденныеСтроки = ДанныеНастроекСтадий.СоответствияСтадийНаправлений.НайтиСтроки(Новый Структура("ИдНаправления, Значение1С", ИдентификаторНаправления, ЗначениеСтадии));
				Если НайденныеСтроки.Количество() > 0 Тогда
					ИдентификаторСтадии = НайденныеСтроки[0].ИдСтадии; 	
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;
#КонецОбласти

	ПоляШапки = Новый Структура;
	ПоляШапки.Вставить("xmlId"		, КлючЭлемента);
	
	Если ЗначениеЗаполнено(ИдентификаторНаправления) Тогда
		ПоляШапки.Вставить("categoryId"	, XMLСтрока(ИдентификаторНаправления));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторСтадии) Тогда
		ПоляШапки.Вставить("stageId"		, XMLСтрока(ИдентификаторСтадии));
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИнформацияОПолях"				, ИнформацияОПолях);
	ДополнительныеПараметры.Вставить("ОписаниеКлючей"				, СтруктураНастроек.КлючиСмартПроцесса.КлючиШапки);
	ДополнительныеПараметры.Вставить("КлючЭлемента"					, КлючЭлемента);
	ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"		, МенеджерВременныхТаблиц);
	ДополнительныеПараметры.Вставить("ЭтоНовыйЭлемент"				, ЭтоНовыйЭлемент);
	ДополнительныеПараметры.Вставить("ИнформацияОбъекта1С"			, ИнформацияЭлементаСмартПроцесса);
	ДополнительныеПараметры.Вставить("ТипВладельцаСмартПроцесса"	, ДанныеЭлемента.ТипВладельцаСмартПроцесса);
	ДополнительныеПараметры.Вставить("ИдСмартПроцесса"				, ДанныеЭлемента.ИдСмартПроцесса);
	
	ЗаполнитьСтруктуруКлючейЭлементаВыгрузки(СтруктураНастроек, ДополнительныеПараметры, ПоляШапки);
	
	Если ЗначениеЗаполнено(АлгоритмПриВыгрузкеДанныхНаПортал) Тогда
		
		Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
		Параметры.СтруктураНастроек = СтруктураНастроек;
		Параметры.Объект1С 			= ИнформацияЭлементаСмартПроцесса.Объект;
		Параметры.СтруктураДанных 	= ПоляШапки;
		
		Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(АлгоритмПриВыгрузкеДанныхНаПортал, Параметры, "Процедура");		
			
	КонецЕсли;
	
	ТелоЗапроса = "";
	Для каждого ТекПоле Из ПоляШапки Цикл
		Если ТекПоле.Значение <> Неопределено Тогда   
			Если НЕ ЗначениеЗаполнено(ТекПоле.Значение) Тогда 
				ТелоЗапроса = ТелоЗапроса + "&fields["+ ТекПоле.Ключ + "]=";    
			Иначе
				ТелоЗапроса = ТелоЗапроса + "&fields["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение);    
			КонецЕсли;
		КонецЕсли;		
	КонецЦикла;  
	
	Если ЭтоНовыйЭлемент Тогда
		СтрокаЗапроса = "cmd["+КлючЭлемента+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.item.add?entityTypeId=" + ДанныеЭлемента.ТипВладельцаСмартПроцесса + ТелоЗапроса);	
	Иначе
		СтрокаЗапроса = "cmd["+КлючЭлемента+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.item.update?id=" + ИдЭлемента + "&entityTypeId=" + ДанныеЭлемента.ТипВладельцаСмартПроцесса + ТелоЗапроса);	
	КонецЕсли;
				
	СтруктураНастроек.КлючиПодчиненныхБатчЗапросов.Вставить(КлючЭлемента, ПолучитьСтруктуруКлючейБатчЗапросов(ДанныеЭлемента, ИнформацияЭлементаСмартПроцесса.Объект));
	
#КонецОбласти

#Область ТабличнаяЧасть
	
	ТабличнаяЧасть1С = "";
	Если ДанныеЭлемента.Данные.ТабличнаяЧасть1С <> Неопределено Тогда
		ТабличнаяЧасть1С = ДанныеЭлемента.Данные.ТабличнаяЧасть1С.Значение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТабличнаяЧасть1С) Тогда
		
		ТЧТовары = ИнформацияЭлементаСмартПроцесса.Объект[ТабличнаяЧасть1С];
		
		ЗапросПоТоварамЭлемента	= "";
		НомерСтроки 			= 1;	
		Для каждого СтрокаТЧ Из ТЧТовары Цикл
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ИнформацияОПолях"			, ИнформацияОПоляхТЧ);
			ДополнительныеПараметры.Вставить("ОписаниеКлючей"			, СтруктураНастроек.КлючиСмартПроцесса.КлючиТЧ);
			ДополнительныеПараметры.Вставить("КлючЭлемента"				, КлючЭлемента);
			ДополнительныеПараметры.Вставить("МенеджерВременныхТаблиц"	, МенеджерВременныхТаблиц);
			ДополнительныеПараметры.Вставить("ЭтоНовыйЭлемент"			, ЭтоНовыйЭлемент);
			ДополнительныеПараметры.Вставить("ИнформацияОбъекта1С"		, ИнформацияЭлементаСмартПроцесса);
			ДополнительныеПараметры.Вставить("Товары"					, ТЧТовары);
			ДополнительныеПараметры.Вставить("ИнформацияСтрокиТЧ"		, СтрокаТЧ);

			ПоляТЧ = Новый Структура;
			ПолучитьСтруктуруКлючейТЧЭлементаВыгрузки(СтруктураНастроек, ДополнительныеПараметры, ПоляТЧ);

			Для каждого ТекПоле Из ПоляТЧ Цикл
				Если ТекПоле.Значение <> Неопределено Тогда
					ЗапросПоТоварамЭлемента = ЗапросПоТоварамЭлемента + "&productRows["+Формат(НомерСтроки, "ЧГ=0")+"]["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
				КонецЕсли;	
			КонецЦикла;  
			
			НомерСтроки = НомерСтроки + 1;
			
		КонецЦикла;	
		                                      
		Если ЗначениеЗаполнено(ЗапросПоТоварамЭлемента) Тогда
			
			ТипВладельцаСмартПроцесса16 = "T" + Б24_К_ОбщегоНазначенияВызовСервера.КонвертацияДесятичногоЧислаВШестнадцатеричное(ДанныеЭлемента.ТипВладельцаСмартПроцесса);
			
			Если НЕ ЗначениеЗаполнено(ИдЭлемента) Тогда
				ЗапросПоТоварамЭлемента = "ownerType="+ТипВладельцаСмартПроцесса16+"&ownerId=$result["+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(КлючЭлемента)+"][item][id]"+ЗапросПоТоварамЭлемента;
			Иначе
				ЗапросПоТоварамЭлемента = "ownerType="+ТипВладельцаСмартПроцесса16+"&ownerId="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдЭлемента)+ЗапросПоТоварамЭлемента;	
			КонецЕсли;		
			
			СтрокаЗапроса = СтрокаЗапроса + "&" + "cmd[]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("crm.item.productrow.set?" + ЗапросПоТоварамЭлемента);	

		КонецЕсли;
		
	КонецЕсли;

#КонецОбласти
	
#Область Дело	

	ТелоДела = Б24_К_ВыгрузкаДелВызовСервера.ПолучитьТелоЗапросаДела(СтруктураНастроек, ДанныеЭлемента.ТипВладельцаСмартПроцесса, ИнформацияЭлементаСмартПроцесса.Объект, Строка(ИнформацияЭлементаСмартПроцесса.Объект), ИнформацияЭлементаСмартПроцесса.ИдентификаторБ24, КлючЭлемента);   
	ТелоДела = СтрЗаменить(ТелоДела, "$result["+КлючЭлемента+"]", "$result["+КлючЭлемента+"][item][id]"); 
	Если ЗначениеЗаполнено(ТелоДела) Тогда
		
		КлючДела = "D_" + КлючЭлемента;
		
		ИдентификаторДела = ИнформацияЭлементаСмартПроцесса.ИдентификаторДела;
		
		СтрокаЗапроса = СтрокаЗапроса + Б24_К_ВыгрузкаДелВызовСервера.ПолучитьЗапросДела(СтруктураНастроек, КлючДела, ИдентификаторДела, ТелоДела);
				
		СтруктураНастроек.КлючиПодчиненныхБатчЗапросов.Вставить(КлючДела, ПолучитьСтруктуруКлючейБатчЗапросов(ДанныеЭлемента, ИнформацияЭлементаСмартПроцесса.Объект));
	
	КонецЕсли;

#КонецОбласти

	Возврат СтрокаЗапроса;
	
КонецФункции


Процедура ЗаполнитьСтруктуруКлючейЭлементаВыгрузки(СтруктураНастроек, ДополнительныеПараметры, Поля)
	
	ИнформацияОПолях 			= ДополнительныеПараметры.ИнформацияОПолях;
	ОписаниеКлючей 				= ДополнительныеПараметры.ОписаниеКлючей;
	МенеджерВременныхТаблиц 	= ДополнительныеПараметры.МенеджерВременныхТаблиц;
	ИнформацияОбъекта1С 		= ДополнительныеПараметры.ИнформацияОбъекта1С;
	ЭтоНовыйЭлемент 			= ДополнительныеПараметры.ЭтоНовыйЭлемент;
	КлючЭлемента 				= ДополнительныеПараметры.КлючЭлемента;
	ТипВладельцаСмартПроцесса 	= ДополнительныеПараметры.ТипВладельцаСмартПроцесса;
	ИдСмартПроцесса 			= ДополнительныеПараметры.ИдСмартПроцесса;
	
	СсылкаНаОбъект1С 			= ИнформацияОбъекта1С.Объект;
	
	Для каждого ТекПоле Из ИнформацияОПолях Цикл
		
		Если ТекПоле.ИсточникДанных	= "" ИЛИ ТекПоле.ИсточникДанных = "Не изменять" Тогда		
			Продолжить;
		КонецЕсли;   
		
		Если НЕ ЗначениеЗаполнено(ТекПоле.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипЗначенияКлюча = ОписаниеКлючей.Получить(ТекПоле.КлючЗапроса);
		Если ТипЗначенияКлюча = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеКлюча = Неопределено;
		
		Попытка
			
			Если ТекПоле.ИсточникДанных = "Фиксированное значение" Тогда
				
				ЗначениеРеквизитаОбъекта = ТекПоле.Значение;
				
				ЗначениеКлюча = ПолучитьЗначениеКлючаСмартПроцесса(СтруктураНастроек, МенеджерВременныхТаблиц, ТекПоле, ТипЗначенияКлюча, ЗначениеРеквизитаОбъекта);
					
			ИначеЕсли ТекПоле.ИсточникДанных = "Реквизит объекта" Тогда
				
				ЗначениеРеквизитаОбъекта 		= СсылкаНаОбъект1С[ТекПоле.Значение];          //закэшуется
				
				ЗначениеКлюча = ПолучитьЗначениеКлючаСмартПроцесса(СтруктураНастроек, МенеджерВременныхТаблиц, ТекПоле, ТипЗначенияКлюча, ЗначениеРеквизитаОбъекта);
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Произвольный алгоритм" Тогда
				
				Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
				Параметры.СтруктураНастроек = СтруктураНастроек;
				Параметры.Объект1С 			= СсылкаНаОбъект1С;
				Параметры.СтруктураДанных 	= Поля;

				ЗначениеРеквизитаОбъекта = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(ТекПоле.Значение, Параметры, "Функция");		
				
				ЗначениеКлюча = ПолучитьЗначениеКлючаСмартПроцесса(СтруктураНастроек, МенеджерВременныхТаблиц, ТекПоле, ТипЗначенияКлюча, ЗначениеРеквизитаОбъекта);
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Предопределенный алгоритм 'Компания'" Тогда
				
				ЗначениеРеквизитаОбъекта 		= СсылкаНаОбъект1С[ТекПоле.Значение]; 

				ЭтоКонтакт = ОпределитьСущностьЯвляетсяКонтактомБитрикс24(СтруктураНастроек, ЗначениеРеквизитаОбъекта);					

				Если ЭтоКонтакт = Ложь Тогда
					ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", ЗначениеРеквизитаОбъекта);	
					Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
						ЗначениеКлюча = СформироватьКомпаниюПоСсылке(СтруктураНастроек, ЗначениеРеквизитаОбъекта);
					КонецЕсли;
				Иначе
					ЗначениеКлюча = "";	
				КонецЕсли;
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Предопределенный алгоритм 'Контакт'" Тогда
				
				ЗначениеРеквизитаОбъекта 		= СсылкаНаОбъект1С[ТекПоле.Значение]; 

				ЭтоКонтакт = ОпределитьСущностьЯвляетсяКонтактомБитрикс24(СтруктураНастроек, ЗначениеРеквизитаОбъекта);					
				Если ЭтоКонтакт = Истина Тогда
					ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", ЗначениеРеквизитаОбъекта);	
					Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
						ЗначениеКлюча = СформироватьКонтактПоСсылке(СтруктураНастроек, ЗначениеРеквизитаОбъекта);
					КонецЕсли;
					Если Лев(ЗначениеКлюча, 1) = "#" Тогда ЗначениеКлюча = Прав(ЗначениеКлюча, СтрДлина(ЗначениеКлюча)-1) КонецЕсли;
				Иначе
					ЗначениеКлюча = "";	
				КонецЕсли;
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Из дополнительного реквизита" ИЛИ ТекПоле.ИсточникДанных = "Из дополнительного сведения" Тогда
				
				Свойство1С = ТекПоле.Значение;
				
				ЗначениеСвойства 	= Неопределено;
				ИдЗначенияСвойства 	= Неопределено;
				
				ЗапросЗначенийСвойств = Новый Запрос;
				ЗапросЗначенийСвойств.УстановитьПараметр("Объект"	, СсылкаНаОбъект1С);
				ЗапросЗначенийСвойств.УстановитьПараметр("Свойство"	, Свойство1С);
				
				ЗапросЗначенийСвойств.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
				ЗапросЗначенийСвойств.Текст = "ВЫБРАТЬ *
				|ИЗ
				|	ВТ_ДанныеСвойствСИд
				| ГДЕ
				| ВТ_ДанныеСвойствСИд.Объект = &Объект
				| И ВТ_ДанныеСвойствСИд.Свойство = &Свойство";
				ВыполненныйЗапросЗначенийСвойств = ЗапросЗначенийСвойств.Выполнить();	
				Если НЕ ВыполненныйЗапросЗначенийСвойств.Пустой() Тогда
					
					ВыборкаЗначенийСвойств = ВыполненныйЗапросЗначенийСвойств.Выбрать();
					Пока ВыборкаЗначенийСвойств.Следующий() Цикл
						ЗначениеСвойства 	= ВыборкаЗначенийСвойств.Значение;
						ИдЗначенияСвойства 	= ВыборкаЗначенийСвойств.ИдентификаторЗначенияСвойстваБитрикс24;
						Прервать;
					КонецЦикла;
				КонецЕсли;
				
				ТипыЗначенийСвойства = Свойство1С.ТипЗначения.Типы();
				КоличествоТипов = ТипыЗначенийСвойства.Количество();
				
				ТипЗначения = Неопределено;
				Если КоличествоТипов = 0 Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "В свойстве: " + Строка(Свойство1С) + " не указан тип значения. Ключ: " + ТекПоле.КлючЗапроса + " будет не заполнен.");
					Продолжить;	
				ИначеЕсли КоличествоТипов = 1 Тогда
					ТипЗначения = ТипыЗначенийСвойства[0];	
				ИначеЕсли КоличествоТипов > 1 Тогда
					ТипЗначения = ТипыЗначенийСвойства[0];	
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "В свойстве: " + Строка(Свойство1С) + " не указано несколько типов значений. Будет использован первый");
				КонецЕсли;		
						
				
				Если ТипЗначения = Тип("СправочникСсылка.ЗначенияСвойствОбъектов") Тогда
					
					ЗначениеКлюча = ИдЗначенияСвойства;	
					Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
						ЗначениеКлюча = СформироватьЗначениеСвойстваПоСсылке(СтруктураНастроек, ТекПоле, ЗначениеСвойства);
					КонецЕсли;
					
				ИначеЕсли ТипЗначения = Тип("СправочникСсылка.Валюты") Тогда
					ЗначениеКлюча = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьВалютуПоКоду(ЗначениеСвойства.Код);	
				ИначеЕсли ТипЗначения = Тип("СправочникСсылка.Пользователи") Тогда
					ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыПользователей", ЗначениеСвойства);	
				ИначеЕсли ТипЗначения = Тип("СправочникСсылка.Партнеры") Тогда
					
					ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", ЗначениеСвойства);	
					Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
						ЗначениеКлюча = СформироватьКомпаниюПоСсылке(СтруктураНастроек, ЗначениеСвойства);
					КонецЕсли;
					
					Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
						
						ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", ЗначениеСвойства);	
						Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
							ЗначениеКлюча = СформироватьКонтактПоСсылке(СтруктураНастроек, ЗначениеСвойства);
						КонецЕсли;
						
						Если Лев(ЗначениеКлюча, 1) = "#" Тогда ЗначениеКлюча = Прав(ЗначениеКлюча, СтрДлина(ЗначениеКлюча)-1) КонецЕсли;
					КонецЕсли;
					
				ИначеЕсли ТипЗначения = Тип("Булево") Тогда
					ЗначениеКлюча = ?(ЗначениеСвойства = Истина, "Y", "N");	
				Иначе
					ЗначениеКлюча = ЗначениеСвойства;	
				КонецЕсли;
				
			КонецЕсли;
				
		Исключение                                                                                                                              
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, ТекПоле.КлючЗапроса + "/" + ТекПоле.Значение + "/" + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
		Поля.Вставить(ТекПоле.КлючЗапроса, ЗначениеКлюча);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьЗначениеКлючаСмартПроцесса(СтруктураНастроек, МенеджерВременныхТаблиц, ОписаниеЗаполненияКлюча, ТипЗначенияКлюча, ЗначениеРеквизитаОбъекта)
	                      
	ЗначениеКлюча = Неопределено;
	
	ПрефиксыВнешнихКодовБитрикс24 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();
	
	Если ТипЗначенияКлюча.Тип = "crm" Тогда 
		
		Если ТипЗначенияКлюча.Настройки <> Неопределено Тогда
			
			Для каждого ТекКлюч Из ТипЗначенияКлюча.Настройки Цикл
				
				ТипЗначения = ТипЗнч(ЗначениеРеквизитаОбъекта);
				
				Если ТипЗначенияКлюча.Настройки.Получить("DEAL") = "Y" ИЛИ ТипЗначенияКлюча.Настройки.Получить("ORDER")= "Y" Тогда
					Если ТипЗначения = Тип("ДокументСсылка.ЗаказКлиента") Тогда
						ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыСделокЗаказов", ЗначениеРеквизитаОбъекта);	
						Если Лев(ЗначениеКлюча, 2) = ПрефиксыВнешнихКодовБитрикс24.Сделки Тогда ЗначениеКлюча = Прав(ЗначениеКлюча, СтрДлина(ЗначениеКлюча)-СтрДлина(ПрефиксыВнешнихКодовБитрикс24.Сделка)) КонецЕсли;
						Если Лев(ЗначениеКлюча, 2) = ПрефиксыВнешнихКодовБитрикс24.Заказы Тогда ЗначениеКлюча = Прав(ЗначениеКлюча, СтрДлина(ЗначениеКлюча)-СтрДлина(ПрефиксыВнешнихКодовБитрикс24.Заказы)) КонецЕсли;
					КонецЕсли;	
				КонецЕсли;
				
				Если ТипЗначенияКлюча.Настройки.Получить("COMPANY") = "Y" Тогда
					Если ТипЗначения = Тип("СправочникСсылка.Партнеры") ИЛИ ТипЗначения = Тип("СправочникСсылка.Организации") Тогда
						ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", ЗначениеРеквизитаОбъекта);	
						Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
							ЗначениеКлюча = СформироватьКомпаниюПоСсылке(СтруктураНастроек, ЗначениеРеквизитаОбъекта);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если ТипЗначенияКлюча.Настройки.Получить("CONTACT") = "Y" Тогда
					ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", ЗначениеРеквизитаОбъекта);	
					Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
						ЗначениеКлюча = СформироватьКонтактПоСсылке(СтруктураНастроек, ЗначениеРеквизитаОбъекта);
					КонецЕсли;
					Если Лев(ЗначениеКлюча, 1) = "#" Тогда ЗначениеКлюча = Прав(ЗначениеКлюча, СтрДлина(ЗначениеКлюча)-1) КонецЕсли;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
					Для каждого ТекСмартПроцесс Из СтруктураНастроек.НастройкиСинхронизации Цикл
						Если ТипЗначенияКлюча.Настройки.Получить("DYNAMIC_" + ТекСмартПроцесс.ТипВладельца) = "Y" Тогда
							ЗначениеКлюча = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьИдОбъекта(СтруктураНастроек.Портал, ТекСмартПроцесс.ТипВладельца, ТекСмартПроцесс.Ид, ЗначениеРеквизитаОбъекта);
							Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
								ЗначениеКлюча = СформироватьЭлементСмартПроцессаПоСсылке(СтруктураНастроек, ТекСмартПроцесс, ЗначениеРеквизитаОбъекта);
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			ЗначениеКлюча = Строка(ЗначениеРеквизитаОбъекта);
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияКлюча.Тип = "employee" Тогда
		
		ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыПользователей", ЗначениеРеквизитаОбъекта);	
		
	ИначеЕсли ТипЗначенияКлюча.Тип = "crm_entity" Тогда
		
		Если ТипЗначенияКлюча.Настройки <> Неопределено Тогда
			
			ТипВладельца = Формат(ТипЗначенияКлюча.Настройки.Получить("parentEntityTypeId"), "ЧГ=0");
			Если ТипВладельца = "2" Тогда
				ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыСделокЗаказов", ЗначениеРеквизитаОбъекта);	
				Если Лев(ЗначениеКлюча, 2) = ПрефиксыВнешнихКодовБитрикс24.Сделки Тогда ЗначениеКлюча = Прав(ЗначениеКлюча, СтрДлина(ЗначениеКлюча)-СтрДлина(ПрефиксыВнешнихКодовБитрикс24.Сделки)) КонецЕсли;
				Если Лев(ЗначениеКлюча, 2) = ПрефиксыВнешнихКодовБитрикс24.Заказы Тогда ЗначениеКлюча = Прав(ЗначениеКлюча, СтрДлина(ЗначениеКлюча)-СтрДлина(ПрефиксыВнешнихКодовБитрикс24.Заказы)) КонецЕсли;
			ИначеЕсли ТипВладельца = "3" Тогда
				
				ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", ЗначениеРеквизитаОбъекта);	
				Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
					ЗначениеКлюча = СформироватьКонтактПоСсылке(СтруктураНастроек, ЗначениеРеквизитаОбъекта);
				КонецЕсли;
				
				Если Лев(ЗначениеКлюча, 1) = "#" Тогда ЗначениеКлюча = Прав(ЗначениеКлюча, СтрДлина(ЗначениеКлюча)-1) КонецЕсли;
				
			ИначеЕсли ТипВладельца = "4" Тогда
				
				ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", ЗначениеРеквизитаОбъекта);	
				Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
					ЗначениеКлюча = СформироватьКомпаниюПоСсылке(СтруктураНастроек, ЗначениеРеквизитаОбъекта);
				КонецЕсли;
				
			ИначеЕсли ТипВладельца = "31" Тогда
				
				ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыСчетов", ЗначениеРеквизитаОбъекта);	
				
			Иначе            //предполагаем смарт-процесс
				
				Для каждого ТекСмартПроцесс Из СтруктураНастроек.НастройкиСинхронизации Цикл
					Если ТекСмартПроцесс.ТипВладельца = ТипВладельца Тогда 
						ЗначениеКлюча = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьИдОбъекта(СтруктураНастроек.Портал, ТекСмартПроцесс.ТипВладельца, ТекСмартПроцесс.Ид, ЗначениеРеквизитаОбъекта);
						Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
							ЗначениеКлюча = СформироватьЭлементСмартПроцессаПоСсылке(СтруктураНастроек, ТекСмартПроцесс, ЗначениеРеквизитаОбъекта);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе
			ЗначениеКлюча = Строка(ЗначениеРеквизитаОбъекта);
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияКлюча.Тип = "crm_contact" Тогда
		
		ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКонтактов", ЗначениеРеквизитаОбъекта);	
		Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
			СформироватьКонтактПоСсылке(СтруктураНастроек, ЗначениеРеквизитаОбъекта);
		КонецЕсли;
		
		Если Лев(ЗначениеКлюча, 1) = "#" Тогда ЗначениеКлюча = Прав(ЗначениеКлюча, СтрДлина(ЗначениеКлюча)-1) КонецЕсли;
		
	ИначеЕсли ТипЗначенияКлюча.Тип = "crm_company" Тогда
		
		ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыКомпаний", ЗначениеРеквизитаОбъекта);	
		
		Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
			ЗначениеКлюча = СформироватьКомпаниюПоСсылке(СтруктураНастроек, ЗначениеРеквизитаОбъекта);
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияКлюча.Тип = "user" Тогда
		
		ЗначениеКлюча = ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыПользователей", ЗначениеРеквизитаОбъекта);	
		
	ИначеЕсли ТипЗначенияКлюча.Тип = "crm_currency" ИЛИ ТипЗначенияКлюча.Тип = "money" Тогда
		
		ЗначениеКлюча = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьВалютуПоКоду(ЗначениеРеквизитаОбъекта.Код);
		
	ИначеЕсли ТипЗначенияКлюча.Тип = "enumeration" И ОписаниеЗаполненияКлюча.ИсточникДанных = "Фиксированное значение" Тогда
		
		Если ЗначениеЗаполнено(ЗначениеРеквизитаОбъекта) Тогда
			ЗначениеКлюча = ЗначениеРеквизитаОбъекта;  
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияКлюча.Тип = "enumeration" 
		И (ОписаниеЗаполненияКлюча.ИсточникДанных = "Реквизит объекта"
		ИЛИ ОписаниеЗаполненияКлюча.ИсточникДанных = "Произвольный алгоритм") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Наименование"	, Строка(ЗначениеРеквизитаОбъекта));
		Запрос.УстановитьПараметр("ДопИдентификатор", ОписаниеЗаполненияКлюча.Идентификатор);
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_ИдентификаторыЗначенийПользовательскихПолей.Идентификатор КАК Идентификатор
		|ИЗ
		|	ВТ_ИдентификаторыЗначенийПользовательскихПолей КАК ВТ_ИдентификаторыЗначенийПользовательскихПолей
		|ГДЕ
		|	ВТ_ИдентификаторыЗначенийПользовательскихПолей.ДополнительныйИдентификатор = &ДопИдентификатор
		|	И ВТ_ИдентификаторыЗначенийПользовательскихПолей.Наименование = &Наименование";
		
		ВыполненныйЗапрос = Запрос.Выполнить();
		Если НЕ ВыполненныйЗапрос.Пустой() Тогда
			
			Выборка = ВыполненныйЗапрос.Выбрать();
			Выборка.Следующий();
			ЗначениеКлюча = Выборка.Идентификатор;
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
			ЗначениеКлюча = СформироватьЗначениеСвойстваПоСсылке(СтруктураНастроек, ОписаниеЗаполненияКлюча, ЗначениеРеквизитаОбъекта)
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияКлюча.Тип = "boolean" Тогда
		ЗначениеКлюча = ?(ЗначениеРеквизитаОбъекта = Истина, "Y", "N");
	Иначе
		
		МассивПростыхТипов = ПолучитьМассивПростыхТипов();
		Если МассивПростыхТипов.Найти(ТипЗнч(ЗначениеРеквизитаОбъекта)) = Неопределено Тогда
			ЗначениеКлюча = Строка(ЗначениеРеквизитаОбъекта);	      
		Иначе
			ЗначениеКлюча = ЗначениеРеквизитаОбъекта;	      
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЗначениеКлюча;
	
КонецФункции


Процедура ПолучитьСтруктуруКлючейТЧЭлементаВыгрузки(СтруктураНастроек, ДополнительныеПараметры, ПоляТЧ)
	
	ИнформацияОПолях 		= ДополнительныеПараметры.ИнформацияОПолях;
	ОписаниеКлючей 			= ДополнительныеПараметры.ОписаниеКлючей;
	МенеджерВременныхТаблиц = ДополнительныеПараметры.МенеджерВременныхТаблиц;
	ИнформацияОбъекта1С 	= ДополнительныеПараметры.ИнформацияОбъекта1С;
	ЭтоНовыйЭлемент 		= ДополнительныеПараметры.ЭтоНовыйЭлемент;
	КлючЭлемента 			= ДополнительныеПараметры.КлючЭлемента;
	ИнформацияСтрокиТЧ		= ДополнительныеПараметры.ИнформацияСтрокиТЧ;
	СсылкаНаОбъект1С 		= ИнформацияОбъекта1С.Объект;
	
	Для каждого ТекПоле Из ИнформацияОПолях Цикл
		
		Если ТекПоле.ИсточникДанных	= "" ИЛИ ТекПоле.ИсточникДанных = "Не изменять" Тогда		
			Продолжить;
		КонецЕсли;   
		
		Если НЕ ЗначениеЗаполнено(ТекПоле.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеКлюча = Неопределено;
		
		Попытка
			
			Если ТекПоле.ИсточникДанных = "Фиксированное значение" Тогда
				
				ЗначениеРеквизитаОбъекта = ТекПоле.Значение;
				
				ЗначениеКлюча = ПолучитьЗначениеКлючаТЧСмартПроцесса(СтруктураНастроек, ДополнительныеПараметры, ТекПоле, ЗначениеРеквизитаОбъекта);
					
			ИначеЕсли ТекПоле.ИсточникДанных = "Реквизит объекта" Тогда
				
				ЗначениеРеквизитаОбъекта 		= ИнформацияСтрокиТЧ[ТекПоле.Значение];          
				
				ЗначениеКлюча = ПолучитьЗначениеКлючаТЧСмартПроцесса(СтруктураНастроек, ДополнительныеПараметры, ТекПоле, ЗначениеРеквизитаОбъекта);
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Предопределенный алгоритм" Тогда
				
				ЗначениеРеквизитаОбъекта = ТекПоле.Значение;
				
				ЗначениеКлюча = ПолучитьЗначениеКлючаТЧСмартПроцесса(СтруктураНастроек, ДополнительныеПараметры, ТекПоле, ЗначениеРеквизитаОбъекта);
				
			ИначеЕсли ТекПоле.ИсточникДанных = "Произвольный алгоритм" Тогда
				
				Параметры = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруПараметровПредопределенногоАлгоритма();
				Параметры.СтруктураНастроек 		= СтруктураНастроек;
				Параметры.Объект1С 					= СсылкаНаОбъект1С;
				Параметры.СтруктураДанных 			= ПоляТЧ;
				Параметры.СтрокаТаблицыОбъекта1С 	= ИнформацияСтрокиТЧ;
				
				ЗначениеРеквизитаОбъекта = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПредопределенногоАлгоритма(ТекПоле.Значение, Параметры, "Функция");		
				
				ЗначениеКлюча = ПолучитьЗначениеКлючаТЧСмартПроцесса(СтруктураНастроек, ДополнительныеПараметры, ТекПоле, ЗначениеРеквизитаОбъекта);
				
			КонецЕсли;
				
		Исключение                                                                                                                              
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, ТекПоле.КлючЗапроса + "/" + ТекПоле.Значение + "/" + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
		ПоляТЧ.Вставить(ТекПоле.КлючЗапроса, ЗначениеКлюча);
		
	КонецЦикла;
		
КонецПроцедуры

Функция ПолучитьЗначениеКлючаТЧСмартПроцесса(СтруктураНастроек, ДополнительныеПараметры, ОписаниеЗаполненияКлюча, ЗначениеРеквизитаОбъекта)
	
	МенеджерВременныхТаблиц = ДополнительныеПараметры.МенеджерВременныхТаблиц;
	ИнформацияСтрокиТЧ		= ДополнительныеПараметры.ИнформацияСтрокиТЧ;
	СсылкаНаОбъект1С 		= ДополнительныеПараметры.ИнформацияОбъекта1С.Объект;
	РеквизитыТЧ				= Метаданные.НайтиПоТипу(ТипЗнч(ИнформацияСтрокиТЧ)).Реквизиты;	 
	
	ЗначениеКлюча = ЗначениеРеквизитаОбъекта;
	
	Если ОписаниеЗаполненияКлюча.ИсточникДанных = "Предопределенный алгоритм" Тогда
		
		Если ОписаниеЗаполненияКлюча.Значение = "Идентификатор товара" Тогда

			Если СтруктураНастроек.ВерсияТоваров = "v.2" Тогда
				
				ЗначениеКлюча = ПолучитьИдентификаторПозиции(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыПредложений", ИнформацияСтрокиТЧ.Номенклатура, ИнформацияСтрокиТЧ.Характеристика);
				Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда     
					ЗначениеКлюча = СформироватьТоварПоСсылке(СтруктураНастроек, ИнформацияСтрокиТЧ.Номенклатура, ИнформацияСтрокиТЧ.Характеристика);
				КонецЕсли;     
				
				Если НЕ ЗначениеЗаполнено(ЗначениеКлюча) Тогда
					ЗначениеКлюча = ПолучитьИдентификаторПозиции(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыТоваров", ИнформацияСтрокиТЧ.Номенклатура, ИнформацияСтрокиТЧ.Характеристика);
				КонецЕсли;
				
			Иначе
				
				ЗначениеКлюча = ПолучитьИдентификаторПозиции(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыТоваров", ИнформацияСтрокиТЧ.Номенклатура, ИнформацияСтрокиТЧ.Характеристика);
				
			КонецЕсли;
			
		ИначеЕсли ОписаниеЗаполненияКлюча.Значение = "Описание товарной позиции" Тогда
			
			ОписаниеТовара = "";			
			Если НЕ ЗначениеЗаполнено(ИнформацияСтрокиТЧ.Характеристика) Тогда
				ОписаниеТовара = Строка(ИнформацияСтрокиТЧ.Номенклатура);
			Иначе     
				
				Если СтруктураНастроек.ВерсияТоваров = "v.2" Тогда
					ОписаниеТовара = Строка(ИнформацияСтрокиТЧ.Характеристика); 
				Иначе
					ОписаниеТовара = Строка(ИнформацияСтрокиТЧ.Номенклатура) + " (" + Строка(ИнформацияСтрокиТЧ.Характеристика) + ")";
				КонецЕсли;  
				
			КонецЕсли;
			
			ЗначениеКлюча = ОписаниеТовара;
			
		ИначеЕсли ОписаниеЗаполненияКлюча.Значение = "Код единицы измерения" Тогда
			
			Если РеквизитыТЧ.Найти("ЕдиницаИзмерения") <> Неопределено Тогда
				ЗначениеКлюча = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИнформацияСтрокиТЧ.ЕдиницаИзмерения, "Код"));
			КонецЕсли;
			
		ИначеЕсли ОписаниеЗаполненияКлюча.Значение = "Наименование единицы измерения" Тогда
			
			Если РеквизитыТЧ.Найти("ЕдиницаИзмерения") <> Неопределено Тогда
				ЗначениеКлюча = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИнформацияСтрокиТЧ.ЕдиницаИзмерения, "Наименование");
			КонецЕсли;
			
		ИначеЕсли ОписаниеЗаполненияКлюча.Значение = "Количество" Тогда
			
			Если РеквизитыТЧ.Найти("Количество") <> Неопределено Тогда
				ЗначениеКлюча = ИнформацияСтрокиТЧ.Количество;
			КонецЕсли;  
			
		ИначеЕсли ОписаниеЗаполненияКлюча.Значение = "Цена" Тогда
			
			Если РеквизитыТЧ.Найти("Сумма") <> Неопределено И РеквизитыТЧ.Найти("Количество") <> Неопределено Тогда  
				Если СсылкаНаОбъект1С.ЦенаВключаетНДС = Истина Тогда
					ЗначениеКлюча = ИнформацияСтрокиТЧ.Сумма/ИнформацияСтрокиТЧ.Количество;	      
				Иначе
					ЗначениеКлюча = (ИнформацияСтрокиТЧ.Сумма+ИнформацияСтрокиТЧ.СуммаНДС)/ИнформацияСтрокиТЧ.Количество;	      
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ОписаниеЗаполненияКлюча.Значение = "Ставка НДС" Тогда
			
			Если РеквизитыТЧ.Найти("СтавкаНДС") <> Неопределено Тогда    
				ЗначениеКлюча = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтавкуПоНДС(ИнформацияСтрокиТЧ.СтавкаНДС);
			КонецЕсли;
			
		ИначеЕсли ОписаниеЗаполненияКлюча.Значение = "НДС включен в цену" Тогда
			
			ЗначениеКлюча = ?(СсылкаНаОбъект1С.ЦенаВключаетНДС, "Y", "N");
			
		ИначеЕсли ОписаниеЗаполненияКлюча.Значение = "Процент скидки" Тогда
			
			Если РеквизитыТЧ.Найти("ПроцентРучнойСкидки") <> Неопределено Тогда  
				ЗначениеКлюча = ИнформацияСтрокиТЧ.ПроцентРучнойСкидки;	
			КонецЕсли;
			
		ИначеЕсли ОписаниеЗаполненияКлюча.Значение = "Сумма скидки" Тогда
			
			Если РеквизитыТЧ.Найти("СуммаРучнойСкидки") <> Неопределено Тогда  
				ЗначениеКлюча = ИнформацияСтрокиТЧ.СуммаРучнойСкидки/ИнформацияСтрокиТЧ.Количество;	      
			КонецЕсли;   
			
		ИначеЕсли ОписаниеЗаполненияКлюча.Значение = "Тип скидки" Тогда
			
			ЗначениеКлюча = "2";
			
		ИначеЕсли ОписаниеЗаполненияКлюча.Значение = "Изменен" Тогда
			
			ЗначениеКлюча = "Y";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЗначениеКлюча;
	
КонецФункции

#КонецОбласти


#Область ПринудительнаяВыгрузкаПодчиненныхОбъектов1С

Функция ОпределитьСущностьЯвляетсяКонтактомБитрикс24(СтруктураНастроек, ЗначениеРеквизитаОбъекта)
	
	Результат = Ложь;
	
	ТипЗначенияРеквизитаОбъекта = ТипЗнч(ЗначениеРеквизитаОбъекта);
	Если ТипЗначенияРеквизитаОбъекта = Тип("СправочникСсылка.Организации") Тогда
		Результат = Ложь;
	ИначеЕсли ТипЗначенияРеквизитаОбъекта = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
		Результат = Истина	
	ИначеЕсли ТипЗначенияРеквизитаОбъекта = Тип("СправочникСсылка.Партнеры") Тогда
		Результат = СтруктураНастроек.ТипыКонтрагентовДляКомпаний.Найти(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеРеквизитаОбъекта, "ЮрФизЛицо")) = Неопределено;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьКомпаниюПоСсылке(СтруктураНастроек, Объект1С)
	
	Ид = "";
	
	Если НЕ ЗначениеЗаполнено(Объект1С) Тогда
		Возврат Ид;
	КонецЕсли;
	
	ЭтоКонтакт = ОпределитьСущностьЯвляетсяКонтактомБитрикс24(СтруктураНастроек, Объект1С);
	Если ЭтоКонтакт = Ложь И Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") Тогда

 		Если СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроекСинхронизации = СтруктураНастроек.СтруктураНастроекДляПодсистемыСинхронизации;
			СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации 	= СтруктураНастроек.НастройкиПодключенияАвторизации;
		Иначе
			МодульОбщегоНазначенияВызовСервера = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ОбщегоНазначенияВызовСервера");
			СтруктураНастроекСинхронизации 	= МодульОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(СтруктураНастроек.НастройкаПодключения,,3, Истина);   
			Если СтруктураНастроекСинхронизации = Неопределено Тогда
				Возврат Ид;
			КонецЕсли;  		
		КонецЕсли;    		
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		СтруктураНастроекСинхронизации.Логирование = СтруктураНастроек.Логирование;

		МодульВыгрузкаСервер = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ВыгрузкаСервер");
		Ид = МодульВыгрузкаСервер.СформироватьКомпаниюПоСсылке(СтруктураНастроекСинхронизации, Объект1С);
		
		СтруктураНастроек.Логирование.НомерСообщения 		= СтруктураНастроекСинхронизации.Логирование.НомерСообщения;
		СтруктураНастроек.НастройкиПодключенияАвторизации 	= СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////		
		Если НЕ СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроек.Вставить("СтруктураНастроекДляПодсистемыСинхронизации", СтруктураНастроекСинхронизации);
		КонецЕсли;  	
		
	Иначе
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроекСинхронизации,  СтруктураНастроек.ТипыСообщений.Ошибка, "Объект: " + Строка(Объект1С) + " не может быть выгружен как компания. Не является компанией по настройке синхронизации.");
	КонецЕсли;
	
	Возврат Ид;
	
КонецФункции

Функция СформироватьКонтактПоСсылке(СтруктураНастроек, Объект1С)
	
	Ид = "";
	
	Если НЕ ЗначениеЗаполнено(Объект1С) Тогда
		Возврат Ид;
	КонецЕсли;
	
	ЭтоКонтакт = ОпределитьСущностьЯвляетсяКонтактомБитрикс24(СтруктураНастроек, Объект1С);
	Если ЭтоКонтакт = Истина И Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") Тогда
		
 		Если СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроекСинхронизации = СтруктураНастроек.СтруктураНастроекДляПодсистемыСинхронизации;
			СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации 	= СтруктураНастроек.НастройкиПодключенияАвторизации;
		Иначе
			МодульОбщегоНазначенияВызовСервера = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ОбщегоНазначенияВызовСервера");
			СтруктураНастроекСинхронизации 	= МодульОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(СтруктураНастроек.НастройкаПодключения,,3, Истина);   
			Если СтруктураНастроекСинхронизации = Неопределено Тогда
				Возврат Ид;
			КонецЕсли;  			
		КонецЕсли;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////	
		СтруктураНастроекСинхронизации.Логирование = СтруктураНастроек.Логирование; 
		
		МодульВыгрузкаСервер = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ВыгрузкаСервер");
		Ид = МодульВыгрузкаСервер.СформироватьКонтактПоСсылке(СтруктураНастроекСинхронизации, Объект1С);
		
		СтруктураНастроек.Логирование.НомерСообщения 		= СтруктураНастроекСинхронизации.Логирование.НомерСообщения;
		СтруктураНастроек.НастройкиПодключенияАвторизации 	= СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации;    
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////		
		Если НЕ СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроек.Вставить("СтруктураНастроекДляПодсистемыСинхронизации", СтруктураНастроекСинхронизации);
		КонецЕсли;
		
	Иначе
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроекСинхронизации,  СтруктураНастроек.ТипыСообщений.Ошибка, "Объект: " + Строка(Объект1С) + " не может быть выгружен как контакт. Не является контактом по настройке синхронизации.");
	КонецЕсли;
	
	Возврат Ид;
	
КонецФункции

Функция СформироватьЗначениеСвойстваПоСсылке(СтруктураНастроек, ИнформацияОКлюче, ЗначениеСвойства)
	
	Ид = "";
	
	Если НЕ ЗначениеЗаполнено(ЗначениеСвойства) Тогда
		Возврат Ид;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИнформацияОКлюче.Идентификатор) Тогда   
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удается создать значение свойств по ссылке. Указано не пользовательское поле:" + ИнформацияОКлюче.Описание);
		Возврат Ид;
	КонецЕсли;
	
	ПараметрыЗапроса = "&moduleId=crm&id="+ИнформацияОКлюче.Идентификатор;
	ДанныеПортала = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/userfieldconfig.get", ПараметрыЗапроса); 
	Если ДанныеПортала = Неопределено Тогда
		Возврат Ид;
	КонецЕсли;

	result = ДанныеПортала.Получить("result");
	Если result = Неопределено Тогда
		Возврат Ид;
	КонецЕсли;
	
	field = result.Получить("field");
	Если field = Неопределено Тогда
		Возврат Ид;
	КонецЕсли;
	
	enum = field.Получить("enum");
	Если enum = Неопределено Тогда
		Возврат Ид;
	КонецЕсли;
	
	ЗначениеСтрокой = Строка(ЗначениеСвойства);
	Для каждого ТекЭлемент Из enum Цикл
		Если ТекЭлемент.Получить("value") = ЗначениеСтрокой Тогда
			Ид = Формат(ТекЭлемент.Получить("id"), "ЧГ=0");	
		КонецЕсли;
	КонецЦикла;
	
	Если Ид = "" Тогда
		
		ПараметрыЗапросаЗначения = "&field[userTypeId]=enumeration&field[enum][1][value]="+ Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ЗначениеСтрокой);
		
		Итератор = 2;
		Для каждого ТекЭлемент Из enum Цикл
			ПараметрыЗапросаЗначения = ПараметрыЗапросаЗначения + "&field[enum]["+Формат(Итератор, "ЧГ=0")+"][value]="+ Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Формат(ТекЭлемент.Получить("value"), "ЧГ=0"));
			ПараметрыЗапросаЗначения = ПараметрыЗапросаЗначения + "&field[enum]["+Формат(Итератор, "ЧГ=0")+"][id]="+ Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Формат(ТекЭлемент.Получить("id"), "ЧГ=0"));
			Итератор = Итератор + 1;
		КонецЦикла;
		
		ПараметрыЗапроса = "&moduleId=crm&id="+ИнформацияОКлюче.Идентификатор + ПараметрыЗапросаЗначения;
		ДанныеПортала = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/userfieldconfig.update", ПараметрыЗапроса); 
		
		result = ДанныеПортала.Получить("result");
		Если result = Неопределено Тогда
			Возврат Ид;
		КонецЕсли;
		
		field = result.Получить("field");
		Если field = Неопределено Тогда
			Возврат Ид;
		КонецЕсли;
		
		enum = field.Получить("enum");
		Если enum = Неопределено Тогда
			Возврат Ид;
		КонецЕсли;
		
		ЗначениеСтрокой = Строка(ЗначениеСвойства);
		Для каждого ТекЭлемент Из enum Цикл
			Если ТекЭлемент.Получить("value") = ЗначениеСтрокой Тогда
				Ид = Формат(ТекЭлемент.Получить("id"), "ЧГ=0");	
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;

	Если ЗначениеЗаполнено(Ид) Тогда
		РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваСмартПроцесса, ЗначениеСвойства, Ид, ИнформацияОКлюче.Идентификатор);
	КонецЕсли;
	
	Возврат Ид;
	
КонецФункции

Функция СформироватьЭлементСмартПроцессаПоСсылке(СтруктураНастроек, СмартПроцесс, ЗначениеЭлемента)
	
	Ид = "";
	
	Если НЕ ЗначениеЗаполнено(ЗначениеЭлемента) Тогда
		Возврат Ид;
	КонецЕсли;
	
	Если СмартПроцесс.Данные.Сущность1С = Неопределено Тогда
		Возврат Ид;
	КонецЕсли;

	ПредыдущиеКлючиСмартПроцесса = СтруктураНастроек.КлючиСмартПроцесса;
	
	лПолнаяВыгрузка = СтруктураНастроек.ПолнаяВыгрузка;
	СтруктураНастроек.ПолнаяВыгрузка = Ложь;
	
	СтруктураНастроек.КлючиСмартПроцесса = Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьСтруктуруКлючейСмартПроцесса(СтруктураНастроек.НастройкаПодключения, СмартПроцесс.ТипВладельца);
	
	ДатаВМиллисекундах = ТекущаяУниверсальнаяДатаВМиллисекундах() - 60*60*24*1000;
	
	
	НоваяЗапись = РегистрыСведений.Б24_СМ_ТаблицаИзменений.СоздатьМенеджерЗаписи();
	НоваяЗапись.НастройкаПодключения		= СтруктураНастроек.НастройкаПодключения;
	НоваяЗапись.ТипВладельцаСмартПроцесса 	= СмартПроцесс.ТипВладельца;
	НоваяЗапись.ИдСмартПроцесса 			= СмартПроцесс.Ид; 
	НоваяЗапись.ТипСущности1С 				= СмартПроцесс.Данные.ТипСущности1С; 
	НоваяЗапись.Сущность1С					= СмартПроцесс.Данные.Сущность1С.Значение;
	НоваяЗапись.ИдентификаторОбъекта		= XMLСтрока(ЗначениеЭлемента);
	НоваяЗапись.ВремяЗаписиВМиллисекундах	= ДатаВМиллисекундах;
	НоваяЗапись.Записать();
	
	
	ДанныеЭлемента = Новый Структура;
	ДанныеЭлемента.Вставить("ИдСмартПроцесса"			, СмартПроцесс.Ид);
	ДанныеЭлемента.Вставить("ТипВладельцаСмартПроцесса"	, СмартПроцесс.ТипВладельца);
	ДанныеЭлемента.Вставить("Наименование"				, СмартПроцесс.Наименование);
	ДанныеЭлемента.Вставить("Данные"					, СмартПроцесс.Данные);
	
	Б24_СМ_ОбщегоНазначенияВызовСервера.СформироватьИОтправить(СтруктураНастроек, ДанныеЭлемента);	
	
	СтруктураНастроек.ПолнаяВыгрузка = лПолнаяВыгрузка;
	
	Ид = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьИдОбъекта(СтруктураНастроек.Портал, СмартПроцесс.ТипВладельца, СмартПроцесс.Ид, ЗначениеЭлемента);
	
	СтруктураНастроек.КлючиСмартПроцесса = ПредыдущиеКлючиСмартПроцесса;	
	
	Возврат Ид;
	
КонецФункции

Функция СформироватьТоварПоСсылке(СтруктураНастроек, Объект1С, ПодчиненныйОбъект)
	
	Ид = "";
		
	Если НЕ ЗначениеЗаполнено(Объект1С) Тогда
		Возврат Ид;
	КонецЕсли;
	
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") Тогда
		
		Если СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроекСинхронизации = СтруктураНастроек.СтруктураНастроекДляПодсистемыСинхронизации;
			СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации 	= СтруктураНастроек.НастройкиПодключенияАвторизации;
		Иначе
			МодульОбщегоНазначенияВызовСервера = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ОбщегоНазначенияВызовСервера");
			СтруктураНастроекСинхронизации 	= МодульОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(СтруктураНастроек.НастройкаПодключения,,3, Истина);    
			Если СтруктураНастроекСинхронизации = Неопределено Тогда
				Возврат Ид;
			КонецЕсли;  		
		КонецЕсли;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////			
       	СтруктураНастроекСинхронизации.Логирование = СтруктураНастроек.Логирование; 
		
 		МодульВыгрузкаСервер = ОбщегоНазначения.ОбщийМодуль("Б24_КС_ВыгрузкаСервер");
		Ид = МодульВыгрузкаСервер.СформироватьТоварПоСсылке(СтруктураНастроекСинхронизации, Объект1С, ПодчиненныйОбъект);
		
		СтруктураНастроек.Логирование.НомерСообщения 		= СтруктураНастроекСинхронизации.Логирование.НомерСообщения;
		СтруктураНастроек.НастройкиПодключенияАвторизации 	= СтруктураНастроекСинхронизации.НастройкиПодключенияАвторизации;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////			
		Если НЕ СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСинхронизации") Тогда
			СтруктураНастроек.Вставить("СтруктураНастроекДляПодсистемыСинхронизации", СтруктураНастроекСинхронизации);
		КонецЕсли;
		
	КонецЕсли;	
		
	Возврат Ид;
	
КонецФункции


Функция ПолучитьИдентификаторСмартПроцессаДляДругихПодсистем(СтруктураНастроек, ТипВладельцаСмартПроцесса, Значение) Экспорт
	
	Ид = Неопределено;
	
	Если СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСмартПроцессы") Тогда
		СтруктураНастроекСмартПроцессов = СтруктураНастроек.СтруктураНастроекДляПодсистемыСмартПроцессы;
		СтруктураНастроекСмартПроцессов.СтруктураНастроек.НастройкиПодключенияАвторизации = СтруктураНастроек.НастройкиПодключенияАвторизации;     //передаем актуальный токен
	Иначе
		
		СтруктураНастроекСмартПроцессов = Новый Структура;
		СтруктураНастроекСмартПроцессов.Вставить("СмартПроцессы", Новый Соответствие);
		
		СтруктураНастроекСмартПроцессы = Б24_СМ_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(СтруктураНастроек.НастройкаПодключения, Ложь, 3);
		Если СтруктураНастроекСмартПроцессы = Неопределено Тогда
			Возврат Ид;
		КонецЕсли;
		СтруктураНастроекСмартПроцессов.Вставить("СтруктураНастроек", СтруктураНастроекСмартПроцессы);
		
	КонецЕсли;
		
	СтруктураНастроекСмартПроцессов.СтруктураНастроек.Логирование = СтруктураНастроек.Логирование;            //передаем актуальный данные по логированию
			
			
	ДанныеСмартПроцесса = СтруктураНастроекСмартПроцессов.СмартПроцессы.Получить(ТипВладельцаСмартПроцесса);
	Если ДанныеСмартПроцесса = Неопределено Тогда
		
		ДанныеСмартПроцесса = Новый Структура;
		ДанныеСмартПроцесса.Вставить("СмартПроцесс"			, Неопределено);
		ДанныеСмартПроцесса.Вставить("КлючиСмартПроцесса"	, Неопределено);
		
		
		НастройкиСинхронизации = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(СтруктураНастроек.НастройкаПодключения);
		Если НастройкиСинхронизации.Настройки = Неопределено Тогда
			Возврат Ид;
		КонецЕсли;
			
		ДанныеСмартПроцессов = НастройкиСинхронизации.Настройки.Получить();
		Если ДанныеСмартПроцессов = Неопределено Тогда
			Возврат Ид;
		КонецЕсли;

		Для каждого ТекСмартПроцесс Из ДанныеСмартПроцессов Цикл
		
			Если ТипВладельцаСмартПроцесса = ТекСмартПроцесс.ТипВладельца Тогда
				
				ДанныеСмартПроцесса.СмартПроцесс 		= ТекСмартПроцесс;
				ДанныеСмартПроцесса.КлючиСмартПроцесса 	= Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьСтруктуруКлючейСмартПроцесса(СтруктураНастроек.НастройкаПодключения, ТекСмартПроцесс.ТипВладельца);
				
			КонецЕсли;
		
		КонецЦикла;
		
		СтруктураНастроекСмартПроцессов.СмартПроцессы.Вставить(ТипВладельцаСмартПроцесса, ДанныеСмартПроцесса);

	КонецЕсли;
	
	Если ДанныеСмартПроцесса.СмартПроцесс <> Неопределено Тогда
		
		Ид = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьИдОбъекта(СтруктураНастроек.Портал, ТипВладельцаСмартПроцесса, ДанныеСмартПроцесса.СмартПроцесс.Ид, Значение);  
		Если НЕ ЗначениеЗаполнено(Ид) Тогда
					
			СтруктураНастроекСмартПроцессов.СтруктураНастроек.КлючиСмартПроцесса = ДанныеСмартПроцесса.КлючиСмартПроцесса;
						
			Ид = СформироватьЭлементСмартПроцессаПоСсылке(СтруктураНастроекСмартПроцессов.СтруктураНастроек, ДанныеСмартПроцесса.СмартПроцесс, Значение);
			
		КонецЕсли;
		
	КонецЕсли;
	
	
	Если НЕ СтруктураНастроек.Свойство("СтруктураНастроекДляПодсистемыСмартПроцессы") Тогда
		СтруктураНастроек.Вставить("СтруктураНастроекДляПодсистемыСмартПроцессы", СтруктураНастроекСмартПроцессов);
	КонецЕсли;
		
	СтруктураНастроек.Логирование.НомерСообщения 		= СтруктураНастроекСмартПроцессов.СтруктураНастроек.Логирование.НомерСообщения;              //чтобы не затирались. В разрезе номеров хранятся в регистре сведений
	СтруктураНастроек.НастройкиПодключенияАвторизации 	= СтруктураНастроекСмартПроцессов.СтруктураНастроек.НастройкиПодключенияАвторизации;         //возвращаем актуальный токен
	
	Возврат Ид;
		
КонецФункции

#КонецОбласти


#Область ПрочиеПроцедурыИФункции

Функция ПолучитьИдентификаторБитрикс24ПоЗначению(МенеджерВременныхТаблиц, ИмяТаблицы, Объект)
	
	//МенеджерВременныхТаблиц.Таблицы[ИмяТаблицы].ПолучитьДанные().Выгрузить()
	
	Результат = "";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_Идентификаторы.Идентификатор КАК Идентификатор
	|ИЗ
	|	"+ИмяТаблицы+" КАК ВТ_Идентификаторы
	|ГДЕ
	|	ВТ_Идентификаторы.Объект = &Объект";
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат = Выборка.Идентификатор;	
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция ПолучитьМассивПростыхТипов()
	
	Результат = Новый Массив;
	Результат.Добавить(Тип("Строка"));
	Результат.Добавить(Тип("Число"));
	Результат.Добавить(Тип("Дата"));
	Результат.Добавить(Тип("Булево"));
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСтруктуруКлючейБатчЗапросов(ДанныеЭлемента, Объект, ЗаписыватьПриИстина = Ложь) 
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИдСмартПроцесса"			, ДанныеЭлемента.ИдСмартПроцесса);
	Результат.Вставить("ТипВладельцаСмартПроцесса"	, ДанныеЭлемента.ТипВладельцаСмартПроцесса);
	Результат.Вставить("ТипСущности1С"				, ДанныеЭлемента.Данные.ТипСущности1С);
	Результат.Вставить("Сущность1С"					, ДанныеЭлемента.Данные.Сущность1С.Значение);
	Результат.Вставить("Объект"						, Объект);
	Результат.Вставить("ЗаписыватьПриИстина"		, ЗаписыватьПриИстина);
					
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТаблицуОбъектовДел(СтруктураНастроек, ОписаниеТиповОбъектаДел, МассивДанных)

	Массив = Новый Массив;
	Массив.Добавить(Тип("Строка"));
	ОписаниеТиповС = Новый ОписаниеТипов(Массив, , Новый КвалификаторыСтроки(100));
	
	тзнОбъектовДел = Новый ТаблицаЗначений;
	тзнОбъектовДел.Колонки.Добавить("Объект"				, ОписаниеТиповОбъектаДел);
	тзнОбъектовДел.Колонки.Добавить("ТипДанных"				, Новый ОписаниеТипов("ПеречислениеСсылка.Б24_К_ТипыДанныхДляОбменаСПорталом"));
	тзнОбъектовДел.Колонки.Добавить("Портал"				, ОписаниеТиповС);
	тзнОбъектовДел.Колонки.Добавить("ТипОбъекта"			, ОписаниеТиповС);
	тзнОбъектовДел.Колонки.Добавить("ИдентификаторОбъекта"	, ОписаниеТиповС);
                                                 
	Для каждого ТекЭлемент Из МассивДанных Цикл  
		
		Если НЕ ЗначениеЗаполнено(ТекЭлемент) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = тзнОбъектовДел.Добавить();
		НоваяСтрока.Портал 					= СтруктураНастроек.Портал;	
		НоваяСтрока.ТипДанных 				= СтруктураНастроек.ТипыОбъектовОбмена.ДелоСмартПроцесса;	
		НоваяСтрока.Объект 					= ТекЭлемент.Объект;	
		НоваяСтрока.ТипОбъекта 				= ТекЭлемент.Объект.Метаданные().ПолноеИмя();	
		НоваяСтрока.ИдентификаторОбъекта 	= ТекЭлемент.ИдентификаторОбъекта;	
		
	КонецЦикла; 
	
	Возврат тзнОбъектовДел;

КонецФункции   

Функция ПолучитьИдентификаторПозиции(МенеджерВременныхТаблиц, ИмяВТ, Номенклатура, Характеристика)
	
	Результат = "";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Объект"				, Номенклатура);
	Запрос.УстановитьПараметр("ПодчиненныйОбъект"	, Характеристика);
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_Идентификаторы.Идентификатор КАК Идентификатор
	|ИЗ
	|	"+ИмяВТ+" КАК ВТ_Идентификаторы
	|ГДЕ
	|	ВТ_Идентификаторы.Объект = &Объект
	|	И ВТ_Идентификаторы.ПодчиненныйОбъект = &ПодчиненныйОбъект";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат = Выборка.Идентификатор;	
			Прервать;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции  

#КонецОбласти
