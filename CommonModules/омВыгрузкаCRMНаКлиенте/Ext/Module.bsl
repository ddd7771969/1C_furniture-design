
&НаКлиенте
Функция ВыгрузитьВPDM(ЭлементВыгрузки, ТипВыгрузки = "Комплект", ПараметрыВыгрузки = Неопределено) Экспорт       
	стВозврата = Новый Структура("ОписаниеОшибки,ВыгруженВPDM","","");
	
	стДанныеВыгрузки = омВыгрузкаCRMНаСевере.ПолучитьДанныеВыгрузки();
	
	Если ТипВыгрузки = "Комплект" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXE;
	ИначеЕсли ТипВыгрузки = "Проект" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEПроекта;
	ИначеЕсли ТипВыгрузки = "Конструктор" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEКонструктор;
	ИначеЕсли ТипВыгрузки = "Композиция" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEКомпозиция;
	ИначеЕсли ТипВыгрузки = "ГабаритныйЧертеж" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEГабаритныйЧерчеж;
	ИначеЕсли ТипВыгрузки = "СтатусГЧ" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEИзменениеСтатусаГЧ;
	ИначеЕсли ТипВыгрузки = "ЗагрузкаТЗ" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEЗагрузкаТЗ;
	ИначеЕсли ТипВыгрузки = "РаздатьПрава" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEРаздатьПрава;
	ИначеЕсли ТипВыгрузки = "ЗабратьПрава" Тогда
		ПутьКФайлуEXE = стДанныеВыгрузки.ПутьКФайлуEXEЗабратьПрава;
	КонецЕсли; 
	
	Если ПустаяСтрока(ПутьКФайлуEXE) Тогда       
		
		стВозврата.ОписаниеОшибки = "Не заполнен путь к исполняемому файлу.";
		Возврат стВозврата;	
	
	КонецЕсли;
	
	ПутьКФайлуЗагрузки = стДанныеВыгрузки.ПутьКФайлуЗагрузки; 
	
	Если ПустаяСтрока(ПутьКФайлуЗагрузки) Тогда
		
		стВозврата.ОписаниеОшибки = "Каталог выгрузки не заполнен.";
		Возврат стВозврата;	
	
	КонецЕсли;
	
	ТестКаталог = Новый Файл(ПутьКФайлуЗагрузки); 
	
	Если НЕ ТестКаталог.Существует() Тогда
		
		стВозврата.ОписаниеОшибки = "Каталог " + ПутьКФайлуЗагрузки + " не существует.";
		Возврат стВозврата;	
	
	КонецЕсли; 

	стДанныеЭлемента = омВыгрузкаCRMНаСевере.ПолучитьДанныеЭлемента(ЭлементВыгрузки, ТипВыгрузки, ПараметрыВыгрузки);
	
	НаименованиеЭлемента = стДанныеЭлемента.НаименованиеЭлемента;
	
	Если стДанныеЭлемента.мДанныеЭлемента.Количество() = 0 Тогда
		
		Если ПустаяСтрока(стДанныеЭлемента.Ошибка) Тогда
			стВозврата.ОписаниеОшибки = "Нет данных для выгрузки";	
		Иначе
			стВозврата.ОписаниеОшибки = стДанныеЭлемента.Ошибка;	
		КонецЕсли; 
		
		Возврат стВозврата;
		
	КонецЕсли;
	
	ТекстДок = СоздатьФайлВыгрузки(стДанныеЭлемента.мДанныеЭлемента, ТипВыгрузки);
	
	Если ТипЗнч(ТекстДок) = Тип("Строка") Тогда
		стВозврата.ОписаниеОшибки = ТекстДок;	
		Возврат стВозврата;
	КонецЕсли;
	
	
	ИмяФайла = ТестКаталог.ПолноеИмя + "\" + НаименованиеЭлемента + ".csv";
	
	мНеЗапускатьПроверку = Новый Массив;
	
	Попытка
	
		ТекстДок.Записать(ИмяФайла,,КодировкаТекста.UTF8);
        стВозврата.ВыгруженВPDM = "Данные о " + ЭлементВыгрузки + " выгружены в файл " + ИмяФайла;
		
		Если НЕ ПустаяСтрока(ПутьКФайлуEXE) Тогда
		
			стрОшибка = ВыгрузитьФайлВPDM(ПутьКФайлуEXE);
			
			Если ПустаяСтрока(стрОшибка) Тогда
				
				Если мНеЗапускатьПроверку.Найти(ТипВыгрузки) = Неопределено Тогда
					ом_ПДМ_на_клиенте.УправлениеКонтроляВыгрузкиВPDM(Истина);
				КонецЕсли;
				
			Иначе
			
				стВозврата.ОписаниеОшибки = стрОшибка;	
			
			КонецЕсли;
		
		КонецЕсли;
	Исключение
		стВозврата.ОписаниеОшибки = "Не удалось сохранить выгрузку в файл " + ИмяФайла;
	КонецПопытки;
	
	Возврат стВозврата;
	
КонецФункции // ВыгрузитьВPDM()

&НаКлиенте
Функция СоздатьФайлВыгрузки(мДанныеЭлемента, ТипВыгрузки)
	
	ТекстДок = Новый ТекстовыйДокумент;
	
	Если ТипВыгрузки = "Комплект" Тогда
		
		стрТекст = "Номер проекта;Наименование проекта;Номер помещения;Номер изделия;Наименование изделия;ID 1C";
		
		Для каждого текИзделие Из мДанныеЭлемента Цикл
			
			стрТекст = стрТекст + Символы.ВК 
			+ текИзделие.НомерПроекта 			+ ";" 
			+ текИзделие.НаименованиеПроекта 	+ ";" 
			+ текИзделие.НомерПомещения 		+ ";" 
			+ текИзделие.НомерИзделия 			+ ";" 
			+ текИзделие.НаименованиеИзделия 	+ ";" 
			+ текИзделие.Код1С;
			
		КонецЦикла;
		
	ИначеЕсли ТипВыгрузки = "Проект"  Тогда
		
		
		стрТекст = "Номер проекта;Наименование проекта;Суффикс проекта;ID 1С;ID PDM";
		
		Для каждого текИзделие Из мДанныеЭлемента Цикл
			
			стрТекст = стрТекст + Символы.ВК 
			+ текИзделие.НомерПроекта 			+ ";" 
			+ текИзделие.НаименованиеПроекта 	+ ";" 
			+ текИзделие.СуффикПроекта 			+ ";" 
			+ текИзделие.Код1С 					+ ";" 
			+ текИзделие.ID_PDM;
			
		КонецЦикла; 
		
	ИначеЕсли ТипВыгрузки = "Конструктор"  Тогда
		стрТекст = "Номер проекта;Номер помещения;Номер изделия;Наименование изделия;ID 1С Изделия;Конструктор;Код в PDM;Зарегестрировать";
		
		Для каждого текИзделие Из мДанныеЭлемента Цикл
			
			Если ПустаяСтрока(текИзделие.Код_в_PDM) Тогда
				Возврат "Нет кода PDM";	
			КонецЕсли;
			
			стрТекст = стрТекст + Символы.ВК 
			+ текИзделие.НомерПроекта 			+ ";" 
			+ текИзделие.НомерПомещения 		+ ";" 
			+ текИзделие.НомерИзделия 			+ ";" 
			+ текИзделие.НаименованиеИзделия	+ ";" 
			+ текИзделие.Код1С					+ ";"
			+ текИзделие.Конструктор           	+ ";"
			+ текИзделие.Код_в_PDM           	+ ";"
			+ текИзделие.Отменить;
			
		КонецЦикла; 
		
	ИначеЕсли ТипВыгрузки = "Композиция"  Тогда
		стрТекст = "ID PDM проекта;Наименование композиции;ID 1С";
		
		Для каждого текИзделие Из мДанныеЭлемента Цикл
			Если ПустаяСтрока(текИзделие.IDПроекта) Тогда
				Возврат "Нет кода PDM";	
			КонецЕсли;
			
			стрТекст = стрТекст + Символы.ВК 
			+ текИзделие.IDПроекта	 			+ ";" 
			+ текИзделие.НаименованиеКомпозиции	+ ";"
			+ текИзделие.Код1С;
			
		КонецЦикла; 
	ИначеЕсли ТипВыгрузки = "ГабаритныйЧертеж"  Тогда
		стрТекст = "ID проекта;Наименование;ID 1С";
		
		Для каждого текИзделие Из мДанныеЭлемента Цикл
			Если ПустаяСтрока(текИзделие.IDПроекта) Тогда
				Возврат "Нет кода PDM";	
			КонецЕсли;
			
			стрТекст = стрТекст + Символы.ВК 
			+ текИзделие.IDПроекта 		+ ";" 
			+ текИзделие.Наименование	+ ";" 
			+ текИзделие.Код1С;
			
		КонецЦикла; 
	
	ИначеЕсли ТипВыгрузки = "СтатусГЧ"  Тогда
		
		стрТекст = "ID_GH;ID_1C;Status";
		
		Для каждого текИзделие Из мДанныеЭлемента Цикл
			Если ПустаяСтрока(текИзделие.ID_GH) Тогда
				Возврат "Нет кода PDM у ГЧ";	
			КонецЕсли;
			
			стрТекст = стрТекст + Символы.ВК 
			+ текИзделие.ID_GH 	+ ";" 
			+ текИзделие.ID_1C	+ ";" 
			+ текИзделие.Status;
			
		КонецЦикла; 
	ИначеЕсли ТипВыгрузки = "ЗагрузкаТЗ"  Тогда
		
		стрТекст = "Тип справочника;ID PDM;ID 1С;Путь к файлу";
		
		Для каждого текИзделие Из мДанныеЭлемента Цикл
			Если ПустаяСтрока(текИзделие.Код_в_PDM) Тогда
				Возврат "Нет кода PDM у объекты";	
			КонецЕсли;
			
			стрТекст = стрТекст + Символы.ВК 
			+ текИзделие.Тип_справочника 	+ ";" 
			+ текИзделие.Код_в_PDM 			+ ";" 
			+ текИзделие.ID_1C				+ ";" 
			+ текИзделие.Path;
			
		КонецЦикла; 
	ИначеЕсли ТипВыгрузки = "РаздатьПрава" ИЛИ ТипВыгрузки = "ЗабратьПрава" Тогда
		стрТекст = "ID PDM;UsrName";
		
		Для каждого текИзделие Из мДанныеЭлемента Цикл
			
			стрТекст = стрТекст + Символы.ВК 
			+ текИзделие.ID_PDM 	+ ";" 
			+ текИзделие.UsrName;
			
		КонецЦикла; 
	КонецЕсли;
	
	ТекстДок.УстановитьТекст(стрТекст);
	
	Возврат  ТекстДок;

КонецФункции // СоздатьФайлВыгрузки(стДанныеЭлемента.ТаблицаДанных, ТипВыгрузки)     

&НаКлиенте
Функция ВыгрузитьФайлВPDM(ПутьКФайлуEXE)

	ИсполняемыйФайл = Новый Файл(ПутьКФайлуEXE); 
	
	Если НЕ ИсполняемыйФайл.Существует() Тогда
		
		Возврат "Файл " + ПутьКФайлуEXE + " не существует.";
	
	КонецЕсли;
	
    Попытка
	
		ЗапуститьПриложениеАсинх(ПутьКФайлуEXE);
	
	Исключение
		Возврат "Не удалось выполнить файл " + ПутьКФайлуEXE;
	КонецПопытки;

	Возврат "";

КонецФункции // ВыгрузитьФайлВPDM()

