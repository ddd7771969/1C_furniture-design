
Процедура ДобавлениеКнопокТелефонии(Форма) Экспорт
	
	Для каждого ТекЭлемент Из Форма.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов Цикл
		
		Если ТекЭлемент.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
			
			ЭлементРодитель = Форма.Элементы[ТекЭлемент.ИмяРеквизита].Родитель;
			
			ИмяЭлемента  = "Б24_К_" + ТекЭлемент.ИмяРеквизита;          
			
			НоваяКоманда = Форма.Команды.Добавить(ИмяЭлемента);
			НоваяКоманда.Действие 		= "Б24_К_Позвонить";
			НоваяКоманда.Картинка 		= БиблиотекаКартинок.Б24_К_КартинкаПодсистемы;
			НоваяКоманда.Отображение 	= ОтображениеКнопки.Картинка;
			
			НовыйЭлемент 			= Форма.Элементы.Добавить(ИмяЭлемента, Тип("КнопкаФормы"));
			НовыйЭлемент.ИмяКоманды = ИмяЭлемента;
			НовыйЭлемент.Заголовок 	= "Позвонить";     
			Форма.Элементы.Переместить(НовыйЭлемент, ЭлементРодитель); 

		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьИмяЭлементаПредставленияКИ(ИмяДекорацииФормыКИ) Экспорт

	Возврат "Представление" + СтрЗаменить(ИмяДекорацииФормыКИ, "Б24_К_Действие", "");
	
КонецФункции

Функция ПолучитьМассивНастроекПодключенияСТелефонией() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_НастройкиПодключения.Ссылка КАК Настройка
	|ИЗ
	|	Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|ГДЕ
	|	Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ
	|	И Б24_К_НастройкиПодключения.ТелефонияБитрикс24 = ИСТИНА";
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Настройка");
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьКоличествоНастроекПодключенияСТелефонией() Экспорт
	
УстановитьПривилегированныйРежим(Истина);
	
	Результат = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Б24_К_НастройкиПодключения.Ссылка) КАК Количество
	|ИЗ
	|	Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|ГДЕ
	|	Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ
	|	И Б24_К_НастройкиПодключения.ТелефонияБитрикс24 = ИСТИНА";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат = Выборка.Количество; 
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкуПодключенияПоУмолчаниюСТелефонией() Экспорт
	
УстановитьПривилегированныйРежим(Истина);
	
	Результат = Справочники.Б24_К_НастройкиПодключения.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_НастройкиПодключения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|ГДЕ
	|	Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ
	|	И Б24_К_НастройкиПодключения.ТелефонияБитрикс24 = ИСТИНА";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка; 	
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции
