Процедура УстановитьПриоритет(Комплект, Приоритет) Экспорт

	КомплектМассив = ТипЗнч(Комплект) = Тип("Массив");
	
	Если КомплектМассив Тогда
		МассивКомплектов = ОбщегоНазначения.СкопироватьРекурсивно(Комплект);
	КонецЕсли;
	
	ТекущийПриоритет 	= Справочники.Приоритеты.ПустаяСсылка();
	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	ТД 					= ТекущаяДата();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ *
		|ИЗ
		|	РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект В(&Комплект)) КАК ДанныеКомплектаСрезПоследних";
	
	Запрос.УстановитьПараметр("Комплект", Комплект);
	
	НачатьТранзакцию();
		
	Попытка

		БлокировкаДанных = Новый БлокировкаДанных;
		
		Если КомплектМассив Тогда
		
			Для каждого ТекущийКомплект Из Комплект Цикл
			
				ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ДанныеКомплекта");
				ЭлементБлокировкиДанных.УстановитьЗначение("Комплект", ТекущийКомплект);
				ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			
			КонецЦикла;	
		
		Иначе
			
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ДанныеКомплекта");
			ЭлементБлокировкиДанных.УстановитьЗначение("Комплект", Комплект);
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			
		КонецЕсли;
		  
		БлокировкаДанных.Заблокировать();
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если КомплектМассив Тогда
				МассивКомплектов.Удалить(МассивКомплектов.Найти(Выборка.Комплект));
			КонецЕсли;
			
			ТекущийПриоритет = Выборка.Приоритет;
			
			Если ТекущийПриоритет = Приоритет Тогда
				Продолжить;	
			КонецЕсли;  
			
			МенеджерЗаписи = РегистрыСведений.ДанныеКомплекта.СоздатьМенеджерЗаписи();
			
			Если Выборка.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
			КонецЕсли; 
			
			МенеджерЗаписи.Период 		= ТД;
			МенеджерЗаписи.Автор 		= ТекущийПользователь;
			МенеджерЗаписи.Приоритет 	= Приоритет;
			
			МенеджерЗаписи.Записать();
			
		КонецЦикла; 
		
		Если КомплектМассив Тогда
			
			Для каждого ТекущийКомплект Из МассивКомплектов Цикл
				
				МенеджерЗаписи 				= РегистрыСведений.ДанныеКомплекта.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.Период 		= ТД;
				МенеджерЗаписи.Комплект 	= Комплект;
				МенеджерЗаписи.Приоритет 	= Приоритет;
				МенеджерЗаписи.Автор 		= ТекущийПользователь;
				
				МенеджерЗаписи.Записать();
				
			КонецЦикла;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заметки'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
КонецПроцедуры

Процедура СнятьПриоритет(Комплект) Экспорт

	УстановитьПриоритет(Комплект, Справочники.Приоритеты.ПустаяСсылка());	

КонецПроцедуры

