

&НаСервере
Функция ОпределениеОстаткаПотребности(Дата, мНоменклатура = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК ИзделияКомплект
		|ПОМЕСТИТЬ втИзделияКомплект
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыпускиПроизводственныхКомплектов КАК ВыпускиПроизводственныхКомплектов
		|		ПО (ВыпускиПроизводственныхКомплектов.ИзделиеКомплект = ИзделияКомплект.Ссылка)
		|ГДЕ
		|	НЕ ЕСТЬNULL(ВыпускиПроизводственныхКомплектов.ВсеИзготовленно, ЛОЖЬ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказПоставщикуОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказПоставщикуОстатки.КоличествоОстаток КАК Остаток
		|ПОМЕСТИТЬ втЗаказ
		|ИЗ
		|	РегистрНакопления.ЗаказПоставщику.Остатки(
		|			&Дата,
		|			ВЫБОР
		|				КОГДА &ВсяНоменклатура
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ Номенклатура В (&Номенклатура)
		|			КОНЕЦ) КАК ЗаказПоставщикуОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПотребностьВМатериалахОбороты.Номенклатура КАК Номенклатура,
		|	ПотребностьВМатериалахОбороты.ПотребностьОборот КАК ПотребностьОборот
		|ПОМЕСТИТЬ втПотребность
		|ИЗ
		|	РегистрНакопления.ПотребностьВМатериалах.Обороты(
		|			,
		|			&Дата,
		|			Период,
		|			ВЫБОР
		|					КОГДА &ВсяНоменклатура
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ Номенклатура В (&Номенклатура)
		|				КОНЕЦ
		|				И ИзделиеКомплект В
		|					(ВЫБРАТЬ
		|						втИзделияКомплект.ИзделияКомплект
		|					ИЗ
		|						втИзделияКомплект)) КАК ПотребностьВМатериалахОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТоваровНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ втОстатки
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровНаСкладах.Остатки(
		|			&Дата,
		|			ВЫБОР
		|				КОГДА &ВсяНоменклатура
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ Номенклатура В (&Номенклатура)
		|			КОНЕЦ) КАК ОстаткиТоваровНаСкладахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыданноСоСклада.Номенклатура КАК Номенклатура,
		|	СУММА(ВыданноСоСклада.Количество) КАК Количество
		|ПОМЕСТИТЬ втВыданно
		|ИЗ
		|	РегистрСведений.ВыданноСоСклада КАК ВыданноСоСклада
		|ГДЕ
		|	ВыданноСоСклада.ИзделиеКомплект В
		|			(ВЫБРАТЬ
		|				втИзделияКомплект.ИзделияКомплект
		|			ИЗ
		|				втИзделияКомплект)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыданноСоСклада.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПотребность.Номенклатура КАК Номенклатура,
		|	втПотребность.Номенклатура.Артикул КАК Артикул,
		|	втПотребность.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	втПотребность.ПотребностьОборот КАК Потребность,
		|	ЕСТЬNULL(втЗаказ.Остаток, 0) КАК Заказано,
		|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0) КАК Остаток,
		|	ЕСТЬNULL(втВыданно.Количество, 0) КАК Выдано
		|ИЗ
		|	втПотребность КАК втПотребность
		|		ЛЕВОЕ СОЕДИНЕНИЕ втЗаказ КАК втЗаказ
		|		ПО втПотребность.Номенклатура = втЗаказ.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
		|		ПО втПотребность.Номенклатура = втОстатки.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ втВыданно КАК втВыданно
		|		ПО втПотребность.Номенклатура = втВыданно.Номенклатура";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Номенклатура", мНоменклатура);
	Запрос.УстановитьПараметр("ВсяНоменклатура", мНоменклатура = Неопределено);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьОстатокПотребности()

&НаСервере
Функция ИзделияКомплектБезВыпуска() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК ИзделияКомплект
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыпускиПроизводственныхКомплектов КАК ВыпускиПроизводственныхКомплектов
		|		ПО (ВыпускиПроизводственныхКомплектов.ИзделиеКомплект = ИзделияКомплект.Ссылка)
		|ГДЕ
		|	НЕ ЕСТЬNULL(ВыпускиПроизводственныхКомплектов.ВсеИзготовленно, ЛОЖЬ)"; 
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИзделияКомплект");
		
КонецФункции // ИзделияКомплектБезВыпуска()
 
&НаСервере
Функция ОпределениеОстаткаПоступления(Дата, мНоменклатура = Неопределено, Поставщик = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПоставщикуОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказПоставщикуОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.ЗаказПоставщику.Остатки(
		|			&Дата,
		|			ВЫБОР
		|					КОГДА &ВсяНоменклатура
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ Номенклатура В (&Номенклатура)
		|				КОНЕЦ
		|				И ВЫБОР
		|					КОГДА &ВсеПоставщики
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ Поставщик В (&Поставщик)
		|				КОНЕЦ) КАК ЗаказПоставщикуОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Номенклатура", мНоменклатура);
	Запрос.УстановитьПараметр("ВсяНоменклатура", мНоменклатура = Неопределено);
	Запрос.УстановитьПараметр("Поставщик", Поставщик);
	Запрос.УстановитьПараметр("ВсеПоставщики", Поставщик = Неопределено);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ОпределениеОстаткаПоступления()
