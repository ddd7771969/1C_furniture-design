
#Область ЗагрузкаДанных

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Функция ЗагрузитьОбновитьКомпании(СтруктураНастроек, ПодробныеДанные, ИзЛида = Ложь) Экспорт
	
	мДанных = ПодробныеДанные.ИнформацияОКомпаниях;
	
	Результат = Неопределено; 
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
#Область ФормированиеВременныхТаблицДанных		
	мТипыКИ = Новый Массив;
	мТипыКИ.Добавить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	мТипыКИ.Добавить(Перечисления.ТипыКонтактнойИнформации.Телефон);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"				, СтруктураНастроек.ТипыОбъектовОбмена.Компания);
	Запрос.УстановитьПараметр("ТипДанныхРеквизит"		, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит);
	Запрос.УстановитьПараметр("ТипыКИ"					, мТипыКИ);
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыРеквизитов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхРеквизит
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Партнеры.Ссылка КАК КомпанияКонтакт,
	|	Партнеры.Наименование КАК Наименование,
	|	ВЫРАЗИТЬ(Партнеры.НаименованиеПолное КАК СТРОКА(100)) КАК НаименованиеПолное,
	|	ВТ_Идентификаторы.Идентификатор КАК Идентификатор


	|ПОМЕСТИТЬ ВТ_КомпанииКонтакты
	|ИЗ
	|	Справочник.Партнеры КАК Партнеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Идентификаторы КАК ВТ_Идентификаторы
	|		ПО Партнеры.Ссылка = ВТ_Идентификаторы.Объект

	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор,
	|	Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Партнеры.Ссылка КАК КомпанияКонтакт,
	|	Контрагенты.ИНН КАК ИНН,
	|	Контрагенты.КПП КАК КПП,
	|	ВТ_ИдентификаторыРеквизитов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИННКомпанииКонтакты
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
	|		ПО Контрагенты.Партнер = Партнеры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыРеквизитов КАК ВТ_ИдентификаторыРеквизитов
	|		ПО Контрагенты.Ссылка = ВТ_ИдентификаторыРеквизитов.Объект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИНН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Идентификаторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартнерыКонтактнаяИнформация.Ссылка КАК КомпанияКонтакт,
	|	ПартнерыКонтактнаяИнформация.Тип КАК Тип,
	|	ПартнерыКонтактнаяИнформация.Вид КАК Вид,
	|	ПартнерыКонтактнаяИнформация.Представление КАК Представление,
	|	ПартнерыКонтактнаяИнформация.АдресЭП КАК АдресЭП,
	|	ПартнерыКонтактнаяИнформация.НомерТелефона КАК НомерТелефона
	|ПОМЕСТИТЬ ВТ_КИ
	|ИЗ
	|	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|		ПО ПартнерыКонтактнаяИнформация.Ссылка = Б24_К_ИдентификаторыОбъектов.Объект
	|ГДЕ
	|	ПартнерыКонтактнаяИнформация.Тип В(&ТипыКИ)
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Представление";
	
	Запрос.Выполнить();
#КонецОбласти
	
	СтруктураИспКИ = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьСтруктуруВидовКИКонтрагентов();
	
	ЗагруженныеОбъекты = Новый Массив;
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		ИдЭлемента = Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");
		
		ИНН = "";
		КПП = "";
		Для Каждого РеквизитКомпании Из ПодробныеДанные.ИнформацияОРеквизитах Цикл
			Если Формат(РеквизитКомпании.Получить("ENTITY_ID"),"ЧГ=0") = ИдЭлемента Тогда
				ИНН = РеквизитКомпании.Получить("RQ_INN");
				КПП = РеквизитКомпании.Получить("RQ_KPP");
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);
		
		ВнешнийКод 			= ТекЭлемент.Получить("ORIGIN_ID"); 
		ЗаголовокКомпании 	= Строка(ТекЭлемент.Получить("TITLE"));
		ТипКомпании			= ТекЭлемент.Получить("COMPANY_TYPE");
		Телефоны 	= ТекЭлемент.Получить("PHONE");
		Почта 		= ТекЭлемент.Получить("EMAIL");
		
#Область ПоискСозданиеКомпании

		Компания = Неопределено;
		
		Если СтруктураНастроек.Свойство("НастройкиКлиентов") Тогда
			ПорядокИдентификации = СтруктураНастроек.НастройкиКлиентов.ПорядокИдентификацииЮрЛиц;	
		Иначе
			ПорядокИдентификации = Новый СписокЗначений;
			ПорядокИдентификации.Добавить("Внешний идентификатор");
		КонецЕсли;
		
		Для Каждого ТекЗначение Из ПорядокИдентификации Цикл
			
			Критерий = ТекЗначение.Значение;
			
			СтруктураДанныхПоиска = Новый Структура;
			СтруктураДанныхПоиска.Вставить("НаименованиеКомпанииКонтакта", ЗаголовокКомпании);
			СтруктураДанныхПоиска.Вставить("Телефоны"					 , Телефоны);
			СтруктураДанныхПоиска.Вставить("Почта"						 , Почта);
			СтруктураДанныхПоиска.Вставить("ИНН"						 , ИНН);
			СтруктураДанныхПоиска.Вставить("КПП"						 , КПП);
			СтруктураДанныхПоиска.Вставить("ТипОбъектаОбмена"			 , СтруктураНастроек.ТипыОбъектовОбмена.Компания);
			
			Компания = ПолучитьКомпаниюКонтактПоКритерию(СтруктураНастроек, Критерий, МенеджерВременныхТаблиц, ТекЭлемент, СтруктураДанныхПоиска);
			
			Если ЗначениеЗаполнено(Компания) ИЛИ  Компания = Неопределено Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Компания = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВнешнийКод) И НЕ ЗначениеЗаполнено(Компания) Тогда
			Компания = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Партнеры"), ВнешнийКод);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Компания) Тогда      
			
			Если СтруктураНастроек.Свойство("НастройкиКлиентов") Тогда
				ОбновлятьКонтрагентов = СтруктураНастроек.НастройкиКлиентов.ОбновлятьКонтрагентов;	
			Иначе
				ОбновлятьКонтрагентов = Ложь;
			КонецЕсли;	
			
			Если НЕ ОбновлятьКонтрагентов Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что контрагенты не обновляются. Контрагент: " + ЗаголовокКомпании + " не будет обновлен.");
				
				Если НЕ ИзЛида Тогда
					РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Компания, Компания.Ссылка, ИдЭлемента);
				КонецЕсли;
				
				Продолжить;
				
			Иначе
				Компания = Компания.ПолучитьОбъект();
			КонецЕсли;
			
		Иначе
			
			Если СтруктураНастроек.Свойство("НастройкиКлиентов") И НЕ СтруктураНастроек.НастройкиКлиентов.ЗагружатьКонтрагентов Тогда
				Продолжить;
			КонецЕсли;
			
			Компания 			= Справочники.Партнеры.СоздатьЭлемент();
			Компания.ЮрФизЛицо	= Перечисления.КомпанияЧастноеЛицо.Компания;
			Компания.ДатаРегистрации = СтруктураНастроек.ДатаФормирования;
			
		КонецЕсли;	
#КонецОбласти

#Область ТипКомпании
		НайденнаяСтрока = СтруктураНастроек.ПресетыТиповКомпаний.Найти(ТипКомпании);
		Если НайденнаяСтрока <> Неопределено Тогда
			Компания.Клиент 			= НайденнаяСтрока.Клиент;	
			Компания.Конкурент 			= НайденнаяСтрока.Конкурент;	
			Компания.Поставщик 			= НайденнаяСтрока.Поставщик;	
			Компания.ПрочиеОтношения 	= НайденнаяСтрока.ПрочиеОтношения;	
		КонецЕсли;
#КонецОбласти

		Компания.Наименование 			= ЗаголовокКомпании;
		Компания.НаименованиеПолное 	= ?(ЗначениеЗаполнено(Компания.НаименованиеПолное), Компания.НаименованиеПолное, ЗаголовокКомпании);
		Компания.Комментарий			= ТекЭлемент.Получить("COMMENTS");
		
		ЗаполнениеТелефонаИЕмейлПартнера(СтруктураНастроек, СтруктураИспКИ, Компания, Телефоны, Почта);
		
		Попытка
			
			Компания.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			Компания.Записать();
			
			Результат = Компания.Ссылка;
			
			Если Не ИзЛида Тогда
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Компания, Компания.Ссылка, ИдЭлемента);
			КонецЕсли;
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан контрагент: " + ЗаголовокКомпании);
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи контрагента: " + ЗаголовокКомпании + " возникли ошибки.");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ЗагрузитьОбновитьКонтакты(СтруктураНастроек, ПодробныеДанные, ИзЛида = Ложь) Экспорт
	
	мДанных = ПодробныеДанные.ИнформацияОКонтактах;
	
	Результат = Неопределено; 
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
#Область ФормированиеВременныхТаблицДанных		
	мТипыКИ = Новый Массив;
	мТипыКИ.Добавить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	мТипыКИ.Добавить(Перечисления.ТипыКонтактнойИнформации.Телефон);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"				, СтруктураНастроек.ТипыОбъектовОбмена.Контакт);
	Запрос.УстановитьПараметр("ТипДанныхРеквизит"		, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит);
	Запрос.УстановитьПараметр("ТипыКИ"					, мТипыКИ);
	Запрос.УстановитьПараметр("Портал"					, СтруктураНастроек.Портал);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыРеквизитов
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхРеквизит
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Партнеры.Ссылка КАК КомпанияКонтакт,
	|	Партнеры.Наименование КАК Наименование,
	|	Партнеры.НаименованиеПолное КАК НаименованиеПолное,
	|	ВТ_Идентификаторы.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_КомпанииКонтакты
	|ИЗ
	|	Справочник.Партнеры КАК Партнеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Идентификаторы КАК ВТ_Идентификаторы
	|		ПО Партнеры.Ссылка = ВТ_Идентификаторы.Объект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор,
	|	Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Партнеры.Ссылка КАК КомпанияКонтакт,
	|	Контрагенты.ИНН КАК ИНН,
	|	Контрагенты.КПП КАК КПП,
	|	ВТ_ИдентификаторыРеквизитов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИННКомпанииКонтакты
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
	|		ПО Контрагенты.Партнер = Партнеры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыРеквизитов КАК ВТ_ИдентификаторыРеквизитов
	|		ПО Контрагенты.Ссылка = ВТ_ИдентификаторыРеквизитов.Объект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИНН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Идентификаторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартнерыКонтактнаяИнформация.Ссылка КАК КомпанияКонтакт,
	|	ПартнерыКонтактнаяИнформация.Тип КАК Тип,
	|	ПартнерыКонтактнаяИнформация.Вид КАК Вид,
	|	ПартнерыКонтактнаяИнформация.Представление КАК Представление,
	|	ПартнерыКонтактнаяИнформация.АдресЭП КАК АдресЭП,
	|	ПартнерыКонтактнаяИнформация.НомерТелефона КАК НомерТелефона
	|ПОМЕСТИТЬ ВТ_КИ
	|ИЗ
	|	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|		ПО ПартнерыКонтактнаяИнформация.Ссылка = Б24_К_ИдентификаторыОбъектов.Объект
	|ГДЕ
	|	ПартнерыКонтактнаяИнформация.Тип В(&ТипыКИ)
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор ЕСТЬ NULL

	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Представление";
	
	Запрос.Выполнить();
#КонецОбласти	
	
	СтруктураИспКИ = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьСтруктуруВидовКИКонтрагентов();
	
	ЗагруженныеОбъекты = Новый Массив;	
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		ИдЭлемента 			= Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");
		
		ИНН = "";
		КПП = "";
		Для Каждого РеквизитКомпании Из ПодробныеДанные.ИнформацияОРеквизитах Цикл
			Если Формат(РеквизитКомпании.Получить("ENTITY_ID"),"ЧГ=0") = ИдЭлемента Тогда
				ИНН = РеквизитКомпании.Получить("RQ_INN");
				КПП = РеквизитКомпании.Получить("RQ_KPP");
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);
		
		ВнешнийКод 	= ТекЭлемент.Получить("ORIGIN_ID");    
		
		Имя 		= Строка(ТекЭлемент.Получить("NAME"));
		Фамилия 	= Строка(ТекЭлемент.Получить("LAST_NAME"));
		Отчество 	= Строка(ТекЭлемент.Получить("SECOND_NAME"));	
		ЭтоИП 		= Строка(ТекЭлемент.Получить("HONORIFIC"))= "ИП";	
		
		ДанныеФИО	= Б24_К_ОбщегоНазначенияВызовСервера.ПреобразоватьФИО(Фамилия, Имя, Отчество, ЭтоИП);

		ДатаРождения= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Дата"),   ТекЭлемент.Получить("BIRTHDATE"));
		
		ТипКонтакта = Строка(ТекЭлемент.Получить("TYPE_ID"));
		
		Телефоны 	= ТекЭлемент.Получить("PHONE");
		Почта 		= ТекЭлемент.Получить("EMAIL");
		
		НаименованиеКонтрагента = СокрЛП(ДанныеФИО.Фамилия + " " + ДанныеФИО.Имя + " " + ДанныеФИО.Отчество); 
		
#Область ПоискСозданиеКонтакта
		Контакт = Неопределено;
		
		Если СтруктураНастроек.Свойство("НастройкиКлиентов") Тогда
			ПорядокИдентификации = СтруктураНастроек.НастройкиКлиентов.ПорядокИдентификацииЮрЛиц;	
		Иначе
			ПорядокИдентификации = Новый СписокЗначений;
			ПорядокИдентификации.Добавить("Внешний идентификатор");
		КонецЕсли;
		
		Для Каждого ТекЗначение Из ПорядокИдентификации Цикл  

			Критерий = ТекЗначение.Значение;
			
			СтруктураДанныхПоиска = Новый Структура;
			СтруктураДанныхПоиска.Вставить("НаименованиеКомпанииКонтакта", НаименованиеКонтрагента);
			СтруктураДанныхПоиска.Вставить("Телефоны"					 , Телефоны);
			СтруктураДанныхПоиска.Вставить("Почта"						 , Почта);
			СтруктураДанныхПоиска.Вставить("ИНН"						 , ИНН);
			СтруктураДанныхПоиска.Вставить("КПП"						 , КПП);			
			СтруктураДанныхПоиска.Вставить("ТипОбъектаОбмена"			 , СтруктураНастроек.ТипыОбъектовОбмена.Контакт);
			
			Контакт = ПолучитьКомпаниюКонтактПоКритерию(СтруктураНастроек, Критерий, МенеджерВременныхТаблиц, ТекЭлемент, СтруктураДанныхПоиска);
			
			Если ЗначениеЗаполнено(Контакт) ИЛИ  Контакт = Неопределено Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Контакт = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВнешнийКод) И НЕ ЗначениеЗаполнено(Контакт) Тогда
			Контакт = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Партнеры"), ВнешнийКод);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Контакт) Тогда
			
			Если СтруктураНастроек.Свойство("НастройкиКлиентов") Тогда
				ОбновлятьКонтрагентов = СтруктураНастроек.НастройкиКлиентов.ОбновлятьКонтрагентов;	
			Иначе
				ОбновлятьКонтрагентов = Ложь;
			КонецЕсли;			
			
			Если НЕ ОбновлятьКонтрагентов Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что контакты не обновляются. Контакт: " + НаименованиеКонтрагента + " не будет обновлен.");
				
				Если Не ИзЛида Тогда
					РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Контакт, Контакт.Ссылка, ИдЭлемента);
				КонецЕсли;
				
				Продолжить;
				
			Иначе
				Контакт = Контакт.ПолучитьОбъект();
			КонецЕсли;
			
		Иначе
			
			Если СтруктураНастроек.Свойство("НастройкиКлиентов") И НЕ СтруктураНастроек.НастройкиКлиентов.ЗагружатьКонтрагентов Тогда
				Продолжить;
			КонецЕсли;
			
			
			Контакт = Справочники.Партнеры.СоздатьЭлемент();
			Контакт.ЮрФизЛицо 		= Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо;
			Контакт.ДатаРегистрации = СтруктураНастроек.ДатаФормирования;
			
		КонецЕсли;	
#КонецОбласти

#Область ТипКонтакта     
		НайденнаяСтрока = СтруктураНастроек.ПресетыТиповКонтактов.Найти(ТипКонтакта);
		Если НайденнаяСтрока <> Неопределено Тогда
			Контакт.Клиент 			= НайденнаяСтрока.Клиент;	
			Контакт.Конкурент 		= НайденнаяСтрока.Конкурент;	
			Контакт.Поставщик 		= НайденнаяСтрока.Поставщик;	
			Контакт.ПрочиеОтношения = НайденнаяСтрока.ПрочиеОтношения;	
		КонецЕсли;
#КонецОбласти	

		Контакт.Наименование			= СокрЛП(НаименованиеКонтрагента);
		Контакт.НаименованиеПолное 		= ?(ЗначениеЗаполнено(Контакт.НаименованиеПолное), Контакт.НаименованиеПолное, НаименованиеКонтрагента);
		Контакт.Комментарий				= ТекЭлемент.Получить("COMMENTS");
		Контакт.ДатаРождения			= ДатаРождения;
		
		ЗаполнениеТелефонаИЕмейлПартнера(СтруктураНастроек, СтруктураИспКИ, Контакт, Телефоны, Почта);
		
		Попытка
			
			Контакт.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			Контакт.Записать();
			
			Результат = Контакт.Ссылка;
			
			Если Не ИзЛида Тогда 
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Контакт, Контакт.Ссылка, ИдЭлемента);
			КонецЕсли;
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан контрагент: " + НаименованиеКонтрагента);
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи контрагента: " + НаименованиеКонтрагента + " возникли ошибки.");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции


Функция ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, НазваниеВременнойТаблицы, ИдЭлемента)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Идентификатор", ИдЭлемента);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_Идентификаторы.Объект КАК Объект
	|ИЗ
	|	"+НазваниеВременнойТаблицы+" КАК ВТ_Идентификаторы
	|ГДЕ
	|	ВТ_Идентификаторы.Идентификатор = &Идентификатор";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат = Выборка.Объект;
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция ПолучитьКомпаниюКонтактПоКритерию(СтруктураНастроек, Критерий, МенеджерВременныхТаблиц, ИнформацияОКонтрагенте, СтруктураДанныхПоиска)
	
	НаименованиеКомпанииКонтакта = "";
	СтруктураДанныхПоиска.Свойство("НаименованиеКомпанииКонтакта", НаименованиеКомпанииКонтакта);
	
	Телефоны = Новый Массив;
	СтруктураДанныхПоиска.Свойство("Телефоны", Телефоны);
	
	Почта = Новый Массив;
	СтруктураДанныхПоиска.Свойство("Почта", Почта);
	
	ИНН = "";
	СтруктураДанныхПоиска.Свойство("ИНН", ИНН);
	
	КПП = "";
	СтруктураДанныхПоиска.Свойство("КПП", КПП);
	
	Результат = Справочники.Контрагенты.ПустаяСсылка();
	
	Если Критерий = "Внешний идентификатор" Тогда
		
		лИд = Формат(ИнформацияОКонтрагенте.Получить("ID"),"ЧГ=0");
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Идентификатор", лИд);
		Запрос.УстановитьПараметр("Портал"		 , СтруктураНастроек.Портал);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_КомпанииКонтакты.КомпанияКонтакт КАК КомпанияКонтакт
		|ИЗ
		|	ВТ_КомпанииКонтакты КАК ВТ_КомпанииКонтакты
		|ГДЕ
		|	ВТ_КомпанииКонтакты.Идентификатор = &Идентификатор";
		
		ВыполненныйЗапрос = Запрос.Выполнить();
		
		Если НЕ ВыполненныйЗапрос.Пустой() Тогда
			
			тзнВрем = ВыполненныйЗапрос.Выгрузить();
			
			КоличествоНайденных =  тзнВрем.Количество();
			
			Если КоличествоНайденных > 1 Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Найдено несколько контрагентов по идентификатору: " + лИд + ". Контрагент будет пропущен.");
				Результат = Неопределено;
				
			ИначеЕсли КоличествоНайденных = 1 Тогда
				Результат = тзнВрем[0].КомпанияКонтакт; 	   
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Критерий = "Не использовать" Тогда
		Возврат Результат;
	ИначеЕсли Критерий = "ИНН+КПП" Тогда
		
		Если НЕ ЗначениеЗаполнено(ИНН) Тогда
			Возврат Результат;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ИНН", ИНН);
		Запрос.УстановитьПараметр("КПП", КПП);
		
		ТекстЗапроса = "ВЫБРАТЬ 
		|	ВТ_ИННКомпанииКонтакты.Идентификатор КАК Идентификатор,
		|	ВТ_ИННКомпанииКонтакты.КомпанияКонтакт КАК КомпанияКонтакт
		|ИЗ
		|	ВТ_ИННКомпанииКонтакты КАК ВТ_ИННКомпанииКонтакты
		|ГДЕ
		|	ВТ_ИННКомпанииКонтакты.Идентификатор ЕСТЬ NULL
		|	И ВТ_ИННКомпанииКонтакты.ИНН = &ИНН";
		
		Если ЗначениеЗаполнено(КПП) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			| И ВТ_ИННКомпанииКонтакты.КПП = &КПП";
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		ВыполненныйЗапрос = Запрос.Выполнить();
		
		Если НЕ ВыполненныйЗапрос.Пустой() Тогда
			
			тзнВрем = ВыполненныйЗапрос.Выгрузить();
			
			КоличествоНайденных =  тзнВрем.Количество();
			
			Если КоличествоНайденных > 1 Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Найдено несколько контрагентов по ИНН: " + ИНН + ". Контрагент будет пропущен.");
				Результат = Неопределено;
				
			ИначеЕсли КоличествоНайденных = 1 Тогда
				Результат = тзнВрем[0].КомпанияКонтакт; 	   
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Критерий = "Наименование" Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Наименование", НаименованиеКомпанииКонтакта);
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_КомпанииКонтакты.Идентификатор КАК Идентификатор,
		|	ВТ_КомпанииКонтакты.КомпанияКонтакт КАК КомпанияКонтакт
		|ИЗ
		|	ВТ_КомпанииКонтакты КАК ВТ_КомпанииКонтакты
		|ГДЕ
		|	ВТ_КомпанииКонтакты.Идентификатор ЕСТЬ NULL
		|	И ВТ_КомпанииКонтакты.Наименование = &Наименование";
		
		ВыполненныйЗапрос = Запрос.Выполнить();
		
		Если НЕ ВыполненныйЗапрос.Пустой() Тогда
			
			тзнВрем = ВыполненныйЗапрос.Выгрузить();
			
			КоличествоНайденных =  тзнВрем.Количество();
			
			Если КоличествоНайденных > 1 Тогда
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Найдено несколько контрагентов по заголовку: " + НаименованиеКомпанииКонтакта + ". Контрагент будет пропущен.");
				Результат = Неопределено;
				
			ИначеЕсли КоличествоНайденных = 1 Тогда
				Результат = тзнВрем[0].КомпанияКонтакт; 	   
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Критерий = "Email" Тогда 
		
		Если Почта <> Неопределено Тогда
			
			МассивАдресов = Новый Массив;
			Для Каждого ТекАдрес Из Почта Цикл
				МассивАдресов.Добавить(ТекАдрес.Получить("VALUE"));
			КонецЦикла;
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("МассивАдресов", МассивАдресов);
			Запрос.УстановитьПараметр("Тип"			 , Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
			Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВТ_КИ.КомпанияКонтакт КАК КомпанияКонтакт
			|ИЗ
			|	ВТ_КИ КАК ВТ_КИ
			|ГДЕ
			|	ВТ_КИ.АдресЭП В (&МассивАдресов)
			|	И ВТ_КИ.Тип = &Тип";
			
			ВыполненныйЗапрос = Запрос.Выполнить();
			
			Если НЕ ВыполненныйЗапрос.Пустой() Тогда
				
				тзнВрем = ВыполненныйЗапрос.Выгрузить();
				
				КоличествоНайденных =  тзнВрем.Количество();
				
				Если КоличествоНайденных > 1 Тогда
					
					ПочтыДляОшибки = "";
					Для каждого ТекЭлемент Из МассивАдресов Цикл
						ПочтыДляОшибки = ПочтыДляОшибки + Строка(ТекЭлемент) + ", ";	
					КонецЦикла;
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Найдено несколько контрагентов по одной Из электронных почт:"+ПочтыДляОшибки+". Контрагент будет пропущен.");
					Результат = Неопределено;
					
				ИначеЕсли КоличествоНайденных = 1 Тогда
					Результат = тзнВрем[0].КомпанияКонтакт; 	   
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Критерий = "Телефон" Тогда
		
		Если Телефоны <> Неопределено Тогда
			
			МассивТелефонов = Новый Массив;
			Для Каждого ТекТелефон Из Телефоны Цикл
				
				ПолныйНомерТелефона = ТекТелефон.Получить("VALUE");  
				Если Лев(ПолныйНомерТелефона, 1) = "+" Тогда  
					МассивТелефонов.Добавить(Прав(ПолныйНомерТелефона, СтрДлина(ПолныйНомерТелефона)-1));	
				КонецЕсли;
				
				МассивТелефонов.Добавить(ПолныйНомерТелефона);   
				
			КонецЦикла;
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("МассивТелефонов", МассивТелефонов);
			Запрос.УстановитьПараметр("Тип"			 , Перечисления.ТипыКонтактнойИнформации.Телефон);
			Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВТ_КИ.КомпанияКонтакт КАК КомпанияКонтакт
			|ИЗ
			|	ВТ_КИ КАК ВТ_КИ
			|ГДЕ
			|	ВТ_КИ.НомерТелефона В(&МассивТелефонов)
			|	И ВТ_КИ.Тип = &Тип";
			
			ВыполненныйЗапрос = Запрос.Выполнить();
			
			Если НЕ ВыполненныйЗапрос.Пустой() Тогда
				
				тзнВрем = ВыполненныйЗапрос.Выгрузить();
				
				КоличествоНайденных =  тзнВрем.Количество();
				
				Если КоличествоНайденных > 1 Тогда
					
					ТелефоныДляОшибки = "";
					Для каждого ТекЭлемент Из МассивТелефонов Цикл
						ТелефоныДляОшибки = ТелефоныДляОшибки + Строка(ТекЭлемент) + ", ";	
					КонецЦикла;
					
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Найдено несколько контрагентов по одному Из телефону:"+ТелефоныДляОшибки+". Контрагент будет пропущен.");
					Результат = Неопределено;
					
				ИначеЕсли КоличествоНайденных = 1 Тогда
					Результат = тзнВрем[0].КомпанияКонтакт; 	   
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Процедура ЗаполнениеТелефонаИЕмейлПартнера(СтруктураНастроек, СтруктураИспКИ, КомпанияКонтакт, Телефоны, Почта)
	
	ТзнВрем = КомпанияКонтакт.КонтактнаяИнформация.ВыгрузитьКолонки();
	
	ТелефонРабочий 		= СтруктураИспКИ.ТелефонРабочий; 	
	ТелефонМобильный 	= СтруктураИспКИ.ТелефонМобильный; 	
	ТелефонДомашний 	= СтруктураИспКИ.ТелефонДомашний; 
		
	ПочтаРабочий 		= СтруктураИспКИ.ПочтаРабочий; 	
	ПочтаЧастная 		= СтруктураИспКИ.ПочтаЧастная; 	
	
	
	Для Каждого ТекСтрока Из КомпанияКонтакт.КонтактнаяИнформация Цикл
		
		Если ТекСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон И (ТекСтрока.Вид = ТелефонРабочий ИЛИ ТекСтрока.Вид = ТелефонМобильный ИЛИ ТекСтрока.Вид = ТелефонДомашний) Тогда
			Продолжить;	
		КонецЕсли;
		
		Если ТекСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты И (ТекСтрока.Вид = ПочтаРабочий ИЛИ ТекСтрока.Вид = ПочтаЧастная) Тогда
			Продолжить;	
		КонецЕсли;
		
		НовСтрока = ТзнВрем.Добавить();
		
		ЗаполнитьЗначенияСвойств(НовСтрока, ТекСтрока); 
		
	КонецЦикла;
	
	КомпанияКонтакт.КонтактнаяИнформация.Очистить();
	КомпанияКонтакт.КонтактнаяИнформация.Загрузить(ТзнВрем);
	
	Если Телефоны <> Неопределено Тогда
		
		Для Каждого ТекТелефон Из Телефоны Цикл
			
			НомерТелефона = ТекТелефон.Получить("VALUE");
			
			Если Не ЗначениеЗаполнено(НомерТелефона) Тогда
				Продолжить;					
			КонецЕсли;
			
			ВидТелефона = ТекТелефон.Получить("VALUE_TYPE");
			
			Если ВидТелефона = "WORK" Тогда
				
				НовыйКонтакт = КомпанияКонтакт.КонтактнаяИнформация.Добавить(); 
				
				НовыйКонтакт.Вид = ТелефонРабочий;
				НовыйКонтакт.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
				НовыйКонтакт.Представление 			= НомерТелефона; 
				НовыйКонтакт.НомерТелефона 			= НомерТелефона;
				НовыйКонтакт.НомерТелефонаБезКодов 	= НомерТелефона;
				НовыйКонтакт.ЗначенияПолей 			= ПолучитьЗначениеПолейТелефонаИПочты("Телефон", НомерТелефона)
			ИначеЕсли ВидТелефона = "MOBILE" Тогда 
				
				НовыйКонтакт = КомпанияКонтакт.КонтактнаяИнформация.Добавить(); 
				
				НовыйКонтакт.Вид = ТелефонМобильный;
				НовыйКонтакт.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
				НовыйКонтакт.Представление 			= НомерТелефона; 
				НовыйКонтакт.НомерТелефона 			= НомерТелефона;
				НовыйКонтакт.НомерТелефонаБезКодов 	= НомерТелефона;
				НовыйКонтакт.ЗначенияПолей 			= ПолучитьЗначениеПолейТелефонаИПочты("Телефон", НомерТелефона)
			ИначеЕсли ВидТелефона = "HOME" Тогда 
				
				НовыйКонтакт = КомпанияКонтакт.КонтактнаяИнформация.Добавить(); 
				
				НовыйКонтакт.Вид = ТелефонДомашний;
				НовыйКонтакт.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
				НовыйКонтакт.Представление 			= НомерТелефона; 
				НовыйКонтакт.НомерТелефона 			= НомерТелефона;
				НовыйКонтакт.НомерТелефонаБезКодов 	= НомерТелефона;
				НовыйКонтакт.ЗначенияПолей 			= ПолучитьЗначениеПолейТелефонаИПочты("Телефон", НомерТелефона)
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Почта <> Неопределено Тогда
		
		Для Каждого ТекПочта Из Почта Цикл
			
			АдресПочты = ТекПочта.Получить("VALUE");
			
			Если Не ЗначениеЗаполнено(АдресПочты) Тогда
				Продолжить;					
			КонецЕсли;
			
			ВидПочты = ТекПочта.Получить("VALUE_TYPE");
			
			Если ВидПочты = "WORK" Тогда
				
				НовыйКонтакт = КомпанияКонтакт.КонтактнаяИнформация.Добавить(); 
				
				НовыйКонтакт.Вид = ПочтаРабочий;
				НовыйКонтакт.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
				НовыйКонтакт.Представление 			= АдресПочты; 
				НовыйКонтакт.АдресЭП 				= АдресПочты;
				НовыйКонтакт.ЗначенияПолей 			= ПолучитьЗначениеПолейТелефонаИПочты("ЭПочта", АдресПочты)
			ИначеЕсли ВидПочты = "HOME" Тогда 
				
				НовыйКонтакт = КомпанияКонтакт.КонтактнаяИнформация.Добавить(); 
				
				НовыйКонтакт.Вид = ПочтаЧастная;
				НовыйКонтакт.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
				НовыйКонтакт.Представление 			= АдресПочты; 
				НовыйКонтакт.АдресЭП 				= АдресПочты;
				НовыйКонтакт.ЗначенияПолей 			= ПолучитьЗначениеПолейТелефонаИПочты("ЭПочта", АдресПочты)
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначениеПолейТелефонаИПочты(ТипКИ, ЗначениеКИ)
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.УстановитьСтроку();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("КонтактнаяИнформация");
	
	ЗаписьXML.ЗаписатьАтрибут("xmlns", XMLСтрока("http://www.v8.1c.ru/ssl/contactinfo"));
	ЗаписьXML.ЗаписатьАтрибут("xmlns:xs", XMLСтрока("http://www.w3.org/2001/XMLSchema"));
	ЗаписьXML.ЗаписатьАтрибут("xmlns:xsi", XMLСтрока("http://www.w3.org/2001/XMLSchema-instance"));
	ЗаписьXML.ЗаписатьАтрибут("Представление", XMLСтрока(ЗначениеКИ));
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("Комментарий");
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("Состав");
	
	Если ТипКИ = "Телефон" Тогда
		ЗаписьXML.ЗаписатьАтрибут("xsi:type"	, "НомерТелефона");
		ЗаписьXML.ЗаписатьАтрибут("КодСтраны"	, "");
		ЗаписьXML.ЗаписатьАтрибут("КодГорода"	, "");
		ЗаписьXML.ЗаписатьАтрибут("Номер"		, XMLСтрока(ЗначениеКИ));
		ЗаписьXML.ЗаписатьАтрибут("Добавочный"	, "");
	ИначеЕсли ТипКИ = "ЭПочта" Тогда
		ЗаписьXML.ЗаписатьАтрибут("xsi:type"	, "ЭлектроннаяПочта");
		ЗаписьXML.ЗаписатьАтрибут("Значение"	, XMLСтрока(ЗначениеКИ));
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	Возврат ЗаписьXML.Закрыть();	
	
КонецФункции

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Функция ЗагрузитьОбновитьРеквизиты(СтруктураНастроек, мДанных) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	мТипыКИ = Новый Массив;
	мТипыКИ.Добавить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	мТипыКИ.Добавить(Перечисления.ТипыКонтактнойИнформации.Телефон);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"			, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит);
	Запрос.УстановитьПараметр("ТипДанныхКомпаний"	, СтруктураНастроек.ТипыОбъектовОбмена.Компания);
	Запрос.УстановитьПараметр("ТипДанныхКонтактов"	, СтруктураНастроек.ТипыОбъектовОбмена.Контакт);
	Запрос.УстановитьПараметр("Портал"				, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор,
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных КАК ТипДанных
	|ПОМЕСТИТЬ ВТ_ИдентификаторыВладельца
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И (Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКомпаний
	|			ИЛИ Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхКонтактов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТипДанных";
	
	Запрос.Выполнить();
	
	ЗагруженныеОбъекты = Новый Массив;
	
	Для каждого ТекЭлемент Из мДанных.ИнформацияОРеквизитах Цикл	
		
		НовыйРеквизит 	= Ложь;
		
		ИдЭлемента 		= Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");
		ИдВладельца 	= Формат(ТекЭлемент.Получить("ENTITY_ID"),"ЧГ=0");
		ВнешнийКод 		= ТекЭлемент.Получить("XML_ID"); 
		Фамилия 		= Строка(ТекЭлемент.Получить("RQ_LAST_NAME")); 
		Имя 			= Строка(ТекЭлемент.Получить("RQ_FIRST_NAME")); 
		Отчество 		= Строка(ТекЭлемент.Получить("RQ_SECOND_NAME"));  
		ЭтоИП 			= ЗначениеЗаполнено(ТекЭлемент.Получить("RQ_INN")); 
		
		ДанныеФИО		= Б24_К_ОбщегоНазначенияВызовСервера.ПреобразоватьФИО(Фамилия, Имя, Отчество, ЭтоИП);
		
		ФИО				= ДанныеФИО.Фамилия + " " + ДанныеФИО.Имя + " " + ДанныеФИО.Отчество; 
		
		Если ЗагруженныеОбъекты.Найти(ИдВладельца + "_" + ИдЭлемента) <> Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдВладельца + "_" + ИдЭлемента);
		
		ТипРеквизита 	= Строка(ТекЭлемент.Получить("ENTITY_TYPE_ID"));
		
		Если ТипРеквизита = "3" Тогда   
			
			НаименованиеПолное	= ФИО;
			НаименованиеПолное	= ?(ЗначениеЗаполнено(НаименованиеПолное), НаименованиеПолное, Строка(ТекЭлемент.Получить("RQ_NAME")));
			Наименование		= НаименованиеПолное;
			
		Иначе
			
			Наименование 		= Строка(ТекЭлемент.Получить("RQ_COMPANY_NAME"));
			Наименование		= ?(ЗначениеЗаполнено(Наименование), Наименование, ФИО);
			
			НаименованиеПолное 	= Строка(ТекЭлемент.Получить("RQ_COMPANY_FULL_NAME"));
			НаименованиеПолное	= ?(ЗначениеЗаполнено(НаименованиеПолное), НаименованиеПолное, Наименование);
			
		КонецЕсли;
		
		Если НЕ (ТипРеквизита = "4" ИЛИ ТипРеквизита = "3") Тогда
			Продолжить;
		КонецЕсли;
		
		Активный 				= ТекЭлемент.Получить("ACTIVE") = "Y";
		ИНН 					= Строка(ТекЭлемент.Получить("RQ_INN"));
		КодПоОКПО				= Строка(ТекЭлемент.Получить("RQ_OKPO"));
		КПП 					= Строка(ТекЭлемент.Получить("RQ_KPP"));
		
		СвидетельствоСерияНомер	= Строка(ТекЭлемент.Получить("RQ_IDENT_DOC_SER")) + " " + Строка(ТекЭлемент.Получить("RQ_IDENT_DOC_NUM"));
		
		ДУЛ 	= Строка(ТекЭлемент.Получить("RQ_IDENT_DOC"));
		
		ОГРН 	= Строка(ТекЭлемент.Получить("RQ_OGRN"));
		ОГРН	= ?(ЗначениеЗаполнено(ОГРН), ОГРН, Строка(ТекЭлемент.Получить("RQ_OGRNIP")));
		
		
#Область ПоискСозданиеРеквизита
		Реквизит = Неопределено;
		
		Реквизит = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Идентификаторы", ИдЭлемента);	
		
		Если ЗначениеЗаполнено(ВнешнийКод) И НЕ ЗначениеЗаполнено(Реквизит) Тогда
			Реквизит = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Контрагенты"), ВнешнийКод);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Реквизит) Тогда
			
			НовыйРеквизит = Истина;
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Идентификатор", ИдВладельца);
			
			Если ТипРеквизита = "3" Тогда     
				Запрос.УстановитьПараметр("ТипДанных", СтруктураНастроек.ТипыОбъектовОбмена.Контакт);
			Иначе
				Запрос.УстановитьПараметр("ТипДанных", СтруктураНастроек.ТипыОбъектовОбмена.Компания);
			КонецЕсли;
			
			Запрос.Текст = "ВЫБРАТЬ
			|	ВТ_Идентификаторы.Объект КАК Объект
			|ИЗ
			|	ВТ_ИдентификаторыВладельца КАК ВТ_Идентификаторы
			|ГДЕ
			|	ВТ_Идентификаторы.Идентификатор = &Идентификатор
			|   И ВТ_Идентификаторы.ТипДанных = &ТипДанных";
			
			ВыполненныйЗапрос = Запрос.Выполнить();
			
			Если НЕ ВыполненныйЗапрос.Пустой() Тогда
				Выборка = ВыполненныйЗапрос.Выбрать();
				Пока Выборка.Следующий() Цикл
					ВладелецРеквизита = Выборка.Объект;
					Прервать;
				КонецЦикла;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ВладелецРеквизита) Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек.ТипыСообщений.Ошибка, "Для заполнения реквизитов не найден партнер: " + НаименованиеПолное + ". Будет пропущен.");
				Продолжить;
			Иначе
				
				Если ТипЗнч(ВладелецРеквизита) <> Тип("СправочникСсылка.Организации") Тогда
					Реквизит = Справочники.Контрагенты.СоздатьЭлемент();
					Реквизит.Партнер = ВладелецРеквизита; 
				Иначе
					Продолжить;
				КонецЕсли;
				
			КонецЕсли;	
		Иначе
			Реквизит = Реквизит.ПолучитьОбъект();
		КонецЕсли;			
#КонецОбласти

		ЭтоОрганизация 	= ТипЗнч(Реквизит) = Тип("СправочникОбъект.Организации"); 
		ЭтоКонтакт		= ТипЗнч(Реквизит) = Тип("СправочникОбъект.КонтактныеЛицаПартнеров"); 
		
		Если ЭтоКонтакт Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Для контактного лица: " + НаименованиеПолное + ". Не записывается реквизит.");
			Продолжить;
		КонецЕсли;

		
		Если ЭтоОрганизация Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Организации пропускаются");
			Продолжить;
		КонецЕсли;
		
		Если НовыйРеквизит Тогда    
			
			НайденнаяСтрока = СтруктураНастроек.ПресетыШаблоновРеквизитов.Найти(ТекЭлемент.Получить("PRESET_ID"), "ИдШаблона");
			Если НайденнаяСтрока <> Неопределено Тогда
				
				Реквизит.ЮрФизЛицо = НайденнаяСтрока.ТипКонтрагента; 	
				
				Если НайденнаяСтрока.ТипКонтрагента = Перечисления.ЮрФизЛицо.ЮрЛицо ИЛИ НайденнаяСтрока.ТипКонтрагента = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
					Реквизит.ЮридическоеФизическоеЛицо	= Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
				Иначе
					Реквизит.ЮридическоеФизическоеЛицо	= Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
				КонецЕсли;
				
			Иначе
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось определить шаблон Битрикс24 для реквизита: " + Наименование + ". Будет пропущен. Их нужно перезаполнить настройках");
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если СтруктураНастроек.Свойство("НастройкиКлиентов") Тогда
			ОбновлятьРеквизиты = СтруктураНастроек.НастройкиКлиентов.ОбновлятьКонтрагентов;	
		Иначе
			ОбновлятьРеквизиты = Ложь;
		КонецЕсли;			 
		
		Если ОбновлятьРеквизиты Тогда
			
			Реквизит.Наименование 			= Наименование;
			Реквизит.НаименованиеПолное 	= НаименованиеПолное;
			Реквизит.ПометкаУдаления 		= НЕ Активный;	
			Реквизит.ИНН 					= ИНН;	
			Реквизит.КПП 					= КПП;	
			Реквизит.КодПоОКПО 				= КодПоОКПО;	
			Реквизит.РегистрационныйНомер 	= ОГРН;
			
		Иначе
			Реквизит.Наименование 		= ?(ЗначениеЗаполнено(Реквизит.Наименование), Реквизит.Наименование, Наименование);
			Реквизит.НаименованиеПолное = ?(ЗначениеЗаполнено(Реквизит.НаименованиеПолное), Реквизит.НаименованиеПолное, НаименованиеПолное);
			Реквизит.ПометкаУдаления 	= НЕ Активный;	
			Реквизит.ИНН = ?(ЗначениеЗаполнено(Реквизит.ИНН), Реквизит.ИНН, ИНН);
			Реквизит.КПП = ?(ЗначениеЗаполнено(Реквизит.КПП), Реквизит.КПП, КПП);
			Реквизит.КодПоОКПО = ?(ЗначениеЗаполнено(Реквизит.КодПоОКПО), Реквизит.КодПоОКПО, КодПоОКПО);
			
			Реквизит.РегистрационныйНомер = ?(ЗначениеЗаполнено(Реквизит.РегистрационныйНомер), Реквизит.РегистрационныйНомер, ОГРН);
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Реквизит.Наименование) И ЗначениеЗаполнено(ВладелецРеквизита) Тогда
			Реквизит.Наименование = ВладелецРеквизита.Наименование;	
		КонецЕсли;
		
		Попытка
			
			Реквизит.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			Реквизит.Записать();
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит, Реквизит.Ссылка, ИдЭлемента);
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записаны реквизиты контрагента: " + Наименование);
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи реквизитов контрагента: " + Наименование + " возникли ошибки.");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Функция ЗагрузитьОбновитьАдресаРеквизитов(СтруктураНастроек, мДанных) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных", СтруктураНастроек.ТипыОбъектовОбмена.Реквизит);
	Запрос.УстановитьПараметр("Портал"	, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Запрос.Выполнить();
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		ТипВладельца 	= Формат(ТекЭлемент.Получить("ENTITY_TYPE_ID"),"ЧГ=0"); 		
		ИдВладельца 	= Формат(ТекЭлемент.Получить("ENTITY_ID"),"ЧГ=0");
		ТипАдреса		= ТекЭлемент.Получить("TYPE_ID");
		Страна 			= Строка(ТекЭлемент.Получить("COUNTRY"));
		Регион 			= Строка(ТекЭлемент.Получить("PROVINCE"));
		Район 			= Строка(ТекЭлемент.Получить("REGION"));
		Индекс 			= Строка(ТекЭлемент.Получить("POSTAL_CODE"));
		ГородНП 		= Строка(ТекЭлемент.Получить("CITY"));
		Адрес1 			= Строка(ТекЭлемент.Получить("ADDRESS_1"));
		Адрес2 			= Строка(ТекЭлемент.Получить("ADDRESS_2"));
		
		Если ТипВладельца <> "8" Тогда
			Продолжить;
		КонецЕсли;
		
#Область ПоискРеквизита
		КомпанияКонтакт = Неопределено;
		
		КомпанияКонтакт = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Идентификаторы", ИдВладельца);	
		
		Если КомпанияКонтакт = Неопределено Тогда
			Продолжить;
		ИначеЕсли КомпанияКонтакт = Справочники.Контрагенты.ПустаяСсылка() Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Для заполнения адреса реквизита не найден реквизит с ид: " + ИдВладельца + ". Вид адреса: " + ТипАдреса +"Будет пропущен.");
			Продолжить;
		КонецЕсли;	
		
		Если СтруктураНастроек.Свойство("НастройкиКлиентов") Тогда
			ОбновлятьРеквизиты = СтруктураНастроек.НастройкиКлиентов.ОбновлятьКонтрагентов;	
		Иначе
			ОбновлятьРеквизиты = Ложь;
		КонецЕсли;			
		
		Если Строка(ТипАдреса) = "1" Тогда
			ВидКИ = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
			ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.ФактАдресРеквизита;
		Иначе
			ВидКИ = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
			ТипОбъектаОбмена = СтруктураНастроек.ТипыОбъектовОбмена.ЮрАдресРеквизита;
		КонецЕсли;
		
		КомпанияКонтакт 	= КомпанияКонтакт.ПолучитьОбъект();
#КонецОбласти
		
		Страна	= ?(ЗначениеЗаполнено(Страна), ВРег(Страна), Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьНазваниеСтраны()); 
		
		СтруктураДанных = Б24_К_РаботаСАдресамиСервер.ПолучитьСтруктуруСПолямиКИ(Страна, Регион, Район, Индекс, ГородНП, Адрес1, Адрес2);
		
		НайденнаяСтрока = КомпанияКонтакт.КонтактнаяИнформация.Найти(ВидКИ, "Вид"); 
		
		Если НайденнаяСтрока = Неопределено Тогда
			
			НовыйКИ =  КомпанияКонтакт.КонтактнаяИнформация.Добавить();
			НовыйКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес;
			НовыйКИ.Вид = ВидКИ;
			
			НовыйКИ.Страна 			= СтруктураДанных.Страна;
			НовыйКИ.Регион 			= СтруктураДанных.Регион;
			НовыйКИ.Город 			= СтруктураДанных.Город;
			НовыйКИ.Представление 	= СтруктураДанных.Представление;
			НовыйКИ.ЗначенияПолей	= Б24_К_РаботаСАдресамиСервер.ПолучитьЗначениеПолейАдреса(СтруктураДанных);
			
		Иначе
			Если НЕ ОбновлятьРеквизиты И ЗначениеЗаполнено(НайденнаяСтрока.Представление) Тогда
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипОбъектаОбмена, КомпанияКонтакт.Ссылка, ИдВладельца);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что контрагенты не обновляются. У контрагента : " + КомпанияКонтакт.Наименование + " не будет обновлен " + ВидКИ.Наименование);
				Продолжить;
			КонецЕсли;
			
			НайденнаяСтрока.Страна 			= СтруктураДанных.Страна;
			НайденнаяСтрока.Регион 			= СтруктураДанных.Регион;
			НайденнаяСтрока.Город 			= СтруктураДанных.Город;
			
			НайденнаяСтрока.Представление 	= СтруктураДанных.Представление;
			
			НайденнаяСтрока.ЗначенияПолей 	= Б24_К_РаботаСАдресамиСервер.ПолучитьЗначениеПолейАдреса(СтруктураДанных);	
			
		КонецЕсли;
		
		Попытка
			
			КомпанияКонтакт.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			КомпанияКонтакт.Записать();
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипОбъектаОбмена, КомпанияКонтакт.Ссылка, ИдВладельца);
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записаны адреса контрагента: " + КомпанияКонтакт.Наименование);
			
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи адреса реквизита : " + КомпанияКонтакт.Наименование + " возникли ошибки.");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Функция ЗагрузитьОбновитьБанковскиеСчетаКомпанийКонтактов(СтруктураНастроек, мДанных) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипДанных"			, СтруктураНастроек.ТипыОбъектовОбмена.БанковскийСчетРеквизита);
	Запрос.УстановитьПараметр("ТипДанныхВладельца"	, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит);
	Запрос.УстановитьПараметр("Портал"				, СтруктураНастроек.Портал);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК Объект,
	|	БанковскиеСчета.НомерСчета КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НомераСчетов
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ИдентификаторыВладельца
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанныхВладельца
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Запрос.Выполнить();
	
	ЗагруженныеОбъекты = Новый Массив;	
	
	Для каждого ТекЭлемент Из мДанных Цикл	
		
		ИдЭлемента 		= Формат(ТекЭлемент.Получить("ID"),"ЧГ=0");
		
		Если ЗагруженныеОбъекты.Найти(ИдЭлемента) <> Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		ЗагруженныеОбъекты.Добавить(ИдЭлемента);    
		
		ВнешнийКод 		= Строка(ТекЭлемент.Получить("XML_ID"));		
		ИдВладельца 	= Формат(ТекЭлемент.Получить("ENTITY_ID"),"ЧГ=0");
		НомерСчета		= Строка(ТекЭлемент.Получить("RQ_ACC_NUM"));
		БикБанка 		= Строка(ТекЭлемент.Получить("RQ_BIK"));
		НаименованиеБС	= Строка(ТекЭлемент.Получить("NAME"));
		
		БанковскийСчет 	= Неопределено;
		
		Если Не ЗначениеЗаполнено(БикБанка) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не указан БИК в счете:" + НомерСчета + " у реквизита с кодом " + ИдВладельца);
			Продолжить;
		КонецЕсли;
		
#Область ПоискСозданиеБанкСчета
		
		БанковскийСчет = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_Идентификаторы", ИдЭлемента);	
		
		Если ЗначениеЗаполнено(ВнешнийКод) И НЕ ЗначениеЗаполнено(БанковскийСчет)  Тогда
			БанковскийСчет = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.БанковскиеСчетаКонтрагентов"), ВнешнийКод);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НомерСчета) И НЕ ЗначениеЗаполнено(БанковскийСчет)  Тогда
			БанковскийСчет = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_НомераСчетов", НомерСчета);	
		КонецЕсли;
		
		Если БанковскийСчет = Неопределено Тогда
			
			ВладелецБСчета = ПолучитьОбъектПоИдентификаторуИзВременнойТаблицы(МенеджерВременныхТаблиц, "ВТ_ИдентификаторыВладельца", ИдВладельца);	
			
			Если ВладелецБСчета = Неопределено Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не найден владелец банковского счета. Ид реквизита: " + ИдВладельца + ". Будет пропущен банковский счет : " + ИдЭлемента);
				Продолжить;
			КонецЕсли;
			
			БанковскийСчет = Справочники.БанковскиеСчетаКонтрагентов.СоздатьЭлемент();
			БанковскийСчет.Владелец = ВладелецБСчета;
			
		Иначе
			
			Если СтруктураНастроек.Свойство("НастройкиКлиентов") Тогда
				ОбновлятьКонтрагентов = СтруктураНастроек.НастройкиКлиентов.ОбновлятьКонтрагентов;	
			Иначе
				ОбновлятьКонтрагентов = Ложь;
			КонецЕсли;	
			
			Если ОбновлятьКонтрагентов Тогда
				БанковскийСчет = БанковскийСчет.ПолучитьОбъект();
			Иначе
				РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.БанковскийСчетРеквизита, БанковскийСчет.Ссылка, ИдЭлемента);
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "В настройках указано, что контрагенты не обновляются. Банковский счет: " + НомерСчета + " не будет обновлен.");
				Продолжить;  				
			КонецЕсли;
		КонецЕсли;
#КонецОбласти
		
		Активный 						= ТекЭлемент.Получить("ACTIVE") = "Y";
		БанковскийСчет.ПометкаУдаления 	= НЕ Активный;	
		
		БанковскийСчет.НомерСчета		= НомерСчета; 
		
		НайденныйБанк = Справочники.КлассификаторБанков.НайтиПоКоду(БикБанка);
		
		Если ЗначениеЗаполнено(НайденныйБанк) Тогда
			БанковскийСчет.Банк = НайденныйБанк;
		Иначе
			БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина;
			БанковскийСчет.СВИФТБанка 			= Строка(ТекЭлемент.Получить("RQ_SWIFT"));
			БанковскийСчет.НаименованиеБанка 	= Строка(ТекЭлемент.Получить("RQ_BANK_NAME"));
			БанковскийСчет.АдресБанка 			= Строка(ТекЭлемент.Получить("RQ_BANK_ADDR"));
			БанковскийСчет.БИКБанка 			= БикБанка;
			БанковскийСчет.КоррСчетБанка 		= Строка(ТекЭлемент.Получить("RQ_COR_ACC_NUM"));
		КонецЕсли;

		КодВалюты = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьКодПоВалюте(Строка(ТекЭлемент.Получить("RQ_ACC_CURRENCY")));
		БанковскийСчет.ВалютаДенежныхСредств = Справочники.Валюты.НайтиПоКоду(КодВалюты);
		
		БанковскийСчет.Наименование = НаименованиеБС;
		
		Попытка
			
			БанковскийСчет.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
			БанковскийСчет.Записать();
			
			РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.БанковскийСчетРеквизита, БанковскийСчет.Ссылка, ИдЭлемента);
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Записан номер счета: " + НомерСчета);
	
		Исключение
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Во время записи банковского счета контрагента: " + НомерСчета + " возникли ошибки.");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти


#Область ВыгрузкаДанных

Функция СформироватьСтруктуруДанныхКонтакта(СтруктураНастроек, Партнер) Экспорт
	
	ИдКлиента = "";
	
	СтруктураИспКИ = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьСтруктуруВидовКИКонтрагентов();
	Идентификатор1С 	= XMLСтрока(Партнер);

	Поля = Новый Структура;
	
	ФИО = Б24_К_ОбщегоНазначенияВызовСервера.РазобратьФИО(Партнер.Наименование);
	
	Поля.Вставить("NAME"			, ?(ЗначениеЗаполнено(ФИО.Имя), ФИО.Имя, Партнер.Наименование));
	Поля.Вставить("LAST_NAME"		, ?(ЗначениеЗаполнено(ФИО.Имя), ФИО.Фамилия, ""));
	Поля.Вставить("SECOND_NAME"		, ФИО.Отчество);
	
	Если СтрНайти(Партнер.Наименование, "ИП") > 0 Тогда
		Поля.Вставить("HONORIFIC"	, "ИП");   
	Иначе
		Поля.Вставить("HONORIFIC"	, "");   
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Партнер.ДатаРождения) Тогда
		Поля.Вставить("BIRTHDATE"		, Партнер.ДатаРождения);
	КонецЕсли;
	Поля.Вставить("COMMENTS"		, Партнер.Комментарий); 
	Поля.Вставить("ORIGIN_ID"		, Идентификатор1С); 
	Поля.Вставить("ORIGINATOR_ID"	, "ONE_C"); 
	
	Если СтруктураНастроек.Свойство("НастройкиКлиентов") И ТипЗнч(Партнер) = Тип("СправочникСсылка.Партнеры") Тогда
		
		НайденныеСтроки = СтруктураНастроек.ПресетыТиповКонтактов.НайтиСтроки(Новый Структура("Клиент, Конкурент, Поставщик, ПрочиеОтношения", Партнер.Клиент, Партнер.Конкурент, Партнер.Поставщик, Партнер.ПрочиеОтношения));
		Если НайденныеСтроки.Количество() > 0 Тогда
			Поля.Вставить("TYPE_ID"	, НайденныеСтроки[0].ИдТипа); 
		КонецЕсли;
			
	КонецЕсли;
	
	ТелоЗапроса = "";
	
	Для каждого ТекПоле Из Поля Цикл
		
		ТелоЗапроса = ТелоЗапроса + ?(СтрДлина(ТелоЗапроса) >0, "&", "") +"fields["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
		
	КонецЦикла;         
	
	МенеджерВременныхТаблицКИ = ПолучитьДанныеПартнераПоПочтеТелефону(Партнер);

	ЗаполнениеТелефонаИПочтыДляПартнера(СтруктураНастроек, Партнер, МенеджерВременныхТаблицКИ, СтруктураИспКИ, ТелоЗапроса);
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.contact.add", ТелоЗапроса); 
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат ИдКлиента;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат ИдКлиента;
	КонецЕсли;
	
	ИдКлиента = Формат(result, "ЧГ=0"); 	
	
	РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Контакт, Партнер, ИдКлиента);

	Возврат	ИдКлиента;
	
КонецФункции

Функция СформироватьСтруктуруДанныхКомпании(СтруктураНастроек, Партнер) Экспорт
	
	ИдКлиента = "";
	
	СтруктураИспКИ = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьСтруктуруВидовКИКонтрагентов();
	Идентификатор1С 	= XMLСтрока(Партнер);

	Поля = Новый Структура;
	
	Поля.Вставить("TITLE"			, Партнер.Наименование); 
	Поля.Вставить("COMMENTS"		, Партнер.Комментарий); 
	Поля.Вставить("ORIGIN_ID"		, Идентификатор1С); 
	Поля.Вставить("ORIGINATOR_ID"	, "ONE_C"); 
	Поля.Вставить("IS_MY_COMPANY"	, "N"); 
	
	НайденныеСтроки = СтруктураНастроек.ПресетыТиповКомпаний.НайтиСтроки(Новый Структура("Клиент, Конкурент, Поставщик, ПрочиеОтношения", Партнер.Клиент, Партнер.Конкурент, Партнер.Поставщик, Партнер.ПрочиеОтношения));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Поля.Вставить("COMPANY_TYPE", НайденныеСтроки[0].ИдТипа); 
	КонецЕсли;
	
	ТелоЗапроса = "";
	
	Для каждого ТекПоле Из Поля Цикл
		
		ТелоЗапроса = ТелоЗапроса + ?(СтрДлина(ТелоЗапроса) >0, "&", "") +"fields["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
		
	КонецЦикла;                                                                     
	
	МенеджерВременныхТаблицКИ = ПолучитьДанныеПартнераПоПочтеТелефону(Партнер);

	ЗаполнениеТелефонаИПочтыДляПартнера(СтруктураНастроек, Партнер, МенеджерВременныхТаблицКИ, СтруктураИспКИ, ТелоЗапроса);
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.company.add", ТелоЗапроса); 
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат ИдКлиента;
	КонецЕсли;
	
	result = СтруктураОтвета.Получить("result");
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат ИдКлиента;
	КонецЕсли;
	
	ИдКлиента = Формат(result, "ЧГ=0"); 	
	
	РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Компания, Партнер, ИдКлиента);

	Возврат	ИдКлиента;
	
КонецФункции


Функция ПолучитьДанныеПартнераПоПочтеТелефону(Партнер)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
#Область ФормированиеВременныхТаблицДанных
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипТелефона"	, Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("ТипПочты"	, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.УстановитьПараметр("Партнер"		, Партнер);	
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ПартнерыКонтактнаяИнформация.Ссылка КАК Объект,
	|	ПартнерыКонтактнаяИнформация.Представление КАК Представление,
	|	ПартнерыКонтактнаяИнформация.НомерТелефона КАК НомерТелефона,
	|	ПартнерыКонтактнаяИнформация.НомерТелефонаБезКодов КАК НомерТелефонаБезКодов,
	|	ПартнерыКонтактнаяИнформация.Вид КАК Вид
	|ПОМЕСТИТЬ ВТ_Телефоны
	|ИЗ
	|	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	|ГДЕ
	|	ПартнерыКонтактнаяИнформация.Тип = &ТипТелефона
	|	И ПартнерыКонтактнаяИнформация.Ссылка = &Партнер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтактныеЛицаКонтактнаяИнформация.Ссылка,
	|	КонтактныеЛицаКонтактнаяИнформация.Представление,
	|	КонтактныеЛицаКонтактнаяИнформация.НомерТелефона,
	|	КонтактныеЛицаКонтактнаяИнформация.НомерТелефонаБезКодов,
	|	КонтактныеЛицаКонтактнаяИнформация.Вид
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
	|ГДЕ
	|	КонтактныеЛицаКонтактнаяИнформация.Тип = &ТипТелефона
	|	И КонтактныеЛицаКонтактнаяИнформация.Ссылка = &Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартнерыКонтактнаяИнформация.Ссылка КАК Объект,
	|	ПартнерыКонтактнаяИнформация.Представление КАК Представление,
	|	ПартнерыКонтактнаяИнформация.АдресЭП КАК АдресЭП,
	|	ПартнерыКонтактнаяИнформация.Вид КАК Вид
	|ПОМЕСТИТЬ ВТ_Почта
	|ИЗ
	|	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	|ГДЕ
	|	ПартнерыКонтактнаяИнформация.Тип = &ТипПочты
	|	И ПартнерыКонтактнаяИнформация.Ссылка = &Партнер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтактныеЛицаКонтактнаяИнформация.Ссылка,
	|	КонтактныеЛицаКонтактнаяИнформация.Представление,
	|	КонтактныеЛицаКонтактнаяИнформация.АдресЭП,
	|	КонтактныеЛицаКонтактнаяИнформация.Вид
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
	|ГДЕ
	|	КонтактныеЛицаКонтактнаяИнформация.Тип = &ТипПочты
	|	И КонтактныеЛицаКонтактнаяИнформация.Ссылка = &Партнер";	
#КонецОбласти

	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;	
	
КонецФункции

Процедура ЗаполнениеТелефонаИПочтыДляПартнера(СтруктураНастроек, Партнер, МенеджерВременныхТаблиц, СтруктураИспВидовКИ, ТелоЗапроса)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Объект", Партнер);
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_Телефоны.Представление КАК Представление,
	|	ВТ_Телефоны.Вид КАК Вид
	|ИЗ
	|	ВТ_Телефоны КАК ВТ_Телефоны
	|ГДЕ
	|	ВТ_Телефоны.Объект = &Объект";
	
	ВыборкаТелефонов = Запрос.Выполнить().Выбрать();	
	
	Счетчик = 0;
	Пока ВыборкаТелефонов.Следующий() Цикл       
		
		ТипТелефона = "";
		Если ВыборкаТелефонов.Вид = СтруктураИспВидовКИ.ТелефонРабочий ИЛИ ВыборкаТелефонов.Вид = СтруктураИспВидовКИ.ТелефонРабочийКонЛица Тогда
			ТипТелефона = "WORK";
		ИначеЕсли ВыборкаТелефонов.Вид = СтруктураИспВидовКИ.ТелефонМобильный ИЛИ ВыборкаТелефонов.Вид = СтруктураИспВидовКИ.ТелефонМобильныйКонЛица Тогда
			ТипТелефона = "MOBILE";
		ИначеЕсли ВыборкаТелефонов.Вид = СтруктураИспВидовКИ.ТелефонДомашний ИЛИ ВыборкаТелефонов.Вид = СтруктураИспВидовКИ.ТелефонДомашнийКонЛица Тогда
			ТипТелефона = "HOME";
		Иначе
			Продолжить;
		КонецЕсли;
		
		ЕстьЗапятые 		= Ложь;
		ЕстьТочкаЗапятые 	= Ложь;
		МассивКонтактов 	= Новый Массив;
		
		Если СтрНайти(ВыборкаТелефонов.Представление, ",") > 0 Тогда
			ЕстьЗапятые = Истина;
			МассивКонтактов = СтрРазделить(ВыборкаТелефонов.Представление, ",")
		КонецЕсли;
		
		Если СтрНайти(ВыборкаТелефонов.Представление, ";") > 0 Тогда
			ЕстьТочкаЗапятые = Истина;
			МассивКонтактов = СтрРазделить(ВыборкаТелефонов.Представление, ";")
		КонецЕсли;
		
		Если НЕ ЕстьТочкаЗапятые И НЕ ЕстьЗапятые Тогда
			МассивКонтактов.Добавить(ВыборкаТелефонов.Представление);	
		КонецЕсли;
		
		Для Каждого ТекКонтакт Из МассивКонтактов Цикл 
			ТелоЗапроса = ТелоЗапроса + "&fields[PHONE]["+Строка(Счетчик)+"][VALUE]="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(СокрЛП(ТекКонтакт))+"&fields[PHONE]["+Строка(Счетчик)+"][VALUE_TYPE]=" + ТипТелефона; 
			Счетчик = Счетчик + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Объект", Партнер);
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_Почта.Представление КАК Представление,
	|	ВТ_Почта.Вид КАК Вид
	|ИЗ
	|	ВТ_Почта КАК ВТ_Почта
	|ГДЕ
	|	ВТ_Почта.Объект = &Объект";
	
	ВыборкаПочт = Запрос.Выполнить().Выбрать();	
	
	Счетчик = 0;
	Пока ВыборкаПочт.Следующий() Цикл
		
		ТипПочты = "";
		
		Если ВыборкаПочт.Вид = СтруктураИспВидовКИ.ПочтаРабочий ИЛИ ВыборкаПочт.Вид = СтруктураИспВидовКИ.ПочтаРабочийКонЛица Тогда
			ТипПочты = "WORK";
		ИначеЕсли ВыборкаПочт.Вид = СтруктураИспВидовКИ.ПочтаЧастная ИЛИ ВыборкаПочт.Вид = СтруктураИспВидовКИ.ПочтаЧастнаяКонЛица Тогда
			ТипПочты = "HOME";
		Иначе
			Продолжить;                                                                          
		КонецЕсли;
		
		ЕстьЗапятые 		= Ложь;
		ЕстьТочкаЗапятые 	= Ложь;
		МассивКонтактов 	= Новый Массив;
		
		Если СтрНайти(ВыборкаПочт.Представление, ",") > 0 Тогда
			ЕстьЗапятые = Истина;
			МассивКонтактов = СтрРазделить(ВыборкаПочт.Представление, ",")
		КонецЕсли;
		
		Если СтрНайти(ВыборкаПочт.Представление, ";") > 0 Тогда
			ЕстьТочкаЗапятые = Истина;
			МассивКонтактов = СтрРазделить(ВыборкаПочт.Представление, ";")
		КонецЕсли;
		
		Если НЕ ЕстьТочкаЗапятые И НЕ ЕстьЗапятые Тогда
			МассивКонтактов.Добавить(ВыборкаПочт.Представление);	
		КонецЕсли;
		
		Для Каждого ТекКонтакт Из МассивКонтактов Цикл 
			ТелоЗапроса = ТелоЗапроса + "&fields[EMAIL]["+Строка(Счетчик)+"][VALUE]="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(СокрЛП(ТекКонтакт))+"&fields[EMAIL]["+Строка(Счетчик)+"][VALUE_TYPE]=" + ТипПочты; 
			Счетчик = Счетчик + 1;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Функция СформироватьСтруктуруДанныхРеквизита(СтруктураНастроек, Контрагент, ИдВладельца, ТипВладельца) Экспорт
	
	ИдРеквизита = "";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Контрагенты.Ссылка КАК Объект,
	|	Контрагенты.Ссылка КАК Идентификатор1С,
	|	Контрагенты.ПометкаУдаления КАК ПометкаУдаления,
	|	Контрагенты.Наименование КАК Наименование,
	|	ВЫРАЗИТЬ(Контрагенты.НаименованиеПолное КАК СТРОКА(100)) КАК НаименованиеПолное,
	|	Контрагенты.ИНН КАК ИНН,
	|	Контрагенты.КПП КАК КПП,
	|	Контрагенты.КодПоОКПО КАК КодПоОКПО,
	|	Контрагенты.СтранаРегистрации.Наименование КАК СтранаРегистрации,
	|	Контрагенты.РегистрационныйНомер КАК РегистрационныйНомер,
	|	"""" КАК СвидетельствоСерияНомер,
	|	"""" КАК СвидетельствоДатаВыдачи,
	|	"""" КАК ОсновноеКонтактноеЛицо,
	|	Контрагенты.ЮрФизЛицо КАК ТипКонтрагента
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &Контрагент";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	мЭлементы 	=  Новый массив;
	
	ТелоЗапроса = "";
	Пока Выборка.Следующий() Цикл
		
		Поля = Новый Структура;
		
		Поля.Вставить("ENTITY_TYPE_ID"	, ТипВладельца);
		Поля.Вставить("ENTITY_ID"		, ИдВладельца);  	
		
		ИдШаблона = "";        
		НайденнаяСтрока = СтруктураНастроек.ПресетыШаблоновРеквизитов.Найти(Выборка.ТипКонтрагента);
		Если НайденнаяСтрока = Неопределено Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Не найден пресет для типа:" + Строка(Выборка.ТипКонтрагента));
			Возврат ИдРеквизита;	
		КонецЕсли;
		ИдШаблона = НайденнаяСтрока.ИдШаблона;
		
		Поля.Вставить("PRESET_ID"		, ИдШаблона);  	
		Поля.Вставить("NAME"			, ?(ЗначениеЗаполнено(Выборка.НаименованиеПолное), Выборка.НаименованиеПолное, Выборка.Наименование));  	
		Поля.Вставить("RQ_INN"			, Выборка.ИНН);  	
		
		Если ТипВладельца = "4" Тогда
			
			Поля.Вставить("RQ_COMPANY_NAME"		, Выборка.Наименование);  	
			Поля.Вставить("RQ_COMPANY_FULL_NAME", Выборка.НаименованиеПолное);  
			
			Поля.Вставить("RQ_KPP"		, Выборка.КПП);  
			
			Если СтрДлина(Выборка.РегистрационныйНомер) > 13 Тогда
				Поля.Вставить("RQ_OGRNIP"	, Выборка.РегистрационныйНомер);  
			Иначе
				Поля.Вставить("RQ_OGRN"		, Выборка.РегистрационныйНомер);  
			КонецЕсли;
			
		Иначе
			
			лНаименованиеПолное = ?(ЗначениеЗаполнено(Выборка.НаименованиеПолное), Выборка.НаименованиеПолное, Выборка.Наименование); 
			ФИО = Б24_К_ОбщегоНазначенияВызовСервера.РазобратьФИО(лНаименованиеПолное);
			Поля.Вставить("RQ_NAME"				, лНаименованиеПолное);
			Поля.Вставить("RQ_FIRST_NAME"		, ?(ЗначениеЗаполнено(ФИО.Имя), ФИО.Имя, Выборка.Наименование));
			Поля.Вставить("RQ_LAST_NAME"		, ?(ЗначениеЗаполнено(ФИО.Имя), ФИО.Фамилия, ""));
			Поля.Вставить("RQ_SECOND_NAME"		, ФИО.Отчество);
			
			Поля.Вставить("RQ_IDENT_DOC_SER"	, Лев(Выборка.СвидетельствоСерияНомер, 4));  	
			Поля.Вставить("RQ_IDENT_DOC_NUM"	, СокрЛП(Прав(Выборка.СвидетельствоСерияНомер, СтрДлина(Выборка.СвидетельствоСерияНомер)-4)));  	
			Поля.Вставить("RQ_IDENT_DOC_DATE"	, Выборка.СвидетельствоДатаВыдачи);  	
			
			Поля.Вставить("RQ_OGRNIP"			, Выборка.РегистрационныйНомер);  	
			
		КонецЕсли;
		
		Поля.Вставить("ACTIVE"			, ?(Выборка.ПометкаУдаления,"N","Y"));  	
		Поля.Вставить("XML_ID"			, XMLСтрока(Выборка.Идентификатор1С));  	
		Поля.Вставить("ORIGINATOR_ID"	, "ONE_C");  	
		
		
		Поля.Вставить("RQ_OKPO"				, Выборка.КодПоОКПО);  	
		Поля.Вставить("RQ_RESIDENCE_COUNTRY", Выборка.СтранаРегистрации);  	
		Поля.Вставить("RQ_CONTACT"			, Выборка.ОсновноеКонтактноеЛицо);  	
		
		Для каждого ТекПоле Из Поля Цикл
			ТелоЗапроса = ТелоЗапроса + ?(СтрДлина(ТелоЗапроса) >0, "&", "") +"fields["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
		КонецЦикла;         
		
	КонецЦикла;
	
	Если ТелоЗапроса <> "" Тогда
		
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.requisite.add", ТелоЗапроса); 
		Если СтруктураОтвета = Неопределено Тогда
			Возврат ИдРеквизита;
		КонецЕсли;
		
		result = СтруктураОтвета.Получить("result");
		ИдРеквизита = Формат(result, "ЧГ=0"); 	
		
	КонецЕсли;
	
	РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Реквизит, Контрагент, ИдРеквизита);
	
	Возврат ИдРеквизита;	
	
КонецФункции


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


Функция СформироватьСтруктуруДанныхАдреса(СтруктураНастроек, ТипДанных, Контрагент, ИдВладельца) Экспорт
	
	ИдАдреса = "";
	
	Если ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.ФактАдресРеквизита Тогда
		ВидКИ 		= Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
	Иначе
		ВидКИ		= Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент"	, Контрагент);
	Запрос.УстановитьПараметр("ВидКИ"		, ВидКИ);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	КонтрагентыКонтактнаяИнформация.Ссылка КАК Идентификатор1С,
	|	КонтрагентыКонтактнаяИнформация.Представление КАК Представление,
	|	КонтрагентыКонтактнаяИнформация.Страна КАК Страна,
	|	КонтрагентыКонтактнаяИнформация.Регион КАК Регион,
	|	КонтрагентыКонтактнаяИнформация.Город КАК Город,
	|	КонтрагентыКонтактнаяИнформация.ЗначенияПолей КАК ЗначенияПолей
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	|ГДЕ
	|	КонтрагентыКонтактнаяИнформация.Ссылка = &Контрагент
	|	И КонтрагентыКонтактнаяИнформация.Вид = &ВидКИ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Поля = Новый Структура;
		
		Поля.Вставить("ENTITY_TYPE_ID", "8");
		
		Если ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.ФактАдресРеквизита Тогда
			Поля.Вставить("TYPE_ID", "1");
		Иначе
			Поля.Вставить("TYPE_ID", "6");
		КонецЕсли;
		
		Поля.Вставить("ENTITY_ID", ИдВладельца);  	
		
		Б24_К_РаботаСАдресамиСервер.ДобавитьПоляАдресаВСтруктуруВыгрузки(Поля, Выборка); 
		
		ТелоЗапроса = "";
		Для каждого ТекПоле Из Поля Цикл
			ТелоЗапроса = ТелоЗапроса + ?(СтрДлина(ТелоЗапроса) >0, "&", "") +"fields["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
		КонецЦикла;         
		
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.address.add", ТелоЗапроса); 
		Если СтруктураОтвета = Неопределено Тогда
			Возврат ИдАдреса;
		КонецЕсли;
		
		result = СтруктураОтвета.Получить("result");
		
		Если СтруктураОтвета = Неопределено Тогда
			Возврат ИдАдреса;
		КонецЕсли;
		
		ИдАдреса = Формат(result, "ЧГ=0"); 	
		
		РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипДанных, Контрагент, ИдАдреса);
		
		Возврат ИдАдреса;	
		
	КонецЦикла;
	
	Возврат ИдАдреса;	
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


Процедура СформироватьСтруктуруДанныхБанковскогоСчета(СтруктураНастроек, Контрагент, ИдВладельца) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент"	, Контрагент);
	Запрос.Текст = "ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК Объект,
	|	БанковскиеСчета.Ссылка КАК Идентификатор1С,
	|	БанковскиеСчета.ПометкаУдаления КАК ПометкаУдаления,
	|	БанковскиеСчета.Наименование КАК Наименование,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	БанковскиеСчета.Банк КАК Банк,
	|	Банки.Код КАК КодБанка,
	|	Банки.КоррСчет КАК КоррСчетБанка,
	|	Банки.Адрес КАК АдресБанка,
	|	Банки.Город КАК ГородБанка,
	|	Банки.Наименование КАК НаименованиеБанка,
	|	Валюты.Наименование КАК ВалютаКод,
	|	Банки.СВИФТБИК КАК СВИФТБИК,
	|	Банки.Код КАК БикБанка,
	|	БанковскиеСчета.Владелец КАК Владелец
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторБанков КАК Банки
	|		ПО БанковскиеСчета.Банк = Банки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Валюты КАК Валюты
	|		ПО БанковскиеСчета.ВалютаДенежныхСредств = Валюты.Ссылка
	|ГДЕ
	|	БанковскиеСчета.Ссылка = &Контрагент";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	
	Пока Выборка.Следующий() Цикл
		
		Поля = Новый Структура;
		
		Поля.Вставить("ENTITY_TYPE_ID"	, "8");
		Поля.Вставить("ENTITY_ID"		, ИдВладельца);  	
		Поля.Вставить("ACTIVE"			, ?(Выборка.ПометкаУдаления,"N","Y"));  	
		Поля.Вставить("XML_ID"			, Выборка.Идентификатор1С);  	
		Поля.Вставить("ORIGINATOR_ID"	, "ONE_C");  	
		
		Поля.Вставить("NAME"			, Выборка.Наименование);
		
		Поля.Вставить("RQ_BANK_NAME"	, Выборка.НаименованиеБанка);
		Поля.Вставить("RQ_BANK_ADDR"	, Выборка.АдресБанка);
		Поля.Вставить("RQ_BIK"			, Выборка.БикБанка);
		
		лВалютаСимвольныйКод = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьВалютуПоКоду(Лев(Выборка.ВалютаКод, 3));
		
		Если ЗначениеЗаполнено(лВалютаСимвольныйКод) Тогда
			Поля.Вставить("RQ_ACC_CURRENCY", лВалютаСимвольныйКод);
		КонецЕсли;
		
		Поля.Вставить("RQ_ACC_NUM"		, Выборка.НомерСчета);
		Поля.Вставить("RQ_COR_ACC_NUM"	, Выборка.КоррСчетБанка);
		Поля.Вставить("RQ_SWIFT"		, Выборка.СВИФТБИК);
		
		ТелоЗапроса = "";
		
		Для каждого ТекПоле Из Поля Цикл
			
			ТелоЗапроса = ТелоЗапроса + ?(СтрДлина(ТелоЗапроса) >0, "&", "") +"fields["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
			
		КонецЦикла;         
		
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.bankdetail.add", ТелоЗапроса); 
		Если СтруктураОтвета = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		result = СтруктураОтвета.Получить("result");
		
		Если СтруктураОтвета = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИдСчета = Формат(result, "ЧГ=0"); 	
		
		РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.БанковскийСчетРеквизита, Выборка.Объект, ИдСчета);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
