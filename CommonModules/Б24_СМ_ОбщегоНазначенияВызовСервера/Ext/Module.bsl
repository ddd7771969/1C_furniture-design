
#Область СтандартизированныеПроцедурыИФункции

Функция СформироватьСтруктуруНастроек(НастройкаПодключения, ПолнаяСинхронизация = Ложь, ВидСинхронизации = "") Экспорт
		          
	//Если НЕ Б24_К_СлужебныйВызовСервера.МодульСмартПроцессовДоступен(НастройкаПодключения) Тогда
	//	Возврат Неопределено;
	//КонецЕсли;
	
	Если РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьМассивНастроекСинхронизации("НастройкаПодключения", НастройкаПодключения).Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураНастроек = Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Истина);
	Если СтруктураНастроек = Неопределено Тогда
		ТипыСообщений = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыСообщений();
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, ТипыСообщений.КритическаяОшибка, "Невозможно получить общие настройки. Продолжение невозможно.");
		Возврат СтруктураНастроек; 
	КонецЕсли;
	
	СтруктураНастроек.ИдентификаторПодключения = СтруктураНастроек.ИдентификаторПодключения + ПолучитьПостфиксИдКоннектора();
	СтруктураНастроек.ПараметрКоннектора = "auth_connector=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(СтруктураНастроек.ИдентификаторПодключения);

	Если ВидСинхронизации = 1 Тогда
		СтруктураНастроек.Логирование.Измерение1 = "В режиме реального времени";
	ИначеЕсли ВидСинхронизации = 0 Тогда
		СтруктураНастроек.Логирование.Измерение1 = "По расписанию";
	ИначеЕсли ВидСинхронизации = 2 Тогда
		СтруктураНастроек.Логирование.Измерение1 = "Интеграция сервисов";
	ИначеЕсли ВидСинхронизации = 3 Тогда
		СтруктураНастроек.Логирование.Измерение1 = "Смарт-процесс";
	Иначе
		СтруктураНастроек.Логирование.Измерение1 = "Интерактивно";
	КонецЕсли;
	  	
	РазрешенныеТипыКартинок = Новый Массив;
	РазрешенныеТипыКартинок.Добавить("gif");
	РазрешенныеТипыКартинок.Добавить("jpg");
	РазрешенныеТипыКартинок.Добавить("jpeg");
	РазрешенныеТипыКартинок.Добавить("png");
	РазрешенныеТипыКартинок.Добавить(".gif");
	РазрешенныеТипыКартинок.Добавить(".jpg");
	РазрешенныеТипыКартинок.Добавить(".jpeg");
	РазрешенныеТипыКартинок.Добавить(".png");
	РазрешенныеТипыКартинок.Добавить("");
	СтруктураНастроек.Вставить("РасширенияКартинок", РазрешенныеТипыКартинок);
	
	СтруктураНастроек.Вставить("ПолнаяЗагрузка"					, ПолнаяСинхронизация);	
	СтруктураНастроек.Вставить("ПолнаяВыгрузка"					, ПолнаяСинхронизация);	
	СтруктураНастроек.Вставить("СтатусПроверкиОчереди"			, 0);	
	СтруктураНастроек.Вставить("КоличествоДанныхВПакете"		, 50); 	
	
	СтруктураНастроек.Вставить("ВыполненоБезОшибок"				, Истина);
	СтруктураНастроек.Вставить("ИдПроцессаЗагрузки"				, "");
	СтруктураНастроек.Вставить("ТаблицаСопоставленияИзменений"	, Новый Массив);
	СтруктураНастроек.Вставить("НеобновляемыеДанныеНаПортале"	, Новый Массив);       // например данные которые не должны обновляться на сайте(флаг)
	
	СтруктураНастроек.Вставить("КартинкиИФайлы"					, Новый Массив);
	
	НастройкиСМ = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	
	СтруктураНастроек.Вставить("ПорядокВыполненияСинхронизации"			, НастройкиСМ.ПорядокВыполненияСинхронизации);
	СтруктураНастроек.Вставить("ИгнорироватьОшибкиВоВремяСинхронизации"	, НастройкиСМ.ИгнорироватьОшибкиВоВремяСинхронизации);
	СтруктураНастроек.Вставить("КоличествоПовторенийПриОшибках"			, НастройкиСМ.КоличествоПовторенийПриОшибках);
	СтруктураНастроек.Вставить("НеРегистрироватьИзмененияАвтоматически"	, НастройкиСМ.НеРегистрироватьИзмененияАвтоматически);
	СтруктураНастроек.Вставить("СпособСинхронизацииДанных"				, НастройкиСМ.СпособСинхронизацииДанных);
	СтруктураНастроек.Вставить("СвойствоДляХраненияЗначенийСвойств"		, Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп.СоздатьПолучитьСвойствоДляХраненияЗначенийСвойствБитрикс24());
	
	СохраненныеНастройкиСинхронизации = НастройкиСМ.Настройки.Получить();
	Если СохраненныеНастройкиСинхронизации = Неопределено Тогда
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.КритическаяОшибка, "Невозможно получить настройки синхронизации. Будут взяты по умолчанию");
		СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьНастройкиСинхронизацииПоУмолчанию();
	КонецЕсли;
	
	СтруктураНастроек.Вставить("НастройкиСинхронизации"					, СохраненныеНастройкиСинхронизации);
	СтруктураНастроек.Вставить("КлючиПодчиненныхБатчЗапросов"			, Новый Соответствие);
	
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") Тогда
		НастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
		СтруктураНастроек.Вставить("ВерсияТоваров", НастройкиСинхронизации.ВерсияТоваров);
	КонецЕсли;
	
	КлючиСмартПроцесса = Новый Структура;
	КлючиСмартПроцесса.Вставить("КлючиШапки", Новый Соответствие);
	КлючиСмартПроцесса.Вставить("КлючиТЧ"	, Новый Соответствие);

	СтруктураНастроек.Вставить("КлючиСмартПроцесса", КлючиСмартПроцесса);
	
	Возврат СтруктураНастроек;

КонецФункции

Функция ПолучитьПостфиксИдКоннектора() Экспорт
	
	Возврат "_SP";
	
КонецФункции

#КонецОбласти


#Область ОбщиеПроцедурыИФункцииСинхронизации

Процедура ВыгрузкаВРеальномВремени(НастройкаПодключения, СпособСинхронизацииДанных) Экспорт
	
	Если СпособСинхронизацииДанных = "ВРежимеРеальногоВремени"  Тогда
		
		СтруктураНастроек = СформироватьСтруктуруНастроек(НастройкаПодключения, Ложь, 1);
		Если СтруктураНастроек = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураНастроек.Логирование.Измерение2 = "Выгрузка данных на Битрикс24";
		СтруктураНастроек.СтатусПроверкиОчереди = 0;
		ЗапуститьВыполнениеОчередиВыгрузки(СтруктураНастроек);
		
		Если РегистрыСведений.Б24_СМ_ТаблицаИзменений.НаличиеИзмененийПоНастройкеПодключения(НастройкаПодключения) Тогда  
			ВыгрузкаВРеальномВремени(НастройкаПодключения, СпособСинхронизацииДанных);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьСинхронизациюПоФоновомуЗаданию(ПараметрыСинхронизации, АдресРезультата = Неопределено) Экспорт
	
	НастройкаПодключения 	= ПараметрыСинхронизации.НастройкаПодключения;
	//Если Б24_К_СлужебныйВызовСервера.МодульСмартПроцессовДоступен(НастройкаПодключения) = Ложь Тогда
	//	Возврат;	
	//КонецЕсли;
	
	ВидСинхронизации 		= ПараметрыСинхронизации.ВидСинхронизации;
	ПолнаяСинхронизация 	= ПараметрыСинхронизации.ПолнаяСинхронизация;
	ВыполнятьЗагрузку 		= ?(ПараметрыСинхронизации.ВыполнятьЗагрузку = Неопределено, Истина, ПараметрыСинхронизации.ВыполнятьЗагрузку);
	ВыполнятьВыгрузку 		= ?(ПараметрыСинхронизации.ВыполнятьВыгрузку = Неопределено, Истина, ПараметрыСинхронизации.ВыполнятьВыгрузку);
	
	ВыполнитьСинхронизацию(НастройкаПодключения, ВидСинхронизации, ПолнаяСинхронизация, ВыполнятьЗагрузку, ВыполнятьВыгрузку);
	
	ПоместитьВоВременноеХранилище(ПолучитьСообщенияПользователю(Истина), АдресРезультата);

КонецПроцедуры

Процедура СинхронизацияПоРасписанию(НастройкаПодключения) Экспорт
	
	НастройкаСинхронизации = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	
	Если НастройкаСинхронизации.СпособСинхронизацииДанных = "ПоРасписанию" И НЕ НастройкаПодключения.ПометкаУдаления Тогда
				
		ВыполнитьСинхронизацию(НастройкаПодключения, 0, Ложь);
			
	КонецЕсли;	
	
КонецПроцедуры

// Процедура - Выполняет синхронизацию с порталом
//
// Параметры:
//  ВидСинхронизации				 - 	 1 - Обмен в режиме реального времени, 2 - интерактивный
//  ПолныйОбмен						 - 	 Признак выполнения полной синхронизации 
//  ВыполнятьЗагрузку				 - 	 Признак выполнения загрузки данных с портала 
//  ВыполнятьВыгрузку				 - 	 Признак выполнения выгрузки данных на портал  
//
Процедура ВыполнитьСинхронизацию(НастройкаПодключения, ВидСинхронизации, ПолнаяСинхронизация, ВыполнятьЗагрузку = Истина, ВыполнятьВыгрузку = Истина) Экспорт
	
	Если Тип("Структура") = ТипЗнч(НастройкаПодключения) Тогда
		СтруктураНастроек = НастройкаПодключения;
	Иначе
		СтруктураНастроек = СформироватьСтруктуруНастроек(НастройкаПодключения, ПолнаяСинхронизация, ВидСинхронизации);
	КонецЕсли;
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураНастроек.ПорядокВыполненияСинхронизации = "Сперва 1С, затем Битрикс24" Тогда
		
		СтруктураНастроек.ВыполненоБезОшибок = Истина;
		
		Если ВыполнятьВыгрузку Тогда
			СтруктураНастроек.Логирование.Измерение2 = "Выгрузка данных на Битрикс24";
			СтруктураНастроек.СтатусПроверкиОчереди = 0;
			ЗапуститьВыполнениеОчередиВыгрузки(СтруктураНастроек);
		КонецЕсли;
		
		Если СтруктураНастроек.ВыполненоБезОшибок ИЛИ СтруктураНастроек.ИгнорироватьОшибкиВоВремяСинхронизации Тогда
			
			Если ВыполнятьЗагрузку Тогда
				СтруктураНастроек.Логирование.Измерение2 = "Загрузка данных с Битрикс24";
				ЗапуститьВыполнениеЗагрузки(СтруктураНастроек);
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		СтруктураНастроек.ВыполненоБезОшибок = Истина;
		
		Если ВыполнятьЗагрузку Тогда
			СтруктураНастроек.Логирование.Измерение2 = "Загрузка данных с Битрикс24";
			ЗапуститьВыполнениеЗагрузки(СтруктураНастроек);
		КонецЕсли;
		
		Если СтруктураНастроек.ВыполненоБезОшибок ИЛИ СтруктураНастроек.ИгнорироватьОшибкиВоВремяСинхронизации Тогда
			
			Если ВыполнятьВыгрузку Тогда
				СтруктураНастроек.Логирование.Измерение2 = "Выгрузка данных на Битрикс24";
				СтруктураНастроек.СтатусПроверкиОчереди = 0;
				ЗапуститьВыполнениеОчередиВыгрузки(СтруктураНастроек);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	УдалениеНеВыгружаемыхДанныхИзРегистраИзменений(НастройкаПодключения, СтруктураНастроек.ВремяЗапускаВМиллисекундах);
	
КонецПроцедуры

Процедура УдалениеНеВыгружаемыхДанныхИзРегистраИзменений(НастройкаПодключения, ВремяЗапускаВМиллисекундах)
	
	БлокировкаДанных = Новый БлокировкаДанных;
	БлокировкаДанных.Добавить("РегистрСведений.Б24_СМ_ТаблицаИзменений");
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	Попытка
		БлокировкаДанных.Заблокировать();	
		
		Запрос = Новый Запрос;
		Если Тип("Структура") = ТипЗнч(НастройкаПодключения) Тогда
			Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения.НастройкаПодключения);
		Иначе
			Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ВремяЗаписиВМиллисекундах"	, ВремяЗапускаВМиллисекундах);
		Запрос.Текст = "ВЫБРАТЬ
		|	Б24_СМ_ТаблицаИзменений.НастройкаПодключения КАК НастройкаПодключения,
		|	Б24_СМ_ТаблицаИзменений.ТипВладельцаСмартПроцесса КАК ТипВладельцаСмартПроцесса,
		|	Б24_СМ_ТаблицаИзменений.ИдСмартПроцесса КАК ИдСмартПроцесса,
		|	Б24_СМ_ТаблицаИзменений.ТипСущности1С КАК ТипСущности1С,
		|	Б24_СМ_ТаблицаИзменений.Сущность1С КАК Сущность1С,
		|	Б24_СМ_ТаблицаИзменений.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
		|	Б24_СМ_ТаблицаИзменений.ВремяЗаписиВМиллисекундах КАК ВремяЗаписиВМиллисекундах
		|ИЗ
		|	РегистрСведений.Б24_СМ_ТаблицаИзменений КАК Б24_СМ_ТаблицаИзменений
		|ГДЕ
		|	Б24_СМ_ТаблицаИзменений.НастройкаПодключения = &НастройкаПодключения
		|	И Б24_СМ_ТаблицаИзменений.ВремяЗаписиВМиллисекундах > &ВремяЗаписиВМиллисекундах";
		
		ТзнИзменений = Запрос.Выполнить().Выгрузить();
		
		НаборЗаписей = РегистрыСведений.Б24_СМ_ТаблицаИзменений.СоздатьНаборЗаписей(); 
		НаборЗаписей.Отбор.НастройкаПодключения.Установить(НастройкаПодключения); 
		НаборЗаписей.Загрузить(ТзнИзменений); 
		НаборЗаписей.Записать(Истина);

 ЗафиксироватьТранзакцию();
	Исключение
 ОтменитьТранзакцию();
	КонецПопытки;

КонецПроцедуры

#КонецОбласти


#Область ПроцедурыИФункцииВыгрузки

Процедура ЗапуститьВыполнениеОчередиВыгрузки(СтруктураНастроек) Экспорт
	
	Если СтруктураНастроек.ОбщиеНастройки.ДелатьЗапросНаВнешнийСервис Тогда
		Если ЗначениеЗаполнено(СтруктураНастроек.ОбщиеНастройки.АдресЗапросаНаВнешнийСервис) Тогда
			Б24_К_RestApiВызовСервера.ОтправитьЗапросаНаВнешнийСервис(СтруктураНастроек.ОбщиеНастройки.АдресЗапросаНаВнешнийСервис);
		КонецЕсли;
	КонецЕсли;
	
	ОчередьВыгрузки = ПолучитьОчередьВыгрузки(СтруктураНастроек);
	
	ПроверитьОчередьВыполненияВыгрузки(СтруктураНастроек, ОчередьВыгрузки);
	
КонецПроцедуры

Функция ПолучитьОчередьВыгрузки(СтруктураНастроек)
	
	МассивОчереди = Новый Массив;
	
	НастройкаПодключения 	= СтруктураНастроек.НастройкаПодключения; 
	НастройкиСинхронизации 	= СтруктураНастроек.НастройкиСинхронизации;
	
	Если НастройкиСинхронизации = Неопределено Тогда
		Возврат МассивОчереди;
	КонецЕсли;
	
	Если СтруктураНастроек.ПолнаяВыгрузка Тогда 
		
		Для каждого ТекСмартПроцесс Из НастройкиСинхронизации Цикл
			
			Если ТекСмартПроцесс.Данные = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТекСмартПроцесс.Данные.ВыгружатьНаПортал <> Истина Тогда
				Продолжить;
			КонецЕсли;
			
			ДанныеЭлемента = Новый Структура;
			ДанныеЭлемента.Вставить("ИдСмартПроцесса"			, ТекСмартПроцесс.Ид);
			ДанныеЭлемента.Вставить("ТипВладельцаСмартПроцесса"	, ТекСмартПроцесс.ТипВладельца);
			ДанныеЭлемента.Вставить("Наименование"				, ТекСмартПроцесс.Наименование);
			ДанныеЭлемента.Вставить("Данные"					, ТекСмартПроцесс.Данные);
			
			ДобавитьЗаписьВОчередьВыгрузкиДанных(НастройкаПодключения, МассивОчереди, ДанныеЭлемента, Истина);
			
		КонецЦикла;
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ВремяЗапускаВМиллисекундах"	, СтруктураНастроек.ВремяЗапускаВМиллисекундах);
		Запрос.УстановитьПараметр("НастройкаПодключения"		, НастройкаПодключения);
		Запрос.Текст ="ВЫБРАТЬ
		              |	Б24_СМ_ПакетыВыгрузки.ИдСмартПроцесса КАК ИдСмартПроцесса,
		              |	Б24_СМ_ПакетыВыгрузки.ТипВладельцаСмартПроцесса КАК ТипВладельцаСмартПроцесса,
		              |	Б24_СМ_ПакетыВыгрузки.ИдентификаторОбъекта КАК ИдентификаторОбъекта
		              |ИЗ
		              |	РегистрСведений.Б24_СМ_ПакетыВыгрузки КАК Б24_СМ_ПакетыВыгрузки
		              |ГДЕ
		              |	Б24_СМ_ПакетыВыгрузки.НастройкаПодключения = &НастройкаПодключения
		              |
		              |ОБЪЕДИНИТЬ ВСЕ
		              |
		              |ВЫБРАТЬ
		              |	Б24_СМ_ТаблицаИзменений.ИдСмартПроцесса,
		              |	Б24_СМ_ТаблицаИзменений.ТипВладельцаСмартПроцесса,
		              |	Б24_СМ_ТаблицаИзменений.ИдентификаторОбъекта
		              |ИЗ
		              |	РегистрСведений.Б24_СМ_ТаблицаИзменений КАК Б24_СМ_ТаблицаИзменений
		              |ГДЕ
		              |	Б24_СМ_ТаблицаИзменений.ВремяЗаписиВМиллисекундах <= &ВремяЗапускаВМиллисекундах
		              |	И Б24_СМ_ТаблицаИзменений.НастройкаПодключения = &НастройкаПодключения";
		
		тзнДанных = Запрос.Выполнить().Выгрузить();
		тзнДанных.Свернуть("ИдСмартПроцесса, ТипВладельцаСмартПроцесса, ИдентификаторОбъекта");
		
		Для каждого ТекСмартПроцесс Из НастройкиСинхронизации Цикл
			
			Если ТекСмартПроцесс.Данные = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТекСмартПроцесс.Данные.ВыгружатьНаПортал <> Истина Тогда
				Продолжить;
			КонецЕсли;
			
		 	НайденаяСтрока = тзнДанных.Найти(ТекСмартПроцесс.Ид, "ИдСмартПроцесса");
			Если НайденаяСтрока <> Неопределено Тогда
			
				ДанныеЭлемента = Новый Структура;
				ДанныеЭлемента.Вставить("ИдСмартПроцесса"			, ТекСмартПроцесс.Ид);
				ДанныеЭлемента.Вставить("ТипВладельцаСмартПроцесса"	, ТекСмартПроцесс.ТипВладельца);
				ДанныеЭлемента.Вставить("Наименование"				, ТекСмартПроцесс.Наименование);
				ДанныеЭлемента.Вставить("Данные"					, ТекСмартПроцесс.Данные);
				
				Если ТекСмартПроцесс.Данные.ТипСущности1С = "Справочник" Тогда
					СсылкаНаОбъект = Справочники[ТекСмартПроцесс.Данные.Сущность1С.Значение].ПолучитьСсылку(Новый УникальныйИдентификатор(НайденаяСтрока.ИдентификаторОбъекта));
				ИначеЕсли ТекСмартПроцесс.Данные.ТипСущности1С = "Документ" Тогда
					СсылкаНаОбъект = Документы[ТекСмартПроцесс.Данные.Сущность1С.Значение].ПолучитьСсылку(Новый УникальныйИдентификатор(НайденаяСтрока.ИдентификаторОбъекта));
				Иначе	
				    СсылкаНаОбъект = Неопределено;
				КонецЕсли;
				
				ДанныеЭлемента.Вставить("СсылкаНаОбъект"			, СсылкаНаОбъект);
				
				ДобавитьЗаписьВОчередьВыгрузкиДанных(НастройкаПодключения, МассивОчереди, ДанныеЭлемента, Истина);
				
			КонецЕсли;
			
		КонецЦикла;
		
		
	КонецЕсли;
	
	Возврат МассивОчереди;
	
КонецФункции

Процедура ДобавитьЗаписьВОчередьВыгрузкиДанных(НастройкаПодключения, МассивОчереди, ДанныеЭлемента, ПолнаяВыгрузка) 
	
		НоваяЗапись = Новый Структура;
		НоваяЗапись.Вставить("НастройкаПодключения"	 , НастройкаПодключения);
		НоваяЗапись.Вставить("ДанныеЭлемента"		 , ДанныеЭлемента);
		НоваяЗапись.Вставить("ДатаУстановкиВОчередь" , ТекущаяУниверсальнаяДатаВМиллисекундах());
		НоваяЗапись.Вставить("ПолнаяВыгрузка"		 , ПолнаяВыгрузка);
		НоваяЗапись.Вставить("Статус"		 		 , 0);
		НоваяЗапись.Вставить("КоличествоВыполнений"	 , 0);
		
		МассивОчереди.Добавить(НоваяЗапись);
	
КонецПроцедуры

Процедура ПроверитьОчередьВыполненияВыгрузки(СтруктураНастроек, ОчередьВыгрузки)
	
	// 0 - Новый
	// 1 - выгружен	
	// 2 - ошибка  
	
	ЕстьОчередь = Истина;
	
	Пока ЕстьОчередь Цикл
		
		ЕстьОчередь = Ложь;
	
		Для Каждого ЭлементОчереди Из ОчередьВыгрузки Цикл

			Если ЭлементОчереди.Статус <> 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭлементОчереди.КоличествоВыполнений > СтруктураНастроек.КоличествоПовторенийПриОшибках Тогда
				ЭлементОчереди.Статус = 2; 
				Продолжить;
			КонецЕсли;     
			
			ЕстьОчередь = Истина;
			
			СтруктураНастроек.Операция 			 	= ЭлементОчереди.ДанныеЭлемента.Наименование; 
			СтруктураНастроек.СтатусПроверкиОчереди = ЭлементОчереди.Статус;
			СтруктураНастроек.ВыполненоБезОшибок 	= Истина;
			
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Получение описания полей смарт-процесса: " + ЭлементОчереди.ДанныеЭлемента.Наименование);
			
			СтруктураНастроек.КлючиСмартПроцесса = Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьСтруктуруКлючейСмартПроцесса(СтруктураНастроек.НастройкаПодключения, ЭлементОчереди.ДанныеЭлемента.ТипВладельцаСмартПроцесса);
			
			Если СтруктураНастроек.ПолнаяВыгрузка Тогда
				СтруктураНастроек.Логирование.Измерение3 = "Полная выгрузка данных на портал для смарт-процесса: " + Строка(СтруктураНастроек.Операция);
			Иначе
				СтруктураНастроек.Логирование.Измерение3 = "Выгрузка изменений на портал для смарт-процесса: " + Строка(СтруктураНастроек.Операция);
			КонецЕсли;
			
			СформироватьИОтправить(СтруктураНастроек, ЭлементОчереди.ДанныеЭлемента);
			
			СтруктураНастроек.Логирование.Измерение4 = "";
			СтруктураНастроек.Логирование.Измерение5 = "";
			
			Если СтруктураНастроек.ВыполненоБезОшибок Тогда		
				ЭлементОчереди.Статус = 1;
				
			Иначе
				ЭлементОчереди.КоличествоВыполнений = ЭлементОчереди.КоличествоВыполнений + 1; 
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры 



Процедура СформироватьИОтправить(СтруктураНастроек, ДанныеЭлемента) Экспорт
	
	Если СтруктураНастроек.СтатусПроверкиОчереди = 0 Тогда
		СтруктураНастроек.Логирование.Измерение4 = "Первая отправка данных на портал"
	ИначеЕсли СтруктураНастроек.СтатусПроверкиОчереди = 2 Тогда
		СтруктураНастроек.Логирование.Измерение4 = "Повторная отправка данных на портал"
	КонецЕсли;
	
	Если СтруктураНастроек.СтатусПроверкиОчереди = 0 Тогда
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Выгрузка объектов для смарт-процесса: " + ДанныеЭлемента.Наименование);
	ИначеЕсли СтруктураНастроек.СтатусПроверкиОчереди = 2 Тогда
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Повторная выгрузка объектов для смарт-процесса: " + ДанныеЭлемента.Наименование);
	КонецЕсли;
	
	СтруктураНастроек.НеобновляемыеДанныеНаПортале.Очистить();
	
	СтруктураНастроек.Логирование.Измерение5 = "Формирование пакетов для выгрузки на портал";
	Б24_СМ_ВыгрузкаСервер.ФормированиеПакетовДанных(СтруктураНастроек, ДанныеЭлемента);
	СтруктураНастроек.Логирование.Измерение5 = "";
	
	ПодготовкаИВыгрузкаДанныхНаПортал(СтруктураНастроек, ДанныеЭлемента);
	
	СтруктураНастроек.Логирование.Измерение4 = "";
	СтруктураНастроек.Логирование.Измерение5 = "";
	СтруктураНастроек.Логирование.Измерение6 = "";
	
КонецПроцедуры

Процедура ПодготовкаИВыгрузкаДанныхНаПортал(СтруктураНастроек, ДанныеЭлемента)
	
	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Информация, "Формирование данных для смарт-процесса: " + ДанныеЭлемента.Наименование);
	
	КоличествоПакетов = Б24_СМ_ВыгрузкаСервер.ПолучитьКоличествоВыгружаемыхПакетов(СтруктураНастроек, ДанныеЭлемента);
	
	Для Пакет = 1 По КоличествоПакетов Цикл
		
		СтруктураНастроек.Логирование.Измерение6 = "Выгрузка на портал " + Строка(Пакет) + " пакета";
		
		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Информация, "Формирования пакета №:"+Строка(Пакет)+" для смарт-процесса: " + ДанныеЭлемента.Наименование);
		
		тзнДанных = Б24_СМ_ВыгрузкаСервер.ПолучитьПакетИзРегистраПакетов(СтруктураНастроек, ДанныеЭлемента, Пакет);
		
		Б24_СМ_ВыгрузкаСервер.УдалениеРегистрацииПоТипуДанных(СтруктураНастроек, ДанныеЭлемента, тзнДанных);
		
		ДобавитьЗаполнитьКолонкуОбъектТаблицы(ДанныеЭлемента.Данные, тзнДанных);
		
		СформированныеДанные = Новый Массив;
		СтруктураНастроек.КлючиПодчиненныхБатчЗапросов 	= Новый Соответствие;
		
		СформированныеДанные =  Б24_СМ_ВыгрузкаСервер.СформироватьДанныеПоСмартПроцессу(СтруктураНастроек, ДанныеЭлемента, тзнДанных, Пакет);
		

		Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Информация, "Выгрузка данных пакета на портал. Смарт-процесс: " + ДанныеЭлемента.Наименование);
		ВыгрузкаДанныхНаПортал(СтруктураНастроек, ДанныеЭлемента, СформированныеДанные, Пакет);
		
		Для каждого ТекЭлемент Из СтруктураНастроек.НеобновляемыеДанныеНаПортале Цикл
			Б24_СМ_ВыгрузкаСервер.УдалениеЗаписиИзПакетаДанных(СтруктураНастроек, Истина, ДанныеЭлемента, Пакет, ТекЭлемент);
		КонецЦикла;
		
		СтруктураНастроек.Логирование.Измерение6 ="";
		
	КонецЦикла;
	
КонецПроцедуры


Процедура ДобавитьЗаполнитьКолонкуОбъектТаблицы(ИнформацияОСущности1С, ТаблицаДанных) Экспорт
	
	Если ИнформацияОСущности1С.ТипСущности1С = "Справочник" Тогда
		ТипОбъектаСтрокой = "СправочникСсылка."+ИнформацияОСущности1С.Сущность1С.Значение;
	ИначеЕсли ИнформацияОСущности1С.ТипСущности1С = "Документ" Тогда
		ТипОбъектаСтрокой = "ДокументСсылка."+ИнформацияОСущности1С.Сущность1С.Значение;
	КонецЕсли;
	
	ТаблицаДанных.Колонки.Добавить("Объект", Новый ОписаниеТипов(ТипОбъектаСтрокой));
	
	ТипОбъекта = Тип(ТипОбъектаСтрокой);
	
	Для каждого ТекСтрока Из ТаблицаДанных Цикл
		ТекСтрока.Объект = XMLЗначение(ТипОбъекта, ТекСтрока.ИдентификаторОбъекта);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьРеквизитыТабличнойЧастиСущности1С(ИнформацияОСущности1С, ИмяТЧ) Экспорт
	
	Результат = Новый Массив;
	
	Если ИмяТЧ = "" Тогда
		Возврат Результат;
	КонецЕсли;
	
	МетаданныеОбъекта = Неопределено;
	Если ИнформацияОСущности1С.ТипСущности1С = "Справочник" Тогда
		МетаданныеОбъекта = Метаданные.Справочники[ИнформацияОСущности1С.Сущность1С.Значение]	
	ИначеЕсли ИнформацияОСущности1С.ТипСущности1С = "Документ" Тогда
		МетаданныеОбъекта = Метаданные.Документы[ИнформацияОСущности1С.Сущность1С.Значение]	
	КонецЕсли;
	
	Если МетаданныеОбъекта <> Неопределено Тогда
		
		МетаданныеОбъектаТЧ = МетаданныеОбъекта.ТабличныеЧасти.Найти(ИмяТЧ);
		Если МетаданныеОбъектаТЧ <> Неопределено Тогда
			
			Для каждого ТекРеквизит Из МетаданныеОбъектаТЧ.Реквизиты Цикл
				Результат.Добавить(ТекРеквизит.Имя);	
			КонецЦикла;
		
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


Процедура ВыгрузкаДанныхНаПортал(СтруктураНастроек, ДанныеЭлемента, СформированныеДанные, Пакет)
	
	Если СформированныеДанные.Количество() > 0 Тогда
		
		ТелоHTTPЗапроса= "";
		Для каждого ТекЭлемент Из СформированныеДанные Цикл
			ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + ТекЭлемент;	
		КонецЦикла;
		
		СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
		
		Если СтруктураОтвета = Неопределено Тогда
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
			Возврат;	
		КонецЕсли;
		
		ВремКлючиИдВладельцев 	= Новый Соответствие;
		ВремКлючиИдДел 			= Новый Соответствие;
		
		ТипОбъектаДело 			= СтруктураНастроек.ТипыОбъектовОбмена.ДелоСмартПроцесса;  
		
		result = СтруктураОтвета.Получить("result");
		Если result <> Неопределено Тогда
			
			result2 = result.Получить("result");
			Если result2 <> Неопределено Тогда
				
				Для каждого ТекСтрока Из result2 Цикл
					
					Если ТипЗнч(ТекСтрока) = Тип("Булево") ИЛИ ТипЗнч(ТекСтрока) = Тип("Соответствие") ИЛИ СтрДлина(ТекСтрока.Ключ) < 30 Тогда
						Продолжить;
					КонецЕсли;
					
					ДанныеКлюча = СтруктураНастроек.КлючиПодчиненныхБатчЗапросов.Получить(ТекСтрока.Ключ);
					Если ДанныеКлюча = Неопределено Тогда
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Не найден объект с ид:" + Строка(ТекСтрока.Ключ));  
						СтруктураНастроек.ВыполненоБезОшибок = Ложь;
						Продолжить;
					КонецЕсли;
					
					Если Лев(ТекСтрока.Ключ, 2) = "D_" Тогда
						
						ИдДела 		= ДанныеКлюча.ТипВладельцаСмартПроцесса+"_"+Формат(ТекСтрока.Значение, "ЧГ=0");
						
						ВнешнийКодВладельца = Прав(ТекСтрока.Ключ, СтрДлина(ТекСтрока.Ключ)-2);
							
						ДанныеКлючаВладельца = ВремКлючиИдВладельцев.Получить(ВнешнийКодВладельца);
						Если ЗначениеЗаполнено(ДанныеКлючаВладельца) Тогда
							РегистрыСведений.Б24_К_ИдентификаторыДел.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипОбъектаДело, ДанныеКлюча.Объект, ИдДела, ДанныеКлючаВладельца);
						Иначе
							
							ИнформацияОДеле = Новый Структура;
							ИнформацияОДеле.Вставить("Ид"		, ИдДела);
							ИнформацияОДеле.Вставить("Данные"	, ДанныеКлюча.Объект);
							
							ВремКлючиИдДел.Вставить(ВнешнийКодВладельца,  ИнформацияОДеле); 
								
						КонецЕсли;
							
					Иначе
						
						item = ТекСтрока.Значение.Получить("item");
						Если item = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						ИдЭлемента = Формат(item.Получить("id"), "ЧГ=0");
						
						ВремКлючиИдВладельцев.Вставить(ТекСтрока.Ключ, ИдЭлемента);
						
						СтруктураДляЗаписиИдентификатора = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьСтруктуруДляЗаписиИдентификатора();
						ЗаполнитьЗначенияСвойств(СтруктураДляЗаписиИдентификатора, ДанныеКлюча);
						СтруктураДляЗаписиИдентификатора.Идентификатор 	= ИдЭлемента;
						СтруктураДляЗаписиИдентификатора.Объект 		= ДанныеКлюча.Объект;
						
						РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураДляЗаписиИдентификатора);
						Б24_СМ_ВыгрузкаСервер.УдалениеЗаписиИзПакетаДанных(СтруктураНастроек, Истина, ДанныеЭлемента, Пакет, ДанныеКлюча.Объект);
						
						ИнформацияОДеле = ВремКлючиИдДел.Получить(ТекСтрока.Ключ);
						Если ИнформацияОДеле <> Неопределено Тогда
							РегистрыСведений.Б24_К_ИдентификаторыДел.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипОбъектаДело, ИнформацияОДеле.Данные, ИнформацияОДеле.Ид, ИдЭлемента);
						КонецЕсли;
							
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
			
			result_error = result.Получить("result_error"); 
			Если result_error <> Неопределено Тогда
				
				Для каждого ТекСтрока Из result_error Цикл     					
					
					Если ТипЗнч(ТекСтрока) <> Тип("Соответствие") Тогда                  
					
						ДанныеКлюча = СтруктураНастроек.КлючиПодчиненныхБатчЗапросов.Получить(ТекСтрока.Ключ);
						
						Если ДанныеКлюча = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						лОписаниеОшибки = ТекСтрока.Значение.Получить("error_description");
						
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, лОписаниеОшибки + " у объекта:" + Строка(ДанныеКлюча.Объект)); 
						
						РазобратьОшибку(СтруктураНастроек, ДанныеКлюча, лОписаниеОшибки);
						
						Б24_СМ_ВыгрузкаСервер.УдалениеЗаписиИзПакетаДанных(СтруктураНастроек, Ложь, ДанныеЭлемента, Пакет, ДанныеКлюча.Объект);
						
						СтруктураНастроек.ВыполненоБезОшибок = Ложь;
					
					КонецЕсли;					
					
				КонецЦикла;
				
			КонецЕсли;
		Иначе
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Не удалось разобрать JSON ответа");
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Тело отправляемого запроса: " + ТелоHTTPЗапроса);
			
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РазобратьОшибку(СтруктураНастроек, ДанныеКлюча, ОписаниеОшибки)
	
	НастройкаПодключения = СтруктураНастроек.НастройкаПодключения;
	
	СтруктураДляЗаписиИдентификатора = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.ПолучитьСтруктуруДляЗаписиИдентификатора();
	ЗаполнитьЗначенияСвойств(СтруктураДляЗаписиИдентификатора, ДанныеКлюча);
	СтруктураДляЗаписиИдентификатора.Объект 		= ДанныеКлюча.Объект;
					
	Если СтрНайти(ОписаниеОшибки, "not found") > 0 ИЛИ СтрНайти(ОписаниеОшибки, "Элемент не найден") > 0 Тогда	
		РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.УдалитьИдБ24Объекта(СтруктураНастроек, СтруктураДляЗаписиИдентификатора);  
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ПроцедурыИФункцииЗагрузки

Процедура ЗапуститьВыполнениеЗагрузкиВФоне(ПараметрыВыгрузки, АдресРезультата) Экспорт

	НастройкаПодключения = ПараметрыВыгрузки.НастройкаПодключения;
		
	СтруктураНастроек = СформироватьСтруктуруНастроек(НастройкаПодключения, Ложь, 1);
	Если СтруктураНастроек <> Неопределено Тогда
		ЗапуститьВыполнениеЗагрузки(СтруктураНастроек);
	КонецЕсли;
	
	Б24_К_ОбщегоНазначенияВызовСервера.ПроверитьФоновоеЗаданиеВОчереди(ПараметрыВыгрузки);

КонецПроцедуры


Процедура ЗапуститьВыполнениеЗагрузки(СтруктураНастроек) Экспорт
	
	Если СтруктураНастроек.ОбщиеНастройки.ДелатьЗапросНаВнешнийСервис Тогда
		Если ЗначениеЗаполнено(СтруктураНастроек.ОбщиеНастройки.АдресЗапросаНаВнешнийСервис) Тогда
			Б24_К_RestApiВызовСервера.ОтправитьЗапросаНаВнешнийСервис(СтруктураНастроек.ОбщиеНастройки.АдресЗапросаНаВнешнийСервис);
		КонецЕсли;
	КонецЕсли;
	
	НастройкаПодключения = СтруктураНастроек.НастройкаПодключения; 
	
	НастройкиСинхронизации = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	СохраненныеНастройки = НастройкиСинхронизации.Настройки.Получить();
	
	Если СохраненныеНастройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	
	ЗагружаемыеСмартПроцессы = Новый Соответствие;
	Для каждого ТекСтрока Из СохраненныеНастройки Цикл
		
		Если ТекСтрока.Данные = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекСтрока.Данные.ЗагружатьВ1С = Истина Тогда
			
			СтруктураДанных = Новый Структура;
			СтруктураДанных.Вставить("ИдСмартПроцесса"	, ТекСтрока.Ид);
			СтруктураДанных.Вставить("Наименование"		, ТекСтрока.Наименование);
			СтруктураДанных.Вставить("ДанныеНастроек"	, ТекСтрока.Данные);
			СтруктураДанных.Вставить("ДанныеПортала"	, Новый Массив);
			
			ЗагружаемыеСмартПроцессы.Вставить(ТекСтрока.ТипВладельца, СтруктураДанных);
			
		КонецЕсли;
		
	КонецЦикла;
	
	
	Если СтруктураНастроек.ПолнаяЗагрузка Тогда
		
		Для каждого ТекЭлемент Из ЗагружаемыеСмартПроцессы Цикл
			ОбработатьЗагрузкуДанныхПолнойВыгрузки(СтруктураНастроек, ТекЭлемент);
		КонецЦикла;
		
	Иначе
		
		ОбработатьЗагрузкуДанныхВРежимеИзменений(СтруктураНастроек, ЗагружаемыеСмартПроцессы);	
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ОбработатьЗагрузкуДанныхПолнойВыгрузки(СтруктураНастроек, ЗагружаемыйСмартПроцесс)
	
	СтруктураНастроек.Логирование.Измерение3 = "Полная загрузка данных с портала с типом: " + ЗагружаемыйСмартПроцесс.Значение.Наименование;
	
	СтруктураНастроек.Операция = ЗагружаемыйСмартПроцесс.Значение.Наименование; 
	ЗагрузитьСПорталаИОбработать(СтруктураНастроек, ЗагружаемыйСмартПроцесс);
	
	СтруктураНастроек.Логирование.Измерение3 = "";
	
КонецПроцедуры

Процедура ОбработатьЗагрузкуДанныхВРежимеИзменений(СтруктураНастроек, ЗагружаемыеСмартПроцессы)
	
	ТелоHTTPЗапроса = "&"+ СтруктураНастроек.ПараметрКоннектора + "&clear=1";	
	
	ВидыСобытий = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.СобытияБитрикс24();
	
	ЕстьСобытия = Истина;
	Пока ЕстьСобытия = Истина Цикл	
		
		СтруктураНастроек.ТаблицаСопоставленияИзменений.Очистить();
		
		СтруктураДанных = Новый Структура;
		Для каждого ТекСмартПроцесс Из ЗагружаемыеСмартПроцессы Цикл
			СтруктураДанных.Вставить("СмартПроцесс_"+ ТекСмартПроцесс.Ключ 	, Новый Структура("Данные, Операция", Новый Массив, ТекСмартПроцесс));
			
		КонецЦикла;
		
		СтруктураНастроек.Логирование.Измерение3 = "Запрос изменений с портала";
		
		ДанныеСПортала = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/event.offline.get", ТелоHTTPЗапроса); 
		
		Если НЕ ЗначениеЗаполнено(ДанныеСПортала) Тогда
			СтруктураНастроек.ВыполненоБезОшибок = Ложь;
			ЕстьСобытия = Ложь;
			Возврат;
		КонецЕсли;
		
		result = ДанныеСПортала.Получить("result");
		
		Если НЕ ЗначениеЗаполнено(result) Тогда
			ЕстьСобытия = Ложь;
			Возврат;
		КонецЕсли;
		
		СтруктураНастроек.ИдПроцессаЗагрузки = Строка(result.Получить("process_id"));
		
		События = result.Получить("events");
		
		Если НЕ ЗначениеЗаполнено(События) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Нет изменений на портале");
			ЕстьСобытия = Ложь;
			Возврат;
		КонецЕсли;
		
		Для Каждого ТекСобытие Из События Цикл
			
			НазваниеСобытия 	= ВРег(ТекСобытие.Получить("EVENT_NAME"));
			ИнформацияОСобытии 	= ТекСобытие.Получить("EVENT_DATA");
			ИдЗаписи 			= ТекСобытие.Получить("ID");
			ИдСообщения 		= ТекСобытие.Получить("MESSAGE_ID");
			
			Если НЕ ЗначениеЗаполнено(ИнформацияОСобытии) Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ (НазваниеСобытия = ВРег(ВидыСобытий.ДобавлениеСП) ИЛИ НазваниеСобытия = ВРег(ВидыСобытий.ОбновлениеСП) ИЛИ НазваниеСобытия = ВРег(ВидыСобытий.УдалениеСП)) Тогда
				Продолжить;
			КонецЕсли;
			
			ДанныеПолейСобытия 	= ИнформацияОСобытии.Получить("FIELDS");
			Если НЕ ЗначениеЗаполнено(ДанныеПолейСобытия) Тогда
				Продолжить;
			КонецЕсли;
			
			ТипВладельца 	= Формат(ДанныеПолейСобытия.Получить("ENTITY_TYPE_ID"), "ЧГ=0");
			ИдЭлемента 		= Формат(ДанныеПолейСобытия.Получить("ID"), "ЧГ=0");
			
			Если ЗагружаемыеСмартПроцессы.Получить(ТипВладельца) <> Неопределено Тогда
				СтруктураДанных["СмартПроцесс_" + ТипВладельца].Данные.Добавить(ИдЭлемента);
			КонецЕсли;
			
			СтруктураОтборка = Новый Структура;
			СтруктураОтборка.Вставить("ИдЗаписи"	, ИдЗаписи);
			СтруктураОтборка.Вставить("ИдСообщения"	, ИдСообщения);
			СтруктураОтборка.Вставить("Событие"		, НазваниеСобытия);
			СтруктураОтборка.Вставить("ИдЭлемента"	, ИдЭлемента);
			
			СтруктураНастроек.ТаблицаСопоставленияИзменений.Добавить(СтруктураОтборка);

#Область УдалениеИдЭлементов
			Если НазваниеСобытия = ВРег(ВидыСобытий.УдалениеСП) Тогда
			//	РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.УдалитьОбъектПоИдентификаторуБ24(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Компания, ИдЭлемента); 
			КонецЕсли;
#КонецОбласти			
			
		КонецЦикла;
		
		Для Каждого ТекСвойство Из СтруктураДанных Цикл
			
			ЗначениеСвойстваСтруктуры = ТекСвойство.Значение;
			
			Если ЗначениеСвойстваСтруктуры.Данные.Количество()> 0 Тогда
				
				СтруктураНастроек.Операция = ЗначениеСвойстваСтруктуры.Операция.Значение.Наименование; 
				СтруктураНастроек.Логирование.Измерение3 = "Загрузка изменений в 1С смарт-процесса: " + Строка(СтруктураНастроек.Операция);
				ЗагрузитьСПорталаИОбработать(СтруктураНастроек, ЗначениеСвойстваСтруктуры.Операция, ЗначениеСвойстваСтруктуры.Данные);
				СтруктураНастроек.Логирование.Измерение3 = "";
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры


Процедура ЗагрузитьСПорталаИОбработать(СтруктураНастроек,  ЗагружаемыйСмартПроцесс, МассивИдЭлементов = Неопределено) Экспорт 
	
	СтруктураНастроек.Логирование.Измерение4 = Строка(СтруктураНастроек.Операция);
	
	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Получение описания полей смарт-процесса: " + ЗагружаемыйСмартПроцесс.Значение.Наименование);
	
	СтруктураНастроек.КлючиСмартПроцесса = Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьСтруктуруКлючейСмартПроцесса(СтруктураНастроек.НастройкаПодключения, ЗагружаемыйСмартПроцесс.Ключ);
	
	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Получение данных элементов смарт процесса с портала");
	
	ТелоЗапроса = "&entityTypeId="+ЗагружаемыйСмартПроцесс.Ключ;
	ФильтрПоИд 	= "";
	Если МассивИдЭлементов <> Неопределено Тогда
		
		ФильтрПоИд = "";
		Для каждого ТекЭлемент Из МассивИдЭлементов Цикл
			ФильтрПоИд =  ФильтрПоИд + "&filter[id][]=" + ТекЭлемент;
		КонецЦикла;
		
	КонецЕсли;   
	
	ДанныеСмартПроцесса = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/crm.item.list", "id", ТелоЗапроса + ФильтрПоИд, "items");
	
	ДанныеТЧСмартПроцесса = Новый Массив;
	Если ЗагружаемыйСмартПроцесс.Значение.ДанныеНастроек.ТабличнаяЧасть1С <> Неопределено Тогда
		
		Для каждого ТекЭлемент Из ДанныеСмартПроцесса Цикл
			
			ТипВладельцаСмартПроцесса16 = "T" + Б24_К_ОбщегоНазначенияВызовСервера.КонвертацияДесятичногоЧислаВШестнадцатеричное(ЗагружаемыйСмартПроцесс.Ключ);
			
			ИдЭлемента = Формат(ТекЭлемент.Получить("id"), "ЧГ=0");
			
			ТелоЗапросаТЧ = "&filter[%3DownerType]="+ТипВладельцаСмартПроцесса16+"&filter[%3DownerId]="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдЭлемента);  //свои особенности
			
			ДанныеЭлементаТЧСмартПроцесса = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/crm.item.productrow.list", "id", ТелоЗапросаТЧ, "productRows");
			
			ТекЭлемент.Вставить("productrow", ДанныеЭлементаТЧСмартПроцесса);
			
		КонецЦикла;
		
	КонецЕсли;
	

	Б24_СМ_ЗагрузкаСервер.ЗагрузитьОбновитьЭлементыСмартПроцесса(СтруктураНастроек, ЗагружаемыйСмартПроцесс, ДанныеСмартПроцесса);
	
	СтруктураНастроек.Логирование.Измерение4 = "";
	
КонецПроцедуры

#КонецОбласти
