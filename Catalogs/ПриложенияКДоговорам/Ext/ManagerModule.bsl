&НаСервере
Функция ПолучитьИмяПапки(СсылкаНаГруппу) Экспорт
	Возврат СсылкаНаГруппу.Наименование;
КонецФункции // ПолучитьИмяПапки()

Процедура ПечатьРасширенный(ТабДок, Ссылка) Экспорт
	Макет = Справочники.ПриложенияКДоговорам.ПолучитьМакет("ПечатьРасширенный");
	ПечатьПриложения(ТабДок, Ссылка, Макет);
КонецПроцедуры

Процедура ПечатьКраткое(ТабДок, Ссылка) Экспорт
	Макет = Справочники.ПриложенияКДоговорам.ПолучитьМакет("ПечатьКраткий");
	ПечатьПриложения(ТабДок, Ссылка, Макет);
КонецПроцедуры

Процедура ПечатьПриложения(ТабДок, Ссылка, Макет) Экспорт

	обШапка 	= Макет.ПолучитьОбласть("Шапка");
	обИзделие 	= Макет.ПолучитьОбласть("Изделие");
	обУслуга 	= Макет.ПолучитьОбласть("Услуга");
	обПодвал 	= Макет.ПолучитьОбласть("Подвал");
	
	стДанные = ПолучитьЗапрос(Ссылка,Ложь);
	
	Выборка 			= стДанные.Выборка;
	тзИзделиеЗаказчика 	= стДанные.тзИзделиеЗаказчика;

	ТабДок.Очистить();

	стОтбор = Новый Структура("Договор",); 
	
	ВыводитьСуммы = РольДоступна("ЦенаВДоговоре");
	ПустаяКартинка = БиблиотекаКартинок.Пустая;	
	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		Итого 			= 0;
		НомерПоПорядку 	= 1;

		обШапка.Параметры.Заполнить(Выборка);
		обШапка.Параметры.ДатаДоговора = Формат(Выборка.ДатаДоговора,"ДЛФ=DD");
		ТабДок.Вывести(обШапка);

		стОтбор.Договор = Выборка.Договор;      
		
		Для Каждого х из тзИзделиеЗаказчика.НайтиСтроки(стОтбор) Цикл
			обИзделие.Параметры.Заполнить(х);
			Если ВыводитьСуммы Тогда
				обИзделие.Параметры.Сумма = х.Цена * х.Количество;
				Итого = Итого + обИзделие.Параметры.Сумма;
			Иначе
				обИзделие.Параметры.Цена = "";	
			КонецЕсли;
			обИзделие.Параметры.Номер = НомерПоПорядку;
			
			Если ТипЗнч(х.Картинка) = Тип("ХранилищеЗначения") Тогда
				Данные = х.Картинка.Получить();
				Если ТипЗнч(Данные) = Тип("Картинка") Тогда
					лкКартинка = Данные;
				ИначеЕсли ТипЗнч(Данные) = Тип("ДвоичныеДанные") Тогда
					лкКартинка = Новый Картинка(Данные, Ложь);
				Иначе
					лкКартинка = Неопределено;
				КонецЕсли;	
			Иначе
				лкКартинка = Неопределено;
			КонецЕсли;
			
			Если лкКартинка = Неопределено Тогда
				обИзделие.Рисунки.D1.Картинка = ПустаяКартинка; 			
			Иначе
				обИзделие.Рисунки.D1.Картинка = лкКартинка; 			
			КонецЕсли;
			
			ТабДок.Вывести(обИзделие);
			
			НомерПоПорядку = НомерПоПорядку + 1;
		КонецЦикла;
		
		ВыборкаУслугиПриложенияДоговора = Выборка.УслугиПриложенияДоговора.Выбрать(); 
		
		Пока ВыборкаУслугиПриложенияДоговора.Следующий() Цикл
			обУслуга.Параметры.Заполнить(ВыборкаУслугиПриложенияДоговора);
			Если ВыводитьСуммы Тогда
				Итого = Итого + ВыборкаУслугиПриложенияДоговора.Сумма;
			Иначе
				обУслуга.Параметры.Цена  = "";	
				обУслуга.Параметры.Сумма = "";	
			КонецЕсли;
			обУслуга.Параметры.Номер = НомерПоПорядку;
			ТабДок.Вывести(обУслуга);
			НомерПоПорядку = НомерПоПорядку + 1;
		КонецЦикла;
		
		Если ВыводитьСуммы Тогда
			обПодвал.Параметры.Итого = Итого;	
			ТабДок.Вывести(обПодвал); 
		КонецЕсли;
		
		ВставлятьРазделительСтраниц = Истина;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ПолучитьЗапрос(Ссылка, Краткое = Истина)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриложенияКДоговорам.ДатаДоговора КАК ДатаДоговора,
	|	ПриложенияКДоговорам.Отказ КАК Отказ,
	|	ПриложенияКДоговорам.Проект КАК Проект,
	|	ПриложенияКДоговорам.УслугиПриложенияДоговора.(
	|		НомерСтроки КАК НомерСтроки,
	|		УслугаПриложенияДоговора КАК Наименование,
	|		Количество КАК Количество,
	|		Комментарий КАК Комментарий,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Цена КАК Цена,
	|		"""" КАК РазмерТЗ,
	|		"""" КАК Помещения,
	|		Сумма КАК Сумма
	|	) КАК УслугиПриложенияДоговора,
	|	ПриложенияКДоговорам.Ссылка КАК Договор
	|ИЗ
	|	Справочник.ПриложенияКДоговорам КАК ПриложенияКДоговорам
	|ГДЕ
	|	ПриложенияКДоговорам.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеИзделияЗаказчика.НаименованиеСАртикулом КАК Наименование,
	|	ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика.Количество КАК Количество,
	|	ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика.ЦенаВДоговоре КАК Цена,
	|	ПриложенияКДоговорамИзделияЗаказчика.НомерСтроки КАК НомерСтроки,
	|	ПриложенияКДоговорамИзделияЗаказчика.Ссылка КАК Договор,
	|	ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика.РазмерПоТЗ КАК РазмерТЗ,
	|	ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика.ПомещенияСтрокой КАК Помещение,
	|	ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика.Описание КАК Описание,
	|	ПриложенияКДоговорамИзделияЗаказчика.Комментарий КАК Комментарий,
	|	КартинкиНоменклатуры.Картинка КАК Картинка
	|ИЗ
	|	Справочник.ПриложенияКДоговорам.ИзделияЗаказчика КАК ПриложенияКДоговорамИзделияЗаказчика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеИзделияЗаказчика КАК ДанныеИзделияЗаказчика
	|		ПО ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика = ДанныеИзделияЗаказчика.ИзделиеЗаказчика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КартинкиНоменклатуры КАК КартинкиНоменклатуры
	|		ПО ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика = КартинкиНоменклатуры.ИзделиеЭлемент
	|ГДЕ
	|	ПриложенияКДоговорамИзделияЗаказчика.Ссылка В(&Ссылка)
	|	И ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика.Количество > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	
	Результат = Запрос.ВыполнитьПакет();
	
	тзИзделиеЗаказчика = Результат[1].Выгрузить();
	
	Выборка = Результат[0].Выбрать();

	                                                   
	Возврат Новый Структура("тзИзделиеЗаказчика,Выборка",тзИзделиеЗаказчика,Выборка);
	
КонецФункции // ПолучитьЗапрос()

&НаСервере
Функция ПолучитНаименованиеТабличногоДокумента(ПараметрКоманды) Экспорт

	Если Тип("Массив") = ТипЗнч(ПараметрКоманды) Тогда
		Если ПараметрКоманды.Количество() = 0 Тогда
			Возврат "";
		Иначе
		СсылкаПриложения = ПараметрКоманды[0];
		КонецЕсли;
	
	ИначеЕсли Тип("СправочникСсылка.ПриложенияКДоговорам") = ТипЗнч(ПараметрКоманды) Тогда
		СсылкаПриложения = ПараметрКоманды;
	Иначе
		Возврат "";	
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриложенияКДоговорам.ДатаДоговора КАК ДатаДоговора,
		|	ПриложенияКДоговорам.НомерДоговора КАК НомерДоговора,
		|	ПриложенияКДоговорам.Проект.Наименование КАК Проект
		|ИЗ
		|	Справочник.ПриложенияКДоговорам КАК ПриложенияКДоговорам
		|ГДЕ
		|	ПриложенияКДоговорам.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаПриложения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаименованиеТабличногоДокумента = "";
	Пока Выборка.Следующий() Цикл
		НаименованиеТабличногоДокумента = "Приложение " + Выборка.Проект + " №" +  Выборка.НомерДоговора + " от " + Формат(Выборка.ДатаДоговора,"ДФ=dd.MM.yy");
	КонецЦикла;
	
    Возврат НаименованиеТабличногоДокумента;
КонецФункции // ПолучитНаименованиеТабличногоДокумента()
