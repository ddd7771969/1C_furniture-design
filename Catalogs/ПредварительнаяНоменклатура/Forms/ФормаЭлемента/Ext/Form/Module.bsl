&НаКлиенте
Перем ТекущииеОписание, ТекущийURL, стРоли, НоменклатураИзмененина;

&НаКлиенте
Процедура ТипНоменклатурыПриИзменении(Элемент)
	ДоступностьЭлементов();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЕдиницуИзмерения(ТипНоменклатуры)
	
	мЕдиницыПоУмолчанию = омЕдиницыИзмерения.ПолучитьЕдиницуПоУмолчанию(); 
	
	ОтчетНастройки = Отчеты.ОбщиеНастройки.Создать();
	
	НаименованиеНастройки = ОтчетНастройки.ПолучитьНаименованиеРеквизитаЕдиницы(ТипНоменклатуры);
	
	Если ПустаяСтрока(НаименованиеНастройки) Тогда
		Возврат Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	КонецЕсли;
	
	Для каждого текЕдиница Из мЕдиницыПоУмолчанию Цикл
		Для каждого ед Из текЕдиница Цикл
			Если ед.Ключ = НаименованиеНастройки Тогда
				Возврат ед.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	
КонецФункции // ПолучитьЕдиницуИзмерения()

&НаКлиенте
Процедура ДоступностьЭлементов(НачальноеЗаполнение = Истина)
	
	Если омРолиНаСервере.ДоступностьПредварительногоРасчета() Тогда
		Если НЕ ЗначениеЗаполнено(Объект.ТипНоменклатуры) Тогда
			Если НачальноеЗаполнение Тогда
				Объект.НормаРасхода = 0;
			КонецЕсли;
			Объект.ЕдиницаИзмерения = ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка");
			Элементы.ЕдиницаИзмерения.ТолькоПросмотр = Истина; 
		ИначеЕсли Объект.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Прочее") Тогда
			Если НачальноеЗаполнение  Тогда
				Объект.НормаРасхода = 0;
			КонецЕсли;
			Объект.ЕдиницаИзмерения = ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка");
			Элементы.ЕдиницаИзмерения.ТолькоПросмотр = Ложь; 
		Иначе
			Если НачальноеЗаполнение Тогда
				Объект.НормаРасхода = ПолучитьНормуРасходаНасСервере(Объект.ТипНоменклатуры);
			КонецЕсли;
			Объект.ЕдиницаИзмерения = ПолучитьЕдиницуИзмерения(Объект.ТипНоменклатуры);
			Элементы.ЕдиницаИзмерения.ТолькоПросмотр = ЗначениеЗаполнено(Объект.ЕдиницаИзмерения); 
		КонецЕсли;
	Иначе
		Элементы.ГруппаКнопкиКартинка.Видимость = Ложь;
		Элементы.Описание.ТолькоПросмотр 		= Истина;
		Элементы.ГруппаСсылка.Видимость 		= Ложь;
	КонецЕсли;
	
КонецПроцедуры // ДоступностьЭлементов()

&НаСервереБезКонтекста
Функция ПолучитьНормуРасходаНасСервере(ТипНоменклатуры)

	Возврат омНормыРасхода.ПолучитьНормуРасхода(ТипНоменклатуры);	

КонецФункции // ПолучитьНормуРасходаНасСервере()

#Область СобытиеФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ЭтаФорма.Параметры.Ключ.Пустая() Тогда
		ЭтаФорма.НомераОписаний.ЗагрузитьЗначения(РегистрыСведений.КартинкиПредварительнойНоменклатуры.ПолучитьМассивНомеровОписаний(Объект.Ссылка));
		Если ЭтаФорма.НомераОписаний.Количество() > 0 Тогда
			ЭтаФорма.НомерОписание = 1;
		КонецЕсли;
	КонецЕсли;
	Элементы.АдресКартинки.РазмерКартинки = РазмерКартинки.АвтоРазмер;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	стРоли = ДоступныеРоли();
	
	НачальнаяУстановкаВидимостиКомплекта();
	ВывестиОписаниеНаКлиенте();
	ДоступностьЭлементов(Ложь);
	ДоступностьЦены();
	ЗаполнитьЦены();
	
	ПолучитьНоменклатуруКлиент();
	НоменклатураИзмененина = Ложь;
	ЗаголовкиЗакладок();

КонецПроцедуры  
	
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ДоступностьЦены();
	Если НоменклатураИзмененина Тогда
		НоменклатураИзмененина = Ложь;
		СохранитьНоменклатуру();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ПослеИзмененияСвязиНоменклатураПредварительнаяНоменклатура" Тогда
		ПолучитьНоменклатуруКлиент();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти  //СобытиеФормы

&НаСервереБезКонтекста
Функция ДоступныеРоли()

	стРоли = Новый Структура("ЦеныЗакупки", РольДоступна("ЦеныЗакупки"));	
	
	Возврат стРоли;
КонецФункции // ДоступныеРоли()


#Область Цены

&НаКлиенте
Процедура ЗаполнитьЦены()
	
	Если ЭтаФорма.Параметры.Ключ.Пустая() Тогда
	
		Возврат;	
	
	КонецЕсли;
	
	мЗначений = омЦеныНаСервере.ПолучитьЦеныПредварительнойНоменклатуры(Объект.Ссылка);
	
	ЭтаФорма.ТаблицаЦены.Очистить();
	
	Для каждого х Из мЗначений Цикл
	
		ЗаполнитьЗначенияСвойств(ЭтаФорма.ТаблицаЦены.Добавить(), х);	
	
	КонецЦикла;
	
	Если ЭтаФорма.ТаблицаЦены.Количество() > 0 Тогда
		ЭтаФорма.ТекущаяЦена = ЭтаФорма.ТаблицаЦены[0].Цена;
	КонецЕсли;
	

КонецПроцедуры // ЗаполнитьЦены()

&НаКлиенте
Процедура УстановитьЦену(Команда)
	омЦеныНаСервере.УстановитьЦенуПредварительнаяНоменклатура(Объект.Ссылка,ЭтаФорма.ТекущаяЦена);
	ЗаполнитьЦены();
КонецПроцедуры

&НаКлиенте
Процедура ДоступностьЦены()

	Элементы.ГруппаЦены.Видимость = стРоли.ЦеныЗакупки;

	Если ЭтаФорма.Параметры.Ключ.Пустая() ИЛИ НЕ стРоли.ЦеныЗакупки Тогда
		Элементы.ГруппаЦена.Видимость = Ложь;
	Иначе	
		Элементы.ГруппаЦена.Видимость = Истина;
	КонецЕсли;
	

КонецПроцедуры // ДоступностьЦены()      ГруппаЦены

#КонецОбласти //Цены

#Область Команды
	
&НаКлиенте
Процедура ПредыдущиеОписание(Команда)
	ЭтаФорма.НомерОписание = ЭтаФорма.НомерОписание - 1;
	ВывестиОписаниеНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура СледующиеОписание(Команда)
	ЭтаФорма.НомерОписание = ЭтаФорма.НомерОписание + 1;
	ВывестиОписаниеНаКлиенте();
КонецПроцедуры

&НаКлиенте
Асинх Процедура ДобавитьКартинку(Команда)
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Фильтр = "Файл изображения|*.jpg;*.png;*.jpeg"; 
	ПомещенныйФайл = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога);
	
	Если ПомещенныйФайл <> Неопределено И Не ПомещенныйФайл.ПомещениеФайлаОтменено Тогда
		ЗаписатьФайлКартинкиНаСервере(ПомещенныйФайл.Адрес, ПомещенныйФайл.СсылкаНаФайл.Расширение);
		ПодключитьОбработчикОжидания("ПослеЗагрузкиКартинки",0.5,Истина); 
		Элементы.УдалитьКартинку.Видимость 			= Истина;
		Элементы.ФормаДобавитьКартинку.Видимость 	= Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКартинку(Команда)
	Оповещение = Новый ОписаниеОповещения("ПослеВопросаУдалениеКартинки",ЭтаФорма);
	ПоказатьВопрос(Оповещение,"Вы точно хотите удалить картинку?",РежимДиалогаВопрос.ДаНет,,,"Удаление картинки");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОписание(Команда)
	ЭтаФорма.НомерОписание = ЭтаФорма.НомераОписаний.Количество() + 1;
	ЭтаФорма.НомераОписаний.Добавить(ЭтаФорма.НомерОписание);
	ВывестиОписаниеНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСсылкуURL(Команда)
	Если ПустаяСтрока(ЭтаФорма.СсылкаURL) Тогда
		ПоказатьПредупреждение(,"Не указана строка URL");	
	Иначе	
		Оповещение = Новый ОписаниеОповещения("ПослеОткрытияСсылкиURL",ЭтаФорма);
		НачатьЗапускПриложения(Оповещение,ЭтаФорма.СсылкаURL);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти   

#Область Номенклатура

&НаКлиенте
Процедура СохранитьНоменклатуру()
	мНоменклатура = Новый Массив;
	
	Для каждого х Из ЭтаФорма.Номенклатура Цикл
	
		Если ЗначениеЗаполнено(х.Номенклатура) И мНоменклатура.Найти(х.Номенклатура) = Неопределено Тогда
			мНоменклатура.Добавить(х.Номенклатура);	
		КонецЕсли;	
		
	КонецЦикла;
	
    СвязатьСНоменклатуройНаСервере(Объект.Ссылка,мНоменклатура);
КонецПроцедуры // СохранитьНоменклатуру()

&НаСервереБезКонтекста
Процедура СвязатьСНоменклатуройНаСервере(ПредварительнаяНоменклатура,мНоменклатура)

	РегистрыСведений.СвязьНоменклатурыСПредварительной.СвязатьПредварительнуюСНоменклатурой(ПредварительнаяНоменклатура,мНоменклатура);

КонецПроцедуры // СвязатьСНоменклатуройНаСервере()

&НаКлиенте
Процедура ПолучитьНоменклатуруКлиент()

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;	
	КонецЕсли;
	
	мНоменклатуры = ПолучитьНоменклатуруНаСервере(Объект.Ссылка);
	
	ЭтаФорма.Номенклатура.Очистить();
	
	Для каждого х Из мНоменклатуры Цикл
		ЗаполнитьЗначенияСвойств(ЭтаФорма.Номенклатура.Добавить(),х);
	КонецЦикла;

КонецПроцедуры // ПолучитьНоменклатуруКлиент()

&НаСервереБезКонтекста
Функция ПолучитьНоменклатуруНаСервере(ПредварительнаяНоменклатура)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СвязьНоменклатурыСПредварительной.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.СвязьНоменклатурыСПредварительной КАК СвязьНоменклатурыСПредварительной
		|ГДЕ
		|	СвязьНоменклатурыСПредварительной.ПредварительнаяНоменклатура = &ПредварительнаяНоменклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	СвязьНоменклатурыСПредварительной.Номенклатура.Наименование";
	
	Запрос.УстановитьПараметр("ПредварительнаяНоменклатура", ПредварительнаяНоменклатура);
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
	
КонецФункции // ПолучитьНоменклатуруНаСервере(ПредварительнаяНоменклатура)

#КонецОбласти

#Область СобытияРеквизитовТЧ

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	НоменклатураИзмененина = Истина;
	ЗаголовкиЗакладок(Элемент.Родитель.Имя);	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЦеныПриИзменении(Элемент)
	ЗаголовкиЗакладок(Элемент.Родитель.Имя);	
КонецПроцедуры

&НаКлиенте
Процедура КомплектыПриИзменении(Элемент)
	ЗаголовкиЗакладок(Элемент.Родитель.Имя);	
КонецПроцедуры

&НаКлиенте
Процедура РасценкиПриИзменении(Элемент)
	ЗаголовкиЗакладок(Элемент.Родитель.Имя);	
КонецПроцедуры

#КонецОбласти


&НаКлиенте
Процедура ПослеВопросаУдалениеКартинки(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Да Тогда
		Если УдалитьКартинкуНаСервере(Объект.Ссылка,ЭтаФорма.НомерОписание) Тогда
			ИзменитьНумерациюОписания(Объект.Ссылка);
		КонецЕсли;
		ЭтаФорма.АдресКартинки = "";
		Элементы.УдалитьКартинку.Видимость 			= Ложь;
		Элементы.ФормаДобавитьКартинку.Видимость 	= Истина;
	КонецЕсли;

КонецПроцедуры


&НаСервереБезКонтекста
Процедура ИзменитьНумерациюОписания(ПредварительнаяНоменклатура)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КартинкиПредварительнойНоменклатуры.Номер КАК Номер,
		|	КартинкиПредварительнойНоменклатуры.Картинка КАК Картинка,
		|	КартинкиПредварительнойНоменклатуры.URL КАК URL,
		|	КартинкиПредварительнойНоменклатуры.Описание КАК Описание,
		|	КартинкиПредварительнойНоменклатуры.ЕстьКартинка КАК ЕстьКартинка,
		|	КартинкиПредварительнойНоменклатуры.ТипФайла КАК ТипФайла
		|ИЗ
		|	РегистрСведений.КартинкиПредварительнойНоменклатуры КАК КартинкиПредварительнойНоменклатуры
		|ГДЕ
		|	КартинкиПредварительнойНоменклатуры.ПредварительнаяНоменклатура = &ПредварительнаяНоменклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номер";
	
	Запрос.УстановитьПараметр("ПредварительнаяНоменклатура", ПредварительнаяНоменклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВсегоЗаписей = Выборка.Количество();
	
	текНомер = 0;
	Пока Выборка.Следующий() Цикл
		текНомер = текНомер + 1;
		Если Выборка.Номер = текНомер Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.КартинкиПредварительнойНоменклатуры.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПредварительнаяНоменклатура.Установить(ПредварительнаяНоменклатура);
		НаборЗаписей.Отбор.Номер.Установить(текНомер);
		
		Если ВсегоЗаписей <= текНомер Тогда
			НоваяЗапись = НаборЗаписей.Добавить(); 
			ЗаполнитьЗначенияСвойств(НоваяЗапись,Выборка);
			НоваяЗапись.ПредварительнаяНоменклатура = ПредварительнаяНоменклатура;
			НоваяЗапись.Номер 						= текНомер;
		КонецЕсли;
		
		НаборЗаписей.Записать();	
	КонецЦикла;
	
    Для х = текНомер + 1 По Выборка.Номер Цикл
	
		НаборЗаписей = РегистрыСведений.КартинкиПредварительнойНоменклатуры.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПредварительнаяНоменклатура.Установить(ПредварительнаяНоменклатура);
		НаборЗаписей.Отбор.Номер.Установить(х);
	
		НаборЗаписей.Записать();	
	КонецЦикла;
КонецПроцедуры // ИзменитьНумерациюОписания()


&НаСервереБезКонтекста
Функция УдалитьКартинкуНаСервере(ПредварительнаяНоменклатура,Номер)

	МенеджерЗаписи = РегистрыСведений.КартинкиПредварительнойНоменклатуры.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ПредварительнаяНоменклатура = ПредварительнаяНоменклатура;
	МенеджерЗаписи.Номер = Номер;
	МенеджерЗаписи.Прочитать();
	
	Если ПустаяСтрока(МенеджерЗаписи.URL) И ПустаяСтрока(МенеджерЗаписи.Описание) Тогда
		МенеджерЗаписи.Удалить();
		БылоУдаление = Истина;
	Иначе
		МенеджерЗаписи.Картинка 	= Неопределено;
		МенеджерЗаписи.ЕстьКартинка = Ложь;
		МенеджерЗаписи.ТипФайла 	= ""; 
		МенеджерЗаписи.Записать();
		БылоУдаление = Ложь;
	КонецЕсли;
	
	Возврат БылоУдаление;
КонецФункции // УдалитьКартинкуНаСервере()


&НаКлиенте
Процедура ВывестиОписаниеНаКлиенте()
	ВывестиОписание();
	
	ТекущииеОписание 	= ЭтаФорма.Описание;
	ТекущийURL 			= ЭтаФорма.СсылкаURL;

КонецПроцедуры // ВывестиОписаниеНаКлиенте()
  

&НаКлиенте
Процедура ПослеОткрытияСсылкиURL(КодВозврата, ДополнительныйПараметр) Экспорт

КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗагрузкиКартинки() Экспорт

	ОбновитьКартинку();	
	Элементы.АдресКартинки.Обновить();

КонецПроцедуры // ПослеЗагрузкиКартинки()

#Область Комплект

&НаКлиенте
Процедура НачальнаяУстановкаВидимостиКомплекта()

	ЭтаФорма.Комплект = Объект.Комплекты.Количество() > 0;
	КомплектПриИзменении(Неопределено);

КонецПроцедуры // НачальнаяУстановкаВидимостиКомплекта()
		
#КонецОбласти

&НаСервере
Функция ЗаписатьФайлКартинкиНаСервере(АдресВХ, ТипФайла)
	
	НаборЗаписей = РегистрыСведений.КартинкиПредварительнойНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПредварительнаяНоменклатура.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.Номер.Установить(ЭтаФорма.НомерОписание);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		Запись = НаборЗаписей.Добавить();
		Запись.Номер 						= ЭтаФорма.НомерОписание;
		Запись.Описание 					= ЭтаФорма.Описание;
		Запись.URL 							= ЭтаФорма.СсылкаURL;
		Запись.ПредварительнаяНоменклатура 	= Объект.Ссылка;
	Иначе
		Запись = НаборЗаписей[0];	
	КонецЕсли;
	
	Запись.Картинка 	= Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресВХ));
	Запись.ТипФайла 	= ТипФайла;
	Запись.ЕстьКартинка = Истина;
	
    НаборЗаписей.Записать(Истина);
	
КонецФункции

#Область СобытиеЭлементовФормы

&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)
	Если НЕ ТекущииеОписание = ЭтаФорма.Описание Тогда
		Если ЗаписатьРеквизитОписанияНаСервере("Описание",Объект.Ссылка,ЭтаФорма.НомерОписание,ЭтаФорма.Описание) Тогда
			ИзменитьНумерациюОписания(Объект.Ссылка);	
		КонецЕсли;	
	КонецЕсли;
	ТекущииеОписание 	= ЭтаФорма.Описание;
КонецПроцедуры

&НаКлиенте
Процедура СсылкаПриИзменении(Элемент)
	Если НЕ ТекущийURL = ЭтаФорма.СсылкаURL Тогда
		Если ЗаписатьРеквизитОписанияНаСервере("URL",Объект.Ссылка,ЭтаФорма.НомерОписание,ЭтаФорма.СсылкаURL) Тогда
			ИзменитьНумерациюОписания(Объект.Ссылка);	
		КонецЕсли;		
	КонецЕсли;
	ТекущийURL 			= ЭтаФорма.СсылкаURL;
КонецПроцедуры

&НаКлиенте
Процедура КомплектПриИзменении(Элемент)
	Элементы.ГруппаКомплект.Видимость = ЭтаФорма.Комплект;
	Если ЭтаФорма.Комплект Тогда
		ЗаголовкиЗакладок(Элемент.Родитель.Имя);	
	Иначе
		Объект.Комплекты.Очистить();	
	КонецЕсли;
КонецПроцедуры
	
#КонецОбласти  


&НаКлиенте
Процедура ЗаголовкиЗакладок(ИмяЗакладки = "")

	Если ИмяЗакладки = "" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаНоменклатура"	,"Номенклатура"	,ЭтаФорма.Номенклатура);
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаКомплект"		,"Комплект"		,Объект.Комплекты);
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаРаботы"			,"Работы"		,Объект.Расценки);
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаЦены"			,"Цены"			,ЭтаФорма.ТаблицаЦены);
	ИначеЕсли ИмяЗакладки = "ГруппаНоменклатура" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаНоменклатура"	,"Номенклатура"	,ЭтаФорма.Номенклатура);
	ИначеЕсли ИмяЗакладки = "ГруппаКомплект" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаКомплект"		,"Комплект"		,Объект.Комплекты);
	ИначеЕсли ИмяЗакладки = "ГруппаРаботы" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаРаботы"			,"Работы"		,Объект.Расценки);
	ИначеЕсли ИмяЗакладки = "ГруппаЦены" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаЦены"			,"Цены"			,ЭтаФорма.ТаблицаЦены);
	КонецЕсли;

КонецПроцедуры // ЗаголовкиЗакладок()


#Область Описания 

&НаСервереБезКонтекста
Функция ЗаписатьРеквизитОписанияНаСервере(ТипРеквизвита,ПредварительнаяНоменклатура,Номер,ТекстРеквизита)

	МенеджерЗаписи = РегистрыСведений.КартинкиПредварительнойНоменклатуры.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ПредварительнаяНоменклатура = ПредварительнаяНоменклатура;
	МенеджерЗаписи.Номер = Номер;
	МенеджерЗаписи.Прочитать();
		
	Если ТипРеквизвита = "Описание" Тогда
		МенеджерЗаписи.Описание = ТекстРеквизита; 
	Иначе
		МенеджерЗаписи.URL 		= ТекстРеквизита; 
	КонецЕсли; 
	
	Если ПустаяСтрока(МенеджерЗаписи.URL) И ПустаяСтрока(МенеджерЗаписи.Описание) И НЕ МенеджерЗаписи.ЕстьКартинка Тогда
		МенеджерЗаписи.Удалить();
		БылоУдаление = Истина;
	Иначе
		Если НЕ ЗначениеЗаполнено(МенеджерЗаписи.ПредварительнаяНоменклатура) Тогда
			МенеджерЗаписи.ПредварительнаяНоменклатура = ПредварительнаяНоменклатура;
			МенеджерЗаписи.Номер = Номер;
		КонецЕсли;
		МенеджерЗаписи.Записать();
		БылоУдаление = Ложь;
	КонецЕсли;
    Возврат БылоУдаление;
КонецФункции // ЗаписатьРеквизитОписанияНаСервере(ТипРеквизвита,ПредварительнаяНоменклатура,Номер,ТекстРеквизита)


&НаСервере
Процедура ВывестиОписание()
	
	Если ЭтаФорма.НомерОписание = 0 Тогда
		
		ЭтаФорма.Элементы.ГруппаОписания.Видимость = Ложь;	
		
	Иначе
		ЭтаФорма.Элементы.ГруппаОписания.Видимость = Истина;	
		стОписание = РегистрыСведений.КартинкиПредварительнойНоменклатуры.ПолучитьДанныеОписания(Объект.Ссылка,ЭтаФорма.НомерОписание);
		Если стОписание.ЕстьКартинка Тогда
			ОбновитьКартинку();
			Элементы.ФормаДобавитьКартинку.Видимость 	= Ложь;
			Элементы.УдалитьКартинку.Видимость 			= Истина;
		Иначе
			Элементы.ФормаДобавитьКартинку.Видимость 	= Истина;
			Элементы.УдалитьКартинку.Видимость 			= Ложь;
			АдресКартинки = "";
		КонецЕсли;
		ЭтаФорма.Описание 	= стОписание.Описание;
		ЭтаФорма.СсылкаURL 	= стОписание.URL; 
		
		КоличествоОписаний = ЭтаФорма.НомераОписаний.Количество();
		Если КоличествоОписаний > 1 Тогда
		
			Элементы.ГруппаПеремещениеОписаний.Видимость = Истина;
			ЭтаФорма.СообщениеОписание = Строка(ЭтаФорма.НомерОписание) + "/" + КоличествоОписаний;
			
			Если ЭтаФорма.НомерОписание = 1 Тогда
				Элементы.СледующиеОписание.Доступность 	= Истина;
				Элементы.ПредыдущиеОписание.Доступность = Ложь;
			ИначеЕсли ЭтаФорма.НомерОписание = КоличествоОписаний Тогда
				Элементы.СледующиеОписание.Доступность 	= Ложь;
				Элементы.ПредыдущиеОписание.Доступность = Истина;
			Иначе
				Элементы.СледующиеОписание.Доступность 	= Истина;
				Элементы.ПредыдущиеОписание.Доступность = Истина;
			КонецЕсли;
		Иначе
		
			Элементы.ГруппаПеремещениеОписаний.Видимость = Ложь;	
		
		КонецЕсли; 
	КонецЕсли;
	
	
КонецПроцедуры // ВывестиОписание() 

#КонецОбласти 

&НаСервере
Процедура ОбновитьКартинку()
	УстановитьПривилегированныйРежим(Истина);
	КлючКартинки = РегистрыСведений.КартинкиПредварительнойНоменклатуры.СоздатьКлючЗаписи(
		Новый Структура("ПредварительнаяНоменклатура,Номер", Объект.Ссылка,ЭтаФорма.НомерОписание));
	АдресКартинки = ПолучитьНавигационнуюСсылку(КлючКартинки, "Картинка");
КонецПроцедуры // ОбновитьКартинку()




