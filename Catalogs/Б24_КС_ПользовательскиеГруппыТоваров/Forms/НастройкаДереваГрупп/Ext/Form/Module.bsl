
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.ДеревоГрупп) Тогда
		
		НовыйОбъект = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.СоздатьЭлемент();
		НовыйОбъект.Наименование = Параметры.Наименование;
		НовыйОбъект.Записать();
		Инфоблок = НовыйОбъект.Ссылка;
		
	Иначе
		Инфоблок = Параметры.ДеревоГрупп;	
	КонецЕсли;
	
	
	ПользовательскоеДерево.Параметры.УстановитьЗначениеПараметра("НашаГруппа"					, Инфоблок);
	Номенклатура.Параметры.УстановитьЗначениеПараметра("НашаГруппа"								, Инфоблок);
	Номенклатура.Параметры.УстановитьЗначениеПараметра("НеОтображать"							, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
			
	Оповестить("ДеревоГрупп", Инфоблок);

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаДобавитьВДерево(Команда)
	
	Если Элементы.ПользовательскоеДерево.ТекущиеДанные = НеОпределено Тогда
		ПоказатьОповещениеПользователя("Строка дерева должна быть выбрана.");
		Возврат;
	КонецЕсли;
	
	лВыделенныеСтроки = Элементы.Номенклатура.ВыделенныеСтроки; 
	
	ДобавитьТоварВРазделСервер(Элементы.ПользовательскоеДерево.ТекущиеДанные.Ссылка, лВыделенныеСтроки);
	
	Элементы.ПользовательскоеДерево.Обновить();
	Элементы.Товары.Обновить();
	Элементы.Номенклатура.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьТоварВРазделСервер(Раздел, ВыделенныеТовары)
	
	РазделОбъект = Раздел.ПолучитьОбъект();
	
	Для Каждого ТекНоменклатура Из ВыделенныеТовары Цикл
	
		Если НЕ ЗначениеЗаполнено(ТекНоменклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НоменклатураОтбор.Количество() > 0 Тогда
			Если НоменклатураОтбор.НайтиПоЗначению(ТекНоменклатура) = НеОпределено Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Номенклатура """ + ТекНоменклатура + """ не соответствует отбору.");
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ РазделОбъект.Товары.Найти(ТекНоменклатура) = НеОпределено Тогда
			Продолжить;
		КонецЕсли;
		
		РазделОбъект.Товары.Добавить().Номенклатура = ТекНоменклатура;
		
	КонецЦикла;

	РазделОбъект.Записать();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура КомандаУдалитьИзДерева(Команда)
	
	Если Элементы.Товары.ТекущиеДанные = НеОпределено Тогда
		ПоказатьОповещениеПользователя("Удаляемый товар раздела должен быть выбран.");
		Возврат;
	КонецЕсли;
	
	ВыделенныеТовары = Элементы.Товары.ВыделенныеСтроки;	
	
	Для Каждого ТекИндекс Из ВыделенныеТовары Цикл
		
		лТовар = Элементы.Товары.ДанныеСтроки(ТекИндекс).Товар;
		
		УдалитьТоварИзРазделаСервер(Элементы.ПользовательскоеДерево.ТекущиеДанные.Ссылка, лТовар);
	
	КонецЦикла;
	
	Элементы.ПользовательскоеДерево.Обновить();
	Элементы.Товары.Обновить();
	Элементы.Номенклатура.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьТоварИзРазделаСервер(Раздел, пТовар)
	
	РазделОбъект = Раздел.ПолучитьОбъект();
	
	НайденнаяСтрока = РазделОбъект.Товары.Найти(пТовар);
	
	Если НайденнаяСтрока <> Неопределено Тогда
		
		РазделОбъект.Товары.Удалить(РазделОбъект.Товары.Индекс(НайденнаяСтрока));
		
	КонецЕсли;
	
	РазделОбъект.Записать();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

&НаСервере
Функция ПоказатьВхождениеВГруппыНаСервере(Товар)
	
	Результат = "";
	
	мНоменклатуры = Новый Массив;
	
	ДобавитьРодителяНоменклатурыРекурсивно(мНоменклатуры, Товар);	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_КС_ПользовательскиеГруппыТоваровТовары.Ссылка КАК Раздел
	|ИЗ
	|	Справочник.Б24_КС_ПользовательскиеГруппыТоваров.Товары КАК Б24_КС_ПользовательскиеГруппыТоваровТовары
	|ГДЕ
	|	Б24_КС_ПользовательскиеГруппыТоваровТовары.Номенклатура В(&СписокНоменклатуры)
	|	И Б24_КС_ПользовательскиеГруппыТоваровТовары.Ссылка В ИЕРАРХИИ(&Инфоблок)";
	
	Запрос.УстановитьПараметр("Инфоблок", Инфоблок);
	Запрос.УстановитьПараметр("СписокНоменклатуры", мНоменклатуры);
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	Пока Выборка.Следующий() Цикл
		
		Результат = Результат + Выборка.Раздел + Символы.ПС;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьВхождениеВГруппы(Команда)
	
	
	Если Элементы.Номенклатура.ТекущиеДанные = НеОпределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя("Номенклатура входит в разделы",,ПоказатьВхождениеВГруппыНаСервере(Элементы.Номенклатура.ТекущиеДанные.Ссылка));
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРодителяНоменклатурыРекурсивно(МассивНоменклатуры, Товар)
	
	МассивНоменклатуры.Добавить(Товар);
	
	Если ЗначениеЗаполнено(Товар.Родитель) Тогда
		ДобавитьРодителяНоменклатурыРекурсивно(МассивНоменклатуры, Товар.Родитель);	
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура КомандаЭкспортВФайл(Команда)
		
	СохранитьДерево(Истина);	

КонецПроцедуры

&НаКлиенте
Процедура СохранитьДерево(ВыгружатьНоменклатуру = Ложь)

	АдресДоДерева = СохранитьДеревоНаСервере(ВыгружатьНоменклатуру);
		
	ПолучитьФайл(АдресДоДерева,  Строка(Инфоблок)+ ".xml", Истина);
	
КонецПроцедуры

&НаСервере
Функция СохранитьДеревоНаСервере(ВыгружатьНоменклатуру)
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла();
	
	СтрокаXML = "";
	ПолучитьXMLСервер(СтрокаXML, ВыгружатьНоменклатуру);
	
	ФайлXML = Новый ТекстовыйДокумент;
	ФайлXML.УстановитьТекст(СтрокаXML);
	ФайлXML.Записать(ПутьКФайлу);
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ПутьКФайлу);
	
	УдалитьФайлы(ПутьКФайлу);
	  
	Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
КонецФункции

&НаСервере
Процедура ПолучитьXMLСервер(СтрокаXML, ВыгружатьНоменклатуру)

	ОбъектXML = Новый ЗаписьXML();
	ОбъектXML.УстановитьСтроку("UTF-8");
	ОбъектXML.ЗаписатьОбъявлениеXML();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_КС_ПользовательскиеГруппыТоваров.Ссылка КАК Ссылка,
	|	Б24_КС_ПользовательскиеГруппыТоваров.ПометкаУдаления КАК ПометкаУдаления,
	|	Б24_КС_ПользовательскиеГруппыТоваров.Родитель КАК Родитель,
	|	Б24_КС_ПользовательскиеГруппыТоваров.Родитель.ИдентификаторРаздела КАК ИдРодителя,
	|	Б24_КС_ПользовательскиеГруппыТоваров.Наименование КАК Наименование,
	|	Б24_КС_ПользовательскиеГруппыТоваров.ИдентификаторРаздела КАК ИдентификаторРаздела,
	|	Б24_КС_ПользовательскиеГруппыТоваров.Товары.(
	|		Номенклатура КАК Номенклатура
	|	) КАК Товары,
	|	Б24_КС_ПользовательскиеГруппыТоваров.Инфоблок КАК Инфоблок
	|ИЗ
	|	Справочник.Б24_КС_ПользовательскиеГруппыТоваров КАК Б24_КС_ПользовательскиеГруппыТоваров
	|ГДЕ
	|	Б24_КС_ПользовательскиеГруппыТоваров.Ссылка В ИЕРАРХИИ(&Инфоблок)
	|	И Б24_КС_ПользовательскиеГруппыТоваров.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Инфоблок",Инфоблок);
	
	Выборка	= Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ОбъектXML.ЗаписатьНачалоЭлемента("Разделы");
	
		Пока Выборка.Следующий() Цикл
			
			ОбъектXML.ЗаписатьНачалоЭлемента("Раздел");
			
				ОбъектXML.ЗаписатьНачалоЭлемента("Ид");
				ОбъектXML.ЗаписатьТекст(Выборка.ИдентификаторРаздела);
				ОбъектXML.ЗаписатьКонецЭлемента();
				
				ОбъектXML.ЗаписатьНачалоЭлемента("Наименование");
				ОбъектXML.ЗаписатьТекст(Выборка.Наименование); // По схеме
				ОбъектXML.ЗаписатьКонецЭлемента();
				
				ОбъектXML.ЗаписатьНачалоЭлемента("ЭтоИнфоблок");
				ОбъектXML.ЗаписатьТекст(XMLСтрока(Выборка.Инфоблок)); 
				ОбъектXML.ЗаписатьКонецЭлемента();
				
				ОбъектXML.ЗаписатьНачалоЭлемента("ИдРодителя");
				ОбъектXML.ЗаписатьТекст(XMLСтрока(Выборка.ИдРодителя)); 
				ОбъектXML.ЗаписатьКонецЭлемента();
				
				лТовары = Выборка.Товары.выгрузить();
				Если лТовары.Количество() > 0 И ВыгружатьНоменклатуру Тогда
					
					ОбъектXML.ЗаписатьНачалоЭлемента("Номенклатура");
			
						Для Каждого ТекЭлемент Из лТовары Цикл
							
							ТекНоменклатура = ТекЭлемент.Номенклатура;
							Если Не ЗначениеЗаполнено(ТекНоменклатура) Тогда
								Продолжить;
							КонецЕсли;
							
							ОбъектXML.ЗаписатьНачалоЭлемента("ЭлементНоменклатуры");
								ОбъектXML.ЗаписатьАтрибут("ЭтоГруппа"	, XMLСтрока(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекНоменклатура, "ЭтоГруппа"))); 
								ОбъектXML.ЗаписатьАтрибут("Наименование", XMLСтрока(Строка(ТекНоменклатура))); 
								ОбъектXML.ЗаписатьАтрибут("ВнешнийИд"	, XMLСтрока(ТекНоменклатура)); 
							ОбъектXML.ЗаписатьКонецЭлемента();
							
						КонецЦикла;
					ОбъектXML.ЗаписатьКонецЭлемента();
					
				КонецЕсли;
			ОбъектXML.ЗаписатьКонецЭлемента();
				
		КонецЦикла;
		
	ОбъектXML.ЗаписатьКонецЭлемента();
	
	СтрокаXML = ОбъектXML.Закрыть();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура КомандаИмпортИзФайла(Команда)
	
	Если Элементы.ПользовательскоеДерево.ТекущиеДанные <> Неопределено Тогда
	
		ПоказатьВопрос(Новый ОписаниеОповещения("ПослеЗакрытияВопросаОИмпортеДерева", ЭтаФорма, Параметры), "Дерево групп будет очищено и загружено Из файла. Продолжить?", РежимДиалогаВопрос.ДаНет);

	Иначе
		ЗагрузитьДерево();  	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОИмпортеДерева(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда      
		ЗагрузитьДерево();
    КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УдалитьИнформациюОИнфоблоке()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.Выбрать(Инфоблок);
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Ссылка <> Инфоблок Тогда
			Выборка.ПолучитьОбъект().Удалить();	
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДерево()
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл";
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Фильтр = "(*.xml)|*.xml";
	Диалог.ПроверятьСуществованиеФайла = Истина;
	
	ОбработкаОкончанияЗагрузки = Новый ОписаниеОповещения("Обработчик_Завершения_Загрузки", ЭтотОбъект, Диалог); 

	НачатьПомещениеФайла(ОбработкаОкончанияЗагрузки, , Диалог, Истина, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура Обработчик_Завершения_Загрузки(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		
		 Обработчик_Завершения_ЗагрузкиСервер(Адрес, ВыбранноеИмяФайла); 
		
		Элементы.ПользовательскоеДерево.Обновить();
		Элементы.Товары.Обновить();
		Элементы.Номенклатура.Обновить();
		
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка. Или файл не был указан или же проблемы в файле.");
	КонецЕсли 
	
КонецПроцедуры

&НаСервере
Процедура Обработчик_Завершения_ЗагрузкиСервер(Адрес, ИмяФайла = "") 
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьРасширениеФайлаПоНаименованию(ИмяФайла));
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(Адрес);	
	ДвоичныеДанныеФайла.Записать(ИмяВременногоФайла);
	
	ТекстXML = Новый ТекстовыйДокумент;
	ТекстXML.Прочитать(ИмяВременногоФайла);
	СтрокаXML = ТекстXML.ПолучитьТекст();
	
	РазобратьЗагрузитьXMLСервер(СтрокаXML);
	
	УдалитьФайлы(ИмяВременногоФайла);
	
КонецПроцедуры

&НаСервере
Процедура РазобратьЗагрузитьXMLСервер(СтрокаXML)

	ЧтениеXML = Новый ЧтениеXML;
	
	Попытка
		ЧтениеXML.УстановитьСтроку(СтрокаXML);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	ТзнДанных = Новый ТаблицаЗначений;
	ТзнДанных.Колонки.Добавить("Ид");	
	ТзнДанных.Колонки.Добавить("Наименование");	
	ТзнДанных.Колонки.Добавить("ИдРодителя");	
	ТзнДанных.Колонки.Добавить("ЭтоИнфоблок");	
	ТзнДанных.Колонки.Добавить("Товары");	
	
	лЭтоРазделы			= Ложь;
	лЭтоИд 				= Ложь;
	лЭтоИдРодителя		= Ложь;
	лЭтоНаименование	= Ложь;
	лЭтоИнфоблок		= Ложь;
	лЭтоНоменклатура	= Ложь;
   
    ИдИнфоБлока         = "";
    
	Пока ЧтениеXML.Прочитать() Цикл
			
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.ЛокальноеИмя = "Разделы" Тогда
			лЭтоРазделы = Истина;
		КонецЕсли;
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.ЛокальноеИмя = "Разделы" Тогда
			лЭтоРазделы = Ложь;
		КонецЕсли;
		
		Если лЭтоРазделы Тогда

			Если ЧтениеXML.ТипУзла 	= ТипУзлаXML.НачалоЭлемента И ЧтениеXML.ЛокальноеИмя = "Раздел" Тогда
				СтруктураСтр 	= Новый Структура("Ид, Наименование, ЭтоИнфоблок, ИдРодителя");
				мНоменклатура 	= Новый Массив;
			КонецЕсли;
			
				Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.ЛокальноеИмя = "Ид" Тогда
					лЭтоИд 				= Истина;
				ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.ЛокальноеИмя = "ИдРодителя" Тогда
					лЭтоИдРодителя 		= Истина;
				ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.ЛокальноеИмя = "Наименование" Тогда
					лЭтоНаименование 	= Истина;
				ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.ЛокальноеИмя = "ЭтоИнфоблок" Тогда
					лЭтоИнфоблок 		= Истина;
				ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.ЛокальноеИмя = "ЭлементНоменклатуры" Тогда
					лЭтоНоменклатура 	= Истина;
				КонецЕсли;
				   
				Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.ЛокальноеИмя = "Ид" Тогда
					лЭтоИд 				= Ложь;
				ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.ЛокальноеИмя = "ИдРодителя" Тогда
					лЭтоИдРодителя 		= Ложь;
				ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.ЛокальноеИмя = "Наименование" Тогда
					лЭтоНаименование 	= Ложь;
				ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.ЛокальноеИмя = "ЭтоИнфоблок" Тогда
					лЭтоИнфоблок 		= Ложь;
				ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.ЛокальноеИмя = "ЭлементНоменклатуры" Тогда
					лЭтоНоменклатура 	= Ложь;
				КонецЕсли;
				
				
				Если лЭтоНаименование И ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
					СтруктураСтр.Наименование = ЧтениеXML.Значение;
				КонецЕсли;
				
				Если лЭтоИд И ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
					СтруктураСтр.Ид = ЧтениеXML.Значение;
				КонецЕсли;
				
				Если лЭтоИдРодителя И ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
					СтруктураСтр.ИдРодителя = ЧтениеXML.Значение;
				КонецЕсли;
				
				Если лЭтоИнфоблок И ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
					СтруктураСтр.ЭтоИнфоблок = XMLЗначение(Тип("Булево") , ЧтениеXML.Значение);
				КонецЕсли;
				
				Если лЭтоНоменклатура Тогда
					
					ИнформацияТовара = Новый Структура;
					ИнформацияТовара.Вставить("ЭтоГруппа"		, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Булево"), ЧтениеXML.ПолучитьАтрибут("ЭтоГруппа")));
					ИнформацияТовара.Вставить("Наименование"	, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("Строка"), ЧтениеXML.ПолучитьАтрибут("Наименование")));
					ИнформацияТовара.Вставить("Номенклатура"	, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Номенклатура"), ЧтениеXML.ПолучитьАтрибут("ВнешнийИд")));
					
					мНоменклатура.Добавить(ИнформацияТовара);
					
				КонецЕсли;
				
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.ЛокальноеИмя = "Раздел" Тогда
				
				НовСтр = ТзнДанных.Добавить();
				НовСтр.Ид 			= СтруктураСтр.Ид;
				НовСтр.ИдРодителя 	= СтруктураСтр.ИдРодителя;
				НовСтр.Наименование = СтруктураСтр.Наименование;
				НовСтр.ЭтоИнфоблок 	= СтруктураСтр.ЭтоИнфоблок;
				НовСтр.Товары		= мНоменклатура;  
				
               Если НовСтр.ЭтоИнфоблок Тогда
                    ИдИнфоБлока = НовСтр.Ид;    
                КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	УдалитьИнформациюОИнфоблоке();
   
    ИдентификаторыЭлементов = Новый Соответствие;
    ИдентификаторыЭлементов.Вставить(ИдИнфоБлока, Инфоблок.Ссылка);
    
	ИдИмпортируемогоИнфоблока = "";
	Для каждого ТекСтрока Из ТзнДанных Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Ид) ИЛИ ТекСтрока.ЭтоИнфоблок = Истина Тогда
			
			лОбъект = Инфоблок.ПолучитьОбъект();
			ИдИмпортируемогоИнфоблока = ТекСтрока.Ид; 
			
			лОбъект.Товары.Очистить(); 			
			Для каждого ИнформацияОТоваре Из ТекСтрока.Товары Цикл
				
				НайденнаяНоменклатура = ОпределитьТоварПриИмпорте(ИнформацияОТоваре);
				Если ЗначениеЗаполнено(НайденнаяНоменклатура) Тогда
					лОбъект.Товары.Добавить().Номенклатура = НайденнаяНоменклатура;
				КонецЕсли;
				
			КонецЦикла;
			
			лОбъект.Записать();
			
		Иначе
			
            НайденнаяГруппа = ПолучитьПользовательскуюГруппуПоИД(ТекСтрока.Ид, Инфоблок);
            Если НайденнаяГруппа = Неопределено Тогда
                НовыйЭлемент = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.СоздатьЭлемент();
            Иначе
                НовыйЭлемент = НайденнаяГруппа.ПолучитьОбъект();
            КонецЕсли;
                        
            НовыйЭлемент.Наименование             = ТекСтрока.Наименование;
            НовыйЭлемент.ИдентификаторРаздела     = ТекСтрока.Ид;
            
            Если ИдИмпортируемогоИнфоблока = ТекСтрока.ИдРодителя ИЛИ НЕ ЗначениеЗаполнено(ТекСтрока.ИдРодителя) Тогда
                НовыйЭлемент.Родитель    = Инфоблок;
            Иначе                                                
                Родитель = ПолучитьПользовательскуюГруппуПоИД(ТекСтрока.ИдРодителя, Инфоблок);
                Если Родитель = Неопределено Тогда
                    СсылкаНаРодител = ИдентификаторыЭлементов.Получить(ТекСтрока.ИдРодителя);
                    Если СсылкаНаРодител = Неопределено Тогда
                        УИДРоидителя = Новый УникальныйИдентификатор;
                        СсылкаНаРодител = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.ПолучитьСсылку(УИДРоидителя);
                        ИдентификаторыЭлементов.Вставить(ТекСтрока.ИдРодителя, СсылкаНаРодител);    
                    КонецЕсли;
                    НовыйЭлемент.Родитель = СсылкаНаРодител;
                Иначе
                    НовыйЭлемент.Родитель = Родитель;    
                КонецЕсли;
            КонецЕсли;
			
			Для каждого ИнформацияОТоваре Из ТекСтрока.Товары Цикл
				
				НайденнаяНоменклатура = ОпределитьТоварПриИмпорте(ИнформацияОТоваре);
				Если ЗначениеЗаполнено(НайденнаяНоменклатура) Тогда
					НовыйЭлемент.Товары.Добавить().Номенклатура = НайденнаяНоменклатура; 
				КонецЕсли;
				
			КонецЦикла;
            
            СсылкаЭлемента = ИдентификаторыЭлементов.Получить(ТекСтрока.Ид);
            Если Не СсылкаЭлемента = Неопределено Тогда
                НовыйЭлемент.УстановитьСсылкуНового(СсылкаЭлемента);
            КонецЕсли;
            
            НовыйЭлемент.Записать();
            
            Если СсылкаЭлемента = Неопределено Тогда
                ИдентификаторыЭлементов.Вставить(ТекСтрока.Ид, НовыйЭлемент.Ссылка);        
			КонецЕсли; 

        КонецЕсли;
		
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ОпределитьТоварПриИмпорте(ИнформацияОТоваре)
	
	Результат = Справочники.Номенклатура.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(ИнформацияОТоваре.Номенклатура) Тогда
		Если Строка(ИнформацияОТоваре.Номенклатура) = ИнформацияОТоваре.Наименование Тогда
			Результат = ИнформацияОТоваре.Номенклатура;	
		Иначе 
			Результат = Справочники.Номенклатура.НайтиПоНаименованию(ИнформацияОТоваре.Наименование, Истина);
		КонецЕсли;
	Иначе
		Результат = Справочники.Номенклатура.НайтиПоНаименованию(ИнформацияОТоваре.Наименование, Истина);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

&НаСервереБезКонтекста
Функция ПолучитьПользовательскуюГруппуПоИД(Ид, Инфоблок)     
	
    Запрос = Новый Запрос();
    Запрос.Текст = "ВЫБРАТЬ
	|    Б24_КС_ПользовательскиеГруппыТоваров.Ссылка КАК Ссылка
	|ИЗ
	|    Справочник.Б24_КС_ПользовательскиеГруппыТоваров КАК Б24_КС_ПользовательскиеГруппыТоваров
	|ГДЕ
	|    Б24_КС_ПользовательскиеГруппыТоваров.Ссылка В ИЕРАРХИИ(&Инфоблок)
	|    И Б24_КС_ПользовательскиеГруппыТоваров.ИдентификаторРаздела = &Ид";
    Запрос.УстановитьПараметр("Инфоблок", Инфоблок);
    Запрос.УстановитьПараметр("Ид", Ид);

	Выборка = Запрос.Выполнить().Выбрать();
    Если Выборка.Следующий() Тогда
        Возврат Выборка.Ссылка
    Иначе
        Возврат Неопределено;    
	КонецЕсли;   
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура КомандаВывестиНоменклатуруВОтчет(Команда)
	
	ВременныйТабличныйДокумент = Новый ТабличныйДокумент;
	
	
	СформироватьОтчетПоНоменклатуре(ВременныйТабличныйДокумент);
	
	ВременныйТабличныйДокумент.Показать();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетПоНоменклатуре(ВременныйТабличныйДокумент)
	
	Макет = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.ПолучитьМакет("ОтчетПоНоменклатуреДерева");
	
	ОбластьШапка= Макет.ПолучитьОбласть("Шапка");                                                                                                                                                                                                                                                                                                                                                
	ВременныйТабличныйДокумент.Вывести(ОбластьШапка);
	
	ВременныйТабличныйДокумент.НачатьАвтогруппировкуСтрок();	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_КС_ПользовательскиеГруппыТоваров.Ссылка КАК Раздел
	|ИЗ
	|	Справочник.Б24_КС_ПользовательскиеГруппыТоваров КАК Б24_КС_ПользовательскиеГруппыТоваров
	|ГДЕ
	|	Б24_КС_ПользовательскиеГруппыТоваров.Ссылка В ИЕРАРХИИ(&Инфоблок)";
	Запрос.УстановитьПараметр("Инфоблок", Инфоблок);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьТела= Макет.ПолучитьОбласть("Тело");  
		
		ОбластьТела.Параметры.Группа = Строка(?(Выборка.Раздел = Инфоблок, "Корневой раздел", Выборка.Раздел));
		ВременныйТабличныйДокумент.Вывести(ОбластьТела, 0, "Разделы");
		
		ДобавитьНоменклатуруРазделаВОтчет(Макет, ВременныйТабличныйДокумент, Выборка.Раздел);
		
	КонецЦикла;
	ВременныйТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНоменклатуруРазделаВОтчет(Макет, ВременныйТабличныйДокумент, Раздел);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Номенклатура.Ссылка КАК Товар
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.Ссылка В ИЕРАРХИИ(&МассивТоваров)
	               |	И Номенклатура.ЭтоГруппа = ЛОЖЬ";
	Запрос.УстановитьПараметр("МассивТоваров", Раздел.Товары.ВыгрузитьКолонку("Номенклатура"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьТела= Макет.ПолучитьОбласть("Тело1");                                                                                                                                                                                                                                                                                                                                                
		ОбластьТела.Параметры.Номенклатура = Строка(Выборка.Товар);
		ВременныйТабличныйДокумент.Вывести(ОбластьТела, 1, "Товары", Ложь);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтмечатьВыделенныеТовары(Команда)
	
	Элементы.НоменклатураОтмечатьВыделенныеТовары.Пометка = НЕ Элементы.НоменклатураОтмечатьВыделенныеТовары.Пометка;
	
	Номенклатура.Параметры.УстановитьЗначениеПараметра("НеОтображать"	, НЕ Элементы.НоменклатураОтмечатьВыделенныеТовары.Пометка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскоеДеревоПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Если ЭтоПредопределенныйЭлемент(Элемент.ТекущиеДанные.Ссылка) Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭтоПредопределенныйЭлемент(пОбъект)
	
	Возврат пОбъект.Предопределенный;
	
КонецФункции

&НаКлиенте
Процедура Удалить(Команда)
	
	ВыбраннаяСтрока = Элементы.ПользовательскоеДерево.ТекущиеДанные;
	
	Если ВыбраннаяСтрока <> Неопределено Тогда
		УдалитьУзел(ВыбраннаяСтрока.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьУзел(Узел)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекУзел= Узел.ПолучитьОбъект();
	ТекУзел.Удалить();
	Элементы.ПользовательскоеДерево.Обновить();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

&НаСервере
Функция ПолучитьУникальныйИдентификатор(мИдентификаторы)
	
	лУникальныйИдентификатор = Новый УникальныйИдентификатор;
	лИдентификатор = Строка(лУникальныйИдентификатор);

	Если мИдентификаторы.Найти(лИдентификатор) = Неопределено Тогда
		мИдентификаторы.Добавить(лИдентификатор);
		Возврат лИдентификатор;
	Иначе
		Возврат ПолучитьУникальныйИдентификатор(мИдентификаторы);
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьИерархиюГрупп1С()
	
	УдалитьИнформациюОИнфоблоке();
	
	мИдентификаторы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Группа,
	|	Номенклатура.Родитель КАК Родитель,
	|	Номенклатура.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ЭтоГруппа = ИСТИНА";	
	тзнГрупп = Запрос.Выполнить().Выгрузить();
	тзнГрупп.Индексы.Добавить("Родитель");
	
	НайденныеСтроки = тзнГрупп.НайтиСтроки(Новый Структура("Родитель", Справочники.Номенклатура.ПустаяСсылка()));
	Для каждого ТекСтрока Из НайденныеСтроки Цикл
		
		НайденныеСтроки = тзнГрупп.НайтиСтроки(Новый Структура("Родитель", ТекСтрока.Группа));
		
		НовыйЭлемент = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.СоздатьЭлемент();
		НовыйЭлемент.Родитель				= Инфоблок;
		НовыйЭлемент.Наименование 			= ТекСтрока.Наименование;
		НовыйЭлемент.ИдентификаторРаздела 	= ПолучитьУникальныйИдентификатор(мИдентификаторы);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			НовыйЭлемент.Товары.Добавить().Номенклатура	= ТекСтрока.Группа;
		КонецЕсли;
		
		НовыйЭлемент.Записать();
		
		ЗагрузитьИерархиюГрупп1СРекурсивно(тзнГрупп, НайденныеСтроки, НовыйЭлемент.Ссылка, мИдентификаторы);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИерархиюГрупп1СРекурсивно(тзнГрупп, НайденныеСтроки, Владелец, мИдентификаторы)
	
	Для каждого ТекСтрока Из НайденныеСтроки Цикл
		
		НайденныеСтрокиРек = тзнГрупп.НайтиСтроки(Новый Структура("Родитель", ТекСтрока.Группа));
		
		НовыйЭлемент = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.СоздатьЭлемент();
		НовыйЭлемент.Родитель				= Владелец;
		НовыйЭлемент.Наименование 			= ТекСтрока.Наименование;
		НовыйЭлемент.ИдентификаторРаздела 	= ПолучитьУникальныйИдентификатор(мИдентификаторы);
		
		Если НайденныеСтрокиРек.Количество() = 0 Тогда
			НовыйЭлемент.Товары.Добавить().Номенклатура	= ТекСтрока.Группа;
		КонецЕсли;
		
		НовыйЭлемент.Записать();
		
		ЗагрузитьИерархиюГрупп1СРекурсивно(тзнГрупп, НайденныеСтрокиРек, НовыйЭлемент.Ссылка, мИдентификаторы);
		
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаИмпортИерархииГрупп1С(Команда)
	
	Если Элементы.ПользовательскоеДерево.ТекущиеДанные <> Неопределено Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ПослеЗакрытияВопросаОИмпортеИерархииГрупп1С", ЭтаФорма, Параметры), "Дерево групп будет очищено и загружено из 1С. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗагрузитьИерархиюГрупп1С();  	
	КонецЕсли;
	
	Элементы.ПользовательскоеДерево.Обновить();
	Элементы.Товары.Обновить();
	Элементы.Номенклатура.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОИмпортеИерархииГрупп1С(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда      
		ЗагрузитьИерархиюГрупп1С();
	КонецЕсли;
	
	Элементы.ПользовательскоеДерево.Обновить();
	Элементы.Товары.Обновить();
	Элементы.Номенклатура.Обновить();
	
КонецПроцедуры



&НаКлиенте
Процедура КомандаИмпортИерархииВидов1С(Команда)
	
	Если Элементы.ПользовательскоеДерево.ТекущиеДанные <> Неопределено Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ПослеЗакрытияВопросаОИмпортеИерархииВидов1С", ЭтаФорма, Параметры), "Дерево групп будет очищено и загружено из 1С. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗагрузитьИерархиюВидов1С();  	
	КонецЕсли;
	
	Элементы.ПользовательскоеДерево.Обновить();
	Элементы.Товары.Обновить();
	Элементы.Номенклатура.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОИмпортеИерархииВидов1С(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда      
		ЗагрузитьИерархиюВидов1С();
	КонецЕсли;
	
	Элементы.ПользовательскоеДерево.Обновить();
	Элементы.Товары.Обновить();
	Элементы.Номенклатура.Обновить();
	
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьИерархиюВидов1С()
	
	УдалитьИнформациюОИнфоблоке();
	
	мИдентификаторы = Новый Массив;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВидыКатегорииНоменклатуры.Ссылка КАК Группа,
	|	ВидыКатегорииНоменклатуры.Родитель КАК Родитель,
	|	ВидыКатегорииНоменклатуры.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыКатегорииНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
	|ПОМЕСТИТЬ ВТ_Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидНоменклатуры";	
	тзнГрупп = Запрос.Выполнить().Выгрузить();
	тзнГрупп.Индексы.Добавить("Родитель");
	
	ВидНоменклатуры = Справочники.ВидыНоменклатуры.ПустаяСсылка();
	НайденныеСтроки = тзнГрупп.НайтиСтроки(Новый Структура("Родитель", ВидНоменклатуры));
	Для каждого ТекСтрока Из НайденныеСтроки Цикл
		
		НовыйЭлемент = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.СоздатьЭлемент();
		НовыйЭлемент.Родитель				= Инфоблок;
		НовыйЭлемент.Наименование 			= ТекСтрока.Наименование;
		НовыйЭлемент.ИдентификаторРаздела 	= ПолучитьУникальныйИдентификатор(мИдентификаторы);
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ВидНоменклатуры", ТекСтрока.Группа);
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_Номенклатура.Ссылка КАК Ссылка
		|ИЗ
		|	ВТ_Номенклатура КАК ВТ_Номенклатура
		|ГДЕ
		|	ВТ_Номенклатура.ВидНоменклатуры = &ВидНоменклатуры";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйЭлемент.Товары.Добавить().Номенклатура	= Выборка.Ссылка;
		КонецЦикла;
		
		НовыйЭлемент.Записать();
		
		ЗагрузитьИерархиюВидов1СРекурсивно(МенеджерВременныхТаблиц, тзнГрупп, ТекСтрока.Группа, НовыйЭлемент.Ссылка, мИдентификаторы);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИерархиюВидов1СРекурсивно(МенеджерВременныхТаблиц, тзнГрупп, Категория, Владелец, мИдентификаторы)
	
	НайденныеСтроки = тзнГрупп.НайтиСтроки(Новый Структура("Родитель", Категория));
	Для каждого ТекСтрока Из НайденныеСтроки Цикл
		
		НовыйЭлемент = Справочники.Б24_КС_ПользовательскиеГруппыТоваров.СоздатьЭлемент();
		НовыйЭлемент.Родитель				= Владелец;
		НовыйЭлемент.Наименование 			= ТекСтрока.Наименование;
		НовыйЭлемент.ИдентификаторРаздела 	= ПолучитьУникальныйИдентификатор(мИдентификаторы);
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ВидНоменклатуры", ТекСтрока.Группа);
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_Номенклатура.Ссылка КАК Ссылка
		|ИЗ
		|	ВТ_Номенклатура КАК ВТ_Номенклатура
		|ГДЕ
		|	ВТ_Номенклатура.ВидНоменклатуры = &ВидНоменклатуры";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйЭлемент.Товары.Добавить().Номенклатура	= Выборка.Ссылка;
		КонецЦикла;
		
		НовыйЭлемент.Записать();
		
		ЗагрузитьИерархиюВидов1СРекурсивно(МенеджерВременныхТаблиц, тзнГрупп, ТекСтрока.Группа, НовыйЭлемент.Ссылка, мИдентификаторы);
		
	КонецЦикла;
	
КонецПроцедуры



#КонецОбласти
