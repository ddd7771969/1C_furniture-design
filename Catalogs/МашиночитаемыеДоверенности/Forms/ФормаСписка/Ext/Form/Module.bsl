///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
	
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	Если Параметры.МножественныйВыбор <> Неопределено Тогда
		Элементы.Список.МножественныйВыбор = Параметры.МножественныйВыбор;
	КонецЕсли;
	Если Параметры.РежимВыбора И Не ЗначениеЗаполнено(Параметры.КлючПользовательскихНастроек) Тогда
		Параметры.КлючПользовательскихНастроек = "РежимВыбора";
		Список.АвтоматическоеСохранениеПользовательскихНастроек = Ложь;
	КонецЕсли;
	
	КешироватьЗначенияСтатусов();
	Заголовок = НСтр("ru='Машиночитаемые доверенности'");
	АвторизованныйПользователь = Пользователи.АвторизованныйПользователь();
	ДобавитьПолеОтбора("Доверитель", "ТипДоверителя");
	ДобавитьПолеОтбора("Представитель", "ТипПредставителя");
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ДоверенностиПоРоли, ОтборПоСостоянию, ТолькоДействующие, Полномочия, СертификатПредставителя, СертификатДоверителя");
	ДоверенностиПоРоли = ?(ЗначениеЗаполнено(Параметры.ДоверенностиПоРоли), Параметры.ДоверенностиПоРоли, Элементы.ДоверенностиПоРоли.СписокВыбора[0].Значение);
	Доверитель = ?(ЗначениеЗаполнено(Параметры.Доверитель), Параметры.Доверитель, Доверитель);
	Представитель = ?(ЗначениеЗаполнено(Параметры.Представитель), Параметры.Представитель, Представитель);
	Полномочия = ?(ЗначениеЗаполнено(Параметры.Полномочия), Параметры.Полномочия, Полномочия);
	Сертификат = ?(ТипЗнч(СертификатПредставителя) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования"), СертификатПредставителя, Неопределено);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = КоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьОтбор();
	УстановитьПредставлениеПолномочий();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТолькоДействующиеПриИзменении(Элемент)
	УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПредставительПриИзменении(Элемент)
	УстановитьОтбор();
КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_ДоверительПриИзменении(Элемент)
	УстановитьОтбор();
КонецПроцедуры 

&НаКлиенте
Процедура СертификатПриИзменении(Элемент)
	СертификатПредставителя = Сертификат;
	УстановитьОтбор();
КонецПроцедуры   

&НаКлиенте
Процедура ДоверенностиПоРолиПриИзменении(Элемент)
	УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура ТипДоверителяПриИзменении(Элемент)
	УстановитьТип("Доверитель", "ТипДоверителя");
КонецПроцедуры

&НаКлиенте
Процедура ТипПредставителяПриИзменении(Элемент)
	УстановитьТип("Представитель", "ТипПредставителя");
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДоверительОчистка(СтандартнаяОбработка)
	ОчисткаОтбора("Доверитель", "ТипДоверителя");
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПредставительОчистка(СтандартнаяОбработка)
	ОчисткаОтбора("Представитель", "ТипПредставителя");
КонецПроцедуры

&НаКлиенте
Процедура ПолномочияПриИзменении(Элемент)
	УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура ПолномочияНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораПолномочий", ЭтотОбъект);
	
	СтруктураДанных = Новый Структура("ПолномочияВТекстовомВиде, ТекстПолномочий, Полномочия", Ложь);
	Если ТипЗнч(Полномочия) = Тип("СписокЗначений") Тогда
		МассивПолномочий = Полномочия.ВыгрузитьЗначения();
	Иначе
		МассивПолномочий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Полномочия);
	КонецЕсли;
	
	СтруктураДанных.Полномочия = ПоместитьВоВременноеХранилище(МассивПолномочий, УникальныйИдентификатор);
	
	ПараметрыОткрытия = Новый Структура("ТолькоПросмотр, ВыбранныеПолномочия, РежимКлассификатора", Ложь, СтруктураДанных, Истина);

	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенности.Форма.ФормаВводаПолномочий",
		ПараметрыОткрытия,
		ЭтотОбъект,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыгрузитьВФайл(Команда)
	МашиночитаемыеДоверенностиФНССлужебныйКлиент.ВыгрузитьДоверенностиВФайлы(Элементы.Список.ВыделенныеСтроки);
КонецПроцедуры  

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Загрузка доверенности из файла'");
	ПараметрыЗагрузки.Диалог.Фильтр = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
		"ru = 'Файлы доверенностей (%1)|%1'"), "*.xml;*.p7s;*.sig;");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗагрузкеФайлов", ЭтотОбъект);
	ФайловаяСистемаКлиент.ЗагрузитьФайлы(ОписаниеОповещения, ПараметрыЗагрузки);
	
КонецПроцедуры  

&НаКлиенте
Процедура ЗагрузитьИзРеестраФНС(Команда)
	ПослеЗагрузкиДоверенности = Новый ОписаниеОповещения("ПослеЗагрузкиДоверенности", ЭтотОбъект);
	МашиночитаемыеДоверенностиФНССлужебныйКлиент.ПолучитьДоверенностьИзРеестраФНС(ПослеЗагрузкиДоверенности);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусИзРеестраФНС(Команда)
	ДлительнаяОперация = ПолучитьСтатусНаСервере();
	Контекст = Новый Структура("ВыделенныеСтроки", Элементы.Список.ВыделенныеСтроки);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПолученияСтатуса", ЭтотОбъект, Контекст);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru=';Запрашивается статус для %1 документа;;Запрашивается статус для %1 документов;Запрашивается статус для %1 документов;|Запрашивается статус для %1 документов'"), 
		Контекст.ВыделенныеСтроки.Количество(),, "ЧДЦ=0");
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Обновление статусов из Реестра ФНС'"),
		,
		ТекстСообщения,
		БиблиотекаКартинок.Информация32,
		СтатусОповещенияПользователя.Информация);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура УстановитьОтбор()
	ТекущаяДатаСеанса = КонецДня(ОбщегоНазначенияКлиент.ДатаСеанса());
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Действует", Истина, ВидСравненияКомпоновкиДанных.Равно,,ТолькоДействующие);
	УстановитьОтборСоСтрокойПоиска(ЭтотОбъект.Доверитель, "Доверитель");
	УстановитьОтборСоСтрокойПоиска(ЭтотОбъект.Представитель, "Представитель");
	УстановитьОтборСоСтрокойПоиска(Полномочия, "Полномочия");
	УстановитьОтборПоРоли();
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекущаяДата", ТекущаяДатаСеанса);
	
	Если ОтборПоСостоянию = "СИстекающимСрокомДействия" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ДатаОкончания", ТекущаяДатаСеанса, ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Список.Отбор, "ДатаОкончания", ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, 
			МашиночитаемыеДоверенностиФНССлужебныйКлиентСервер.ДатаОкончанияПериодаИстекающихДоверенностей(ТекущаяДатаСеанса));
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Статус", ЭтотОбъект.СтатусМЧД_Действует, ВидСравненияКомпоновкиДанных.Равно);
		Заголовок = Заголовок+" "+НСтр("ru='(с истекающим сроком)'");
	ИначеЕсли ОтборПоСостоянию = "ТребуютВнимания" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ДатаОкончания", ТекущаяДатаСеанса, ВидСравненияКомпоновкиДанных.Больше);
		МассивСтатусов = Новый Массив;
		МассивСтатусов.Добавить(ЭтотОбъект.ТехническийСтатус_ОшибкаРегистрации);
		МассивСтатусов.Добавить(ЭтотОбъект.ТехническийСтатус_Регистрация);
		МассивСтатусов.Добавить(ЭтотОбъект.ТехническийСтатус_РегистрацияОтмены);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ТехническийСтатус", МассивСтатусов, ВидСравненияКомпоновкиДанных.ВСписке);
		Заголовок = Заголовок+" "+НСтр("ru='(требуют внимания)'");
	ИначеЕсли ОтборПоСостоянию = "ОжидаютПодписания" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ДатаОкончания", ТекущаяДатаСеанса, ВидСравненияКомпоновкиДанных.Больше);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Статус", ЭтотОбъект.СтатусМЧД_Черновик, ВидСравненияКомпоновкиДанных.Равно);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Подписана", Ложь, ВидСравненияКомпоновкиДанных.Равно);
		Заголовок = Заголовок+" "+НСтр("ru='(ожидают подписания)'");
	КонецЕсли;
	
	ВсеОтборыПоСертификатам = ВсеОтборыПоСертификатам();
		
	НастроитьОтборыСертификата(ВсеОтборыПоСертификатам, СертификатПредставителя, Истина);
	НастроитьОтборыСертификата(ВсеОтборыПоСертификатам, СертификатДоверителя, Ложь);
		
	Для Каждого ЭлементОтбора Из ВсеОтборыПоСертификатам Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, ЭлементОтбора.Ключ, ЭлементОтбора.Значение, ЗначениеЗаполнено(ЭлементОтбора.Значение));
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СрокИстекающихДоверенностей",
		МашиночитаемыеДоверенностиФНССлужебныйКлиентСервер.ДатаОкончанияПериодаИстекающихДоверенностей(ТекущаяДатаСеанса));
КонецПроцедуры    

&НаКлиенте
Процедура УстановитьОтборСоСтрокойПоиска(Значение, ИмяПоля) 
	Отбор = Новый Структура("Значение,СтрокаПоиска");
	Если ЗначениеЗаполнено(Значение) Тогда
		Если ТипЗнч(Значение) = Тип("Строка") Тогда
			Отбор.СтрокаПоиска = "%"+СформироватьСтрокуДляПоискаВЗапросе(Значение)+"%"; 
		Иначе
			Отбор.Значение = Значение;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ЭлементОтбора Из Отбор Цикл
		ИмяПоляОтбора = ?(ЭлементОтбора.Ключ = "Значение", ИмяПоля, ИмяПоля+ЭлементОтбора.Ключ);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, ИмяПоляОтбора, ЭлементОтбора.Значение, ЗначениеЗаполнено(ЭлементОтбора.Значение));
	КонецЦикла;
КонецПроцедуры          

&НаКлиенте
Процедура УстановитьОтборПоРоли()
	ИспользоватьОтборПоПредставителю = ДоверенностиПоРоли = "Представитель";
	ИспользоватьОтборПоДоверителю = ДоверенностиПоРоли = "Доверитель";
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ПредставительПользователь", АвторизованныйПользователь, ИспользоватьОтборПоПредставителю);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ДоверительПользователь", АвторизованныйПользователь, ИспользоватьОтборПоДоверителю);
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьСтрокуДляПоискаВЗапросе(Значение)
	Возврат ОбщегоНазначения.СформироватьСтрокуДляПоискаВЗапросе(Значение);
КонецФункции  

&НаКлиенте
Процедура ПослеЗагрузкиДоверенности(Результат, Контекст) Экспорт
	
	Если Результат <> Неопределено И ЗначениеЗаполнено(Результат.Доверенность) Тогда
		ПоказатьОповещениеПользователя(
		НСтр("ru = 'Загрузка завершена'"),
		ПолучитьНавигационнуюСсылку(Результат.Доверенность),
		Результат.Доверенность,
		БиблиотекаКартинок.Информация32,
		СтатусОповещенияПользователя.Информация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтатусНаСервере()
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "МашиночитаемыеДоверенностиФНССлужебный.ОбновитьИПрочитатьСтатусДоверенностей", Элементы.Список.ВыделенныеСтроки);
КонецФункции

&НаКлиенте
Процедура УстановитьТип(ИмяПоля, ИмяПоляТипа)
	ЭтотОбъект[ИмяПоля] = ЭтотОбъект[ИмяПоляТипа].ПривестиЗначение(ЭтотОбъект[ИмяПоля]);
КонецПроцедуры

&НаСервере
Процедура ДобавитьПолеОтбора(ИмяПоля, ИмяПоляТипа)
	ДобавляемыеРеквизиты = Новый Массив;
	РасширенноеОписаниеТипаСтороны = Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.СторонаМЧД.Тип);
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяПоля, РасширенноеОписаниеТипаСтороны));
	Для Каждого Тип Из РасширенноеОписаниеТипаСтороны.Типы() Цикл
		Элементы[ИмяПоляТипа].СписокВыбора.Добавить(Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип)), Строка(Тип));
	КонецЦикла;
	Элементы[ИмяПоляТипа].СписокВыбора.СортироватьПоПредставлению();
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ПолеДоверитель = Элементы.Добавить(ИмяПоля, Тип("ПолеФормы"), Элементы["Группа"+ИмяПоля]);
	ПолеДоверитель.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеДоверитель.ПутьКДанным = ИмяПоля;
	ПолеДоверитель.Вид = ВидПоляФормы.ПолеВвода;
	ПолеДоверитель.УстановитьДействие("ПриИзменении", "Подключаемый_"+ИмяПоля+"ПриИзменении");
	ПолеДоверитель.УстановитьДействие("Очистка", "Подключаемый_"+ИмяПоля+"Очистка");
	ПолеДоверитель.ВыбиратьТип = Ложь;
	ПолеДоверитель.КнопкаОчистки = Истина;
	ПолеДоверитель.Ширина = 25;
	ПолеДоверитель.ПодсказкаВвода = НСтр("ru='Наименование, ИНН, СНИЛС'");
	ОписаниеТипа = Элементы[ИмяПоляТипа].СписокВыбора[0].Значение;
	ЭтотОбъект[ИмяПоля] = ОписаниеТипа.ПривестиЗначение(ЭтотОбъект[ИмяПоля]);
	ЭтотОбъект[ИмяПоляТипа] = ОписаниеТипа;
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияСтатуса(Результат, Контекст) Экспорт
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	СостоянияДоверенностей = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	СОшибками = 0;
	
	Для Каждого СостояниеДоверенности Из СостоянияДоверенностей Цикл
		СОшибками = СОшибками + (СостояниеДоверенности.Значение.ДанныеОшибкиЗапросаСтатуса <> Неопределено);
		ОповеститьОбИзменении(СостояниеДоверенности.Ключ);
	КонецЦикла;
	
	Если СОшибками = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Успешно загружен статус для %1 доверенностей'"), СостоянияДоверенностей.Количество());
	Иначе
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Успешно: %1, с ошибками: %2'"), СостоянияДоверенностей.Количество() - СОшибками, СОшибками);
	КонецЕсли;
	Контекст = Новый Структура("Доверенности", СостоянияДоверенностей);
	
	ОткрытьСписокСобытий = Новый ОписаниеОповещения("ОткрытьСписокСобытий", ЭтотОбъект, Контекст);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Получение статуса завершено'"),
		ОткрытьСписокСобытий,
		ТекстСообщения,
		БиблиотекаКартинок.Информация32,
		СтатусОповещенияПользователя.Информация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗагрузкеФайлов(ЗагруженныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ЗагруженныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗагрузки = ЗагрузитьДоверенностиИзФайлов(ЗагруженныеФайлы);
	ЗагруженныеДоверенности = РезультатЗагрузки.Доверенности;
	Элементы.Список.ВыделенныеСтроки.Очистить();
	Для Каждого Ссылка Из ЗагруженныеДоверенности Цикл
		Элементы.Список.ВыделенныеСтроки.Добавить(Ссылка);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(РезультатЗагрузки.ТекстОшибки) Тогда
		ВызватьИсключение РезультатЗагрузки.ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьДоверенностиИзФайлов(ЗагруженныеФайлы)
	
	Возврат Справочники.МашиночитаемыеДоверенности.ЗагрузитьДоверенностиВИнформационнуюБазу(ЗагруженныеФайлы);
	
КонецФункции

&НаСервере
Процедура КешироватьЗначенияСтатусов()
	ДобавляемыеРеквизиты = Новый Массив;                       
	ТипСтатуса =  Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыМЧД");
	
	Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.СтатусыМЧД.ЗначенияПеречисления Цикл
		НовыйРеквизит = Новый РеквизитФормы("СтатусМЧД_"+ЗначениеПеречисления.Имя, ТипСтатуса);
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит); 
	КонецЦикла; 
	
	ТипСтатуса =  Новый ОписаниеТипов("ПеречислениеСсылка.ТехническиеСтатусыМЧД");
	
	Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.ТехническиеСтатусыМЧД.ЗначенияПеречисления Цикл
		НовыйРеквизит = Новый РеквизитФормы("ТехническийСтатус_"+ЗначениеПеречисления.Имя, ТипСтатуса);
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит); 
	КонецЦикла; 

	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ИндексЗначения = 0;
	Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.СтатусыМЧД.ЗначенияПеречисления Цикл
		ЭтотОбъект["СтатусМЧД_"+ЗначениеПеречисления.Имя] = Перечисления.СтатусыМЧД[ИндексЗначения];
		ИндексЗначения = ИндексЗначения + 1;
	КонецЦикла;
	
	ИндексЗначения = 0;
	Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.ТехническиеСтатусыМЧД.ЗначенияПеречисления Цикл
		ЭтотОбъект["ТехническийСтатус_"+ЗначениеПеречисления.Имя] = Перечисления.ТехническиеСтатусыМЧД[ИндексЗначения];
		ИндексЗначения = ИндексЗначения + 1;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОчисткаОтбора(ИмяПоля, ИмяПоляТипа)
	ОписаниеТипа = Элементы[ИмяПоляТипа].СписокВыбора[0].Значение;
	ЭтотОбъект[ИмяПоля] = ОписаниеТипа.ПривестиЗначение(Неопределено);
	ЭтотОбъект[ИмяПоляТипа] = ОписаниеТипа;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПолномочий(Результат, Контекст) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Полномочия = Новый СписокЗначений;
	Для Каждого Полномочие Из Результат.Полномочия Цикл
		Полномочия.Добавить(Полномочие.Код, Полномочие.Наименование);
	КонецЦикла;
	
	УстановитьПредставлениеПолномочий();
	УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокСобытий(Контекст) Экспорт
	ОткрытьФорму("РегистрСведений.МашиночитаемыеДоверенностиСтатусы.Форма.СписокРезультатов",Контекст,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПредставлениеПолномочий()
	Если ЗначениеЗаполнено(Полномочия) Тогда
		Если ТипЗНЧ(Полномочия) = Тип("Строка") Тогда
			КоличествоПолномочий = 1;
		Иначе
			КоличествоПолномочий = Полномочия.Количество();
		КонецЕсли;         
		ПолномочияПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Отбор по полномочиям (%1)'"), КоличествоПолномочий);
	Иначе
		ПолномочияПредставление = НСтр("ru='Любые полномочия'");
	КонецЕсли;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтборДляДоверенностейПоСертификату(Сертификат, Представитель)
	
	Если ТипЗнч(Сертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		Возврат МашиночитаемыеДоверенностиФНС.ОтборДляДоверенностейПоСертификату(Сертификат.ДанныеСертификата.Получить(), Представитель);
	Иначе
		Возврат МашиночитаемыеДоверенностиФНС.ОтборДляДоверенностейПоСертификату(Сертификат, Представитель);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ВсеОтборыПоСертификатам()
	Отбор = Новый Структура;
	КорниОтбора = СтрРазделить("Представитель,Доверитель", ",");
	СуффиксыОтбора = СтрРазделить("ОГРН,ИННФЛ,ИНН,СНИЛС", ",");
	Для Каждого КореньОтбора Из КорниОтбора Цикл
		Для Каждого СуффиксОтбора Из СуффиксыОтбора Цикл
			Отбор.Вставить(КореньОтбора + СуффиксОтбора);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Отбор;
КонецФункции

&НаКлиенте
Процедура НастроитьОтборыСертификата(ВсеОтборыПоСертификатам, СертификатОтбора, Представитель)
	Если ЗначениеЗаполнено(СертификатОтбора) Тогда

		Если ТипЗнч(СертификатОтбора) = Тип("Структура") Тогда
			Отборы = Новый Структура;
			ТипСтороны = ?(Представитель, "Представитель", "Доверитель");
			Для Каждого ЭлементОтбора Из СертификатОтбора Цикл
				Отборы.Вставить(ТипСтороны + ЭлементОтбора.Ключ, ЭлементОтбора.Значение);
			КонецЦикла;
		Иначе
			Отборы = ОтборДляДоверенностейПоСертификату(СертификатОтбора, Представитель);
		КонецЕсли;
		
		Для Каждого ЭлементОтбора Из Отборы Цикл
			Если ВсеОтборыПоСертификатам.Свойство(ЭлементОтбора.Ключ) Тогда
				ВсеОтборыПоСертификатам[ЭлементОтбора.Ключ] = ЭлементОтбора.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти