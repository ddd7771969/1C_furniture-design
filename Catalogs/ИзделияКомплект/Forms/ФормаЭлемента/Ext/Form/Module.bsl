#Область ОписаниеПеременных

&НаКлиенте
Перем НормаВремениПроизводстваИзменено, НормаВремениМонтажаИзменено, ПутьКФайлуПДМ, ПутьКФайлуТЗ;

&НаКлиенте
Перем НормаВремениПКДРКД, НачальныйКонструктор, НачальнаяСтадия, стПраваВыгрузкаВСРМ;

#КонецОбласти

#Область PDM

&НаСервереБезКонтекста
Процедура ПолучитьПользователяИзPDMНаСервере()
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьПользователяИзPDM(Команда)
	ПолучитьПользователяИзPDMНаСервере();
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьСписокВыбораОбъекта()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Проекты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Проекты КАК Проекты
	|ГДЕ
	|	Проекты.ПометкаУдаления = ЛОЖЬ
	|	И Проекты.ЭтоГруппа = ЛОЖЬ
	|	И НЕ ЕСТЬNULL(Проекты.СтатусОбъекта.Завершен, ЛОЖЬ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Проекты.Наименование";
	
	
	Элементы.Проект.СписокВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры // ЗаполнитьСпмсокВыбораОбъекта()

&НаКлиенте
Процедура ПослеВопросаОчисткаТЧ(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.ИзделияЭлемент.Очистить();
		ОткрытьФорму("Справочник.Проекты.ФормаВыбора",,ЭтотОбъект,,,,Новый ОписаниеОповещения("ПослеВыбораОбъекта", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораОбъекта(Результат, ДопПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	Объект.Проект = Результат;
	омИзделиеКомплектНаКлиентеСервере.СформироватьПолноеНаименование(Объект);

КонецПроцедуры // ОткрытьПодбор()

&НаКлиенте
Процедура ИзделияЗаказчикаИзделиеЗаказчикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		омИзделиеЭлементКлиент.ОткрытьПодборИзделийЭлемент(Объект
															,ЭтаФорма
															,СтандартнаяОбработка
															,Новый Структура("ТД", Элементы.ИзделияЭлемент.ТекущиеДанные));
КонецПроцедуры 

&НаКлиенте
Процедура ПослеВыбораИзделияЭлемент(Результат,ДопПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если ДопПараметры = Неопределено Тогда
		ТД = Объект.ИзделияЭлемент.Добавить(); 
	Иначе
		ТД = ДопПараметры.ТД;
	КонецЕсли;
	
	ТД.ИзделиеЭлемент = Результат; 
	
	Элементы.ИзделияЭлемент.Обновить();
КонецПроцедуры // ПослеВыбораИзделияЗаказчика()


#Область Команды

&НаКлиенте
Процедура ВыгрузкаВPDM(Команда)
	
	стВозврата = Новый Структура("ОписаниеОшибки,РезультатаВыгрузки","","");
	Если ЭтаФорма.Модифицированность Тогда
		ПоказатьПредупреждение(,"Перед выгрузкой комплект необходимо записать");	
	Иначе
		стКудаВыгружалось = Новый Структура("ВPDM,ВБитрикс",Ложь,Ложь);
		
		стВыгрузки = омВыгрузкаCRMНаСевере.ПроверитьВыгрузкуНаСервере(Объект.Ссылка, Истина);
		
		Если НЕ стВыгрузки.ВыгруженВPDM Тогда
			
			стВозврата = омВыгрузкаCRMНаКлиенте.ВыгрузитьВPDM(Объект.Ссылка);
			Если ПустаяСтрока(стВозврата.ОписаниеОшибки) Тогда
				стКудаВыгружалось.ВPDM = Истина;
				ПоказатьПредупреждение(, стВозврата.ВыгруженВPDM, 3, "Файл создан");
			Иначе
				ПоказатьПредупреждение(, стВозврата.ОписаниеОшибки, , "Ошибка");
			КонецЕсли;
			
		КонецЕсли; 
		
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПутьКФайлуПДМ = омФайлы.ПолучитьПутьКФайлуПДМ(Объект.Ссылка, Объект.Ссылка, Объект.Наименование);
	Если ПустаяСтрока(ПутьКФайлуПДМ) Тогда
		Элементы.ОткрытьФайлПДМ.Видимость = Ложь;
	Иначе
		
		Файл = Новый Файл(ПутьКФайлуПДМ);
		
		Элементы.ОткрытьФайлПДМ.Видимость = Файл.Существует();
		
	КонецЕсли;
	
	ПутьКФайлуТЗ = омФайлы.ПолучитьПутьКФайлуТЗ(Объект.Ссылка, Объект.Проект, Объект.Наименование);
	Если ПустаяСтрока(ПутьКФайлуТЗ) Тогда
		Элементы.ОткрытьТЗ.Видимость = Ложь;
	Иначе
		
		Файл = Новый Файл(ПутьКФайлуТЗ);
		
		Элементы.ОткрытьТЗ.Видимость = Файл.Существует();
		
	КонецЕсли;
	
	
	ДоступностьКнопок();
	
	НачальныйКонструктор 	= Конструктор;
	НачальнаяСтадия			= ТекущаяСтадия;
	
	ВидимостьКнопкиВыгрузка();	
	
	НормаВремениПроизводстваИзменено 	= Ложь;
	НормаВремениМонтажаИзменено 		= Ложь;
    НормаВремениПКДРКД					= Ложь;
	РассчетЗавершения();
	
	
	КонструкторПриИзменении(Неопределено);
КонецПроцедуры 

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	РассчетЗавершения();
	Если НормаВремениПроизводстваИзменено Тогда
	
		НормаВремениПроизводстваИзменено = Ложь;
		ЗаписатьНормаВремениПроизводства(НормаВремениПроизводства, Объект.Ссылка, Объект.Проект);
	
	КонецЕсли;
	Если НормаВремениМонтажаИзменено Тогда
	
		НормаВремениМонтажаИзменено = Ложь;
		ЗаписатьНормаВремениМонтажа(НормаВремениМонтажа, КоличествоСотрудниковМонтаж, Объект.Ссылка, Объект.Проект);
	
	КонецЕсли;
	
	Если НЕ (НачальныйКонструктор = Конструктор И НачальнаяСтадия = ТекущаяСтадия) Тогда
		ЗаписатьКонструктора(Объект.Ссылка, Конструктор, ТекущаяСтадия, НачальныйКонструктор = Конструктор);
		
		НачальныйКонструктор 	= Конструктор;
		НачальнаяСтадия 		= ТекущаяСтадия;
	КонецЕсли; 
	
	Если НормаВремениПКДРКД Тогда
		
		стДанные = Новый Структура("НормаВремени_ПКД, НормаВремени_РКД, ДнейПКД, ДнейРКД, ДнейТЗ, НачалоТЗ, ТипКомплекта, ГОСТ, КатегорияКонструктора", );
		ЗаполнитьЗначенияСвойств(стДанные, ЭтаФорма);
		
		стДатыОкончания = ИзменитьНормыВремениНаСервере(Объект.Ссылка, Объект.Проект, стДанные);
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, стДатыОкончания);
	
	КонецЕсли; 
	
	Оповестить("ИзделиеКомплектИзменено", Объект.Ссылка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменитьНормыВремениНаСервере(Комплект, Сделка, стДанные)

	МенеджерЗаписей 			= РегистрыСведений.ДатыКомплекта.СоздатьМенеджерЗаписи();
	МенеджерЗаписей.Комплект 	= Комплект;
	МенеджерЗаписей.Проект 		= Сделка;
	
	МенеджерЗаписей.Прочитать();
	
	ТребуетсяРасчетКраяПКД = Ложь;
	ТребуетсяРасчетКраяРКД = Ложь;
	ПустаяДата 			   = Дата(1, 1, 1);	
	
	Если ЗначениеЗаполнено(МенеджерЗаписей.Комплект) Тогда
		
		Если НЕ МенеджерЗаписей.ДнейПКД = стДанные.ДнейПКД Тогда
			
			МенеджерЗаписей.ДнейПКД = стДанные.ДнейПКД;
			ТребуетсяРасчетКраяПКД 	= Истина;	
		
		КонецЕсли;
		
		Если НЕ МенеджерЗаписей.ДнейРКД = стДанные.ДнейРКД Тогда
			
			МенеджерЗаписей.ДнейРКД = стДанные.ДнейРКД;
			ТребуетсяРасчетКраяРКД 	= Истина;	
		
		КонецЕсли;
		
		Если НЕ МенеджерЗаписей.ДнейТЗ = стДанные.ДнейТЗ Тогда
			
			МенеджерЗаписей.ДнейТЗ 	= стДанные.ДнейТЗ;
			ТребуетсяРасчетКраяТЗ 	= Истина;	

		КонецЕсли;
		
		Если НЕ МенеджерЗаписей.КрайТЗ = стДанные.НачалоТЗ Тогда
			
			МенеджерЗаписей.КрайТЗ 	= стДанные.НачалоТЗ;
			ТребуетсяРасчеетКраяТЗ 	= Истина;	
		
		КонецЕсли;
		
		Если НЕ МенеджерЗаписей.ТипКомплекта = стДанные.ТипКомплекта Тогда
			
			МенеджерЗаписей.ТипКомплекта = стДанные.ТипКомплекта;
			ТребуетсяРасчетКраяРКД 	= Истина;	
		
		КонецЕсли;
		
		Если НЕ МенеджерЗаписей.ГОСТ = стДанные.ГОСТ Тогда
			
			МенеджерЗаписей.ГОСТ 	= стДанные.ГОСТ;
			ТребуетсяРасчетКраяТЗ 	= Истина;	

		КонецЕсли;
		
		Если НЕ МенеджерЗаписей.КатегорияКонструктора = стДанные.КатегорияКонструктора Тогда
			
			МенеджерЗаписей.КатегорияКонструктора 	= стДанные.КатегорияКонструктора;
			ТребуетсяРасчеетКраяТЗ 	= Истина;	
		
		КонецЕсли;
		
	Иначе
		МенеджерЗаписей.Комплект 	= Комплект; 
		МенеджерЗаписей.Проект 		= Сделка; 
		
		МенеджерЗаписей.ДнейПКД 				= стДанные.ДнейПКД;
		МенеджерЗаписей.ДнейРКД 				= стДанные.ДнейРКД;
		МенеджерЗаписей.ДнейТЗ 					= стДанные.ДнейТЗ;
		МенеджерЗаписей.КрайТЗ 					= стДанные.НачалоТЗ;
		МенеджерЗаписей.КатегорияКонструктора 	= стДанные.КатегорияКонструктора;
		МенеджерЗаписей.ГОСТ 					= стДанные.ГОСТ;
		МенеджерЗаписей.ТипКомплекта 			= стДанные.ТипКомплекта;
		
		ТребуетсяРасчетКраяПКД 		= Истина;	
		ТребуетсяРасчетКраяРКД 		= Истина;
		ТребуетсяРасчетНачалоТЗ 	= Истина;
		
	КонецЕсли;
	
	МенеджерЗаписей.НормаВремени_ПКД = стДанные.НормаВремени_ПКД;
	МенеджерЗаписей.НормаВремени_РКД = стДанные.НормаВремени_РКД;
	
	Если ТребуетсяРасчетКраяПКД И НЕ МенеджерЗаписей.НачалоПКД = ПустаяДата Тогда
		МенеджерЗаписей.КрайПКД	= омДаты.ДобавитьДниСУчетомВыходных(МенеджерЗаписей.НачалоПКД, стДанные.ДнейПКД);
	КонецЕсли;
	
	Если ТребуетсяРасчетКраяРКД И НЕ МенеджерЗаписей.НачалоРКД = ПустаяДата Тогда
		МенеджерЗаписей.КрайРКД	= омДаты.ДобавитьДниСУчетомВыходных(МенеджерЗаписей.НачалоРКД, стДанные.ДнейРКД);
	КонецЕсли;
	
	Если ТребуетсяРасчеетКраяТЗ И НЕ МенеджерЗаписей.КрайТЗ = ПустаяДата Тогда
		МенеджерЗаписей.КрайТЗ = омДаты.ДобавитьДниСУчетомВыходных(МенеджерЗаписей.НачалоТЗ, стДанные.ДнейТЗ);
	КонецЕсли;
	
	МенеджерЗаписей.Записать();
	
	стВозврата = Новый Структура("КрайРКД, КрайПКД, КрайТЗ", );
	
	ЗаполнитьЗначенияСвойств(стВозврата, МенеджерЗаписей);
	
    Возврат стВозврата;
КонецФункции // ИзменитьНормыВремениНаСервере()


&НаСервереБезКонтекста
Процедура ЗаписатьКонструктора(Комплект, Конструктор, ТекущаяСтадия, КонструкторНеИзменен)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеКомплектаСрезПоследних.Конструктор КАК Конструктор,
		|	ДанныеКомплектаСрезПоследних.ТекущаяСтадия КАК ТекущаяСтадия
		|ИЗ
		|	РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект = &Комплект) КАК ДанныеКомплектаСрезПоследних";
	
	Запрос.УстановитьПараметр("Комплект", Комплект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Конструктор = Конструктор И Выборка.ТекущаяСтадия = ТекущаяСтадия Тогда
			Возврат;
		КонецЕсли;
		
	КонецЦикла; 
	
	стПараметры = Новый Структура("Комплект, Конструктор, ТекущаяСтадия", Комплект, Конструктор, ТекущаяСтадия);
	
    РегистрыСведений.ДанныеКомплекта.ИзменитьДанныеКомплекта(стПараметры);
	
	Если НЕ КонструкторНеИзменен Тогда
		
		омКонструктор.ИзменитьИсполнителяВЗадаче(Комплект, Конструктор);	

	КонецЕсли;
	
КонецПроцедуры // ЗаписатьКонструктора(Комплект, Конструктор)

Процедура ВидимостьКнопкиЗагрузитьТЗ(Знач текТекущаяСтадия)

	СтадияПодтвержденияТЗ = омНастройкаПовтор.ПолучитьСтадиюПодтвержденияТЗ();
	
	ВидимостьКнопки = Ложь;
	
	Если НЕ текТекущаяСтадия = СтадияПодтвержденияТЗ Тогда
		
		СтадияТЗ = омНастройкаПовтор.ПолучитьСтадиюТЗ();
		
		
		Если текТекущаяСтадия = СтадияТЗ Тогда
			ВидимостьКнопки = Истина;
		Иначе
			Пока ЗначениеЗаполнено(текТекущаяСтадия.Родитель) Цикл
				текТекущаяСтадия = текТекущаяСтадия.Родитель;	
				Если текТекущаяСтадия = СтадияТЗ Тогда
					ВидимостьКнопки = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ФормаЗагрузитьТЗ.Видимость = ВидимостьКнопки;
	
КонецПроцедуры


Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ Параметры.Ключ.Пустая() Тогда
		
		СрокиСделки = Документы.ЗапускВРаботу.КомплектВСрокахСделки(Объект.Ссылка);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДанныеКомплекта.Конструктор КАК Конструктор,
			|	ДанныеКомплекта.ТекущаяСтадия КАК ТекущаяСтадия
			|ИЗ
			|	РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект = &Комплект) КАК ДанныеКомплекта";
		
		Запрос.УстановитьПараметр("Комплект", Объект.Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Конструктор 	= Выборка.Конструктор;
			ТекущаяСтадия 	= Выборка.ТекущаяСтадия;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущаяСтадия) И (РольДоступна("ПолныеПрава") ИЛИ РольДоступна("Дизайнер")) Тогда
		ВидимостьКнопкиЗагрузитьТЗ(ТекущаяСтадия); 
	Иначе
		Элементы.ФормаЗагрузитьТЗ.Видимость = Ложь;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораОбъекта();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДатыКомплекта.КрайПКД КАК КрайПКД,
	|	ДатыКомплекта.КрайРКД КАК КрайРКД,
	|	ДатыКомплекта.КрайТЗ КАК КрайТЗ,
	|	ДатыКомплекта.НормаВремени_ПКД КАК НормаВремени_ПКД,
	|	ДатыКомплекта.НормаВремени_РКД КАК НормаВремени_РКД,
	|	ДатыКомплекта.Код_в_PDM КАК Код_в_PDM,
	|	ДатыКомплекта.ДнейПКД КАК ДнейПКД,
	|	ДатыКомплекта.ДнейРКД КАК ДнейРКД,
	|	ДатыКомплекта.НачалоПКД КАК НачалоПКД,
	|	ДатыКомплекта.НачалоРКД КАК НачалоРКД,
	|	ДатыКомплекта.ДнейТЗ КАК ДнейТЗ,
	|	ДатыКомплекта.НачалоТЗ КАК НачалоТЗ,
	|	ДатыКомплекта.ГОСТ КАК ГОСТ,
	|	ДатыКомплекта.КатегорияКонструктора КАК КатегорияКонструктора,
	|	ДатыКомплекта.ТипКомплекта КАК ТипКомплекта
	|ИЗ
	|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
	|ГДЕ
	|	ДатыКомплекта.Комплект = &Комплект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыКомплектаПроизводство.НормаВремениПроизводства КАК НормаВремениПроизводства
	|ИЗ
	|	РегистрСведений.ДатыКомплектаПроизводство КАК ДатыКомплектаПроизводство
	|ГДЕ
	|	ДатыКомплектаПроизводство.Комплект = &Комплект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыКомплектаМонтаж.НормаВремениМонтажа КАК НормаВремениМонтажа,
	|	ДатыКомплектаМонтаж.КоличествоСотрудниковМонтаж КАК КоличествоСотрудниковМонтаж
	|ИЗ
	|	РегистрСведений.ДатыКомплектаМонтаж КАК ДатыКомплектаМонтаж
	|ГДЕ
	|	ДатыКомплектаМонтаж.Комплект = &Комплект";
	
	Запрос.УстановитьПараметр("Комплект", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса[0].Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса[1].Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса[2].Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктуальныеИзделияКомплектыЭлементы.ПриложениеКДоговору КАК ПриложениеКДоговору
		|ИЗ
		|	РегистрСведений.АктуальныеИзделияКомплектыЭлементы КАК АктуальныеИзделияКомплектыЭлементы
		|ГДЕ
		|	АктуальныеИзделияКомплектыЭлементы.ИзделиеКомплектЭлемент = &ИзделиеКомплектЭлемент";
	
	Запрос.УстановитьПараметр("ИзделиеКомплектЭлемент", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ПриложениеКДоговору = Выборка.ПриложениеКДоговору;
		Актуальность = Истина;
		Элементы.Актуальность.Видимость = Истина;
	КонецЦикла; 
	
	ЗаполнитьКомпозиции();
	
	ПолучитьКодБитрикса();
	
	Если НЕ РольДоступна("ПолныеПрава") Тогда
	
		Элементы.НачалоТЗ.ТолькоПросмотр = Истина;	
		Элементы.ДнейТЗ.ТолькоПросмотр = Истина;	
	
	КонецЕсли;
	
	Элементы.Конструктор.ТолькоПросмотр	= ЗначениеЗаполнено(омКонструктор.ВозможностьИзменитьКонструктора(Объект.Ссылка));
	
	
	
КонецПроцедуры    

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	Отказ = Ложь;
	Б24_СМ_РегистрацияИзмененийСервер.ЗарегистрироватьИзменения(ТекущийОбъект, Ложь, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораГабаритногоЧертежа(Результат, ДополнительныеПараметры) Экспорт

	Если ЗначениеЗаполнено(Результат) Тогда
	
		Объект.ГабаритныйЧертеж = Результат;	
	
	КонецЕсли;	

КонецПроцедуры // ПослеВыбораГабаритногоЧертежа()


#КонецОбласти

&НаСервереБезКонтекста
Процедура ЗаписатьНормаВремениПроизводства(НормаВремениПроизводства, Ссылка, Сделка)

	МенеджерЗаписи = РегистрыСведений.ДатыКомплектаПроизводство.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Комплект = Ссылка;
	МенеджерЗаписи.Проект = Сделка;
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.Комплект = Ссылка;
	МенеджерЗаписи.Проект = Сделка;
	МенеджерЗаписи.НормаВремениПроизводства = НормаВремениПроизводства;
	МенеджерЗаписи.Записать();

КонецПроцедуры // ЗаписатьНормаВремениПроизводства(НормаВремениПроизводства, Ссылка)

&НаСервереБезКонтекста
Процедура ЗаписатьНормаВремениМонтажа(НормаВремениМонтажа, КоличествоСотрудниковМонтаж, Ссылка, Сделка)

	МенеджерЗаписи = РегистрыСведений.ДатыКомплектаМонтаж.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Комплект = Ссылка;
	МенеджерЗаписи.Проект 	= Сделка;
	
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Комплект 					= Ссылка;
	МенеджерЗаписи.Проект 						= Сделка;
	МенеджерЗаписи.НормаВремениМонтажа 			= НормаВремениМонтажа;
	МенеджерЗаписи.КоличествоСотрудниковМонтаж 	= КоличествоСотрудниковМонтаж;
	
	МенеджерЗаписи.Записать();

КонецПроцедуры // ЗаписатьНормаВремениПроизводства(НормаВремениПроизводства, Ссылка)

Функция ПолучитьКодБитрикса()
	
	КодВыгрузкиВБитрикс = "";
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат "";
	КонецЕсли;
	
	КодВыгрузкиВБитрикс = омВыгрузкаCRMНаСевере.ПолучитьКодБитрикс(Объект.Ссылка);
	
КонецФункции // ПолучитьКодБитрикса()

&НаКлиенте
Процедура ДоступностьКнопок()
	
	стПраваВыгрузкаВСРМ = ДоступностьРолей(); 
	
	Если стПраваВыгрузкаВСРМ.ВPDMДоступна Тогда
		
		стВыгрузки = омВыгрузкаCRMНаСевере.ПроверитьВыгрузкуНаСервере(Объект.Ссылка, Истина);
		стПраваВыгрузкаВСРМ.ВPDMДоступна = НЕ стВыгрузки.ВыгруженВPDM;
		
	Иначе
		Элементы.ФормаВыгрузкаВCRM.Видимость = Ложь;	
	КонецЕсли;
	
	Если НЕ (стПраваВыгрузкаВСРМ.Дизайнер ИЛИ стПраваВыгрузкаВСРМ.ГлавныйКонструктор) Тогда
		Элементы.ФормаОткрытьТЗ.Видимость 	= Ложь;
	КонецЕсли;

КонецПроцедуры // НаименованиеКнопкиВыгрузкаПоПравам()

Функция ДоступностьРолей()
	
	стПраваВыгрузкаВСРМ = Новый Структура("ВPDMДоступна, Дизайнер, ГлавныйКонструктор, ПолныеПрава", );
	
	стПраваВыгрузкаВСРМ.ВPDMДоступна 		= РольДоступна("Выгрузка_в_PDM");
	стПраваВыгрузкаВСРМ.Дизайнер 			= РольДоступна("Дизайнер");
	стПраваВыгрузкаВСРМ.ГлавныйКонструктор 	= РольДоступна("ГлавныйКонструктор");
	стПраваВыгрузкаВСРМ.ПолныеПрава 		= РольДоступна("ПолныеПрава");
	
	Возврат стПраваВыгрузкаВСРМ;
	
КонецФункции // ДоступностьРолей()

Процедура ЗаполнитьДатыКомплекта()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплекта.КрайПКД КАК КрайПКД,
		|	ДатыКомплекта.КрайРКД КАК КрайРКД,
		|	ДатыКомплекта.КрайТЗ КАК КрайТЗ
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|ГДЕ
		|	ДатыКомплекта.Комплект = &Комплект";
	
	Запрос.УстановитьПараметр("Комплект", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Выборка);
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьДатыКомплекта()

#Область СобытиеЭлеметовФормы

&НаКлиенте
Процедура ГабаритныйЧертежНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Проект) Тогда
	
		ПоказатьПредупреждение(,"Не заполнена Проект");
		Возврат;
	
	КонецЕсли;
	
	стПараметры = Новый Структура("Проект", Объект.Проект);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораГабаритногоЧертежа", ЭтаФорма);
	
	ОткрытьФорму("Справочник.ГабаритныеЧертежи.ФормаВыбора", стПараметры, , , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Объект.ИзделияЭлемент.Количество() > 0 Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеВопросаОчисткаТЧ",ЭтаФорма);
		ПоказатьВопрос(Оповещение,"При смене объекта будет очищена табличная часть. Продолжить?",РежимДиалогаВопрос.ДаНет,,,"Очистить ТЧ");
	Иначе
		ПослеВопросаОчисткаТЧ(КодВозвратаДиалога.Да,Неопределено);
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура НаименованиеКомплектаПриИзменении(Элемент)
	Объект.НаименованиеКомплекта = омСтроки.ПолучитьРазрешенноеИмяРеквизитаПоСтроке(Объект.НаименованиеКомплекта);
	омИзделиеКомплектНаКлиентеСервере.СформироватьПолноеНаименование(Объект);
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеПланПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеПланПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораИзделияЗаказчика(Результат,ДопПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ТД = Элементы.ИзделияЭлемент.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТД.ИзделиеЭлемент = Результат; 
	
	Элементы.ИзделияЭлемент.Обновить();
КонецПроцедуры // ПослеВыбораИзделияЗаказчика()

&НаКлиенте
Процедура ИзделияЗаказчикаПриИзменении(Элемент)
	//ПоискаМаксМинДаты();
	УстановкаОсновногоИзделия();	
КонецПроцедуры

&НаКлиенте
Процедура НормаВремени_ПКДПриИзменении(Элемент)
	НормаВремениПКДРКД = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НормаВремени_РКДПриИзменении(Элемент)
	НормаВремениПКДРКД = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НормаВремениПроизводстваПриИзменении(Элемент)
	НормаВремениПроизводстваИзменено = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НормаВремениМонтажаПриИзменении(Элемент)
	НормаВремениМонтажаИзменено = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДнейПКДПриИзменении(Элемент)
	НормаВремениПКДРКД = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДнейРКДПриИзменении(Элемент)
	НормаВремениПКДРКД = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КрайТЗПриИзменении(Элемент)
	НормаВремениПКДРКД = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДнейТЗПриИзменении(Элемент)
	НормаВремениПКДРКД = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КатегорияКонструктораПриИзменении(Элемент)
	НормаВремениПКДРКД = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ГОСТПриИзменении(Элемент)
	НормаВремениПКДРКД = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТипКомплекта1ПриИзменении(Элемент)
	НормаВремениПКДРКД = Истина;
КонецПроцедуры

#КонецОбласти 

&НаСервереБезКонтекста
Функция ПолучитьСмещениеДаты(текДата, Смещение)

	Возврат омДаты.ДобавитьДниСУчетомВыходных(текДата, Смещение);	

КонецФункции // ПолучитьСмещениеДаты()

#Область Расписание

&НаКлиенте
Процедура ПоискаМаксМинДаты()
	//МинДата 				= Дата(2999,12,31);
	//Объект.ДатаОкончания 	= Дата(1,1,1); 
	//
	//Для каждого х Из Объект.Стадии Цикл
	//	Если х.ДатаОкончания > Объект.ДатаОкончания Тогда
	//		Объект.ДатаОкончания = х.ДатаОкончания; 
	//	КонецЕсли;
	//	
	//	Если х.ДатаНачала < МинДата Тогда
	//		МинДата = х.ДатаНачала; 
	//	КонецЕсли;
	//КонецЦикла;
	//
	//Если МинДата = Дата(2999,12,31) Тогда
	//	Объект.ДатаНачала = Дата(1,1,1);
	//Иначе
	//	Объект.ДатаНачала = МинДата;
	//КонецЕсли;
КонецПроцедуры // ПоискаМаксМинДаты()

&НаСервереБезКонтекста
Функция ЗаполнитьПродолжительностьНаСервере(мИзделияЗаказчика)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИзделияЗаказчикаРасписаниеПлан.Этап КАК Этап,
	|	СУММА(ИзделияЗаказчикаРасписаниеПлан.ПродолжительностьЭтапа) КАК ПродолжительностьЭтапа
	|ИЗ
	|	Справочник.ИзделияЭлемент.РасписаниеПлан КАК ИзделияЗаказчикаРасписаниеПлан
	|ГДЕ
	|	ИзделияЗаказчикаРасписаниеПлан.Ссылка В(&мИзделияЗаказчика)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзделияЗаказчикаРасписаниеПлан.Этап";
	
	Запрос.УстановитьПараметр("мИзделияЗаказчика", мИзделияЗаказчика);
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПродолжительность(Команда)
	
	ПустаяДата = Дата(1,1,1);
	
	мИзделияЗаказчика = Новый Массив;
	
	Для каждого текИзделие Из Объект.ИзделияЭлемент Цикл
		мИзделияЗаказчика.Добавить(текИзделие.ИзделиеЭлемент);
	КонецЦикла;
	
	Для каждого текИзделие Из Объект.РасписаниеПлан Цикл
		текИзделие.ПродолжительностьЭтапа 	= 0;
		текИзделие.ДатаОкончания 			= ПустаяДата;
		текИзделие.ДатаНачала 				= ПустаяДата;
	КонецЦикла;
	
	
	мДанныеЭтпаов = ЗаполнитьПродолжительностьНаСервере(мИзделияЗаказчика); 
	стОтбор = Новый Структура("Этап", );
	
	Для каждого текДанныеЭтапа Из мДанныеЭтпаов Цикл 
		стОтбор.Этап = текДанныеЭтапа.Этап;
		НайденыеСтроки = Объект.РасписаниеПлан.НайтиСтроки(стОтбор);
		Для каждого текНайденаяСтрока Из НайденыеСтроки Цикл
			текНайденаяСтрока.ПродолжительностьЭтапа = текДанныеЭтапа.ПродолжительностьЭтапа;	
		КонецЦикла;
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти  

#Область СобытиеТЧ

&НаКлиенте
Процедура ИзделияЗаказчикаОсновноеПриИзменении(Элемент)
	УстановкаОсновногоИзделия();	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводствоНачинатьЧерезПриИзменении(Элемент)
	ТД = Элементы.Производство.ТекущиеДанные;
	РассчетЗавершения(ТД);
	
	тзВременная = Объект.Производство;
	СортировкаНаСервере(тзВременная);
   	Объект.Производство.Очистить();
	
	Для каждого х Из тзВременная Цикл
	
		ЗаполнитьЗначенияСвойств(Объект.Производство.Добавить(), х);	
	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПроизводствоНормаВремениПриИзменении(Элемент)
	ТД = Элементы.Производство.ТекущиеДанные;
	РассчетЗавершения(ТД);	
КонецПроцедуры


#КонецОбласти 

#Область РасчетТЧ

&НаКлиенте
Процедура РассчетЗавершения(ТД = Неопределено)
	Если ТД = Неопределено Тогда
		Для каждого х Из Объект.Производство Цикл
			х.ЗавершениеЧерез = х.НачинатьЧерез + х.НормаВремени;	
		КонецЦикла;
	Иначе
		ТД.ЗавершениеЧерез = ТД.НачинатьЧерез + ТД.НормаВремени;	
	КонецЕсли;
КонецПроцедуры // РассчетЗавершения()

&НаСервереБезКонтекста
Процедура СортировкаНаСервере(тзПроизводство)
	тзПроизводство.Сортировать("НачинатьЧерез");	
КонецПроцедуры // СортировкаНаСервере(Производство)


#КонецОбласти

#Область ВыгрузкаВCRM

Функция ВыгрузитьНаСервереВБитрикс(ИзделиеКомплект, стКудаВыгружалось, стВыгрузки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	текОбъект = РеквизитФормыВЗначение("Объект");
	
	Отказ = Ложь;
	Б24_СМ_РегистрацияИзмененийСервер.ЗарегистрироватьИзменения(текОбъект, Ложь, Отказ);
	
КонецФункции  

&НаКлиенте
Процедура ВидимостьКнопкиВыгрузка()
	//ВБитрикс = Ложь;

	//Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
	//	ВидимостьКнопки = Ложь;	
	//ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Проект) Тогда
	//	ВидимостьКнопки = Ложь;	
	//Иначе
	//	ВидимостьКнопки = НЕ ПроверитьВыгрузкуНаСервере(Объект.Ссылка, ВБитрикс);
	//КонецЕсли;
	//
	ВыгруженВБитрикс = Истина; 
КонецПроцедуры // ВидимостьКнопкиВыгрузка()

&НаСервереБезКонтекста
Функция ПроверитьВыгрузкуНаСервере(ИзделиеКомплект, ВБитрикс)

	Возврат омВыгрузкаCRMНаСевере.ПроверитьВыгрузкуНаСервере(ИзделиеКомплект,,ВБитрикс);	

КонецФункции // ПроверитьВыгрузкуНаСервере()

#КонецОбласти

&НаКлиенте
Процедура УстановкаОсновногоИзделия()
	Если Объект.ИзделияЭлемент.Количество() = 1 Тогда 
		Объект.ИзделияЭлемент[0].Основное = Истина;	
	ИначеЕсли Объект.ИзделияЭлемент.Количество() > 1 Тогда
		ТД = Элементы.ИзделияЭлемент.ТекущиеДанные;
		Если НЕ ТД = Неопределено Тогда
			Если ТД.Основное Тогда
				текНомерСтроки = ТД.НомерСтроки;
				Для каждого х Из Объект.ИзделияЭлемент Цикл
					Если х.НомерСтроки = текНомерСтроки Тогда
						Продолжить;
					КонецЕсли;
					х.Основное = Ложь;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	омИзделиеКомплектНаКлиентеСервере.СформироватьПолноеНаименование(Объект);
КонецПроцедуры // УстановкаОсновногоИзделия()

&НаКлиенте
Процедура РасписаниеПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ЗаписьИнтерактивная",Истина);
КонецПроцедуры

&НаСервереБезКонтекста
Функция МатериалыКомплектаНаСервере(Таб,ИзделиеКомплект)
	Макет = Справочники.ИзделияКомплект.ПолучитьМакет("МакетМатериалы");
	Таб.Вывести(Макет.ПолучитьОбласть("Шапка"));
	ИмяТаблицы = ИзделиеКомплект.Наименование;
	обСтрока = Макет.ПолучитьОбласть("Строка");	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПотребностьВМатериалахОбороты.Номенклатура КАК Номенклатура,
		|	ПотребностьВМатериалахОбороты.Номенклатура.ОсновнаяЕдиницаИзмерения КАК Единица,
		|	ПотребностьВМатериалахОбороты.ПотребностьОборот КАК Количество
		|ИЗ
		|	РегистрНакопления.ПотребностьВМатериалах.Обороты(, , Период, ИзделиеКомплект = &ИзделиеКомплект) КАК ПотребностьВМатериалахОбороты";
	
	Запрос.УстановитьПараметр("ИзделиеКомплект", ИзделиеКомплект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		обСтрока.Параметры.Заполнить(Выборка);	
		Таб.Вывести(обСтрока);
	КонецЦикла;
	
	стДанные = Новый Структура("ИмяТаблицы", ИмяТаблицы); 
	
	Возврат стДанные;
КонецФункции

&НаКлиенте
Процедура МатериалыКомплекта(Команда)
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Таб = Новый ТабличныйДокумент;
		Таб.Защита = Истина;
		стДанные = МатериалыКомплектаНаСервере(Таб,Объект.Ссылка); 
		
		Таб.Показать(стДанные.ИмяТаблицы);
	Иначе	
		ПоказатьПредупреждение(,"Необходимо сохранить комплект");
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура КоличествоСотрудниковМонтажПриИзменении(Элемент)
	НормаВремениМонтажаИзменено = Истина;
КонецПроцедуры

&НаКлиенте
Процедура АктуальностьНажатие(Элемент, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ПриложениеКДоговору) Тогда
		ПоказатьЗначение(,ПриложениеКДоговору);
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
КонецПроцедуры


#Область ВидимостьЭлементов


#КонецОбласти 


#Область PDM

&НаКлиенте
Процедура ЧертежиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЧертежиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЧертежиПередНачаломИзменения(Элемент, Отказ)

	Отказ = Истина;
	
	PDF = Новый ДокументPDF();
	PDF.ПрочитатьАсинх(Элементы.Чертежи.ТекущиеДанные.ПутьКФайлу);
	PDF.Показать(Элементы.Чертежи.ТекущиеДанные.ИмяФайла); 
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	//Если ИмяСобытия = "ЗавершенаЗагрузкаДанныхPDF" Тогда
	//	Если Параметр.ПутьКПапке = Объект.ПутьКПапкеPDM Тогда
	//		
	//		Чертежи.Очистить();
	//		
	//		Для каждого ТекущийЧертеж Из Параметр.мДанныеОФайлахPDM Цикл
	//			ЗаполнитьЗначенияСвойств(Чертежи.Добавить(), ТекущийЧертеж);
	//		КонецЦикла;
	//		
	//		омОформлениеФормы.ЗаголовокЗакладки(Элементы, "ГруппаЧертежи" ,"Чертежи", Чертежи);
	//		Элементы.ГруппаЧертежи.Видимость = Истина;
	//		
	//	КонецЕсли;
	//	
	//КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКонструктораВPDM(Команда)
	
	Если Модифицированность Тогда
		ПоказатьПредупреждение(,"Необходимо сохранить комплект");	
	Иначе
		
		стВозврата = омВыгрузкаCRMНаКлиенте.ВыгрузитьВPDM(Объект.Ссылка, "Конструктор");
		Если НЕ ПустаяСтрока(стВозврата.ОписаниеОшибки) Тогда
			
			ПоказатьПредупреждение(,стВозврата.ОписаниеОшибки);	
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция КонструкторПриИзмененииНаСервере(Сотрудник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеСотрудникаВPDM.Username КАК Username
		|ИЗ
		|	Справочник.ДанныеСотрудникаВPDM КАК ДанныеСотрудникаВPDM
		|ГДЕ
		|	ДанныеСотрудникаВPDM.Сотрудник = &Сотрудник";
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Username;
	КонецЦикла;

	Возврат "";
	
КонецФункции

&НаКлиенте
Процедура КонструкторПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Конструктор) Тогда
		КодКонструктора = КонструкторПриИзмененииНаСервере(Конструктор);
	Иначе
		КодКонструктора = "";
	КонецЕсли;
	Если НЕ Элемент = Неопределено Тогда
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКодPDM(Команда)
	
	Если ЭтаФорма.Параметры.Ключ.Пустая() Тогда
		ПоказатьПредупреждение(, "Для установки кода необходимо сохранить комплект.");	
	Иначе
		
		ОписаниеОповещния = Новый ОписаниеОповещения("ПослеВводаКодаPDM", ЭтаФорма);
		ПоказатьВводЧисла(ОписаниеОповещния, Код_в_PDM, "Введите код.", 10, 0); 
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаКодаPDM(КодPDM, ДополнительныеПараметры) Экспорт 

	Если КодPDM = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
    Если Код_в_PDM = КодPDM Тогда
		Возврат;
	КонецЕсли;
	
	Код_в_PDM = КодPDM;
	
	ЗаписатьКод_в_PDM_НаСервере(Код_в_PDM, Объект.Ссылка);
	
КонецПроцедуры // ПослеВводаКодаPDM()

&НаСервереБезКонтекста
Процедура ЗаписатьКод_в_PDM_НаСервере(Код_в_PDM, КомплектСсылка)

	СделкаСсылка = КомплектСсылка.Проект;
	
	МенеджерЗаписи = РегистрыСведений.ДатыКомплекта.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Проект 		= СделкаСсылка;
	МенеджерЗаписи.Комплект 	= КомплектСсылка;

	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Проект 		= СделкаСсылка;
	МенеджерЗаписи.Комплект 	= КомплектСсылка;
    МенеджерЗаписи.Код_в_PDM 	= Код_в_PDM;
	
	МенеджерЗаписи.Записать(); 
	
	
КонецПроцедуры // ЗаписатьКод_в_PDM_НаСервере(Код_в_PDM, Объект.Ссылка)

#КонецОбласти

#Область Композиции

Процедура ЗаполнитьКомпозиции()

	Композиция = Неопределено;
	Если НЕ омИзделиеКомплект.ПервыйУровень(Объект.Родитель, Композиция) Тогда
		Элементы.Композиция.Видимость = Ложь;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Задачи_ПКД.Ссылка КАК ЗадачаПКД,
		|	Задачи_ПКД.Выполнена КАК Выполнена
		|ИЗ
		|	Задача.Задачи_ПКД КАК Задачи_ПКД
		|ГДЕ
		|	Задачи_ПКД.ОбъектТЗ = &ОбъектТЗ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Задачи_ПКД.Дата УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Задачи_РКД.Ссылка КАК ЗадачаРКД,
		|	Задачи_РКД.Выполнена КАК Выполнена
		|ИЗ
		|	Задача.Задачи_РКД КАК Задачи_РКД
		|ГДЕ
		|	Задачи_РКД.ОбъектТЗ = &ОбъектТЗ
		|	И Задачи_РКД.Выполнена
		|
		|УПОРЯДОЧИТЬ ПО
		|	Задачи_РКД.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("ОбъектТЗ", Объект.Ссылка);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[0].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗадачаПКД = Выборка.ЗадачаПКД;
		Элементы.ДекорацияЗадачаПКД.Видимость = Выборка.Выполнена;	
	КонецЦикла;
	
	Выборка = Результат[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗадачаРКД = Выборка.ЗадачаРКД;
		Элементы.ДекорацияЗадачаРКД.Видимость = Выборка.Выполнена;	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрекрепитьТЗНаСервере()
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ПрекрепитьТЗ(Команда)
	ПрекрепитьТЗНаСервере();
КонецПроцедуры

	
#КонецОбласти


&НаКлиенте
Процедура НазначитьКодБитрикс(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
	
		ПоказатьПредупреждение(, "Необходимо сохранить композицию");
		Возврат;
	
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаКодаБитрикс", ЭтаФорма);
	
	Если ПустаяСтрока(КодВыгрузкиВБитрикс) Тогда
		
		КодБитрикс = 0;
		
	Иначе
		
		КодБитрикс = Число(КодВыгрузкиВБитрикс);
		
	КонецЕсли;
	ПоказатьВводЧисла(Оповещение, КодБитрикс, "Введите код битрикс", 5, 0);
	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеВводаКодаБитрикс(КодБитрикс, ДополнительныеПараметры) Экспорт

    Если КодБитрикс = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ЗаписатьКодБитриксаНаСервера(Строка(Объект.Ссылка.УникальныйИдентификатор()), Строка(КодБитрикс));

	КодВыгрузкиВБитрикс = омВыгрузкаCRMНаСевере.ПолучитьКодБитрикс(Объект.Ссылка);

КонецПроцедуры // ПослеВводаКодаБитрикс() 

Процедура ЗаписатьКодБитриксаНаСервера(ИдентификаторОбъекта, КодБитрикс)

	МенеджерЗаписей = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
	МенеджерЗаписей.Портал 						= "pdm.8955.ru";
	МенеджерЗаписей.ТипВладельцаСмартПроцесса 	= "131";
	МенеджерЗаписей.ИдСмартПроцесса 			= "4";
	МенеджерЗаписей.ТипСущности1С 				= "Справочник";
	МенеджерЗаписей.Сущность1С 					= "ИзделияКомплект";
	МенеджерЗаписей.ИдентификаторОбъекта 		= ИдентификаторОбъекта;
	МенеджерЗаписей.Идентификатор 				= КодБитрикс;
	
	МенеджерЗаписей.Записать();
	

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлПДМ(Команда)
	ФайловаяСистемаКлиент.ОткрытьПроводник(ПутьКФайлуПДМ);
КонецПроцедуры

&НаКлиенте
Процедура КонтрольРегистрации(Команда)
	стВозврата = омВыгрузкаCRMНаКлиенте.ВыгрузитьВPDM(Объект.Ссылка, "КонтрольРегистрации");
	
	Если НЕ ПустаяСтрока(стВозврата.ОписаниеОшибки) Тогда
		ПоказатьПредупреждение(,стВозврата.ОписаниеОшибки);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТЗ(Команда)
	ФайловаяСистемаКлиент.ОткрытьПроводник(ПутьКФайлуТЗ);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТЗ(Команда)
	
	ИмяФайлаПДФ = Объект.Наименование + ".ТЗ.pdf";
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "Файл pdf|" + ИмяФайлаПДФ;
	Диалог.Заголовок = "Выберите файл ТЗ"; 
	Диалог.МножественныйВыбор = Ложь;
	ОповещениеЗавершения = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтаФорма);
	Диалог.Показать(ОповещениеЗавершения); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныеФайлы = Неопределено Тогда
		
		ПутьКФайлу = ВыбранныеФайлы[0];
		
		стПараметры = Новый Структура("ПолноеИмяФайла, Версия", ПутьКФайлу, 1);
		
		Результат = омВыгрузкаCRMНаКлиенте.ВыгрузитьВPDM(Объект.Ссылка, "ЗагрузкаТЗ", стПараметры);
		
		Если НЕ ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			Сообщить(Формат(ТекущаяДата(), "ДЛФ=DT") + " Не удалось перенести файла ТЗ " + стПараметры.ПолноеИмяФайла + ". " + Результат.ОписаниеОшибки);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
