
&НаКлиенте
Перем НачальноеЗначениеОсновнойЕИ,ЕдиницыИзмеренияИзменены,СтараяЦена, ИзмененаПредварительнаяНоменклатура;

#Область СобытияФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ИзмененаПредварительнаяНоменклатура = Ложь;
	НачальнаяУстановкаВидимостиКомплекта();
	НачальноеЗначениеОсновнойЕИ = Объект.ОсновнаяЕдиницаИзмерения;
	ЕдиницыИзмеренияИзменены = Ложь;
	СтараяЦена = Объект.Цена;
	
	Если ЭтаФорма.Параметры.Ключ.Пустая() Тогда
		Элементы.ИсторияЦен.Доступность = Ложь;
	КонецЕсли;
	
	ЗаголовкиЗакладок();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ЭтаФорма.Параметры.Ключ.Пустая() Тогда
		ЭтаФорма.ЕдиницыИзмерения.Загрузить(омЕдиницыИзмерения.ПолучитьЕдиницыИзмеренийНоменклатуры(Объект.Ссылка));
		ЭтаФорма.АктуальнаяЦена = ПолучитьАктуальнуюЦену(Объект.Ссылка);
		ЗаполнитьПредварительнуюНоменклатуру();
	КонецЕсли;
	
	Элементы.ГруппаЦена.Видимость = РольДоступна("ЦеныЗакупки");
	Элементы.ЕдиницыИзмеренияМножитель.Видимость = Константы.ИспользоватьМножитель.Получить();  
	
	Элементы.ПредварительнаяНоменклатура.ТолькоПросмотр = НЕ РольДоступна("ПолныеПрава"); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Элементы.ИсторияЦен.Доступность = Истина;
	Если ЕдиницыИзмеренияИзменены Тогда
		ЕдиницыИзмеренияИзменены = Ложь;
		ЗаписатьЕдиницыНаСервере();
	КонецЕсли;                                                        	
	Если СтараяЦена <> Объект.Цена ИЛИ Объект.Цена > ЭтаФорма.АктуальнаяЦена Тогда
		ЗаписатьЦенуВРегистр(Объект.Ссылка,Объект.Цена);	
		СтараяЦена = Объект.Цена;
		ЭтаФорма.АктуальнаяЦена = ПолучитьАктуальнуюЦену(Объект.Ссылка);
	КонецЕсли;
	
	Если ИзмененаПредварительнаяНоменклатура Тогда
		ИзмененаПредварительнаяНоменклатура = Ложь;
		
		мПредварительнаяНоменклатура = Новый Массив;
		
		Для каждого х Из ЭтаФорма.ПредварительнаяНоменклатура Цикл
			Если ЗначениеЗаполнено(х.ПредварительнаяНоменклатура) И мПредварительнаяНоменклатура.Найти(х.ПредварительнаяНоменклатура) = Неопределено Тогда
				мПредварительнаяНоменклатура.Добавить(х.ПредварительнаяНоменклатура);
			КонецЕсли;
		КонецЦикла;
		
		ЗаписатьПредварительнуюНоменклатуруНаСервере(мПредварительнаяНоменклатура,Объект.Ссылка);
		
		Оповестить("ПослеИзмененияСвязиНоменклатураПредварительнаяНоменклатура",Новый Структура("Номенклатура",Объект.Ссылка));
	КонецЕсли;
КонецПроцедуры 

#КонецОбласти

#Область ПредварительнаяНоменклатура

&НаСервереБезКонтекста
Процедура ЗаписатьПредварительнуюНоменклатуруНаСервере(мПредварительнаяНоменклатура,Номенклатура)

	РегистрыСведений.СвязьНоменклатурыСПредварительной.СвязатьНоменклатуруСПредворительной(Номенклатура,мПредварительнаяНоменклатура);

КонецПроцедуры // ЗаписатьПредварительнуюНоменклатуруНаСервере()

&НаСервере
Процедура ЗаполнитьПредварительнуюНоменклатуру()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СвязьНоменклатурыСПредварительной.ПредварительнаяНоменклатура КАК ПредварительнаяНоменклатура
		|ИЗ
		|	РегистрСведений.СвязьНоменклатурыСПредварительной КАК СвязьНоменклатурыСПредварительной
		|ГДЕ
		|	СвязьНоменклатурыСПредварительной.Номенклатура = &Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПредварительнаяНоменклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Объект.Ссылка);
	
	ЭтаФорма.ПредварительнаяНоменклатура.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры // ЗаполнитьПредварительнуюНоменклатуру()

#КонецОбласти 


&НаКлиенте
Процедура ЗаголовкиЗакладок(ИмяЗакладки = "")

	Если ИмяЗакладки = "" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаРасценки"						,"Расценки"						,Объект.Расценки);
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаКомплект"						,"Комплект"						,Объект.Комплекты);
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаЕдиницыИзмерения"				,"Единицы"						,ЭтаФорма.ЕдиницыИзмерения);
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаПредварительнаяНоменклатура"	,"Предварительная номенклатура"	,ЭтаФорма.ПредварительнаяНоменклатура);
	ИначеЕсли ИмяЗакладки = "ГруппаРасценки" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаРасценки"						,"Расценки"						,Объект.Расценки);
	ИначеЕсли ИмяЗакладки = "ГруппаКомплект" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаКомплект"						,"Комплект"						,Объект.Комплекты);
	ИначеЕсли ИмяЗакладки = "ГруппаЕдиницыИзмерения" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаЕдиницыИзмерения"				,"Единицы"						,ЭтаФорма.ЕдиницыИзмерения);
	ИначеЕсли ИмяЗакладки = "ГруппаПредварительнаяНоменклатура" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаПредварительнаяНоменклатура"	,"Предварительная номенклатура"	,ЭтаФорма.ПредварительнаяНоменклатура);
	КонецЕсли;

КонецПроцедуры // ЗаголовкиЗакладок()


#Область СобытиеРеквизитов

&НаКлиенте
Процедура ОсновнаяЕдиницаИзмеренияПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(Объект.ОсновнаяЕдиницаИзмерения) И ЭтаФорма.ЕдиницыИзмерения.Количество() > 0 Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеВопросаООчисткеЕИ", ЭтаФорма);
		ПоказатьВопрос(Оповещение
		,"Если очистить основную единицу измерения, то очистица таблица с дополнительными единицами измерения. Продолжить?"
		,РежимДиалогаВопрос.ДаНет,,,"Единицы измерения");
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомплектПриИзменении(Элемент)
	Элементы.ГруппаКомплект.Видимость = ЭтаФорма.Комплект;
	Если ЭтаФорма.Комплект Тогда
		
		омОформлениеФормы.ЗаголовокЗакладки(Элементы, "ГруппаКомплект", "Комплект" ,Объект.Комплекты);
		
	Иначе
		
		Объект.Комплекты.Очистить();	
	
	КонецЕсли;
КонецПроцедуры
	
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзменениеЦены" Тогда
		мИзмененаяНоменклатура = Параметр.Номенклатура;
		Если НЕ мИзмененаяНоменклатура.Найти(Объект.Ссылка) = Неопределено Тогда
			ЭтаФорма.АктуальнаяЦена = ПолучитьАктуальнуюЦену(Объект.Ссылка);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СобытиеРеквизитовТЧ

&НаКлиенте
Процедура РасценкиПриИзменении(Элемент)
	ЗаголовкиЗакладок(Элемент.Родитель.Имя);	
КонецПроцедуры

&НаКлиенте
Процедура КомплектыПриИзменении(Элемент)
	ЗаголовкиЗакладок(Элемент.Родитель.Имя);	
КонецПроцедуры


&НаКлиенте
Процедура ЕдиницыИзмеренияПриИзменении(Элемент)
	ТД = Элементы.ЕдиницыИзмерения.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЕдиницыИзмеренияИзменены = Истина;
	
	Если ТД.Множитель = 0 Тогда
		ТД.Множитель = 1;
	КонецЕсли;
	
	ЭтаФорма.Модифицированность = Истина;

	ЗаголовкиЗакладок(Элемент.Родитель.Имя);	
КонецПроцедуры 

&НаКлиенте
Процедура ПредварительнаяНоменклатураПриИзменении(Элемент)
	ИзмененаПредварительнаяНоменклатура = Истина;
	ЗаголовкиЗакладок(Элемент.Родитель.Имя);	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПослеВопросаООчисткеЕИ(Результат, Параметры) Экспорт
    Если НЕ Результат = КодВозвратаДиалога.Да Тогда
        Объект.ОсновнаяЕдиницаИзмерения = НачальноеЗначениеОсновнойЕИ;
	Иначе
		НачальноеЗначениеОсновнойЕИ = Объект.ОсновнаяЕдиницаИзмерения;
		ЭтаФорма.ЕдиницыИзмерения.Очистить();
    КонецЕсли;
КонецПроцедуры 

&НаСервере
Процедура ЗаписатьЕдиницыНаСервере()
	омЕдиницыИзмерения.ЗаписатьЕдиницыИзмерения(ЭтаФорма.ЕдиницыИзмерения.Выгрузить(),Объект.Ссылка);
КонецПроцедуры // ЗаписатьЕдиницыНаСервере()

#Область Комплект

&НаКлиенте
Процедура НачальнаяУстановкаВидимостиКомплекта()

	ЭтаФорма.Комплект = Объект.Комплекты.Количество() > 0;
	КомплектПриИзменении(Неопределено);

КонецПроцедуры // НачальнаяУстановкаВидимостиКомплекта()
		
#КонецОбласти

#Область Цена

&НаСервереБезКонтекста
Процедура ЗаписатьЦенуВРегистр(Номенклатура,Цена)
	
	МенеджерЗаписей = РегистрыСведений.ЦеныНоменклатурыДокументов.СоздатьМенеджерЗаписи();
	МенеджерЗаписей.Документ 				= Номенклатура;
	МенеджерЗаписей.Номенклатура 			= Номенклатура;
	МенеджерЗаписей.СпособЗаполненияЦены 	= Перечисления.СпособыЗаполненияЦен.ИзНоменклатуры;
	МенеджерЗаписей.Цена 					= Цена;
	МенеджерЗаписей.Автор 					= ПараметрыСеанса.ТекущийПользователь;
	МенеджерЗаписей.Дата 					= ТекущаяДата();
	МенеджерЗаписей.Записать();
КонецПроцедуры // ЗаписатьЦенуВРегистр()

&НаКлиенте
Процедура ИсторияЦен(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура",Объект.Ссылка);
	ОткрытьФорму("Отчет.ИсторияЦен.Форма", ПараметрыФормы);
	ЭтаФорма.АктуальнаяЦена = ПолучитьАктуальнуюЦену(Объект.Ссылка);
КонецПроцедуры  

&НаСервереБезКонтекста
Функция ПолучитьАктуальнуюЦену(Ссылка)

	Возврат омЦеныНаСервере.ПолучитьМаксимальнуюЦенуНоменклатуры(Ссылка);	

КонецФункции // ПолучитьАктуальнуюЦену()

&НаКлиенте
Процедура Движение(Команда)
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		стПараметры = Новый Структура("Номенклатура", Объект.Ссылка);
		ОткрытьФорму("Отчет.ДвижениеНоменклатуры.Форма.ФормаОтчета", стПараметры, ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
