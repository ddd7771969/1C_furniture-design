&НаСервере
Функция ПолучитьКонструкторов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодразделенияСрезПоследних.Подразделение КАК Подразделение
		|ИЗ
		|	РегистрСведений.Подразделения.СрезПоследних(, ) КАК ПодразделенияСрезПоследних
		|ГДЕ
		|	НЕ ПодразделенияСрезПоследних.НеАктивное
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПодразделенияСрезПоследних.Сотрудник.Наименование";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Подразделение");
	
КонецФункции // ПолучитьКонструкторов()

&НаСервере
Функция ПолучитьПодразделения(Сотрудник, ТолькоСсылки = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодразделенияСрезПоследних.НомерСтрокиТаблицы КАК НомерСтрокиТаблицы,
		|	ПодразделенияСрезПоследних.Подразделение КАК Подразделение
		|ИЗ
		|	РегистрСведений.Подразделения.СрезПоследних(, Сотрудник = &Сотрудник) КАК ПодразделенияСрезПоследних
		|ГДЕ
		|	НЕ ПодразделенияСрезПоследних.НеАктивное
		|";
	
	Запрос.УстановитьПараметр("Сотрудник",Сотрудник);
	
	Если ТолькоСсылки Тогда
		Запрос.Текст = 	Запрос.Текст + "УПОРЯДОЧИТЬ ПО
		|	ПодразделенияСрезПоследних.Сотрудник.Наименование";
		Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Подразделение");
	КонецЕсли;
	
	Запрос.Текст = 	Запрос.Текст + "УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиТаблицы";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьКонструкторов()


#Область Битрикс

Функция ПолучитьДанныеПодразделенийСБитрикс() Экспорт

	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(Константы.НастройкаПодключенияКБитриксПоУмолчанию.Получить());  

	Если СтруктураНастроек = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
 	СтруктураНастроек.Вставить("КоличествоПовторенийПриОшибках", 2);
	СтруктураНастроек.Вставить("ВыполненоБезОшибок", Истина);
	СтруктураНастроек.Операция = СтруктураНастроек.ТипыОперацийСинхронизации.Подразделения;
	
    СформированныеДанные = "";
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/department.get", СформированныеДанные);
	
	Если ТипЗнч(СтруктураОтвета) = Тип("Соответствие") Тогда
		
		тзДанныеИзБитриксаПодразделения = СоздатьПустуюТаблицу();
		
		КоличествоЗаписей = СтруктураОтвета.Получить("total");
		
		ЗаполнитьТаблицуПодразделения(СтруктураОтвета.Получить("result"), тзДанныеИзБитриксаПодразделения);
		
		Если КоличествоЗаписей > 50 Тогда
			ВыполнятьЗапрос = Истина;
			ТекущиеЗначение = 50;    
			
			Пока ВыполнятьЗапрос Цикл
				СформированныеДанные = "START: " + Формат(ТекущиеЗначение, "ЧГ=0");
				СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/department.get", СформированныеДанные);
				Если ТипЗнч(СтруктураОтвета) = Тип("Соответствие") Тогда
					ЗаполнитьТаблицуПодразделения(СтруктураОтвета.Получить("result"), тзДанныеИзБитриксаПодразделения);
				КонецЕсли;
				
				ТекущиеЗначение = ТекущиеЗначение + 50;
				
				Если ТекущиеЗначение > КоличествоЗаписей Тогда
					ВыполнятьЗапрос = Ложь;
				КонецЕсли;
				
			КонецЦикла;
			
			
			
		КонецЕсли;
		
		Возврат тзДанныеИзБитриксаПодразделения;
	
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции  

Процедура ЗаполнитьТаблицуПодразделения(Соответсвие_result, тзДанныеИзБитриксаПодразделения)

	Для каждого ТекущийРезультат Из Соответсвие_result Цикл
		
		НС = тзДанныеИзБитриксаПодразделения.Добавить(); 
		
		НС.idБитрикс 			= ТекущийРезультат.Получить("ID");
		НС.idБитриксРодитель 	= ТекущийРезультат.Получить("PARENT");
		НС.НаименованиеВБитрикс = ТекущийРезультат.Получить("NAME");
		
	КонецЦикла;

КонецПроцедуры

Функция СоздатьПустуюТаблицу()

	ТипСтрока 	= Новый ОписаниеТипов("Строка");
	
	
	тзДанныеИзБитриксаПодразделения = Новый ТаблицаЗначений;
	тзДанныеИзБитриксаПодразделения.Колонки.Добавить("idБитрикс", 				ТипСтрока);
	тзДанныеИзБитриксаПодразделения.Колонки.Добавить("idБитриксРодитель", 		ТипСтрока);
	тзДанныеИзБитриксаПодразделения.Колонки.Добавить("НаименованиеВБитрикс", 	ТипСтрока);
	
	Возврат тзДанныеИзБитриксаПодразделения;

КонецФункции // СоздатьПустуюТаблицу()

Процедура СоздатьПодразделениеВБитрикс(Подразделение, idРодителя1С, НаименованиеВ1С) Экспорт

	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(Константы.НастройкаПодключенияКБитриксПоУмолчанию.Получить());  

	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
 	СтруктураНастроек.Вставить("КоличествоПовторенийПриОшибках", 2);
	СтруктураНастроек.Вставить("ВыполненоБезОшибок", Истина);
	СтруктураНастроек.Операция = СтруктураНастроек.ТипыОперацийСинхронизации.Подразделения;
	
	СформированныеДанные = "name=" + НаименованиеВ1С + "&parent=" + idРодителя1С;
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/department.add", СформированныеДанные);
	
	
КонецПроцедуры

	
#КонецОбласти