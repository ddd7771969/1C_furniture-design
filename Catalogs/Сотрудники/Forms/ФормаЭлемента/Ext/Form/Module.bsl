&НаКлиенте 
Перем ПодразделенияИзменены, PDMИзменены, НачальноеЗначениеUserID, ИзмененыКонтактныеДанные; 

#Область СобытияФормы
	
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НачальноеЗначениеUserID 	= ЭтаФорма.UserID;
	НачальноеНаименование 		= Объект.Наименование;	
	ИзмененыКонтактныеДанные 	= Ложь;
	ПодразделенияИзменены 		= Ложь;
	PDMИзменены					= Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ПодразделенияИзменены Тогда
		мПодразделения = Новый Массив;
		НомерСтрокиТаблицы = 1;
		мСуществующиеПодразделения = Новый Массив;
		Для каждого текСтрока Из ЭтаФорма.Подразделения Цикл
			Если мСуществующиеПодразделения.Найти(текСтрока.Подразделение) = Неопределено Тогда
				мСуществующиеПодразделения.Добавить(текСтрока.Подразделение);
				стрДанные = Новый Структура("Подразделение,НомерСтрокиТаблицы,Проверена",текСтрока.Подразделение,НомерСтрокиТаблицы,Ложь);	
				мПодразделения.Добавить(стрДанные); 
				НомерСтрокиТаблицы = НомерСтрокиТаблицы + 1;
			КонецЕсли;
		КонецЦикла;
		
		ЗаписатьПоразделениеНаСервере(мПодразделения, Объект.Ссылка);
	КонецЕсли;
	
	Если PDMИзменены Тогда
	    стПараметры = Новый Структура("Сотрудник,Enabled,UserID,Username",Объект.Ссылка);
		
		ЗаполнитьЗначенияСвойств(стПараметры,ЭтаФорма);
		стПараметры.Enabled = ЭтаФорма.EnabledPDM;
		
		ЗаписатьPDMНАСервере(стПараметры);
	КонецЕсли; 
	
	Если ИзмененыКонтактныеДанные ИЛИ PDMИзменены Тогда
		ЗаписатьВ_SQL_PDM_НАСервере(Объект.Ссылка);
	КонецЕсли;
КонецПроцедуры  

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект,ТекущийОбъект);
	ЗаполнитьЗначенияСвойств(ЭтаФорма,омЗапросыПДМ.ПолучитьДанныеСотрудникаИзПДМ(Объект.Ссылка));
	Если НЕ РольДоступна("ПолныеПрава") Тогда
		Элементы.ГруппаPDM.ТолькоПросмотр = Истина;	
		Если ЭтаФорма.UserID = 0 Тогда
			Элементы.ГруппаPDM.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект,Объект,"ГруппаКонтактнойИнформации");
	Если НЕ ЭтаФорма.Параметры.Ключ.Пустая() Тогда
		Для каждого текПодразделение Из Справочники.Подразделения.ПолучитьПодразделения(Объект.Ссылка,Ложь) Цикл
			ЗаполнитьЗначенияСвойств(Подразделения.Добавить(),текПодразделение);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры   

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект,ТекущийОбъект);
	Если НЕ ПустаяСтрока(ТекущийОбъект.Фамилия) Тогда
		ТекущийОбъект.Наименование = СокрЛП(ТекущийОбъект.Фамилия) + ?(ПустаяСтрока(ТекущийОбъект.Имя),""," " + СокрЛП(ТекущийОбъект.Имя) 
		+ ?(ПустаяСтрока(ТекущийОбъект.Отчество),""," " + Лев(СокрЛ(ТекущийОбъект.Отчество),1) + "."));		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если Подразделения.Количество() = 0 Тогда
	
		Отказ = Истина;
		ПоказатьПредупреждение(, "Не заполнено подразделение");
	
	КонецЕсли;
КонецПроцедуры



#КонецОбласти


#Область СобытияРеквизитов
 
&НаКлиенте
Процедура ПодразделенияПриИзменении(Элемент)
	ПодразделенияИзменены 	= Истина;
КонецПроцедуры

&НаКлиенте
Процедура UserIDПриИзменении(Элемент)
	PDMИзменены				= Истина;
КонецПроцедуры

&НаКлиенте
Процедура EnabledПриИзменении(Элемент)
	PDMИзменены				= Истина;
КонецПроцедуры

&НаКлиенте
Процедура UsernameПриИзменении(Элемент)
	PDMИзменены				= Истина;
КонецПроцедуры 

#КонецОбласти

&НаСервереБезКонтекста
Процедура ЗаписатьPDMНАСервере(стПараметры)

	Справочники.ДанныеСотрудникаВPDM.ЗаписатьСотрудникPDM(стПараметры);
	омСинхронизацияСотрудниковPDM.СинхронизацияСотрудников(стПараметры.Сотрудник);

КонецПроцедуры // ЗаписатьPDMНАСервере()

&НаСервереБезКонтекста
Процедура ЗаписатьВ_SQL_PDM_НАСервере(Сотрудник)

	омСинхронизацияСотрудниковPDM.СинхронизацияСотрудников(Сотрудник);	

КонецПроцедуры // ЗаписатьВ_SQL_PDM_НАСервере()


&НаСервереБезКонтекста
Процедура ЗаписатьПоразделениеНаСервере(мПодразделения,Сотрудник)
	
	тзДейстивияСРегистром = Новый ТаблицаЗначений;
	тзДейстивияСРегистром.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	тзДейстивияСРегистром.Колонки.Добавить("НомерСтрокиТаблицы", Новый ОписаниеТипов("Число"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Подразделения.НомерСтрокиТаблицы КАК НомерСтрокиТаблицы,
		|	Подразделения.Подразделение КАК Подразделение
		|ИЗ
		|	РегистрСведений.Подразделения КАК Подразделения
		|ГДЕ
		|	Подразделения.Сотрудник = &Сотрудник
		|	И НЕ Подразделения.НеАктивное";
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗначениеНайдено = Ложь;
		Для каждого новоеЗначение Из мПодразделения Цикл
			Если новоеЗначение.Проверена Тогда
				Продолжить;
			КонецЕсли;
			
			Если новоеЗначение.Подразделение = Выборка.Подразделение И новоеЗначение.НомерСтрокиТаблицы = Выборка.НомерСтрокиТаблицы Тогда
				ЗначениеНайдено = Истина;
				новоеЗначение.Проверена = Истина; 
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ЗначениеНайдено Тогда
			ЗаполнитьЗначенияСвойств(тзДейстивияСРегистром.Добавить(), Выборка);
		КонецЕсли;
	КонецЦикла;
	
	ТД = ТекущаяДата();
	Для каждого текСтрока Из тзДейстивияСРегистром Цикл
		МенеджерЗаписей = РегистрыСведений.Подразделения.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.Период 				= ТД; 
		МенеджерЗаписей.Сотрудник 			= Сотрудник;
		МенеджерЗаписей.Подразделение 		= текСтрока.Подразделение;
		МенеджерЗаписей.НомерСтрокиТаблицы 	= текСтрока.НомерСтрокиТаблицы;
		МенеджерЗаписей.НеАктивное 			= Истина;
		МенеджерЗаписей.Записать();
	КонецЦикла;
	
	Для каждого текСтрока Из мПодразделения Цикл
		Если текСтрока.Проверена Тогда
			Продолжить;
		КонецЕсли;
		МенеджерЗаписей = РегистрыСведений.Подразделения.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.Период 				= ТД; 
		МенеджерЗаписей.Сотрудник 			= Сотрудник;
		МенеджерЗаписей.Подразделение 		= текСтрока.Подразделение;
		МенеджерЗаписей.НомерСтрокиТаблицы 	= текСтрока.НомерСтрокиТаблицы;
		МенеджерЗаписей.НеАктивное 			= Ложь;
		МенеджерЗаписей.Записать();
	КонецЦикла;
	
	
КонецПроцедуры // ЗаписатьПоразделениеНаСервере()

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект,Объект,Отказ);
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПодразделение()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодразделенияСрезПоследних.Подразделение КАК Подразделение
		|ИЗ
		|	РегистрСведений.Подразделения.СрезПоследних(, Сотрудник = &Сотрудник) КАК ПодразделенияСрезПоследних";
	
	Запрос.УстановитьПараметр("Сотрудник", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Подразделение;
	КонецЦикла;
	
	Возврат Справочники.Подразделения.ПустаяСсылка();
КонецФункции // ЗаполнитьПодразделение()


#Область КонтактнаяИнформация
////////////////////////////////////////////////////////////////////////////////
// Поддержка контактной информации.

&НаСервере
Процедура ОбновитьКонтактнуюИнформацию(Результат)
	
	МодульУправлениеКонтактнойИнформацией = ОбщегоНазначения.ОбщийМодуль("УправлениеКонтактнойИнформацией");
	МодульУправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	МодульУправлениеКонтактнойИнформациейКлиент =
		ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.НачатьИзменение(ЭтотОбъект, Элемент);
	
	ИзмененыКонтактныеДанные = Истина;
КонецПроцедуры // Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	МодульУправлениеКонтактнойИнформациейКлиент =
		ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент,, СтандартнаяОбработка);
КонецПроцедуры // Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриНажатии(Элемент, СтандартнаяОбработка)
	МодульУправлениеКонтактнойИнформациейКлиент =
		ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент,, СтандартнаяОбработка);
КонецПроцедуры // Подключаемый_КонтактнаяИнформацияПриНажатии(Элемент, СтандартнаяОбработка)

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
	МодульУправлениеКонтактнойИнформациейКлиент =
		ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.НачатьОчистку(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры // Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
	МодульУправлениеКонтактнойИнформациейКлиент =
		ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда.Имя);
КонецПроцедуры // Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	МодульУправлениеКонтактнойИнформациейКлиент =
		ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

// Подключаемый обработчик выбора.
//
// Параметры:
//  Элемент - ПолеФормы
//  ВыбранноеЗначение - Строка
//  СтандартнаяОбработка - Булево
//
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	МодульУправлениеКонтактнойИнформациейКлиент =
		ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
	МодульУправлениеКонтактнойИнформациейКлиент.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, Элемент.Имя, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьОбновлениеКонтактнойИнформации(Результат, ДополнительныеПараметры) Экспорт
	ОбновитьКонтактнуюИнформацию(Результат);
КонецПроцедуры

#КонецОбласти  





