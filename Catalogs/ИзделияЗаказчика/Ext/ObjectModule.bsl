
Процедура ПередЗаписью(Отказ)
	Если НЕ ЭтоГруппа Тогда
		Родитель = омКонтрольПапки.КонтрольПапкиЭлементаСправочника(Проект, Ссылка.Метаданные().Имя);
	КонецЕсли;
КонецПроцедуры 

Процедура ПриЗаписи(Отказ)
	ПриложениеКДоговору = Неопределено;
	пНомерВДоговоре = Неопределено;
	
	Артикул = Справочники.ИзделияЗаказчика.ПолучитьАртикулОбъекта(Ссылка,Проект,,пНомерВДоговоре,ПриложениеКДоговору);
	НаименованиеСАртикулом =  Артикул + "." + СокрЛП(Наименование);
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеИзделияЗаказчика.НаименованиеСАртикулом КАК НаименованиеСАртикулом,
		|	ДанныеИзделияЗаказчика.Проект КАК Проект,
		|	ДанныеИзделияЗаказчика.Артикул КАК Артикул,
		|	ДанныеИзделияЗаказчика.НомерПоПорядку КАК НомерПоПорядку,
		|	ДанныеИзделияЗаказчика.ПриложениеКДоговору КАК ПриложениеКДоговору
		|ИЗ
		|	РегистрСведений.ДанныеИзделияЗаказчика КАК ДанныеИзделияЗаказчика
		|ГДЕ
		|	ДанныеИзделияЗаказчика.ИзделиеЗаказчика = &ИзделиеЗаказчика";
	
	Запрос.УстановитьПараметр("ИзделиеЗаказчика", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НужнаЗапись = Истина;
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.НаименованиеСАртикулом = НаименованиеСАртикулом И
			Выборка.Проект = Проект И
			Выборка.Артикул = Артикул И
			Выборка.НомерПоПорядку = пНомерВДоговоре И
			Выборка.ПриложениеКДоговору = ПриложениеКДоговору Тогда
		
			НужнаЗапись = Ложь;
		
		КонецЕсли;
	КонецЦикла;
	
	Если НужнаЗапись Тогда
		МенеджерЗаписи = РегистрыСведений.ДанныеИзделияЗаказчика.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ИзделиеЗаказчика 		= Ссылка;
		МенеджерЗаписи.НомерПоПорядку 			= пНомерВДоговоре;
		МенеджерЗаписи.Проект 					= Проект;
		МенеджерЗаписи.ПриложениеКДоговору		= ПриложениеКДоговору;
		
		МенеджерЗаписи.Артикул 					= Артикул;
		МенеджерЗаписи.НаименованиеСАртикулом 	= МенеджерЗаписи.Артикул + "." + СокрЛП(Наименование);
		
		МенеджерЗаписи.Записать();
		
	КонецЕсли;
	
КонецПроцедуры


