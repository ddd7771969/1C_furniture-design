&НаКлиенте
Перем АртикулИзменен, НаименованиеИзменено;

&НаСервереБезКонтекста
Процедура ЗаписатьИзмененияАртикулаНаСервере(Наименование, Артикул, СсылкаНаИзделиеЗаказчика,Сделка)

	МенеджерЗаписи = РегистрыСведений.ДанныеИзделияЗаказчика.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ИзделиеЗаказчика = СсылкаНаИзделиеЗаказчика;
	
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.ИзделиеЗаказчика 		= СсылкаНаИзделиеЗаказчика;
	МенеджерЗаписи.Проект 					= Сделка;
	МенеджерЗаписи.Артикул 					= Артикул;
	МенеджерЗаписи.НаименованиеСАртикулом 	= ?(ПустаяСтрока(Артикул),"",СокрЛП(Артикул) + " ") + СокрЛП(Наименование); 
	
	МенеджерЗаписи.Записать();

КонецПроцедуры // ЗаписатьИзмененияАртикулаНаСервере(Объект.Наименование,ЭтаФорма.Артикул,Объект.Сделка)()


&НаСервереБезКонтекста
Процедура ПроверитьКаталогИзделия(СсылкаНаИзделиеЗаказчика,Сделка)
	
	Справочники.ИзделияЗаказчика.ПроверитьКаталогЭлемента(СсылкаНаИзделиеЗаказчика,Сделка);		
	
КонецПроцедуры // ПроверитьРасположениеОбъекта()

&НаКлиенте
Процедура ПослеВыбораИзделияЭлемент(Результат,ДопПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если ДопПараметры = Неопределено Тогда
		ТД = Объект.ИзделияЭлемент.Добавить(); 
	Иначе
		ТД = ДопПараметры.ТД;
	КонецЕсли;
	
	ТД.ИзделиеЭлемент = Результат; 
	
	Элементы.ИзделияЭлемент.Обновить();
КонецПроцедуры // ПослеВыбораИзделияЗаказчика()


&НаСервере
Процедура ПолучитьданныеИзделия()
    ЭтаФорма.Актуальность 	= Ложь;
	ЭтаФорма.НомерПоПорядку = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеИзделияЗаказчика.Актуальность КАК Актуальность,
		|	ДанныеИзделияЗаказчика.НомерПоПорядку КАК НомерПоПорядку
		|ИЗ
		|	РегистрСведений.ДанныеИзделияЗаказчика КАК ДанныеИзделияЗаказчика
		|ГДЕ
		|	ДанныеИзделияЗаказчика.ИзделиеЗаказчика = &ИзделиеЗаказчика";
	
	Запрос.УстановитьПараметр("ИзделиеЗаказчика", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ЭтаФорма,Выборка);
		Элементы.Проект.ТолькоПросмотр = Выборка.Актуальность;
	КонецЦикла;
	

КонецПроцедуры // ПолучитьданныеИзделия()


&НаСервере
Процедура ВидимостьЭлементов()
	Если РольДоступна("ЦенаВДоговоре") Тогда
	
		Элементы.ЦенаВДоговоре.Видимость = Истина;
		
	Иначе
		
		Элементы.ЦенаВДоговоре.Видимость = Ложь;

	КонецЕсли;	
	
	
КонецПроцедуры // ВидимостьЭлементов()
 
&НаСервере
Процедура ОбновитьКартинки()	
	КлючКартинки = РегистрыСведений.КартинкиНоменклатуры.СоздатьКлючЗаписи(Новый Структура("ИзделиеЭлемент", Объект.Ссылка));
	АдресКартинки = ПолучитьНавигационнуюСсылку(КлючКартинки, "Картинка");
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Добавлена картинка" Тогда
		Если Параметр = Объект.Ссылка Тогда
			ПодключитьОбработчикОжидания("ПослеЗагрузкиКартинки",0.5,Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗагрузкиКартинки() Экспорт

	ОбновитьКартинки();	
	Элементы.АдресКартинки.Обновить();

КонецПроцедуры // ПослеЗагрузкиКартинки()


&НаСервереБезКонтекста
Функция ПолучитьДоговорЭлемента(Ссылка, Представление)

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДоговораСделкиЭлементыСделки.Ссылка КАК Договор,
		|	ДоговораСделкиЭлементыСделки.Ссылка.Представление КАК Представление
		|ИЗ
		|	Справочник.ПриложенияКДоговорам.ИзделияЗаказчика КАК ДоговораСделкиЭлементыСделки
		|ГДЕ
		|	ДоговораСделкиЭлементыСделки.ИзделиеЗаказчика = &ИзделиеЗаказчика";
	
	Запрос.УстановитьПараметр("ИзделиеЗаказчика", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Представление = Выборка.Представление;
		Возврат Выборка.Договор;
	КонецЦикла;
	

КонецФункции // ПолучитьДоговорЭлемента() 

&НаСервере
Процедура ЗаполнитьКомплекты()

	стКомплекты = омКомплекты.КомплектыИзИзделияЗаказчика(Объект.Ссылка);	
	
	Для каждого х Из стКомплекты.мИзделияКомплект Цикл
		ЭтаФорма.СписокКомплектов.Добавить(х.ИзделияКомплект, х.Представление);
	КонецЦикла; 
	
КонецПроцедуры // ЗаполнитьКомплекты()

&НаКлиенте
Процедура ПослеВыбораПомещения(Результат, ДополнительныеПараметры) Экспорт	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;                         
	
	Если ДополнительныеПараметры = Неопределено Тогда
		НС = Объект.Помещения.Добавить();
		НС.Помещение = Результат; 
	Иначе
		ДополнительныеПараметры.ТД.Помещение = Результат;	
	КонецЕсли;
КонецПроцедуры // ПослеВыбораЭлементовСделки()


#Область СобытиеЭлеметовФормы
	
&НаКлиенте
Процедура СделкаПриИзменении(Элемент)
	НачальноеЗначениеАртикула = ЭтаФорма.Артикул;
	ПолучитьданныеИзделия();	
	ПолучитьАртикул();
	АртикулИзменен = АртикулИзменен ИЛИ НЕ НачальноеЗначениеАртикула = ЭтаФорма.Артикул;
КонецПроцедуры

&НаКлиенте
Процедура ПомещенияПомещениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Объект.Проект) Тогда
		омПомещениеКлиент.ОткрытьПодборПомещения(Объект, ЭтаФорма);
	Иначе	
		ПоказатьПредупреждение(,"Не заполнена Проект");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокКомплектовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры 

&НаКлиенте
Процедура СписокКомплектовПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокКомплектовПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина; 
	ОткрытьЗначениеАсинх(Элемент.ТекущиеДанные.Значение);
КонецПроцедуры

&НаКлиенте
Процедура ИзделияЭлементаИзделиеЭлементНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Объект.Проект) Тогда
		омИзделиеЭлементКлиент.ОткрытьПодборИзделийЭлемент(Объект
															,ЭтаФорма
															,СтандартнаяОбработка
															,Новый Структура("ТД", Элементы.ИзделияЭлемент.ТекущиеДанные));
	Иначе
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(,"Не заполнена Проект");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзделияЭлементПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	Если ЗначениеЗаполнено(Объект.Проект) Тогда
		омИзделиеЭлементКлиент.ОткрытьПодборИзделийЭлемент(Объект, ЭтаФорма);
	Иначе	
		ПоказатьПредупреждение(,"Не заполнена Проект");
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ПомещенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	Если ЗначениеЗаполнено(Объект.Проект) Тогда
		омПомещениеКлиент.ОткрытьПодборПомещения(Объект, ЭтаФорма);
	Иначе	
		ПоказатьПредупреждение(,"Не заполнена Проект");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	НаименованиеИзменено = Истина;
КонецПроцедуры


#КонецОбласти

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьКартинки();
	Элементы.АдресКартинки.РазмерКартинки = РазмерКартинки.АвтоРазмер;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Представление = "";
		Договор = ПолучитьДоговорЭлемента(Объект.Ссылка, Представление);
		
		ЭтаФорма.СписокКомплектов.Добавить(Договор, "Договор " + Представление);
		ЗаполнитьКомплекты();
		ПолучитьданныеИзделия();	
		
	КонецЕсли;
	
	ВидимостьЭлементов();
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	АртикулИзменен 			= Ложь;
	НаименованиеИзменено 	= Ложь;
	
	ПолучитьАртикул();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Объект.ПомещенияСтрокой = "";
	
	Для каждого х Из Объект.Помещения Цикл
		Объект.ПомещенияСтрокой = омРаботаССтроками.ДобавитьРазделительНеПустойСтроке(Объект.ПомещенияСтрокой) + х.Помещение;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ЗначениеЗаполнено(Объект.Проект) Тогда
		ПроверитьКаталогИзделия(Объект.Ссылка,Объект.Проект);
	КонецЕсли;
	
	Если АртикулИзменен ИЛИ НаименованиеИзменено Тогда
	
		ЗаписатьИзмененияАртикулаНаСервере(Объект.Наименование,ЭтаФорма.Артикул,Объект.Ссылка,Объект.Проект);	
	
	КонецЕсли;
КонецПроцедуры 


#КонецОбласти

#Область Артикул

&НаКлиенте
Процедура ПолучитьАртикул()
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЭтаФорма.Артикул = ПолучитьАртикулНаСервере(Объект.Ссылка, Объект.Проект);	
	Иначе
		ЭтаФорма.Артикул = "";	
	КонецЕсли;
	
КонецПроцедуры // ПолучитьАртикул()

&НаСервереБезКонтекста
Функция ПолучитьАртикулНаСервере(СсылкаНаИзделиеЗаказчика, Сделка)

	Возврат Справочники.ИзделияЗаказчика.ПолучитьАртикулОбъекта(СсылкаНаИзделиеЗаказчика, Сделка);		

КонецФункции // ПолучитьАртикулНаСервере()



#КонецОбласти
