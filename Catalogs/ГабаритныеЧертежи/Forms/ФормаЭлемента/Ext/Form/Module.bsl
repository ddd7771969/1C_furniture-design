&НаКлиенте
Перем ДанныеВыгрузки, ПутьКФайлуПДМ; 
 
&НаКлиенте
Процедура ПомещенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если ЗначениеЗаполнено(Объект.Проект) Тогда
		
		НеПоказыватьПомещения = Новый Массив;
		
		Для каждого СтрокаПомещение Из Объект.Помещения Цикл
			НеПоказыватьПомещения.Добавить(СтрокаПомещение.Помещение);	
		КонецЦикла;
		омПомещениеКлиент.ОткрытьПодборПомещения(Объект, ЭтаФорма, , ,НеПоказыватьПомещения);
		
	Иначе	
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнена Проект";
		Сообщение.Поле = "Проект";
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СделкаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Если Объект.Помещения.Количество() > 0 Тогда
		
		СтандартнаяОбработка = Ложь;

		Оповещение = Новый ОписаниеОповещения("ПослеВопросаОчисткаТЧ", ЭтаФорма);
		ПоказатьВопрос(Оповещение, "После смены проекта будет очищена табличная часть", РежимДиалогаВопрос.ДаНет);
		
	Иначе
		//омПомещениеКлиент.ОткрытьПодборПомещения(Объект, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПомещения(Результат, ДополнительныеПараметры) Экспорт	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;                         
	
	Если ДополнительныеПараметры = Неопределено Тогда
		НС = Объект.Помещения.Добавить();
		НС.Помещение = Результат; 
	Иначе
		ДополнительныеПараметры.ТД.Помещение = Результат;	
	КонецЕсли;
КонецПроцедуры // ПослеВыбораЭлементовСделки()

&НаКлиенте
Процедура ПослеВопросаОчисткаТЧ(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Помещения.Очистить();
	
	ОткрытьФорму("Справочник.Проекты.ФормаВыбора",,ЭтотОбъект,,,,Новый ОписаниеОповещения("ПослеВыбораСделки", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСделки(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Проект = Результат;
	
КонецПроцедуры // ОткрытьПодбор()

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Объект.Родитель) Тогда
		Объект.Проект = Объект.Родитель.Проект;
	КонецЕсли;
	

	Если Параметры.Ключ.Пустая() Тогда
		
		Элементы.НазначитьКодPDM.Доступность 			= Ложь;
		Элементы.ФормаЗапросНаПроектныйСтатус.Видимость = Ложь;
		Элементы.ВыгрузитьВPDM.Видимость 				= Ложь;
			
	Иначе	
		
		ПолучитьКоды();
		ПолучитьКомплекты();
		Если Код_в_PDM > 0 Тогда
			Элементы.ВыгрузитьВPDM.Видимость = Ложь;	
		КонецЕсли;
		
		ПлановаяДатаСтроительнойГотовности = омГабаритныйЧертежНаСервере.ПолучитьПлановуюДатуГотовностиГабаритногоЧертежа(Объект.Ссылка);
		
		стСтатусЗамера = РегистрыСведений.СостояниеЗамера.ПолучитьСтатусЗамера(Объект.Ссылка);
		
		Если стСтатусЗамера.СтатусЗамера = Перечисления.СтатусыЗамера.Проектный Тогда
			Элементы.ФормаЗапросНаПроектныйСтатус.Видимость = Ложь;	
		Иначе
			
			Если НЕ (РольДоступна("АдминистраторСистемы") И РольДоступна("ПолныеПрава")) Тогда
				
				Элементы.ВыгрузитьВPDM.Видимость = Ложь;
				
				Если стСтатусЗамера.СтатусЗамера = Перечисления.СтатусыЗамера.Рабочий Тогда
					
					Если НЕ РольДоступна("РуководительЗамера") Тогда
						Элементы.ФормаЗапросНаПроектныйСтатус.Видимость = Ложь;		
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		СтатусЗамера = Строка(стСтатусЗамера.СтатусЗамера);
		
		Если НЕ стСтатусЗамера.Дата = Дата(1, 1, 1) Тогда
		
			СтатусЗамера = СтатусЗамера + " (" + Формат(стСтатусЗамера.Дата, "ДЛФ=DD") + ")";	
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьКомплекты()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|ГДЕ
		|	ИзделияКомплект.ГабаритныйЧертеж = &ГабаритныйЧертеж
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИзделияКомплект.Наименование";
	
	Запрос.УстановитьПараметр("ГабаритныйЧертеж", Объект.Ссылка);
	
	СписокКомплектов.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));

КонецПроцедуры

Функция ПолучитьКоды()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеГабаритныйЧертеж.Код_в_PDM КАК Код_в_PDM
		|ИЗ
		|	РегистрСведений.ДанныеГабаритныйЧертеж КАК ДанныеГабаритныйЧертеж
		|ГДЕ
		|	ДанныеГабаритныйЧертеж.ГабаритныйЧертеж = &ГабаритныйЧертеж";
	
	Запрос.УстановитьПараметр("ГабаритныйЧертеж", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Выборка);
	КонецЦикла;

	КодВыгрузкиВБитрикс = омВыгрузкаCRMНаСевере.ПолучитьКодБитрикс(Объект.Ссылка);
	
КонецФункции // ПолучитьКодБитрикса()

Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
КонецПроцедуры

&НаКлиенте
Процедура ПомещенияОсновноеПриИзменении(Элемент)
	
	ТД = Элементы.Помещения.ТекущиеДанные;
	
	Для каждого СтрокаПомещение Из Объект.Помещения Цикл
		
		Если СтрокаПомещение = ТД Тогда
			СтрокаПомещение.Основное = ТД.Основное;
			
		Иначе	
		    СтрокаПомещение.Основное = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	//СформироватьНаименование();

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеБезКодаПриИзменении(Элемент)
	СформироватьНаименование();
КонецПроцедуры

&НаКлиенте
Процедура СформироватьНаименование()
	
	//СтруктураНаименования = омГабаритныйЧертежНаСервере.ПолучитьНаименованиеГЧ(Объект.Сделка, Объект.ОсновноеПомещение, Объект.НаименованиеБезКода, Объект.НомерВПомещении);
	//
	//Объект.Наименование 		= СтруктураНаименования.Наименование;
	//Объект.ОсновноеПомещение 	= СтруктураНаименования.ОсновноеПомещение;

КонецПроцедуры // СформироватьНаименование()

&НаКлиенте
Процедура ПомещенияПомещениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТД = Элементы.Помещения.ТекущиеДанные;
	
	ДополнительныеПараметры = Неопределено;
	
	Если НЕ ТД = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура("ТД", ТД);	
	КонецЕсли;
	
	Если Объект.Помещения.Количество() = 0 Тогда
		НеПоказыватьПомещения = Неопределено;
	Иначе	
		
		НеПоказыватьПомещения = Новый Массив;
		
		Для каждого СтрокаПомещение Из Объект.Помещения Цикл
			НеПоказыватьПомещения.Добавить(СтрокаПомещение.Помещение);	
		КонецЦикла;
	
	КонецЕсли;
	
	омПомещениеКлиент.ОткрытьПодборПомещения(Объект, ЭтаФорма, СтандартнаяОбработка, ДополнительныеПараметры, НеПоказыватьПомещения);
КонецПроцедуры

&НаКлиенте
Процедура НомерВПомещенииПриИзменении(Элемент)
	СформироватьНаименование();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Элементы.НазначитьКодPDM.Доступность = Истина;
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПутьКФайлуПДМ = омФайлы.ПолучитьПутьКФайлуПДМ(Объект.Ссылка, Объект.Проект);
	Если ПустаяСтрока(ПутьКФайлуПДМ) Тогда
		Элементы.ОткрытьФайлПДМ.Видимость = Ложь;
	Иначе
		
		Файл = Новый Файл(ПутьКФайлуПДМ);
		
		Элементы.ОткрытьФайлПДМ.Видимость = Файл.Существует();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВPDM(Команда)
	
	Если ЭтаФорма.Модифицированность ИЛИ Параметры.Ключ.Пустая() Тогда
		ПоказатьПредупреждение(, "Необходимо сохранить габаритный чертеж");
		Возврат;
	КонецЕсли;
	
	стВозврата = омВыгрузкаCRMНаКлиенте.ВыгрузитьВPDM(Объект.Ссылка, "ГабаритныйЧертеж");
	Если НЕ ПустаяСтрока(стВозврата.ОписаниеОшибки) Тогда
		ПоказатьПредупреждение(, стВозврата.ОписаниеОшибки, , "Ошибка");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НазначитьКодPDM(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаКодаПДМ", ЭтаФорма);
	
	Заголовок = ?(Код_в_PDM = 0, "Ввод кода PDM", "Изменение кода PDM");
	
	ПоказатьВводЧисла(Оповещение, Код_в_PDM, Заголовок, 10, 0); 

КонецПроцедуры 

&НаКлиенте
Процедура ПослеВводаКодаПДМ(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
	
		Возврат;	
	
	КонецЕсли;

	омГабаритныйЧертежНаСервере.УстановитьКодПДМДляГабаритныйЧертеж(Объект.Ссылка, Результат);
	Код_в_PDM = Результат;
	
	Оповестить("Изменился_код_PDM_габаритный_чертеж");

КонецПроцедуры // ПослеВводаКодаПДМ()

&НаКлиенте
Процедура ЗапросНаПроектныйСтатус(Команда)

	Оповещение = Новый ОписаниеОповещения("ПослеВводаПричиныПеревода", ЭтаФорма);
	
	ПоказатьВводСтроки(Оповещение, "", "Причина перевода", 0, Истина);
	
	ПричинаПеревода = "";
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаПричиныПеревода(ПричинаПеревода, ДополнительныеПараметры) Экспорт

    Если НЕ ЗначениеЗаполнено(ПричинаПеревода) Тогда
	
		ПоказатьПредупреждение(, "Нельзя создать задачу перевод не указав причину.");
		Возврат;
	
	КонецЕсли; 
	
	Результат = СоздатьЗадачуПереводаНеСервере(Объект.Ссылка, ПричинаПеревода);
	
	ПоказатьПредупреждение(, Результат);
	

КонецПроцедуры // ПослеВводаПричиныПеревода()

&НаСервереБезКонтекста
Функция СоздатьЗадачуПереводаНеСервере(ГабаритныйЧертеж, ПричинаПеревода)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГабаритныеЧертежи.Проект КАК Проект,
		|	ГабаритныеЧертежи.Проект.Наименование КАК ПроектНаименование,
		|	ГабаритныеЧертежи.Наименование КАК Наименование,
		|	Пользователи.Ссылка КАК ВедущийКонструктор
		|ИЗ
		|	Справочник.ГабаритныеЧертежи КАК ГабаритныеЧертежи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ГабаритныеЧертежи.Проект.ВедущийКонструктор = Пользователи.Сотрудник
		|ГДЕ
		|	ГабаритныеЧертежи.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ГабаритныйЧертеж);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Проект 				= Неопределено;
	ВедущийКонструктор 	= Неопределено;
	
	Пока Выборка.Следующий() Цикл
		Проект 				= Выборка.Проект;
		ВедущийКонструктор 	= Выборка.ВедущийКонструктор;
		ПроектНаименование	= Выборка.ПроектНаименование;
		НаименованиеГЧ		= Выборка.Наименование;
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(ВедущийКонструктор) Тогда
	
		Возврат "Проекту " + ПроектНаименование + " не назначен ведущий конструктор."	
	
	КонецЕсли;
	
	Задача = задачи.ПереводГабаритногоЧертежаВПроектное.СоздатьЗадачу();
	Задача.Автор 					= ПараметрыСеанса.ТекущийПользователь;
	Задача.ГабаритныйЧертеж 		= ГабаритныйЧертеж;
	Задача.Дата 					= ТекущаяДата();
	Задача.Исполнитель 				= ВедущийКонструктор;
	Задача.Проект 					= Проект;
	Задача.Комментарий 				= ПричинаПеревода;
	Задача.КрайняяДатаИсполнения 	= Задача.Дата + 3 * 24 * 3600;
	Задача.Наименование 			= НаименованиеГЧ + " перевод в проектное"; 
	
	НС = Задача.Наблюдатели.Добавить();
	НС.Наблюдатель = Задача.Автор;
	
	Задача.Записать();
	
	
	Возврат "Задача создана.";
	
КонецФункции // СоздатьЗадачуПереводаНеСервере(ГабаритныйЧертеж, ПричинаПеревода)


&НаКлиенте
Процедура НазначитьКодБитрикс(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
	
		ПоказатьПредупреждение(, "Необходимо сохранить ГЧ");
		Возврат;
	
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаКодаБитрикс", ЭтаФорма);
	
	Если ПустаяСтрока(КодВыгрузкиВБитрикс) Тогда
		
		КодБитрикс = 0;
		
	Иначе
		
		КодБитрикс = Число(КодВыгрузкиВБитрикс);
		
	КонецЕсли;
	ПоказатьВводЧисла(Оповещение, КодБитрикс, "Введите код битрикс", 5, 0);
	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеВводаКодаБитрикс(КодБитрикс, ДополнительныеПараметры) Экспорт

    Если КодБитрикс = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ЗаписатьКодБитриксаНаСервера(Строка(Объект.Ссылка.УникальныйИдентификатор()), Строка(КодБитрикс));

	КодВыгрузкиВБитрикс = омВыгрузкаCRMНаСевере.ПолучитьКодБитрикс(Объект.Ссылка);

КонецПроцедуры // ПослеВводаКодаБитрикс() 

Процедура ЗаписатьКодБитриксаНаСервера(ИдентификаторОбъекта, КодБитрикс)

	МенеджерЗаписей = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
	МенеджерЗаписей.Портал 						= "pdm.8955.ru";
	МенеджерЗаписей.ТипВладельцаСмартПроцесса 	= "183";
	МенеджерЗаписей.ИдСмартПроцесса 			= "6";
	МенеджерЗаписей.ТипСущности1С 				= "Справочник";
	МенеджерЗаписей.Сущность1С 					= "Помещения";
	МенеджерЗаписей.ИдентификаторОбъекта 		= ИдентификаторОбъекта;
	МенеджерЗаписей.Идентификатор 				= КодБитрикс;
	
	МенеджерЗаписей.Записать();
	

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлПДМ(Команда)
	ФайловаяСистемаКлиент.ОткрытьПроводник(ПутьКФайлуПДМ);
КонецПроцедуры

 
