
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ВыгрузкаНастроек

Функция СформироватьДанныеОНастройке(ПараметрыВыгрузки, СуществующаяНастройка = Неопределено)
	
	НастройкаПодключения 		= ПараметрыВыгрузки.НастройкаПодключения;
	Наименование 				= ПараметрыВыгрузки.Наименование;
	СтруктураНастроек 			= ПараметрыВыгрузки.СтруктураНастроек;
	ИдентификаторПодключения 	= ПараметрыВыгрузки.СтруктураНастроек.ИдентификаторПодключения;
	
	Если СуществующаяНастройка = Неопределено ИЛИ ТипЗнч(СуществующаяНастройка) <> Тип("Структура") Тогда
		ИнформацияОНастройке = Новый Структура;
		ИнформацияОНастройке.Вставить("КлючНастройки", ИдентификаторПодключения);
	Иначе
		ИнформацияОНастройке = СуществующаяНастройка;	
	КонецЕсли;
	
	Если ИнформацияОНастройке.Свойство("ПараметрыНастройки") Тогда
		ПараметрыНастройки = ИнформацияОНастройке.ПараметрыНастройки;	
	Иначе
		ПараметрыНастройки = Новый Структура;
	КонецЕсли;
	
#Область ОбщиеНастройки		
	ПараметрыНастройки.Вставить("name"		, Наименование);
	ПараметрыНастройки.Вставить("extAdress"	, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "ВебАдрес1С"));
	ПараметрыНастройки.Вставить("idAdress"	, ИдентификаторПодключения);
	
	СпособОткрытия1С = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "СпособОткрытия1С");
	Если СпособОткрытия1С = "Горячий старт" Тогда
		ПараметрыНастройки.Вставить("slider", "N");
	Иначе
		ПараметрыНастройки.Вставить("slider", "Y");
	КонецЕсли;
	
	СпособВзаимодействияСБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "СпособВзаимодействияСБитрикс24");
	Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() И (СпособВзаимодействияСБитрикс24 = Неопределено ИЛИ СпособВзаимодействияСБитрикс24 = "Через push&pull сервер") Тогда
		ПараметрыНастройки.Вставить("pushPull", "Y");
	Иначе
		ПараметрыНастройки.Вставить("pushPull", "N");
	КонецЕсли;
	
	ПараметрыНастройки.Вставить("httpUser"		, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "ИмяПользователяHTTP"));
	ПараметрыНастройки.Вставить("httpPasword"	, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "ПарольПользователяHTTP"));
	
	
	Информация1С = Новый Структура;
	
	лСистемнаяИнформация = Новый СистемнаяИнформация;
	Информация1С.Вставить("versionOS"		, лСистемнаяИнформация.ВерсияОС);
	Информация1С.Вставить("platform1C"		,  лСистемнаяИнформация.ВерсияПриложения);
	
	Информация1С.Вставить("baseType1C"		, ?(Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ИнформационнаяБазаФайловая(), "Файловая", "Клиент-серверная"));
	Информация1С.Вставить("customizer1C"	, ПараметрыСеанса.ТекущийПользователь.Наименование);
	
	Информация1С.Вставить("configuration1C"			, Метаданные.Синоним);
	Информация1С.Вставить("releaseConfiguration1C"	, Метаданные.Версия);
	
	ПараметрыНастройки.Вставить("information1C"		, Информация1С);
	
	
	ПараметрыНастройки.Вставить("BitrixSearch"	, ?(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("БитриксПоиск"				, "ВключитьБитриксПоиск") 			= Ложь, "N", "Y"));
	ПараметрыНастройки.Вставить("autofilling"	, ?(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("АвтозаполнениеРеквизитов"	, "ЗаполнениеРеквизитовИз1С") 		= Ложь, "N", "Y"));
	
	СпкПользователей = ПолучитьСписокПользователей();	
	ПараметрыНастройки.Вставить("users", СпкПользователей.ВыгрузитьЗначения());
#КонецОбласти	

	ИнформацияОНастройке.Вставить("ПараметрыНастройки", ПараметрыНастройки);
	
	Возврат ИнформацияОНастройке;
	
КонецФункции

Процедура ВыгрузитьНастройкиВФоне(ПараметрыВыгрузки, АдресРезультата) Экспорт
	
	НастройкаПодключения 		= ПараметрыВыгрузки.НастройкаПодключения;
	Наименование 				= ПараметрыВыгрузки.Наименование;
	СтруктураНастроек 			= ПараметрыВыгрузки.СтруктураНастроек;
	ИдентификаторПодключения	= ПараметрыВыгрузки.СтруктураНастроек.ИдентификаторПодключения;
	
	Б24_К_СлужебныйВызовСервера.ОбновитьИнформациюОЛицензии(НастройкаПодключения);
	
	мНастройки = ПолучитьНастройкиСПортала(СтруктураНастроек);
	
	НайденнаяНастройка = Неопределено;
	Для каждого ТекЭлемент Из мНастройки Цикл
		
		Если ТекЭлемент.КлючНастройки = ИдентификаторПодключения Тогда
			НайденнаяНастройка = ТекЭлемент;
			мНастройки.Удалить(мНастройки.Найти(ТекЭлемент));
		КонецЕсли;
		
	КонецЦикла;
	
	ИнформацияОНастройке = СформироватьДанныеОНастройке(ПараметрыВыгрузки, НайденнаяНастройка);
	
	мНастройки.Добавить(ИнформацияОНастройке);
	
	ПараметрыЗапроса = "";
	Для каждого ТекНастройка Из мНастройки Цикл
		Для каждого ТекПараметр Из ТекНастройка.ПараметрыНастройки Цикл
			СформироватьЗапросНастроекРекурсивно(ПараметрыЗапроса, "&options[ACCOUNTING]["+ТекНастройка.КлючНастройки+"]", ТекПараметр);
		КонецЦикла;
	КонецЦикла;
	
	Метод 		= "/rest/app.option.set";	
	
	Результат = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, Метод, ПараметрыЗапроса); 
	
	Если ТипЗнч(Результат) = Тип("Соответствие") Тогда
		Если Результат.Получить("result") = Истина Тогда
			ДобавитьУдалитьВстройкиБитрикс24(ПараметрыВыгрузки, НастройкаПодключения.ПометкаУдаления);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьУдалитьВстройкиБитрикс24(ПараметрыВыгрузки, УдалитьВстройки = Ложь)
	
	СтруктураНастроек 			= ПараметрыВыгрузки.СтруктураНастроек;
	НастройкаПодключения		= ПараметрыВыгрузки.НастройкаПодключения;
	
	Наименование 				= ПараметрыВыгрузки.Наименование;
	Портал 						= СтруктураНастроек.Портал;
	ИдентификаторПодключения 	= СтруктураНастроек.ИдентификаторПодключения;
	
	
	АдресДоПриложения = Б24_К_RestApiВызовСервера.ПолучитьАдресДоПриложенияБитрикс24()+"?idAdress="+ИдентификаторПодключения;
	
	СформированныеЗапросы = Новый Массив;
	
#Область УдалениеВстроек   
	ДанныеПортала = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/placement.get", ""); 
	Если ДанныеПортала <> Неопределено Тогда
	
		мВстройкиПортала = ДанныеПортала.Получить("result");
		Если мВстройкиПортала <> Неопределено Тогда     
			Для каждого ТекЭлемент Из мВстройкиПортала Цикл   
				
				МестоВстройки = ТекЭлемент.Получить("placement");
				
				Если МестоВстройки = "1C_PAGE" Тогда
					Хэндлер = ТекЭлемент.Получить("handler");
					Если СтрНайти(Хэндлер, "?idAdress="+ИдентификаторПодключения) > 0 Тогда
						СформированныеЗапросы.Добавить("cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("placement.unbind?PLACEMENT="+МестоВстройки+"&HANDLER="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Хэндлер)));
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;  
	КонецЕсли; 
#КонецОбласти
		
	Если НЕ УдалитьВстройки Тогда
		СформированныеЗапросы.Добавить("cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("placement.bind?PLACEMENT=1C_PAGE&HANDLER="+АдресДоПриложения+"&TITLE="+Наименование));
	КонецЕсли;
	
	АдресДоХэндлераОфлайнСобытий = Б24_К_RestApiВызовСервера.ПолучитьАдресДоХэндлераСработкиОфлайнСобытияПриложенияБитрикс24() + "?idAdress="+ИдентификаторПодключения;

	СформированныеЗапросы.Добавить("cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("event.unbind?event=onOfflineEvent&handler="+АдресДоХэндлераОфлайнСобытий));
	
	Если Б24_К_RestApiВызовСервера.РеалтаймИспользуется(НастройкаПодключения) Тогда
		СформированныеЗапросы.Добавить("cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("event.bind?event=onOfflineEvent&handler="+АдресДоХэндлераОфлайнСобытий));
	КонецЕсли;	
	
	ТелоHTTPЗапроса = "";
	Для каждого ТекЭлемент Из СформированныеЗапросы Цикл
		
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + ТекЭлемент;	
		Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
		
	КонецЦикла;
	
	Если (СтрНайти(Портал, ".bitrix24.ru") > 0 ИЛИ СтрНайти(Портал, ".bitrix24.ua") > 0) И НЕ УдалитьВстройки Тогда
		
		АдресУРЛ = "https://integration.bitrix.info/app/1ctotal/v1/event.php";
		НастройкиПодключения = Новый Структура;
		Б24_К_RestApiВызовСервера.РазобратьАдресСайта(АдресУРЛ, НастройкиПодключения);
		
		Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/app/1ctotal/v1/event.php", "&save_placements"); 
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьУдалитьВстройкиБитрикс24ВФоне(ПараметрыВыгрузки, АдресРезультата) Экспорт
	
	ДобавитьУдалитьВстройкиБитрикс24(ПараметрыВыгрузки, ПараметрыВыгрузки.УдалитьВстройки);

	Б24_К_ОбщегоНазначенияВызовСервера.ПроверитьФоновоеЗаданиеВОчереди(ПараметрыВыгрузки);
	
КонецПроцедуры

Функция ПолучитьСписокПользователей()
	
	СпкПользователей = Новый СписокЗначений;
	
	Попытка 
		
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 500
		|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Недействителен = ЛОЖЬ";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда	
			
				ПользовательИБ = Пользователи.СвойстваПользователяИБ(Выборка.ИдентификаторПользователяИБ);
				
				Если ПользовательИБ = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Если НЕ ПользовательИБ.ПоказыватьВСпискеВыбора Тогда
					Продолжить;
				КонецЕсли;
				
			КонецЕсли;
			
			СпкПользователей.Добавить(ПользовательИБ.Имя, ПользовательИБ.ПолноеИмя);
			
		КонецЦикла;
		
	Исключение
		
	КонецПопытки;
	
	Возврат СпкПользователей; 
	
КонецФункции

#КонецОбласти


#Область ОбщиеПроцедурыИФункции

Функция ПолучитьНастройкиСПортала(СтруктураНастроек) Экспорт
	
	мНастройки = Новый Массив;

	Метод = "/rest/app.option.get";
	
	Фильтр = "";
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, Метод, Фильтр); 
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат мНастройки;
	КонецЕсли;
	
	Если СтруктураОтвета <> Неопределено Тогда
		
		result = СтруктураОтвета.Получить("result");
		Если ТипЗнч(result) = Тип("Соответствие") Тогда
			
			ACCOUNTING = result.Получить("ACCOUNTING");
			Если ТипЗнч(ACCOUNTING) = Тип("Соответствие") Тогда
				
				Для каждого ТекКлюч Из ACCOUNTING Цикл
					
					Попытка 						
						
						ОписаниеНастройки = Новый Структура;
						ОписаниеНастройки.Вставить("КлючНастройки", ТекКлюч.Ключ);
						
						ПараметрыНастройки = Новый Структура;
						РекурсивноРазобратьНастройкиНаПортале(ПараметрыНастройки, ТекКлюч.Значение);
						
						ОписаниеНастройки.Вставить("ПараметрыНастройки", ПараметрыНастройки);
						
						мНастройки.Добавить(ОписаниеНастройки);
						
					Исключение
						Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Некорректная ошибка на портале: "+ТекКлюч.Ключ+". Будет пропущена.");	
					КонецПопытки;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат мНастройки; 
	
КонецФункции

Процедура РекурсивноРазобратьНастройкиНаПортале(ПараметрыНастройки, КлючНастроек)
	
	ЗначенияТекКлюча = КлючНастроек;
	Если ТипЗнч(ЗначенияТекКлюча) = Тип("Соответствие") Тогда
		
		Для каждого ТекЗначение Из ЗначенияТекКлюча Цикл
			
			Если ТипЗнч(ТекЗначение.Значение) = Тип("Соответствие") Тогда
				
				ВложенныеНастройки = Новый Структура;
				РекурсивноРазобратьНастройкиНаПортале(ВложенныеНастройки, ТекЗначение.Значение);
				ПараметрыНастройки.Вставить("Ключ_" + ТекЗначение.Ключ, ВложенныеНастройки);
				
			Иначе
				ПараметрыНастройки.Вставить(ТекЗначение.Ключ, ТекЗначение.Значение);
			КонецЕсли;
			
		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьЗапросНастроекРекурсивно(ПараметрыЗапроса, НачалоЗапроса, Данные) Экспорт
	
	Если ТипЗнч(Данные) = Тип("КлючИЗначение") Тогда
		
		КлючПараметра = ?(Лев(Данные.Ключ, 5) = "Ключ_", Прав(Данные.Ключ, стрДлина(Данные.Ключ)-5), Данные.Ключ);
		НовоеНачалоЗапроса = НачалоЗапроса + "["+КлючПараметра+"]";
		
		Если  ТипЗнч(Данные.Значение) = Тип("Строка") Тогда
			ПараметрыЗапроса = ПараметрыЗапроса + НовоеНачалоЗапроса + "=" +Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Данные.Значение);
		Иначе
			СформироватьЗапросНастроекРекурсивно(ПараметрыЗапроса, НовоеНачалоЗапроса, Данные.Значение);	
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Данные) = Тип("Массив") Тогда
		
		НовоеНачалоЗапроса = НачалоЗапроса + "[]";
		
		Для каждого ТекЭлементМассива Из Данные Цикл
			
			Если  ТипЗнч(ТекЭлементМассива) = Тип("Строка") Тогда
				ПараметрыЗапроса = ПараметрыЗапроса + НовоеНачалоЗапроса + "=" +Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекЭлементМассива);
			Иначе
				СформироватьЗапросНастроекРекурсивно(ПараметрыЗапроса, НовоеНачалоЗапроса, ТекЭлементМассива);	
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Данные) = Тип("Структура") Тогда
		
		Для каждого ТекЭлементСтруктуры Из Данные Цикл
			
			КлючПараметра = ?(Лев(ТекЭлементСтруктуры.Ключ, 5) = "Ключ_", Прав(ТекЭлементСтруктуры.Ключ, стрДлина(ТекЭлементСтруктуры.Ключ)-5), ТекЭлементСтруктуры.Ключ);
			НовоеНачалоЗапроса = НачалоЗапроса + "["+КлючПараметра+"]";
			
			Если  ТипЗнч(ТекЭлементСтруктуры.Значение) = Тип("Строка") Тогда
				ПараметрыЗапроса = ПараметрыЗапроса + НовоеНачалоЗапроса + "=" +Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекЭлементСтруктуры.Значение);
			Иначе
				СформироватьЗапросНастроекРекурсивно(ПараметрыЗапроса, НовоеНачалоЗапроса, ТекЭлементСтруктуры.Значение);	
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЗависимостиКлиентовОтТиповКонтрагентовПоУмолчанию() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ТипыКонтрагентовДляКомпаний", Новый Массив);	
	Результат.Вставить("ТипыКонтрагентовДляКонтактов", Новый Массив);	

	Результат.ТипыКонтрагентовДляКомпаний.Добавить(Перечисления.КомпанияЧастноеЛицо.Компания);
	Результат.ТипыКонтрагентовДляКонтактов.Добавить(Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо);

	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли

