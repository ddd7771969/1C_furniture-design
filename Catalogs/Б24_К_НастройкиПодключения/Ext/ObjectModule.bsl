#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПредопределенныеПроцедурыОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка И Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();
		
	Если ПометкаУдаления <> Ссылка.ПометкаУдаления Тогда
		
		Токен = Б24_К_RestApiВызовСервера.ПолучитьТокен(Ссылка);

		Если ЗначениеЗаполнено(Токен) Тогда
				
			СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(Ссылка, Истина);  
				
			Если СтруктураНастроек <> Неопределено Тогда
					
				ПараметрыВыгрузки = Новый Структура;
				ПараметрыВыгрузки.Вставить("НастройкаПодключения"			, Ссылка);
				ПараметрыВыгрузки.Вставить("Наименование"					, Наименование);
				ПараметрыВыгрузки.Вставить("СтруктураНастроек"				, СтруктураНастроек);
				ПараметрыВыгрузки.Вставить("УдалитьВстройки"				, ПометкаУдаления);                   
				
				Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("Справочники.Б24_К_НастройкиПодключения.ДобавитьУдалитьВстройкиБитрикс24ВФоне", "Удаление/Добавление встроек Битрикс24", ПараметрыВыгрузки, 0, Истина); 
					
				Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("РаботаИзОдногоОкна") Тогда
					
					НастройкиИнтеграцииСервисов = РегистрыСведений.Б24_КБ_НастройкиИнтеграцииСервисов.ПолучитьНастройкиИнтеграцииСервисовПоНастройкеПодключения(Ссылка);
					Если НастройкиИнтеграцииСервисов.Настройки <> Неопределено Тогда
						
						НастройкиОбмена = НастройкиИнтеграцииСервисов.Настройки.Получить();
						Если НастройкиОбмена <> Неопределено Тогда
							
							Если НастройкиОбмена.Свойство("СущностиДляРаботыИзОдногоОкна") Тогда
								
								ПараметрыВыгрузки = Новый Структура;
								ПараметрыВыгрузки.Вставить("НастройкаПодключения"			, Ссылка);
								ПараметрыВыгрузки.Вставить("Наименование"					, Наименование);
								ПараметрыВыгрузки.Вставить("СтруктураНастроек"				, СтруктураНастроек);
								ПараметрыВыгрузки.Вставить("СущностиДляРаботыИзОдногоОкна"	, НастройкиОбмена.СущностиДляРаботыИзОдногоОкна);
								ПараметрыВыгрузки.Вставить("УдалитьВстройки"				, ПометкаУдаления);
								
								Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("РегистрыСведений.Б24_КБ_НастройкиИнтеграцииСервисов.ДобавитьУдалитьВстройкиБитрикс24ВФоне", "Удаление/Добавление встроек Битрикс24", ПараметрыВыгрузки, 0, Истина);  
								
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("Автоматизация") И НЕ ПометкаУдаления Тогда
					
					НастройкиАвтоматизации = РегистрыСведений.Б24_КА_НастройкиАвтоматизации.ПолучитьНастройкуАвтоматизацииПоНастройкеПодключения(Ссылка);
					
					ПараметрыВыгрузки = Новый Структура;
					ПараметрыВыгрузки.Вставить("НастройкаПодключения"		, Ссылка);
					ПараметрыВыгрузки.Вставить("Наименование"				, Наименование);
					ПараметрыВыгрузки.Вставить("СтруктураНастроек"			, СтруктураНастроек);
					ПараметрыВыгрузки.Вставить("ИспользоватьРоботов"		, НастройкиАвтоматизации.ИспользоватьРоботов);
					ПараметрыВыгрузки.Вставить("ИспользоватьТриггеры"		, НастройкиАвтоматизации.ИспользоватьТриггеры);
					ПараметрыВыгрузки.Вставить("УдалитьВстройки"			, ПометкаУдаления);
					
					Б24_К_ОбщегоНазначенияВызовСервера.ЗапуститьЗадание("РегистрыСведений.Б24_КА_НастройкиАвтоматизации.ДобавитьУдалитьВстройкиБитрикс24ВФоне", "Удаление/Добавление встроек Битрикс24", ПараметрыВыгрузки, 0, Истина);  
					
				КонецЕсли;
				
				Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") И НЕ ПометкаУдаления Тогда
					
					УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
					ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
					ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка регистрации изменений в Битрикс24";
					ПараметрыВыполнения.ЗапуститьВФоне = Истина;
					
					ПараметрыВыгрузки = Новый Структура;
					ПараметрыВыгрузки.Вставить("НастройкаПодключения"	, Ссылка);
					ПараметрыВыгрузки.Вставить("НастройкаУдалена"		, ПометкаУдаления);
					                                 
					ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.Б24_КС_НастройкиСинхронизации.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
					
				КонецЕсли;
			
			КонецЕсли;

		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка И Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		Возврат;
	КонецЕсли;

	Если ПометкаУдаления Тогда
		
		Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("Автоматизация") Тогда
			ЗаписьНастройкиСинхронизации = РегистрыСведений.Б24_КА_НастройкиАвтоматизации.СоздатьМенеджерЗаписи();
			ЗаписьНастройкиСинхронизации.НастройкаПодключения = Ссылка;
			ЗаписьНастройкиСинхронизации.Прочитать();
			ЗаписьНастройкиСинхронизации.Удалить();
		КонецЕсли;
		
		Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("СинхронизацияДанных") Тогда
			ЗаписьНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.СоздатьМенеджерЗаписи();
			ЗаписьНастройкиСинхронизации.НастройкаПодключения = Ссылка;
			ЗаписьНастройкиСинхронизации.Прочитать();
			ЗаписьНастройкиСинхронизации.Удалить();
		КонецЕсли;
		
		
		Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("ИнтеграцияСервисов") Тогда
			ЗаписьНастройкиСинхронизации = РегистрыСведений.Б24_КБ_НастройкиИнтеграцииСервисов.СоздатьМенеджерЗаписи();
			ЗаписьНастройкиСинхронизации.НастройкаПодключения = Ссылка;
			ЗаписьНастройкиСинхронизации.Прочитать();
			ЗаписьНастройкиСинхронизации.Удалить();    
			
			Выборка = РегистрыСведений.Б24_КБ_ОбъектыСозданныеЧерезСлайдер.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Выборка.НастройкаПодключения = Ссылка ИЛИ Выборка.Портал = Портал Тогда
					Выборка.ПолучитьМенеджерЗаписи().Удалить();
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли