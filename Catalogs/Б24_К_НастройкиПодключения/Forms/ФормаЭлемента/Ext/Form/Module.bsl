
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Ключ") Тогда
		
		НастройкаПодключения = Параметры.Ключ;
		
		Наименование 				= НастройкаПодключения.Наименование;
		Портал 						= НастройкаПодключения.Портал;
		ВходящийВебхук 				= НастройкаПодключения.ВходящийВебхук;
		ИдентификаторПодключения 	= НастройкаПодключения.ИдентификаторПодключения;
		ТелефонияБитрикс24 			= НастройкаПодключения.ТелефонияБитрикс24;
		ЭкспортПечатныхФормОтчетов	= НастройкаПодключения.ЭкспортПечатныхФормОтчетов;
		ОбсужденияВЧатах			= НастройкаПодключения.ОбсужденияВЧатах;
		НеОбновлятьДелаБитрикс24	= НастройкаПодключения.НеОбновлятьДелаБитрикс24;
		НеВыгружатьАдресаРеквизитов	= НастройкаПодключения.НеВыгружатьАдресаРеквизитов;
		НеЗагружатьАдресаРеквизитов	= НастройкаПодключения.НеЗагружатьАдресаРеквизитов;

		ОповещатьПользователейБитрикс24ОбОшибках	= НастройкаПодключения.ОповещатьПользователейБитрикс24ОбОшибках;
		ПолучателиДанныхОбОшибках.ЗагрузитьЗначения(НастройкаПодключения.ПолучателиДанныхОбОшибках.ВыгрузитьКолонку("ИдентификаторПользователяБитрикс24"));
		ФильтрыСообщенийОбОшибках.ЗагрузитьЗначения(НастройкаПодключения.ФильтрыСообщенийОбОшибках.ВыгрузитьКолонку("КлючевойТекст"));
		ОграниченияНаВыгрузкуДелСозданныхПоОснованию.ЗагрузитьЗначения(НастройкаПодключения.ОграниченияНаВыгрузкуДелСозданныхПоОснованию.ВыгрузитьКолонку("ПолноеИмяОбъекта"));		
		
		ПресетыШаблоновРеквизитов.Загрузить(НастройкаПодключения.ПресетыШаблоновРеквизитов.Выгрузить());
		ПресетыСтавокНДС.Загрузить(НастройкаПодключения.ПресетыСтавокНДС.Выгрузить());
		
		Для каждого ТекСтрока Из НастройкаПодключения.ЗависимостиКлиентовОтТиповКонтрагентов Цикл
			Если ТекСтрока.ВидКлиента = "Контакт" Тогда
				ТипыКонтрагентовДляКонтактов.Добавить(ТекСтрока.Тип);	
			Иначе
				ТипыКонтрагентовДляКомпаний.Добавить(ТекСтрока.Тип);	
			КонецЕсли;
		КонецЦикла;
		
		Для каждого ТекСтрока Из НастройкаПодключения.ПресетыТиповКлиентов Цикл
			
			Если ТекСтрока.ВидКлиента = "Контакт" Тогда
				НоваяСтрока = ПресетыТиповКонтактов.Добавить();	
			Иначе
				НоваяСтрока = ПресетыТиповКомпаний.Добавить();	
			КонецЕсли;   
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			
		КонецЦикла;      
		
		Токен = Б24_К_RestApiВызовСервера.ПолучитьТокен(НастройкаПодключения);

		ОбновитьСписокПользователейБитрикс24();		
		
	Иначе
		
		Наименование 	= "1С";
		
		Пока Истина Цикл
			
			ГСЧ = Новый ГенераторСлучайныхЧисел;
			ИдентификаторПодключения = Формат(ГСЧ.СлучайноеЧисло(2,1000), "ЧГ=0");
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ИдентификаторПодключения", ИдентификаторПодключения);
			Запрос.Текст = "ВЫБРАТЬ
			|	Б24_К_НастройкиПодключения.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
			|ГДЕ
			|	Б24_К_НастройкиПодключения.ИдентификаторПодключения = &ИдентификаторПодключения";
			ВыполненныйЗапрос = Запрос.Выполнить();
			Если ВыполненныйЗапрос.Пустой() Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ЗависимостиКлиентовОтТиповКонтрагентовПоУмолчанию = ЗависимостиКлиентовОтТиповКонтрагентовПоУмолчанию();
		ТипыКонтрагентовДляКомпаний.ЗагрузитьЗначения(ЗависимостиКлиентовОтТиповКонтрагентовПоУмолчанию.ТипыКонтрагентовДляКомпаний);
		ТипыКонтрагентовДляКонтактов.ЗагрузитьЗначения(ЗависимостиКлиентовОтТиповКонтрагентовПоУмолчанию.ТипыКонтрагентовДляКонтактов);
					
		Модифицированность = Истина;
		
	КонецЕсли;

КонецПроцедуры


&НаКлиенте    
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОтображениеЭлементовФормы();
	
	Элементы.НадписьВерсияМодуля.Заголовок = "Версия модуля: " + Б24_К_ОбщегоНазначенияВызовСервера.Версия();

	ДобавитьИменаПрикладныхОбъектовВФильтрДел();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ТелефонияБитрикс24 И НЕ ЗавершениеРаботы Тогда
		
		ОбновитьПовторноИспользуемыеЗначения();
		
		Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("Телефония") Тогда
			
			МодульТелефонияВызовСервера = ОбщегоНазначенияКлиент.ОбщийМодуль("Б24_К_ТелефонияВызовСервера");
			ТелефонияБитрикс24 =  МодульТелефонияВызовСервера.ПолучитьКоличествоНастроекПодключенияСТелефонией() > 0; 
			Если ТелефонияБитрикс24 = Истина Тогда
				МодульТелефонияКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("Б24_К_ТелефонияКлиент");
				Б24_К_НастройкиСеанса.НастройкиТелефонии.МессенджерУстановлен = МодульТелефонияКлиент.ПроверитьНаличиеМессенджераБитрикс24();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		
		Отказ = Истина; 
		
		Ошибка = Ложь;
		Если НЕ ЗначениеЗаполнено(ИдентификаторПодключения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан идентификатор подключения");
			Ошибка = Истина;	
		КонецЕсли;
		
		Если Ошибка Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Модуль не будет корректно работать");
		КонецЕсли;
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НужноЗакрытьОкно = Истина;
		
		СохранитьВыгрузитьНастройки(Истина);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		НужноЗакрытьОкно = Истина;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СохранитьВыгрузитьНастройки(ЗакрытьПоОкончании = Ложь)
	
	НастройкаСохранена = СохранитьНастройкуСервер();
	
	Если НастройкаСохранена И ВыгружатьНастройкиНаПортал Тогда
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НастройкаПодключения, "ПометкаУдаления") = Ложь Тогда
			
			СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
			
			Если СтруктураНастроек = Неопределено Тогда
				ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
				Возврат Ложь;
			КонецЕсли;
			
			СтруктураНастроек.Логирование.Измерение2 = "Выгрузка настроек для настройки подключения";
	
			ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании);
			
			ВыгружатьНастройкиНаПортал = Ложь;
			
		Иначе
			Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
		КонецЕсли;
		
		ОбновитьИнтерфейс(); 
		
		Оповестить("СозданаИзмененаНастройка");
		
	Иначе
		Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкаСохранена
	
КонецФункции

&НаСервере
Функция СохранитьНастройкуСервер()
	
	Если Модифицированность Тогда
		
		Если НастройкаПодключения.Пустая() Тогда
			ТекущийОбъект = Справочники.Б24_К_НастройкиПодключения.СоздатьЭлемент();
		Иначе
			ТекущийОбъект = НастройкаПодключения.ПолучитьОбъект();	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Портал) Тогда 
			ОбщегоНазначения.СообщитьПользователю("Не указан портал. Записать настройку подключения невозможно.");
			Возврат Ложь;	
		КонецЕсли;
		
		ТекущийОбъект.Наименование 	 				= Наименование;
		ТекущийОбъект.Портал 		 				= Портал;
		ТекущийОбъект.ВходящийВебхук 		 		= ВходящийВебхук;
		ТекущийОбъект.ИдентификаторПодключения 		= ИдентификаторПодключения;
		ТекущийОбъект.ТелефонияБитрикс24 			= ТелефонияБитрикс24;
		ТекущийОбъект.ЭкспортПечатныхФормОтчетов 	= ЭкспортПечатныхФормОтчетов; 
		ТекущийОбъект.ОбсужденияВЧатах 				= ОбсужденияВЧатах; 
		ТекущийОбъект.НеОбновлятьДелаБитрикс24 		= НеОбновлятьДелаБитрикс24;    
		ТекущийОбъект.НеВыгружатьАдресаРеквизитов 	= НеВыгружатьАдресаРеквизитов;    
		ТекущийОбъект.НеЗагружатьАдресаРеквизитов 	= НеЗагружатьАдресаРеквизитов;    
		
		ТекущийОбъект.ОповещатьПользователейБитрикс24ОбОшибках 	= ОповещатьПользователейБитрикс24ОбОшибках;    
		
		ТекущийОбъект.ПолучателиДанныхОбОшибках.Очистить();
		Для каждого ТекЭлемент Из ПолучателиДанныхОбОшибках Цикл   
			ТекущийОбъект.ПолучателиДанныхОбОшибках.Добавить().ИдентификаторПользователяБитрикс24 = ТекЭлемент.Значение;	
		КонецЦикла;
		
		ТекущийОбъект.ФильтрыСообщенийОбОшибках.Очистить();
		Для каждого ТекЭлемент Из ФильтрыСообщенийОбОшибках Цикл   
			ТекущийОбъект.ФильтрыСообщенийОбОшибках.Добавить().КлючевойТекст = ТекЭлемент.Значение;	
		КонецЦикла;
		
		ТекущийОбъект.ОграниченияНаВыгрузкуДелСозданныхПоОснованию.Очистить();
		Для каждого ТекЭлемент Из ОграниченияНаВыгрузкуДелСозданныхПоОснованию Цикл   
			ТекущийОбъект.ОграниченияНаВыгрузкуДелСозданныхПоОснованию.Добавить().ПолноеИмяОбъекта = ТекЭлемент.Значение;	
		КонецЦикла;
		
		ТекущийОбъект.ПресетыШаблоновРеквизитов.Очистить();
		Для каждого ТекСтрока Из ПресетыШаблоновРеквизитов Цикл   
			НоваяСтрока = ТекущийОбъект.ПресетыШаблоновРеквизитов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		КонецЦикла;
		
		ТекущийОбъект.ПресетыСтавокНДС.Очистить();
		Для каждого ТекСтрока Из ПресетыСтавокНДС Цикл   
			НоваяСтрока = ТекущийОбъект.ПресетыСтавокНДС.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		КонецЦикла;
		
		ТекущийОбъект.ЗависимостиКлиентовОтТиповКонтрагентов.Очистить();   
		Для каждого ТекЭлемент Из ТипыКонтрагентовДляКомпаний Цикл   
			НоваяСтрока = ТекущийОбъект.ЗависимостиКлиентовОтТиповКонтрагентов.Добавить();  
			НоваяСтрока.ВидКлиента = "Компания";
			НоваяСтрока.Тип = ТекЭлемент.Значение; 
		КонецЦикла;
		Для каждого ТекЭлемент Из ТипыКонтрагентовДляКонтактов Цикл   
			НоваяСтрока = ТекущийОбъект.ЗависимостиКлиентовОтТиповКонтрагентов.Добавить();  
			НоваяСтрока.ВидКлиента = "Контакт";
			НоваяСтрока.Тип = ТекЭлемент.Значение; 
		КонецЦикла;
		
		ТекущийОбъект.ПресетыТиповКлиентов.Очистить();
		Для каждого ТекСтрока Из ПресетыТиповКомпаний Цикл   
			НоваяСтрока = ТекущийОбъект.ПресетыТиповКлиентов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);   
			НоваяСтрока.ВидКлиента = "Компания";
		КонецЦикла;
		Для каждого ТекСтрока Из ПресетыТиповКонтактов Цикл   
			НоваяСтрока = ТекущийОбъект.ПресетыТиповКлиентов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);      
			НоваяСтрока.ВидКлиента = "Контакт";
		КонецЦикла;
		
		
		Попытка 
			
			ТекущийОбъект.Записать();
			НастройкаПодключения = ТекущийОбъект.Ссылка; 
			
			Б24_К_RestApiВызовСервера.ЗаписатьНовыйТокен(НастройкаПодключения, Токен);
			
			ТекущийОбъект.Записать();    
			
			Токен = Б24_К_RestApiВызовСервера.ПолучитьТокен(НастройкаПодключения);
			
			//Если ОповещатьПользователейБитрикс24ОбОшибках Тогда
			//	Б24_К_ОбщегоНазначенияВызовСервера.ОбновитьЧат(ТекущийОбъект.Ссылка);	
			//КонецЕсли;
			
			//Константы.Б24_КЭ_ОбсудитьВЧате.Установить(ОбсужденияВЧатах);
			
			Модифицированность = Ложь;
			                                                           
		Исключение
			Возврат Ложь;	
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции  

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ЕстьШаблоны = Ложь;		
		
	Для каждого ТекСтрока Из ПресетыШаблоновРеквизитов Цикл
			
		Если ЗначениеЗаполнено(ТекСтрока.ТипКонтрагента) Тогда
			ЕстьШаблоны = Истина;
			Прервать;
		КонецЕсли;					
	КонецЦикла;
	
	Если НЕ ЕстьШаблоны  Тогда	
		ПоказатьОповещениеПользователя("Важно",,"Не настроено сопоставление шаблонов реквизитов Битрикс24 и типов контрагентов 1С",, СтатусОповещенияПользователя.Важное);			
	КонецЕсли;			
	
КонецПроцедуры 

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)

	ЕстьОшибкаЗаполнения = ПроверитьЗаполнениеПресетовНДС();
	ЕстьОшибкаЗаполнения = ЕстьОшибкаЗаполнения ИЛИ ПроверитьЗаполнениеПресетовШаблоновРеквизитов();
	
	Если НЕ ЕстьОшибкаЗаполнения Тогда
		СохранитьВыгрузитьНастройки(); 
	КонецЕсли;      
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
                                                         
	ЕстьОшибкаЗаполнения = ПроверитьЗаполнениеПресетовНДС();
	ЕстьОшибкаЗаполнения = ЕстьОшибкаЗаполнения ИЛИ ПроверитьЗаполнениеПресетовШаблоновРеквизитов();     
	
	Если НЕ ЕстьОшибкаЗаполнения Тогда
		СохранитьВыгрузитьНастройки(Истина);    
	КонецЕсли;      
	
КонецПроцедуры


&НаКлиенте
Процедура КнопкаУдалить(Команда)
	
	Если ЗначениеЗаполнено(НастройкаПодключения) Тогда	
		
		КнопкаУдалитьНаСервере();
				
		Оповестить("СозданаИзмененаНастройка");

		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КнопкаУдалитьНаСервере()
	
	ТекущаяНастройка = НастройкаПодключения.ПолучитьОбъект();
	ТекущаяНастройка.УстановитьПометкуУдаления(НЕ НастройкаПодключения.ПометкаУдаления);	
	ТекущаяНастройка.Записать();	

	Модифицированность = Ложь;
	ВыгружатьНастройкиНаПортал = Истина;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьНДС(Команда)
	
	Если Модифицированность Тогда 
		
		Обещание = ВопросАсинх("Требуется сохранить настройку. Сохранить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Результат = Ждать обещание;
		
		Если Результат <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;   
		
		СохранитьВыгрузитьНастройки();
		
	КонецЕсли;
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка ставок НДС на портале.";
	
	ТелоHTTPЗапроса = "&filter[active]=Y";	
	ДанныеНДС = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/catalog.vat.list", "id", ТелоHTTPЗапроса, "vats");	
	Для каждого ТекЭлемент Из ДанныеНДС Цикл
		
		ИдЭлемента 	= Формат(ТекЭлемент.Получить("id"), "ЧГ=0");
		СтавкаБ24 	= ТекЭлемент.Получить("rate");
		НаименованиеНДС= ТекЭлемент.Получить("name");
		
		Если СтавкаБ24 = Неопределено Тогда
			СтавкаНДС = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтавкуНДСПоСтавке("Без НДС");
		Иначе
			СтавкаНДС = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьСтавкуНДСПоСтавке(СтавкаБ24); 
		КонецЕсли;
			
		НайденныеСтроки = ПресетыСтавокНДС.НайтиСтроки(Новый Структура("ИдСтавки", ИдЭлемента));  
		Если НайденныеСтроки.Количество() = 0 Тогда
			НайденныеСтроки = ПресетыСтавокНДС.НайтиСтроки(Новый Структура("СтавкаНДС", СтавкаНДС));  
		КонецЕсли;
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока = ПресетыСтавокНДС.Добавить();  
			НоваяСтрока.ИдСтавки 	= ИдЭлемента;
			НоваяСтрока.СтавкаНДС 	= СтавкаНДС;
			НоваяСтрока.НаименованиеСтавкиНДС = НаименованиеНДС;
		Иначе
			НайденнаяСтрока = НайденныеСтроки[0];
			НайденнаяСтрока.НаименованиеСтавкиНДС = НаименованиеНДС;
		КонецЕсли;
		
	КонецЦикла;    
	
	Если Команда <> Неопределено Тогда
		Модифицированность = Истина; 
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыгрузитьНДС(Команда)
	
	Если Модифицированность Тогда 
		
		Обещание = ВопросАсинх("Требуется сохранить настройку. Сохранить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Результат = Ждать обещание;
		
		Если Результат <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;   
		
		СохранитьВыгрузитьНастройки();
		
	КонецЕсли;
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Выгрузка ставок НДС";

	ВыгрузитьНДССервер(СтруктураНастроек);
	
	ЗагрузитьНДС(Неопределено);	
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьНДССервер(СтруктураНастроек)
	
	СформированныеЗапросы = Новый Массив;
	
	Для каждого ТекСтрока Из ПресетыСтавокНДС Цикл
		
		СтавкаНДС = ТекСтрока.СтавкаНДС;
		Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда
			Продолжить;	
		КонецЕсли;
		
		КлючСтавкиНДС	= XMLСтрока(СтавкаНДС);	
	    РеквизитыСтавки	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтавкаНДС, "Наименование, Ставка");
		
		Поля = Новый Структура;
		Если НЕ ЗначениеЗаполнено(РеквизитыСтавки.Наименование) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "У ставки НДС: " + РеквизитыСтавки.Наименование + ". Пустое Наименование, ставка не будет выгружена в Битрикс24");
			Продолжить;	
		КонецЕсли;
	
		Поля.Вставить("name"	, РеквизитыСтавки.Наименование);  	
		Поля.Вставить("rate"	, РеквизитыСтавки.Ставка);
		Поля.Вставить("active"	, "Y");
	
		ТелоЗапроса = "";
		Для каждого ТекПоле Из Поля Цикл
			ТелоЗапроса = ТелоЗапроса + ?(СтрДлина(ТелоЗапроса) >0, "&", "") +"fields["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
		КонецЦикла;  
	
		ИдСтавки = ТекСтрока.ИдСтавки;	
		Если НЕ ЗначениеЗаполнено(ИдСтавки) Тогда
			СформированныеЗапросы.Добавить("cmd["+КлючСтавкиНДС+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("catalog.vat.add?" + ТелоЗапроса));	
		Иначе
			СформированныеЗапросы.Добавить("cmd["+КлючСтавкиНДС+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("catalog.vat.update?id=" + ИдСтавки + "&"  + ТелоЗапроса));	
		КонецЕсли;
		
	КонецЦикла;
	
	КоличествоВЗапросе 	= 0;	
	ТелоHTTPЗапроса		= "";
	Для каждого ТекЭлемент Из СформированныеЗапросы Цикл
		
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + ТекЭлемент;	
		
		КоличествоВЗапросе = КоличествоВЗапросе + 1;
		Если КоличествоВЗапросе = 50 Тогда
			
			ДанныеБитрикс24 = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
			
			КоличествоВЗапросе 	= 0;	
			ТелоHTTPЗапроса 	= "";     
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если КоличествоВЗапросе > 0 Тогда
		ДанныеБитрикс24 = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);  
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьПресетыШаблоновСПортала(Команда)
	
	Если Модифицированность Тогда 
		
		Обещание = ВопросАсинх("Требуется сохранить настройку. Сохранить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Результат = Ждать обещание;
		
		Если Результат <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;   
		
		СохранитьВыгрузитьНастройки();
		
	КонецЕсли;
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Загрузка пресетов шаблонов";
	
	ТелоHTTPЗапроса = "&filter[ENTITY_TYPE_ID]=8&filter[ACTIVE]=Y";	
	ДанныеПресетов = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/crm.requisite.preset.list", "ID", ТелоHTTPЗапроса);	
	Для каждого ТекЭлемент Из ДанныеПресетов Цикл
		
		НайденныеСтроки = ПресетыШаблоновРеквизитов.НайтиСтроки(Новый Структура("ИдШаблона", ТекЭлемент.Получить("ID")));
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока = ПресетыШаблоновРеквизитов.Добавить();
			НоваяСтрока.НаименованиеШаблона = ТекЭлемент.Получить("NAME");
			НоваяСтрока.ИдШаблона = Формат(ТекЭлемент.Получить("ID"), "ЧГ=0");
			
		КонецЕсли;
		
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры


&НаКлиенте
Асинх Процедура ЗагрузитьТипыКомпаний(Команда)
	
	Если Модифицированность Тогда 
		
		Обещание = ВопросАсинх("Требуется сохранить настройку. Сохранить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Результат = Ждать обещание;
		
		Если Результат <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;   
		
		СохранитьВыгрузитьНастройки();
		
	КонецЕсли;

	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
		Возврат;
	КонецЕсли;
		
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Загрузка типов компаний";
	
	Фильтр = "";
	ДанныеСтатусов = Б24_К_RestApiВызовСервера.ПолучитьМассивСтатусовCRM(СтруктураНастроек, Фильтр); 
	Для каждого ТекЭлемент Из ДанныеСтатусов Цикл
			
		Если НЕ ТекЭлемент.Получить("ENTITY_ID") = "COMPANY_TYPE" Тогда
			Продолжить;
		КонецЕсли;
		
		ИдТипа = Формат(ТекЭлемент.Получить("STATUS_ID"), "ЧГ=0");
		
		НайденныеСтроки = ПресетыТиповКомпаний.НайтиСтроки(Новый Структура("ИдТипа", ИдТипа));
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока = ПресетыТиповКомпаний.Добавить();
			НоваяСтрока.НаименованиеТипа = Строка(ТекЭлемент.Получить("NAME"));
			НоваяСтрока.ИдТипа =  ИдТипа;
		КонецЕсли;
		
	КонецЦикла;

	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьТипыКонтактов(Команда)
	
	Если Модифицированность Тогда 
		
		Обещание = ВопросАсинх("Требуется сохранить настройку. Сохранить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Результат = Ждать обещание;
		
		Если Результат <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;   
		
		СохранитьВыгрузитьНастройки();
		
	КонецЕсли;
	
	СтруктураНастроек = Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
		Возврат;
	КонецЕсли;
		
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Загрузка типов контактов";
	                                   
	Фильтр = "";
	ДанныеСтатусов = Б24_К_RestApiВызовСервера.ПолучитьМассивСтатусовCRM(СтруктураНастроек, Фильтр); 
	Для каждого ТекЭлемент Из ДанныеСтатусов Цикл
		
		Если НЕ ТекЭлемент.Получить("ENTITY_ID") = "CONTACT_TYPE" Тогда
			Продолжить;
		КонецЕсли;
		
		ИдТипа = Формат(ТекЭлемент.Получить("STATUS_ID"), "ЧГ=0");
		
		НайденныеСтроки = ПресетыТиповКонтактов.НайтиСтроки(Новый Структура("ИдТипа", ИдТипа));
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока = ПресетыТиповКонтактов.Добавить();
			НоваяСтрока.НаименованиеТипа = Строка(ТекЭлемент.Получить("NAME"));
			НоваяСтрока.ИдТипа =  ИдТипа;
		КонецЕсли;
		
	КонецЦикла;

	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюЛицензииБитрикс24(Команда)
	Б24_К_СлужебныйВызовСервера.ОбновитьИнформациюОЛицензии(НастройкаПодключения);
КонецПроцедуры 

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЭлементПриИзменении(Элемент)
	ВыгружатьНастройкиНаПортал = Истина;  
	ОбновитьСписокПользователейБитрикс24();		
	ОбновитьОтображениеЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ТокенОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ОбщиеНастройки = Новый  Структура;
	ОбщиеНастройки.Вставить("НастройкаПодключения"	, Неопределено);
	ОбщиеНастройки.Вставить("Портал"				, "");
	ОбщиеНастройки.Вставить("ТипыСообщений"			, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыСообщений());
	
	Б24_К_RestApiВызовСервера.ДобавлениеНастроекАвторизации(ОбщиеНастройки);
	
	Б24_К_RestApiВызовСервера.ПолучитьТокенДляСоединения(ОбщиеНастройки, Ложь, Текст);
	
	РезультатРазбора = Б24_К_RestApiВызовСервера.РазобратьАдресСайта(ОбщиеНастройки.НастройкиПодключения.АдресСайта, ОбщиеНастройки);
	
	Если РезультатРазбора = Истина Тогда
		
		УжеЕстьТакаяНастройка = ПроверитьНастройкуПоПорталуНаДубль(ОбщиеНастройки.Сервер);
		
		Если УжеЕстьТакаяНастройка Тогда
			Портал	= "";
			Токен 	= "";
			ПоказатьОповещениеПользователя("Настройка с таким порталом уже существует. Создать еще одну нельзя.");
		Иначе
			Портал 	= ОбщиеНастройки.Сервер;
			Токен 	= Текст;
			
		КонецЕсли; 
		
		ДанныеОЛицензиях = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ДополнительнаяИнформация");
		Если ТипЗнч(ДанныеОЛицензиях) <> Тип("Соответствие") Тогда ДанныеОЛицензиях = Новый Соответствие КонецЕсли;
		
		ИнформацияОЛицензии = Новый Структура;
		ИнформацияОЛицензии.Вставить("Дата", Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса()-60*60*24); 
		ИнформацияОЛицензии.Вставить("Ключ", "");
		
		ДанныеОЛицензиях.Вставить(НастройкаПодключения, ИнформацияОЛицензии); 
		Б24_К_ОбщегоНазначенияВызовСервера.УстановитьЗначениеВХранилищаНастроек("ДополнительнаяИнформация", ДанныеОЛицензиях);   
		
	Иначе
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОТарифеЭкспортПечатныхФормНажатие(Элемент)
	Б24_К_СлужебныйКлиент.ОповеститьОбНедоступностиФункционалаЭкспортаПечатныхФорм();
КонецПроцедуры

&НаКлиенте
Процедура ОТарифеТелефонияНажатие(Элемент)
	Б24_К_СлужебныйКлиент.ОповеститьОбНедоступностиФункционалаТелефонияПоТарифномуПлану();
КонецПроцедуры

&НаКлиенте
Процедура ОповещатьПользователейБитрикс24ОбОшибкахПриИзменении(Элемент)
	ОбновитьОтображениеЭлементовФормы();
КонецПроцедуры      

&НаКлиенте
Процедура ИдентификаторПодключенияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если НЕ ЭтоЧисло(Текст) Тогда  
		СтандартнаяОбработка = Ложь;	 
		
		ДанныеПриОтказе = Новый СписокЗначений;
		ДанныеПриОтказе.Добавить(ИдентификаторПодключения, ИдентификаторПодключения);
		ДанныеВыбора 			= ДанныеПриОтказе;  
		
		ПоказатьПредупреждение(, "Можно вводить только числа");
	КонецЕсли;  
	
КонецПроцедуры 


&НаКлиенте
Процедура СоответствияТиповКонтактовПокупательПриИзменении(Элемент)
	ПроверкаДанныхКолонкиТаблицыСоответствийТиповКлиентов(Элементы.СоответствияТиповКонтактов.ТекущиеДанные, ПресетыТиповКонтактов, "Клиент");	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияТиповКонтактовПоставщикПриИзменении(Элемент)
	ПроверкаДанныхКолонкиТаблицыСоответствийТиповКлиентов(Элементы.СоответствияТиповКонтактов.ТекущиеДанные, ПресетыТиповКонтактов, "Поставщик");	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияТиповКонтактовПрочиеОтношенияПриИзменении(Элемент)
	ПроверкаДанныхКолонкиТаблицыСоответствийТиповКлиентов(Элементы.СоответствияТиповКонтактов.ТекущиеДанные, ПресетыТиповКонтактов, "ПрочиеОтношения");	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияТиповКомпанийПокупательПриИзменении(Элемент)
	ПроверкаДанныхКолонкиТаблицыСоответствийТиповКлиентов(Элементы.СоответствияТиповКомпаний.ТекущиеДанные, ПресетыТиповКомпаний, "Клиент");	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияТиповКомпанийПоставщикПриИзменении(Элемент) 
	ПроверкаДанныхКолонкиТаблицыСоответствийТиповКлиентов(Элементы.СоответствияТиповКомпаний.ТекущиеДанные, ПресетыТиповКомпаний, "Поставщик");
КонецПроцедуры

&НаКлиенте
Процедура СоответствияТиповКомпанийПрочиеОтношенияПриИзменении(Элемент)
	ПроверкаДанныхКолонкиТаблицыСоответствийТиповКлиентов(Элементы.СоответствияТиповКомпаний.ТекущиеДанные, ПресетыТиповКомпаний, "ПрочиеОтношения");	
КонецПроцедуры

&НаКлиенте
Процедура ТипыКонтрагентовКомпанииКонтактаЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если НЕ (ТипыКонтрагентовДляКомпаний.НайтиПоЗначению(ВыбранноеЗначение) = Неопределено И ТипыКонтрагентовДляКонтактов.НайтиПоЗначению(ВыбранноеЗначение) = Неопределено) Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(,"Нельзя указать один и тот же тип контрагента 2 раза");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ВыгрузкаНастроек

&НаКлиенте
Процедура ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании)
	
	ДлительнаяОперацияЗавершение = Новый ОписаниеОповещения("ПослеВыгрузкиНастроек", ЭтаФорма, ЗакрытьПоОкончании);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ТекстСообщения             = "Выполняется выгрузка настроек..";
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания       = Истина;
	ПараметрыОжидания.ВыводитьСообщения          = Истина;
	
	ДлительнаяОперация = НачатьВыгрузкуНастройки(СтруктураНастроек);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ДлительнаяОперацияЗавершение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыгрузкиНастроек(Результат, ПараметрыВыгрузки) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность = Ложь;
	КонецЕсли;
	
	Если ПараметрыВыгрузки = Истина Тогда
		Закрыть();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьВыгрузкуНастройки(СтруктураНастроек)
	
	УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка настроек Битрикс24";
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыВыгрузки.Вставить("Наименование"			, Строка(НастройкаПодключения));
	ПараметрыВыгрузки.Вставить("СтруктураНастроек"		, СтруктураНастроек);
	                                 
	Возврат ДлительныеОперации.ВыполнитьВФоне("Справочники.Б24_К_НастройкиПодключения.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти


#Область Прочие

&НаСервере
Функция ПроверитьНастройкуПоПорталуНаДубль(Портал)
	
	Результат = Ложь;
	
	Выборка = Справочники.Б24_К_НастройкиПодключения.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если НЕ Выборка.ПометкаУдаления И Выборка.Портал = Портал И Выборка.Ссылка <> НастройкаПодключения Тогда
			Результат = Истина;
			Возврат Результат;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьОтображениеЭлементовФормы()
	
	Элементы.ПолучателиДанныхОбОшибках.Доступность = ОповещатьПользователейБитрикс24ОбОшибках;
	Элементы.ФильтрыСообщенийОбОшибках.Доступность = ОповещатьПользователейБитрикс24ОбОшибках;       
	
    ПреобразоватьОтображениеПользователейБитрикс24(); 
	 
КонецПроцедуры

&НаСервере
Процедура ПреобразоватьОтображениеПользователейБитрикс24()
	
	Элемент_ПолеТаблицы = Элементы.ПолучателиДанныхОбОшибкахЗначение;
    ПутьКДаннымОбъекта = "ПолучателиДанныхОбОшибках.Значение";
    
    Для каждого ЗначениеСписка Из Элемент_ПолеТаблицы.СписокВыбора Цикл
        
        Элемент = УсловноеОформление.Элементы.Добавить();
        ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
        ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элемент_ПолеТаблицы.Имя);
        
        ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
        ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДаннымОбъекта);
        ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
        ОтборЭлемента.ПравоеЗначение = ЗначениеСписка.Значение;
        
        Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ЗначениеСписка.Представление);
        
	КонецЦикла;	        
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокПользователейБитрикс24()
	
	Элементы.ПолучателиДанныхОбОшибкахЗначение.СписокВыбора.Очистить();
	Выборка = РегистрыСведений.Б24_К_ПользователиБитрикс24.Выбрать(Новый Структура("Портал",Портал));  
	 
	Пока Выборка.Следующий() Цикл 
		Элементы.ПолучателиДанныхОбОшибкахЗначение.СписокВыбора.Добавить(Выборка.Идентификатор, Выборка.Наименование);   
	КонецЦикла;
	 
КонецПроцедуры     

&НаКлиенте
Функция ЭтоЧисло(Знач СтрокаПроверки)
	
	Для НомерСимвола = 1 По СтрДлина(СтрокаПроверки) Цикл
		
		КодСимвола = КодСимвола(Сред(СтрокаПроверки, НомерСимвола, 1));
		
		Если НЕ ((КодСимвола >= 48 И КодСимвола <= 57)
			ИЛИ КодСимвола = 44 ИЛИ КодСимвола = 46) Тогда
			Возврат Ложь;
		КонецЕсли;

	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции   

&НаСервере
Процедура ДобавитьИменаПрикладныхОбъектовВФильтрДел()
	
	Для каждого ТекЭлемент Из Метаданные.Документы Цикл     
		Элементы.ОграниченияНаВыгрузкуДелСозданныхПоОснованиюЗначение.СписокВыбора.Добавить(ТекЭлемент.ПолноеИмя(), "Документ " + ТекЭлемент.Синоним); 
	КонецЦикла;
	
	Для каждого ТекЭлемент Из Метаданные.Справочники Цикл
		Элементы.ОграниченияНаВыгрузкуДелСозданныхПоОснованиюЗначение.СписокВыбора.Добавить(ТекЭлемент.ПолноеИмя(), "Справочник " + ТекЭлемент.Синоним); 
	КонецЦикла;     
	
КонецПроцедуры


&НаСервере
Функция ЗависимостиКлиентовОтТиповКонтрагентовПоУмолчанию()
	
    Возврат Справочники.Б24_К_НастройкиПодключения.ЗависимостиКлиентовОтТиповКонтрагентовПоУмолчанию();
	
КонецФункции

&НаКлиенте
Процедура ПроверкаДанныхКолонкиТаблицыСоответствийТиповКлиентов(ВыбранныеДанные, ТаблицаСоответствий, ИмяКолонки)
	
	НайденныеСтроки = ТаблицаСоответствий.НайтиСтроки(Новый Структура("Клиент, Поставщик, Конкурент, ПрочиеОтношения", ВыбранныеДанные.Клиент, ВыбранныеДанные.Поставщик, ВыбранныеДанные.Конкурент, ВыбранныеДанные.ПрочиеОтношения));
	
	Если НайденныеСтроки.Количество() > 1 Тогда
		НайденныеСтроки = ТаблицаСоответствий.НайтиСтроки(Новый Структура("ИдТипа", ВыбранныеДанные.ИдТипа));
		ПоказатьОповещениеПользователя(,,"Такая комбинация уже есть");
		// Не используем // НайденныеСтроки[0][ИмяКолонки] = НЕ НайденныеСтроки[0][ИмяКолонки];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеПресетовШаблоновРеквизитов()

	ЕстьОшибка = Ложь;
	
	//Если ПресетыШаблоновРеквизитов.Количество() = 0 Тогда      
	//	ЕстьОшибка = Истина;
	//	ОбщегоНазначенияКлиент.СообщитьПользователю("Не задан маппинг пресетов реквизитов",, "ПресетыШаблоновРеквизитов");
	//КонецЕсли;
	
	Возврат ЕстьОшибка;
	
КонецФункции

&НаКлиенте
Функция ПроверитьЗаполнениеПресетовНДС()

	ЕстьОшибка = Ложь;
	
	//Если ПресетыСтавокНДС.Количество() = 0 Тогда      
	//	ЕстьОшибка = Истина;
	//	ОбщегоНазначенияКлиент.СообщитьПользователю("Не задан маппинг пресетов ставок НДС",, "ПресетыСтавокНДС");
	//КонецЕсли;
	
	Возврат ЕстьОшибка;
	
КонецФункции

&НаКлиенте
Процедура ВходящийВебхукПриИзменении(Элемент)
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТестВебхук(Команда)
	стДанные = омRestApiВызовСервера.ПолучитьВремяСервера();
КонецПроцедуры

#КонецОбласти
