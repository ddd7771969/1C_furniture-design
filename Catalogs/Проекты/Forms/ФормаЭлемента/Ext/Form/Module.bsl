&НаКлиенте
Перем НачальныйНомерСделки, Проверка_выгрузки_в_ПДМ;

#Область Заполнение
	
&НаСервере
Процедура ЗаполнитьПомещения()
	тзПомещения = Справочники.Проекты.ПолучитьПомещенияОбъекта(Объект.Ссылка,Истина,Ложь);
	Для каждого текПомещение Из тзПомещения Цикл
		ЗаполнитьЗначенияСвойств(ЭтаФорма.Помещения.Добавить(),текПомещение);
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьИзделия()

&НаСервере
Процедура ЗаполнитьИзделия()
	ДеревоИзделияЗаказчика = РеквизитФормыВЗначение("ИзделияЭлемент");
	ЗначениеВРеквизитФормы(Справочники.Проекты.ПолучитьИзделияЗаказчикаОбъекта(Объект.Ссылка,,Ложь), "ИзделияЭлемент");
КонецПроцедуры // ЗаполнитьИзделия()

#КонецОбласти   

#Область СобытияРеквизитов
&НаКлиенте
Процедура ПомещенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТД = Элементы.Помещения.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ПоказатьЗначение(,ТД.Помещение);
КонецПроцедуры

&НаКлиенте
Процедура ПомещенияПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПомещенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	ВидимостьВыгрузкиВПДМ();
	ПрисвоениеНаименованиеПапкамPDM();
КонецПроцедуры 

&НаКлиенте
Процедура ПрисвоениеНаименованиеПапкамPDM()

	
	Если Объект.Номер > 0 Тогда
		
		НомерПроектаСтрокой = Формат(Объект.Номер, "ЧЦ=2; ЧВН=");
		
		Если ПустаяСтрока(Объект.ИмяКорневойПапкиВPDM) Тогда
			Объект.ИмяКорневойПапкиВPDM = НомерПроектаСтрокой + "-" + Объект.Наименование;
		КонецЕсли;
		
		Если ПустаяСтрока(Объект.ИмяПапкиГЧВPDM) Тогда
			Объект.ИмяПапкиГЧВPDM 			= НомерПроектаСтрокой + ".000.02.ГЧ";
		КонецЕсли;
		
		Если ПустаяСтрока(Объект.ИмяПапкиКомпозицииВPDM) Тогда
			Объект.ИмяПапкиКомпозицииВPDM 	= НомерПроектаСтрокой + ".000.03.Композиция";
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры // ПрисвоениеНаименованиеПапкамPDM()


&НаКлиенте
Процедура ПрефиксПриИзменении(Элемент)
	ВидимостьВыгрузкиВПДМ();
КонецПроцедуры


&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	ВидимостьВыгрузкиВПДМ();
	ПрисвоениеНаименованиеПапкамPDM();
КонецПроцедуры

#КонецОбласти 

#Область СобытияФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзменилосьПомещение" Тогда
		Если ТипЗнч(Параметр) = Тип("Структура") Тогда
			ЭтаФорма.Помещения.Очистить();
			ЗаполнитьПомещения();
		КонецЕсли;
	Иначе
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Проверка_выгрузки_в_ПДМ = НЕ ЗначениеЗаполнено(Объект.Ссылка);
	
	НачальныйНомерСделки = Объект.Номер;
	
	ЭтаФорма.КолонкаНаименование 	= 3;
	ЭтаФорма.КолонкаЕдиница 		= 7;
	ЭтаФорма.КолонкаКоличество 		= 8;
	ЭтаФорма.ПерваяСтрока 			= 6;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ПустаяСтрока(Объект.ИмяКорневойПапкиВPDM) Тогда
		Объект.ИмяКорневойПапкиВPDM = Объект.Наименование;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.Элементы.ГруппаИзделияЗаказчика.Видимость 	= Ложь;
	ЭтаФорма.Элементы.ГруппаПомещения.Видимость 		= Ложь;
	ЭтаФорма.Элементы.ГруппаЗагрузка.Видимость 			= Ложь;
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Элементы.ВыгрузитьВPDM.Видимость = Ложь;
		
	Иначе
		
		Если НЕ (РольДоступна("АдминистраторСистемы") И РольДоступна("ПолныеПрава")) Тогда
		
			Элементы.ВыгрузитьВPDM.Видимость = Ложь;
		
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДанныеПроекта.Код_в_PDM КАК Код_в_PDM
			|ИЗ
			|	РегистрСведений.ДанныеПроекта КАК ДанныеПроекта
			|ГДЕ
			|	ДанныеПроекта.Проект = &Проект";
		
		Запрос.УстановитьПараметр("Проект", Объект.Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
	
		Пока Выборка.Следующий() Цикл
			Код_в_PDM = Выборка.Код_в_PDM;
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если НЕ НачальныйНомерСделки = Объект.Номер Тогда
	
		ИзменитьАртикулПомещений(Объект.Ссылка);	
	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти    

#Область ЗагрузкаИзExcel

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "Книга Excel 2007 (*.xlsx)|*.xlsx|Книга Excel 97 (*.xls)|*.xls";
	Диалог.Заголовок = НСтр("Выберите файл Excel");
	Диалог.МножественныйВыбор = Ложь;
	ОповещениеЗавершения = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтотОбъект);
	Диалог.Показать(ОповещениеЗавершения);
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗавершение(ВыбранныйФайл, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйФайл = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	
	ЗагрузкаВТабличныйДокументНаСервере(ВыбранныйФайл[0]);
	
КонецПроцедуры 

&НаСервере
Процедура ЗагрузкаВТабличныйДокументНаСервере(ИмяФайла)
	табДок = ЭтаФорма.СодержимоеФайла;
	
	табДок.Очистить();
	Попытка
		табДок.Прочитать(ИмяФайла);
	Исключение
		Сообщить(ИнформацияОбОшибке());
		Возврат;
	КонецПопытки;
	
	КоличествоСтрок = табДок.ВысотаТаблицы;
	
	ЭтаФорма.ТаблицаИзделий.Очистить();
	
	Для сч = ЭтаФорма.ПерваяСтрока По КоличествоСтрок Цикл 
		
		
		Попытка
			Наименование = Строка(табДок.ПолучитьОбласть("R" + Формат(сч, "ЧГ=0;") + "C" + ЭтаФорма.КолонкаНаименование).ТекущаяОбласть.Текст);	
			Если ПустаяСтрока(Наименование) Тогда
				Продолжить;
			КонецЕсли;
			
			Количество = Строка(табДок.ПолучитьОбласть("R" + Формат(сч, "ЧГ=0;") + "C" + ЭтаФорма.КолонкаКоличество).ТекущаяОбласть.Текст);
			
			Если ПустаяСтрока(Количество) Тогда
				Продолжить;
			КонецЕсли;
			
			Количество = СтрЗаменить(Количество,",",".");
			Количество = СтрЗаменить(Количество," ","");
			Количество = СтрЗаменить(Количество,Символы.НПП,""); 
			Количество = Число(Количество);
			
			Если Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Единица = Строка(табДок.ПолучитьОбласть("R" + Формат(сч, "ЧГ=0;") + "C" + ЭтаФорма.КолонкаЕдиница).ТекущаяОбласть.Текст);
			Если ПустаяСтрока(Единица) Тогда
				ЕИ = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
			Иначе
				ЕИ = Справочники.ЕдиницыИзмерения.НайтиПоНаименованию(Единица);
			КонецЕсли;
			
			НС = ЭтаФорма.ТаблицаИзделий.Добавить();
			НС.Наименование 		= Наименование;
			НС.Количество 			= Количество;
			НС.ЕдиницаИзмерения 	= ЕИ; 
			НС.НомерСтрокиВФайле 	= сч;
			
		Исключение
		КонецПопытки;
		
	КонецЦикла;
КонецПроцедуры


#КонецОбласти 

&НаСервереБезКонтекста
Процедура ИзменитьАртикулПомещений(СсылкаНаСделку)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Помещения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Помещения КАК Помещения
		|ГДЕ
		|	Помещения.Проект = &Проект
		|	И НЕ Помещения.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("Проект", СсылкаНаСделку);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		об = Выборка.Ссылка.ПолучитьОбъект();
		об.Наименование = Справочники.Помещения.ПолучитьНаименованиеПомещения(СсылкаНаСделку,Выборка.Ссылка);
		
		об.Записать();
	КонецЦикла;
	

КонецПроцедуры // ИзменитьАртикулПомещений(СсылкаНаСделку)


&НаКлиенте
Процедура ВыгрузитьВPDM(Команда)
	
	стВозврата = омВыгрузкаCRMНаКлиенте.ВыгрузитьВPDM(Объект.Ссылка, "Проект");
	
	Если ПустаяСтрока(стВозврата.ОписаниеОшибки) Тогда
		ПоказатьПредупреждение(, стВозврата.ВыгруженВPDM, 3, "Файл создан");
	Иначе
		ПоказатьПредупреждение(, стВозврата.ОписаниеОшибки, , "Ошибка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьВыгрузкиВПДМ()

	Если НЕ Проверка_выгрузки_в_ПДМ Тогда
		Проверка_выгрузки_в_ПДМ = Истина;	
	КонецЕсли;	
	

КонецПроцедуры // ВидимостьВыгрузкиВПДМ()

&НаКлиенте
Процедура ЗаполнитьМатериал(Команда)
	
	мМатериалы = Новый Массив;
	
	Для каждого х Из Объект.МатериалыПроекта Цикл
	
		мМатериалы.Добавить(х.Материал);	
	
	КонецЦикла;
	
	Для каждого х Из ПолучитьМатериалыНаСервере(мМатериалы) Цикл
		
		НС = Объект.МатериалыПроекта.Добавить();
		НС.Материал = х;
		
	КонецЦикла;
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ПолучитьМатериалыНаСервере(мМатериалы)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МатериалыПроектов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.МатериалыПроектов КАК МатериалыПроектов
		|ГДЕ
		|	НЕ МатериалыПроектов.ПометкаУдаления
		|	И НЕ МатериалыПроектов.Ссылка В (&Ссылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	МатериалыПроектов.Наименование";
	
	Запрос.УстановитьПараметр("Ссылка", мМатериалы);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции // ПолучитьМатериалыНаСервере()

&НаКлиенте
Процедура УстановитьКодPDM(Команда)
	
	Если ЭтаФорма.Параметры.Ключ.Пустая() Тогда
		ПоказатьПредупреждение(, "Для установки кода необходимо сохранить проект.");	
	Иначе
		
		ОписаниеОповещния = Новый ОписаниеОповещения("ПослеВводаКодаPDM", ЭтаФорма);
		ПоказатьВводЧисла(ОписаниеОповещния, Код_в_PDM, "Введите код.", 10, 0); 
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаКодаPDM(КодPDM, ДополнительныеПараметры) Экспорт 

	Если КодPDM = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
    Если Код_в_PDM = КодPDM Тогда
		Возврат;
	КонецЕсли;
	
	Код_в_PDM = КодPDM;
	
	ЗаписатьКод_в_PDM_НаСервере(Код_в_PDM, Объект.Ссылка);
	
КонецПроцедуры // ПослеВводаКодаPDM()


&НаСервереБезКонтекста
Процедура ЗаписатьКод_в_PDM_НаСервере(Код_в_PDM, ПроектСсылка)

	МенеджерЗаписи = РегистрыСведений.ДанныеПроекта.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Проект 		= ПроектСсылка;

	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Проект 		= ПроектСсылка;
    МенеджерЗаписи.Код_в_PDM 	= Код_в_PDM;
	
	МенеджерЗаписи.Записать(); 
	
	
КонецПроцедуры // ЗаписатьКод_в_PDM_НаСервере(Код_в_PDM, Объект.Ссылка)



