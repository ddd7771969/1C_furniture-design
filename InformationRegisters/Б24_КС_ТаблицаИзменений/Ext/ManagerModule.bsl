#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ДобавитьОбъектВТаблицуИзменений(НастройкаПодключения, ТипДанныхДляРегистрацииИзменений, Объект, ПодчиненныйОбъект = Неопределено, ВремяЗаписиВМиллисекундах = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект) И НЕ ЗначениеЗаполнено(ПодчиненныйОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	СтруктураОтбора.Вставить("ТипДанных"			, ТипДанныхДляРегистрацииИзменений);
	СтруктураОтбора.Вставить("Объект"				, Объект);
	СтруктураОтбора.Вставить("ПодчиненныйОбъект"	, ПодчиненныйОбъект);
	
	Если РегистрыСведений.Б24_КС_ТаблицаИзменений.Получить(СтруктураОтбора).ВремяЗаписиВМиллисекундах = 0 Тогда  
		НоваяЗапись = РегистрыСведений.Б24_КС_ТаблицаИзменений.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СтруктураОтбора);
		НоваяЗапись.ВремяЗаписиВМиллисекундах = ?(ВремяЗаписиВМиллисекундах = Неопределено, ТекущаяУниверсальнаяДатаВМиллисекундах(), ВремяЗаписиВМиллисекундах);
		НоваяЗапись.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция НаличиеИзмененийНаДату(НастройкаПодключения, ВремяЗаписиВМиллисекундах) Экспорт
	
	Запрос = Новый Запрос; 
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	Запрос.УстановитьПараметр("ВремяЗаписиВМиллисекундах", ВремяЗаписиВМиллисекундах);
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	Б24_КС_ТаблицаИзменений.Объект КАК Объект
	|ИЗ
	|	РегистрСведений.Б24_КС_ТаблицаИзменений КАК Б24_КС_ТаблицаИзменений
	|ГДЕ
	|	Б24_КС_ТаблицаИзменений.НастройкаПодключения = &НастройкаПодключения
	|	И Б24_КС_ТаблицаИзменений.ВремяЗаписиВМиллисекундах > &ВремяЗаписиВМиллисекундах
	|
	|СГРУППИРОВАТЬ ПО
	|	Б24_КС_ТаблицаИзменений.Объект";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Возврат НЕ ВыполненныйЗапрос.Пустой(); 

КонецФункции

#КонецЕсли