
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область РаботаСИдентификаторамиБитрикс24

Функция ПолучитьИдОбъекта(Портал, ТипДанных, Объект, ПодчиненныйОбъект = Неопределено) Экспорт
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Объект"			, Объект);
	СтруктураОтбора.Вставить("Портал"			, Портал);
	СтруктураОтбора.Вставить("ТипДанных"		, ТипДанных);
	СтруктураОтбора.Вставить("ПодчиненныйОбъект", ПодчиненныйОбъект);
	
	Возврат РегистрыСведений.Б24_К_ИдентификаторыОбъектов.Получить(СтруктураОтбора).Идентификатор;
	
КонецФункции

Функция ПолучитьДопИдОбъекта(Портал, Объект, ТипДанных, ПодчиненныйОбъект = Неопределено) Экспорт
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Портал"			, Портал);
	СтруктураОтбора.Вставить("Объект"			, Объект);
	СтруктураОтбора.Вставить("ТипДанных"		, ТипДанных);
	СтруктураОтбора.Вставить("ПодчиненныйОбъект", ПодчиненныйОбъект);
	            
	Возврат РегистрыСведений.Б24_К_ИдентификаторыОбъектов.Получить(СтруктураОтбора).ДополнительныйИдентификатор;
	
КонецФункции

Функция ПолучитьЗначениеПоИдБ24Объекта(Портал, ТипДанных, ИдБитрикс24) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Портал"			, Портал);
	Запрос.УстановитьПараметр("ТипДанных"		, ТипДанных);
	Запрос.УстановитьПараметр("Идентификатор"	, ИдБитрикс24);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор = &Идентификатор";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Выборка.Следующий();
		
		Результат = Новый Структура;
		Результат.Вставить("Объект"				, Выборка.Объект);	
		Результат.Вставить("ПодчиненныйОбъект"	, Выборка.ПодчиненныйОбъект);	
		
		Возврат Результат;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьЗначениеПоДопИдБ24Объекта(Портал, ТипДанных, ДопИдБитрикс24) Экспорт
	
	Результат = Неопределено;
	
	ДопИдентификатор 	= Формат(ДопИдБитрикс24, "ЧГ=0");	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Портал"			, Портал);
	Запрос.УстановитьПараметр("ТипДанных"		, ТипДанных);
	Запрос.УстановитьПараметр("ДопИдентификатор", ДопИдентификатор);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.ДополнительныйИдентификатор = &ДопИдентификатор";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		тзнДанных = ВыполненныйЗапрос.Выгрузить();
		
		Результат = Новый Структура;
		Результат.Вставить("Объект"				, тзнДанных[0].Объект);	
		Результат.Вставить("ПодчиненныйОбъект"	, тзнДанных[0].ПодчиненныйОбъект);	
		
		Возврат Результат;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


Процедура ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипДанных, Данные, Знач ИдБитрикс24, ДополнительныйИдБитрикс24="") Экспорт
	
	Возврат;
	Если НЕ ЗначениеЗаполнено(ИдБитрикс24) И НЕ ЗначениеЗаполнено(ДополнительныйИдБитрикс24) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.ЕдиницаИзмерения2 И ТипЗнч(ИдБитрикс24) = Тип("Соответствие") 
		И ИдБитрикс24.Получить("measure") <> Неопределено Тогда 
		ИдБитрикс24 = ИдБитрикс24.Получить("measure").Получить("id");
	ИначеЕсли ТипЗнч(ИдБитрикс24) = Тип("Соответствие") Тогда
		Возврат;     
	КонецЕсли;
	
	лОбъект 			= Неопределено;
	лПодчиненныйОбъект 	= Неопределено;
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		лОбъект 			= Данные.Объект;
		лПодчиненныйОбъект 	= Данные.ПодчиненныйОбъект;
	Иначе
		лОбъект 			= Данные;
	КонецЕсли;
	
	Идентификатор 		= Формат(ИдБитрикс24, "ЧГ=0");	
	ДопИдентификатор 	= Формат(ДополнительныйИдБитрикс24, "ЧГ=0");	
	
	ПрефиксВнешнихКодовБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();
	
	Если ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Проект ИЛИ ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.РеквизитыСделки 
		ИЛИ ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.ТоварыСделки Тогда
		Идентификатор = ПрефиксВнешнихКодовБитрикс24.Проекты + Формат(ИдБитрикс24, "ЧГ=0");	
		
	ИначеЕсли ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Заказ Тогда
		Идентификатор = ПрефиксВнешнихКодовБитрикс24.Заказы + Формат(ИдБитрикс24, "ЧГ=0");	
	Иначе
		
		Если ТипЗнч(лОбъект) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") И ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.Контакт Тогда
			Идентификатор = "#"+Идентификатор;	
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКомпании ИЛИ ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКонтакта 
		ИЛИ ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСчета ИЛИ ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСделки Тогда 
		
		Выборка = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.Выбрать(Новый Структура("Объект",лОбъект));
		
		ЕстьЗапись = Ложь;
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.ТипДанных = ТипДанных И Выборка.Портал = СтруктураНастроек.Портал Тогда
				ТекЗапись = Выборка.ПолучитьМенеджерЗаписи();
				ТекЗапись.Идентификатор	= Идентификатор;
				ТекЗапись.Записать(Истина);
				ЕстьЗапись = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ЕстьЗапись Тогда
			НоваяЗапись = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
			НоваяЗапись.Объект 			= лОбъект;
			НоваяЗапись.Портал 			= СтруктураНастроек.Портал;
			НоваяЗапись.ТипДанных 		= ТипДанных;
			НоваяЗапись.Идентификатор 	= Идентификатор;
			НоваяЗапись.ДополнительныйИдентификатор = ДопИдентификатор;
			НоваяЗапись.Записать(Истина);
		КонецЕсли;
		
	Иначе
		
		НоваяЗапись = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
		НоваяЗапись.Портал 						= СтруктураНастроек.Портал;
		НоваяЗапись.ТипДанных 					= ТипДанных;
		НоваяЗапись.Идентификатор 				= Идентификатор;
		НоваяЗапись.ДополнительныйИдентификатор = ДопИдентификатор;
		
		НоваяЗапись.Объект 				= лОбъект;
		НоваяЗапись.ПодчиненныйОбъект	= лПодчиненныйОбъект;
		
		НоваяЗапись.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьИдБ24Объекта(СтруктураНастроек, ТипДанных, Данные) Экспорт
		
	Если ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКомпании ИЛИ ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоКонтакта 
		ИЛИ ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСчета ИЛИ ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоСделки 
		ИЛИ ТипДанных = СтруктураНастроек.ТипыОбъектовОбмена.СвойствоЗаказа Тогда 
		
		Выборка = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.Выбрать(Новый Структура("Объект", Данные));
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ТипДанных = ТипДанных И Выборка.Портал = СтруктураНастроек.Портал Тогда
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Удален id для объекта:" + Строка(Данные) + ". Тип объекта:" + Строка(ТипДанных));
				Выборка.ПолучитьМенеджерЗаписи().Удалить();
			КонецЕсли;
			
			Если Данные.ДополнительныеЗначенияИспользуются Тогда
				ВыборкаЗначенийСвойств = Справочники.ЗначенияСвойствОбъектов.Выбрать(,Данные);
				Пока ВыборкаЗначенийСвойств.Следующий() Цикл
					ВыборкаИдЗначенийСвойств = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.Выбрать(Новый Структура("Объект", ВыборкаЗначенийСвойств.Ссылка));
					Пока ВыборкаИдЗначенийСвойств.Следующий() Цикл
						
						Если ВыборкаИдЗначенийСвойств.Портал = СтруктураНастроек.Портал Тогда
							Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Удален id для объекта:" + Строка(ВыборкаИдЗначенийСвойств.Объект) + ". Тип объекта:" + Строка(ВыборкаИдЗначенийСвойств.ТипДанных));
							ВыборкаИдЗначенийСвойств.ПолучитьМенеджерЗаписи().Удалить();
						КонецЕсли;
						
					КонецЦикла;			
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе	
			
		Если ТипЗнч(Данные) = Тип("Структура") Тогда
			
			Выборка = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.Выбрать(Новый Структура("Объект", Данные.Объект));
			
			Пока Выборка.Следующий() Цикл
				
				Если Выборка.ТипДанных = ТипДанных И  Выборка.ПодчиненныйОбъект = Данные.ПодчиненныйОбъект И Выборка.Портал = СтруктураНастроек.Портал Тогда
					ОписаниеОбъекта = Строка(Данные.Объект) + "||" + Строка(Данные.ПодчиненныйОбъект);
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Удален id для объекта:" + ОписаниеОбъекта + ". Тип объекта:" + Строка(ТипДанных));
					Выборка.ПолучитьМенеджерЗаписи().Удалить();
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			Выборка = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.Выбрать(Новый Структура("Объект", Данные));
			
			Пока Выборка.Следующий() Цикл
				
				Если Выборка.ТипДанных = ТипДанных И Выборка.Портал = СтруктураНастроек.Портал Тогда
					Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Удален id для объекта:" + Строка(Данные) + ". Тип объекта:" + Строка(ТипДанных));
					Выборка.ПолучитьМенеджерЗаписи().Удалить();
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;		
	
КонецПроцедуры

Функция ПолучитьМассивЗначенийПоИдБ24Объекта(Портал, ТипДанных, ИдБитрикс24) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Портал"			, Портал);
	Запрос.УстановитьПараметр("ТипДанных"		, ТипДанных);
	Запрос.УстановитьПараметр("Идентификатор"	, ИдБитрикс24);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор = &Идентификатор";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		тзнДанных = ВыполненныйЗапрос.Выгрузить();
		
		Возврат тзнДанных.ВыгрузитьКолонку("Объект");
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УдалитьОбъектПоИдентификаторуБ24(СтруктураНастроек, ТипДанных, ИдБитрикс24) Экспорт
	
	ПрефиксВнешнихКодовБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьПрефиксВнешнихКодовБитрикс24();
	
	ТипыОбъектовОбмена = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();
	
	Если ТипДанных = ТипыОбъектовОбмена.Проект ИЛИ ТипДанных = ТипыОбъектовОбмена.РеквизитыСделки 
		ИЛИ ТипДанных = ТипыОбъектовОбмена.ТоварыСделки Тогда
		Идентификатор = ПрефиксВнешнихКодовБитрикс24.Проекты + ИдБитрикс24;	
	ИначеЕсли ТипДанных = ТипыОбъектовОбмена.Заказ ИЛИ ТипДанных = ТипыОбъектовОбмена.СвойствоЗаказа Тогда
		Идентификатор = ПрефиксВнешнихКодовБитрикс24.Заказы + ИдБитрикс24;	
	Иначе
		Идентификатор = ИдБитрикс24;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Портал"			, СтруктураНастроек.Портал);
	Запрос.УстановитьПараметр("ТипДанных"		, ТипДанных);
	Запрос.УстановитьПараметр("Идентификатор" 	, Идентификатор);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект,
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных КАК ТипДанных,
	|	Б24_К_ИдентификаторыОбъектов.ПодчиненныйОбъект КАК ПодчиненныйОбъект,
	|	Б24_К_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор = &Идентификатор
	|	И Б24_К_ИдентификаторыОбъектов.Портал = &Портал";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Запись = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.Прочитать();
		Запись.Удалить();
		
		Если СтруктураНастроек.ОбрабатыватьУдалениеОбъектовВБ24 = Истина Тогда
			Попытка
				ТекОбъект = Выборка.Объект.ПолучитьОбъект();
				
				ТекОбъект.ДополнительныеСвойства.Вставить("ЗагрузкаСБитрикс24", Истина);
				ТекОбъект.УстановитьПометкуУдаления(Истина);
				ТекОбъект.Записать(); 
				
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Помечен на удаление объект:" + Строка(Выборка.Объект));
				
			Исключение
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, "Не удается пометить на удаление объект:" + Строка(Выборка.Объект));
				Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Ошибка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
