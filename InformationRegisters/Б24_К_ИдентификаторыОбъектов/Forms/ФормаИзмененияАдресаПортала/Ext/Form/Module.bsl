
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСпискиВыбора();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыполнить(Команда)
	
	Если ЗначениеЗаполнено(ЗаменяемыйАдресПортала) И ЗначениеЗаполнено(НовыйАдресПортала) Тогда
		ВыполнитьИзменениеАдресаПортала();	
	КонецЕсли;
	
	ЗаполнитьСпискиВыбора();
	
	ОбщегоНазначенияКлиент.СообщитьПользователю("Изменение адреса портала у идентификаторов завершено");
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьИзменениеАдресаПортала()
	
	Выборка = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.Выбрать();    
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Портал = ЗаменяемыйАдресПортала Тогда
			
			НоваяЗапись = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка); 
			НоваяЗапись.Портал = НовыйАдресПортала;
			НоваяЗапись.Записать();
			
			Если ЗаменяемыйАдресПортала <> НовыйАдресПортала Тогда
				ЗаписьДляУдаления = Выборка.ПолучитьМенеджерЗаписи();
				ЗаписьДляУдаления.Удалить();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗаменяемыйАдресПорталаПриИзменении(Элемент)
	
	Элементы.КомандаВыполнить.Доступность = ЗначениеЗаполнено(ЗаменяемыйАдресПортала) И ЗначениеЗаполнено(НовыйАдресПортала); 
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйАдресПорталаПриИзменении(Элемент)
	
	Элементы.КомандаВыполнить.Доступность = ЗначениеЗаполнено(ЗаменяемыйАдресПортала) И ЗначениеЗаполнено(НовыйАдресПортала); 
	
КонецПроцедуры

#КонецОбласти


#Область Прочие

&НаСервере
Процедура ЗаполнитьСпискиВыбора()
	
	Элементы.ЗаменяемыйАдресПортала.СписокВыбора.Очистить();
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Портал КАК Портал
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|
	|СГРУППИРОВАТЬ ПО
	|	Б24_К_ИдентификаторыОбъектов.Портал";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Элементы.ЗаменяемыйАдресПортала.СписокВыбора.Добавить(Выборка.Портал);
	КонецЦикла;
	
	Элементы.НовыйАдресПортала.СписокВыбора.Очистить();
	
	Выборка = Справочники.Б24_К_НастройкиПодключения.Выбрать();
	Пока Выборка.Следующий() Цикл
		Элементы.НовыйАдресПортала.СписокВыбора.Добавить(Выборка.Портал);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

