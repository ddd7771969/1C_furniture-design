
Процедура УстановитьСтатусЗамера(ОбъектЗамера, ЗначениеЗамера = Неопределено, ОбъектИзменения = Неопределено, ДатаИзменения = Неопределено, ТолькоНовые = Ложь) Экспорт

	Если ДатаИзменения = Неопределено Тогда
		ПериодДляЗаписи = ТекущаяДата();
	Иначе
		ПериодДляЗаписи = ДатаИзменения;
	КонецЕсли;
	
	Если ЗначениеЗамера = Неопределено Тогда
		ЗначениеЗамераДляЗаписи = Перечисления.СтатусыЗамера.Проектный; 
	Иначе
		ЗначениеЗамераДляЗаписи = ЗначениеЗамера; 
	КонецЕсли;
	
	Если ОбъектИзменения = Неопределено Тогда
		ОбъектИзмененияДляЗаписи = ОбъектЗамера; 
	Иначе
		ОбъектИзмененияДляЗаписи = ОбъектИзменения; 
	КонецЕсли;
	
	Если ТолькоНовые Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СостояниеЗамера.ОбъектЗамера КАК ОбъектЗамера
		|ИЗ
		|	РегистрСведений.СостояниеЗамера КАК СостояниеЗамера
		|ГДЕ
		|	СостояниеЗамера.ОбъектЗамера = &ОбъектЗамера";
		
		Запрос.УстановитьПараметр("ОбъектЗамера", ОбъектЗамера);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.ОбъектЗамера) Тогда
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	МенеджерЗаписи 					= РегистрыСведений.СостояниеЗамера.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период 			= ПериодДляЗаписи;
	МенеджерЗаписи.ОбъектЗамера 	= ОбъектЗамера;
	МенеджерЗаписи.ОбъектИзменения 	= ОбъектИзмененияДляЗаписи;
	МенеджерЗаписи.СтатусЗамера 	= ЗначениеЗамераДляЗаписи;

	МенеджерЗаписи.Записать();
	
КонецПроцедуры


Функция ПолучитьСтатусЗамера(ОбъектЗамера) Экспорт
	
	стДанные = Новый Структура("СтатусЗамера, Дата, ОбъектИзменения", Перечисления.СтатусыЗамера.Проектный, Дата(1, 1, 1),);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(СостояниеЗамера.Период) КАК Период,
		|	СостояниеЗамера.ОбъектЗамера КАК ОбъектЗамера
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрСведений.СостояниеЗамера КАК СостояниеЗамера
		|ГДЕ
		|	СостояниеЗамера.ОбъектЗамера = &ОбъектЗамера
		|
		|СГРУППИРОВАТЬ ПО
		|	СостояниеЗамера.ОбъектЗамера
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостояниеЗамера.СтатусЗамера КАК СтатусЗамера,
		|	СостояниеЗамера.ОбъектИзменения КАК ОбъектИзменения,
		|	МАКСИМУМ(СостояниеЗамера.Период) КАК Дата
		|ИЗ
		|	РегистрСведений.СостояниеЗамера КАК СостояниеЗамера
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт КАК вт
		|		ПО СостояниеЗамера.Период = вт.Период
		|			И СостояниеЗамера.ОбъектЗамера = вт.ОбъектЗамера
		|ГДЕ
		|	СостояниеЗамера.ОбъектЗамера = &ОбъектЗамера
		|
		|СГРУППИРОВАТЬ ПО
		|	СостояниеЗамера.СтатусЗамера,
		|	СостояниеЗамера.ОбъектИзменения";
	
	Запрос.УстановитьПараметр("ОбъектЗамера", ОбъектЗамера);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(стДанные, Выборка);
	КонецЦикла;
	
	Возврат стДанные;		

КонецФункции 