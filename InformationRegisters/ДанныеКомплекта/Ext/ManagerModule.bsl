Процедура ИзменитьДанныеКомплекта(стПараметры) Экспорт
	
	ТД = ТекущаяДата();
	
	НаборЗаписей = РегистрыСведений.ДанныеКомплекта.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Комплект.Установить(стПараметры.Комплект);
	НаборЗаписей.Отбор.Период.Установить(ТД);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеКомплектаСрезПоследних.Конструктор КАК Конструктор,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаНачалоМонтажа КАК ВнешняяДатаНачалоМонтажа,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаНачалоПроектирования КАК ВнешняяДатаНачалоПроектирования,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаНачалоПроизводства КАК ВнешняяДатаНачалоПроизводства,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаНачалоСогласования КАК ВнешняяДатаНачалоСогласования,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаОкончаниеМонтажа КАК ВнешняяДатаОкончаниеМонтажа,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаОкончаниеПроектирования КАК ВнешняяДатаОкончаниеПроектирования,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаОкончаниеПроизводства КАК ВнешняяДатаОкончаниеПроизводства,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаОкончаниеСогласования КАК ВнешняяДатаОкончаниеСогласования,
		|	ДанныеКомплектаСрезПоследних.Приоритет КАК Приоритет,
		|	ДанныеКомплектаСрезПоследних.ТекущаяСтадия КАК ТекущаяСтадия
		|ИЗ
		|	РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект = &Комплект) КАК ДанныеКомплектаСрезПоследних";
		
	Запрос.УстановитьПараметр("Комплект", стПараметры.Комплект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(стПараметры.Конструктор) Тогда
		НоваяЗапись.Конструктор = стПараметры.Конструктор; 
	КонецЕсли;
	
	Если стПараметры.Свойство("Приоритет") Тогда
		НоваяЗапись.Приоритет	= стПараметры.Приоритет;
	КонецЕсли;
	
	Если стПараметры.Свойство("ТекущаяСтадия") Тогда
		НоваяЗапись.ТекущаяСтадия	= стПараметры.ТекущаяСтадия;
	КонецЕсли;
	
	НоваяЗапись.Комплект			= стПараметры.Комплект;
	НоваяЗапись.Период				= ТД;
	НоваяЗапись.Автор 				= ПараметрыСеанса.ТекущийПользователь;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры
