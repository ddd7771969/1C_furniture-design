
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаПодключения 	= Параметры.НастройкаПодключения;
	Портал 					= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаПодключения, "Портал");
	
	НастройкаСинхронизации = Б24_СУ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНастройкуПоНастройкеПодключения(НастройкаПодключения);
	НастройкаСинхронизации.НастройкаПодключения = НастройкаПодключения;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, НастройкаСинхронизации); 
	
	СохраненныеНастройки = НастройкаСинхронизации.Настройки.Получить();
	Если СохраненныеНастройки = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю("Некорректные сохраненные настройки");
		Возврат;
	КонецЕсли;
	
	Прайсы.Загрузить(СохраненныеНастройки.НастройкиЦен.Прайсы);
	Склады.Загрузить(СохраненныеНастройки.НастройкиОстатков.Склады);
	
	СохраненныеНастройки.НастройкиТоваров.Свойство("ИдентификаторКаталогаТоваров", ИдентификаторКаталогаТоваров);
	СохраненныеНастройки.НастройкиТоваров.Свойство("ИдентификаторКаталогаПредложений", ИдентификаторКаталогаПредложений);
	
	СохраненныеНастройки.НастройкиТоваров.Свойство("ВыгружатьКартинки"	, ВыгружатьКартинки);
	СохраненныеНастройки.НастройкиТоваров.Свойство("СпособВыгрузки"		, СпособВыгрузкиКартинок);
	СохраненныеНастройки.НастройкиТоваров.Свойство("ИдентификаторРегламентногоЗаданияКартинок", ИдентификаторРегламентногоЗаданияКартинок);
	
	
	СохраненныеНастройки.НастройкиЦен.Свойство("СпособВыгрузки", СпособВыгрузкиЦен);      
	СохраненныеНастройки.НастройкиЦен.Свойство("ИдентификаторРегламентногоЗаданияЦен", ИдентификаторРегламентногоЗаданияЦен);
	
	СохраненныеНастройки.НастройкиОстатков.Свойство("СпособВыгрузки", СпособВыгрузкиОстатков);
	СохраненныеНастройки.НастройкиОстатков.Свойство("ИдентификаторРегламентногоЗаданияОстатков", ИдентификаторРегламентногоЗаданияОстатков);
	
	СохраненныеНастройки.НастройкииЗаказов.Свойство("РезервироватьТовары"	, РезервироватьТовары);
	СохраненныеНастройки.НастройкииЗаказов.Свойство("ОтгружатьТовары"		, ОтгружатьТовары);
	
	СохраненныеНастройки.НастройкииЗаказов.Свойство("АлгоритмЗаполненияЗаказа"	, АлгоритмЗаполненияЗаказа);   
	СохраненныеНастройки.НастройкииЗаказов.Свойство("АлгоритмЗаполненияОтгрузки", АлгоритмЗаполненияОтгрузки);   
	
	СохраненныеНастройки.НастройкииЗаказов.Свойство("Организация"	, Организация);
	СохраненныеНастройки.НастройкииЗаказов.Свойство("Подразделение"	, Подразделение);
	СохраненныеНастройки.НастройкииЗаказов.Свойство("Ответственный"	, Ответственный);
	СохраненныеНастройки.НастройкииЗаказов.Свойство("Соглашение"	, Соглашение);
	СохраненныеНастройки.НастройкииЗаказов.Свойство("СтатьяДДС"		, СтатьяДДС);
	СохраненныеНастройки.НастройкииЗаказов.Свойство("НоменклатураДоставка", НоменклатураДоставка);
	
	СохраненныеНастройки.НастройкииЗаказов.Свойство("СвойствоСлужбаДоставки", СвойствоСлужбаДоставки);
    Если СохраненныеНастройки.НастройкииЗаказов.Свойство("СпособыДоставки") Тогда СпособыДоставки.Загрузить(СохраненныеНастройки.НастройкииЗаказов.СпособыДоставки) КонецЕсли;
	
	
УстановитьПривилегированныйРежим(Истина);	 //Для возможности всем пользователям читать расписание		
		
		НайденноеРегламентноеЗадание = Неопределено;
		Если ЗначениеЗаполнено(ИдентификаторРегламентногоЗаданияЦен) Тогда			
			НайденноеРегламентноеЗадание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗаданияЦен);
		КонецЕсли;			
	
		Если НайденноеРегламентноеЗадание 	= Неопределено Тогда
			ИдентификаторРегламентногоЗадания	= Неопределено;
			РасписаниеРегламентногоЗаданияЦен 	= Новый РасписаниеРегламентногоЗадания;
		Иначе
			РасписаниеРегламентногоЗаданияЦен 	= НайденноеРегламентноеЗадание.Расписание;
		КонецЕсли;		     
		
		НайденноеРегламентноеЗадание = Неопределено;
		Если ЗначениеЗаполнено(ИдентификаторРегламентногоЗаданияОстатков) Тогда			
			НайденноеРегламентноеЗадание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗаданияОстатков);
		КонецЕсли;			
	
		Если НайденноеРегламентноеЗадание 	= Неопределено Тогда
			ИдентификаторРегламентногоЗаданияОстатков	= Неопределено;
			РасписаниеРегламентногоЗаданияОстатков 	= Новый РасписаниеРегламентногоЗадания;
		Иначе
			РасписаниеРегламентногоЗаданияОстатков 	= НайденноеРегламентноеЗадание.Расписание;
		КонецЕсли;	
		
		НайденноеРегламентноеЗадание = Неопределено;
		Если ЗначениеЗаполнено(ИдентификаторРегламентногоЗаданияКартинок) Тогда			
			НайденноеРегламентноеЗадание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗаданияКартинок);
		КонецЕсли;			
	
		Если НайденноеРегламентноеЗадание 	= Неопределено Тогда
			ИдентификаторРегламентногоЗаданияКартинок	= Неопределено;
			РасписаниеРегламентногоЗаданияКартинок 	= Новый РасписаниеРегламентногоЗадания;
		Иначе
			РасписаниеРегламентногоЗаданияКартинок 	= НайденноеРегламентноеЗадание.Расписание;
		КонецЕсли;	
		
УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//
	//Если НЕ Б24_К_СлужебныйВызовСервера.МодульСкладскойУчетДоступен(НастройкаПодключения) Тогда
	//	Б24_К_СлужебныйКлиент.ОповеститьОбНедоступностиФункционалаСкладскойУчетПоТарифномуПлану();
	//	Закрыть();	
	//	Возврат;
	//КонецЕсли;
	
	Заголовок = Строка(НастройкаПодключения);
	
	БазаИспользуетсяВМоделиСервиса = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса();	
	
	Элементы.ДекорацияРасписаниеФреш.Видимость 	= БазаИспользуетсяВМоделиСервиса;
	Элементы.ДекорацияРасписаниеФреш1.Видимость = БазаИспользуетсяВМоделиСервиса;    
	Элементы.ДекорацияРасписаниеФреш2.Видимость = БазаИспользуетсяВМоделиСервиса;    
	Элементы.НастроитьРасписаниеОбменаЦен.Видимость 		= НЕ БазаИспользуетсяВМоделиСервиса;    
	Элементы.НастроитьРасписаниеОбменаОстатков.Видимость 	= НЕ БазаИспользуетсяВМоделиСервиса;    
	Элементы.НастроитьРасписаниеОбменаКартинок.Видимость 	= НЕ БазаИспользуетсяВМоделиСервиса;    
	
	ОбновитьОтображениеЗаголовокСвернутогоОтображения();
	
	ОбновитьОтображениеЭлементовФормы();
	
	УстановитьНадписьРасписанияОбмена(); 
	
	ЗагрузкаСпискаИнфоблоков();
	
	ПроверитьЗаполнитьИдентификаторыИнфоблоковПоУмолчанию();
	
	ПредлагатьПодборТовара = ПредлагатьПодборТовара(НастройкаПодключения);
	
КонецПроцедуры


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НужноЗакрытьОкно = Истина;
		
		Если ПроверитьКорректностьНастроек() Тогда
			СохранитьВыгрузитьНастройки(Истина);
		КонецЕсли;
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		НужноЗакрытьОкно = Истина;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура СохранитьВыгрузитьНастройки(ЗакрытьПоОкончании = Ложь)

#Область ПроверкиПередЗаписью
	ЕстьНеВыгруженныеПрайсы = Ложь;
	Для каждого ТекЭлемент Из Прайсы Цикл
		Если НЕ ЗначениеЗаполнено(ТекЭлемент.ИдПрайсаБ24) И ЗначениеЗаполнено(ТекЭлемент.Прайс1С) Тогда
			ЕстьНеВыгруженныеПрайсы = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьНеВыгруженныеПрайсы Тогда
		
		Обещание = ВопросАсинх("Есть Не выгруженные прайсы в Битрикс24. Выгрузить их?", РежимДиалогаВопрос.ДаНет);
		Результат = Ждать обещание;
		
		Если Результат = КодВозвратаДиалога.Да Тогда
	        ВыгрузитьПрайсы(Неопределено);
	    КонецЕсли;
		
	КонецЕсли;

	
	ЕстьНеВыгруженныеСклады = Ложь;
	Для каждого ТекЭлемент Из Склады Цикл
		Если НЕ ЗначениеЗаполнено(ТекЭлемент.ИдСкладаБ24) И ЗначениеЗаполнено(ТекЭлемент.Склад1С) Тогда
			ЕстьНеВыгруженныеСклады = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьНеВыгруженныеСклады Тогда
		
		Обещание = ВопросАсинх("Есть Не выгруженные склады в Битрикс24. Выгрузить их?", РежимДиалогаВопрос.ДаНет);
		Результат = Ждать обещание;
		
		Если Результат = КодВозвратаДиалога.Да Тогда
	        ВыгрузитьСклады(Неопределено);
	    КонецЕсли;
		
	КонецЕсли;
#КонецОбласти  

	НастройкаСохранена = СохранитьНастройкуСервер();
	
	Оповестить("СозданаИзмененаНастройка");
	
	Если НастройкаСохранена И ВыгружатьНастройкиНаПортал Тогда
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НастройкаПодключения, "ПометкаУдаления") = Ложь Тогда
			ВыгрузкаНастройки(ЗакрытьПоОкончании);
		Иначе
			Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры  

&НаКлиенте
Асинх Процедура ПредложитьДобавитьВстройкуТоваров()
	
	Обещание = ВопросАсинх("Подключить поиска товара из 1С?", РежимДиалогаВопрос.ДаНет);
	
	Результат = Ждать обещание;        
	Если Результат = КодВозвратаДиалога.Да Тогда
		ДобавитьВстройкуТоваров(Неопределено);
	КонецЕсли;
	
КонецПроцедуры  

&НаСервере
Функция СохранитьНастройкуСервер()
	
	Если НЕ Модифицированность Тогда
		Возврат Истина;
	КонецЕсли;
	
УстановитьПривилегированныйРежим(Истина); //Для возможности всем пользователям с правами устанавливать расписания		

	Наименование 					= Строка(НастройкаПодключения);
 	ИменаИспользуемыхРеглЗаданий 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьИменаИспользуемыхРеглЗаданий();
	СтруктураРеглЗаданий 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьИспользуемыеРеглЗадания();

	Если СпособВыгрузкиЦен = "ПоРасписанию"  И НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
#Область ФоновоеЗаданиеЦен
			
		ПараметрыЗапуска = Новый Массив();
		ПараметрыЗапуска.Добавить(СтруктураРеглЗаданий.ВыгрузкаЦен);
		ПараметрыЗапуска.Добавить(НастройкаПодключения);

		Задание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗаданияЦен);
		Если Задание <> Неопределено Тогда

			ПараметрыИзмененияЗадания = Новый Структура();
			ПараметрыИзмененияЗадания.Вставить("Использование", Истина);
			ПараметрыИзмененияЗадания.Вставить("Параметры", ПараметрыЗапуска);
			ПараметрыИзмененияЗадания.Вставить("Расписание", РасписаниеРегламентногоЗаданияЦен);
			ПараметрыИзмененияЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
			ПараметрыИзмененияЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);

			РегламентныеЗаданияСервер.ИзменитьЗадание(ИдентификаторРегламентногоЗаданияЦен, ПараметрыИзмененияЗадания);

		Иначе
			
			ПараметрыЗадания = Новый Структура();
			ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ЗапускДополнительныхОбработок);
			ПараметрыЗадания.Вставить("Наименование", ИменаИспользуемыхРеглЗаданий.ВыгрузкаЦен
			+ " " + Наименование);
			ПараметрыЗадания.Вставить("Ключ", ИменаИспользуемыхРеглЗаданий.ВыгрузкаЦен
			+ " " + Наименование);
			ПараметрыЗадания.Вставить("Использование", Истина);
			ПараметрыЗадания.Вставить("ЭксклюзивноеВыполнение", Ложь);
			ПараметрыЗадания.Вставить("Параметры", ПараметрыЗапуска);
			ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
			ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);
			ПараметрыЗадания.Вставить("Расписание", РасписаниеРегламентногоЗаданияЦен);
			
			Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
			Если ОбщегоНазначения.РазделениеВключено() Тогда
				//@skip-warning
				ИдентификаторРегламентногоЗаданияЦен = Задание.Идентификатор.УникальныйИдентификатор();
			Иначе
				ИдентификаторРегламентногоЗаданияЦен = Задание.УникальныйИдентификатор;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Задание = Неопределено Тогда
			ИдентификаторРегламентногоЗаданияЦен = Неопределено;
		КонецЕсли;
		
	Иначе
		РегламентныеЗаданияСервер.УдалитьЗадание(ИдентификаторРегламентногоЗаданияЦен);
		ИдентификаторРегламентногоЗаданияЦен = Неопределено;
#КонецОбласти
 	КонецЕсли;		 

	Если СпособВыгрузкиОстатков = "ПоРасписанию"  И НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
#Область ФоновоеЗаданиеОстатков
			
		ПараметрыЗапуска = Новый Массив();
		ПараметрыЗапуска.Добавить(СтруктураРеглЗаданий.ВыгрузкаОстатков);
		ПараметрыЗапуска.Добавить(НастройкаПодключения);

		Задание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗаданияОстатков);
		Если Задание <> Неопределено Тогда

			ПараметрыИзмененияЗадания = Новый Структура();
			ПараметрыИзмененияЗадания.Вставить("Использование", Истина);
			ПараметрыИзмененияЗадания.Вставить("Параметры", ПараметрыЗапуска);
			ПараметрыИзмененияЗадания.Вставить("Расписание", РасписаниеРегламентногоЗаданияОстатков);
			ПараметрыИзмененияЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
			ПараметрыИзмененияЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);

			РегламентныеЗаданияСервер.ИзменитьЗадание(ИдентификаторРегламентногоЗаданияОстатков, ПараметрыИзмененияЗадания);

		Иначе
			
			ПараметрыЗадания = Новый Структура();
			ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ЗапускДополнительныхОбработок);
			ПараметрыЗадания.Вставить("Наименование", ИменаИспользуемыхРеглЗаданий.ВыгрузкаОстатков
			+ " " + Наименование);
			ПараметрыЗадания.Вставить("Ключ", ИменаИспользуемыхРеглЗаданий.ВыгрузкаОстатков
			+ " " + Наименование);
			ПараметрыЗадания.Вставить("Использование", Истина);
			ПараметрыЗадания.Вставить("ЭксклюзивноеВыполнение", Ложь);
			ПараметрыЗадания.Вставить("Параметры", ПараметрыЗапуска);
			ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
			ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);
			ПараметрыЗадания.Вставить("Расписание", РасписаниеРегламентногоЗаданияОстатков);
			
			Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
			Если ОбщегоНазначения.РазделениеВключено() Тогда
				//@skip-warning
				ИдентификаторРегламентногоЗаданияОстатков = Задание.Идентификатор.УникальныйИдентификатор();
			Иначе
				ИдентификаторРегламентногоЗаданияОстатков = Задание.УникальныйИдентификатор;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Задание = Неопределено Тогда
			ИдентификаторРегламентногоЗаданияОстатков = Неопределено;
		КонецЕсли;
		
	Иначе
		РегламентныеЗаданияСервер.УдалитьЗадание(ИдентификаторРегламентногоЗаданияОстатков);
		ИдентификаторРегламентногоЗаданияОстатков = Неопределено;
#КонецОбласти
 	КонецЕсли;		 

	Если СпособВыгрузкиКартинок = "ПоРасписанию"  И НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
#Область ФоновоеЗаданиеКартинок
			
		ПараметрыЗапуска = Новый Массив();
		ПараметрыЗапуска.Добавить(СтруктураРеглЗаданий.ВыгрузкаКартинок);
		ПараметрыЗапуска.Добавить(НастройкаПодключения);

		Задание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗаданияКартинок);
		Если Задание <> Неопределено Тогда

			ПараметрыИзмененияЗадания = Новый Структура();
			ПараметрыИзмененияЗадания.Вставить("Использование", Истина);
			ПараметрыИзмененияЗадания.Вставить("Параметры", ПараметрыЗапуска);
			ПараметрыИзмененияЗадания.Вставить("Расписание", РасписаниеРегламентногоЗаданияКартинок);
			ПараметрыИзмененияЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
			ПараметрыИзмененияЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);

			РегламентныеЗаданияСервер.ИзменитьЗадание(ИдентификаторРегламентногоЗаданияКартинок, ПараметрыИзмененияЗадания);

		Иначе
			
			ПараметрыЗадания = Новый Структура();
			ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ЗапускДополнительныхОбработок);
			ПараметрыЗадания.Вставить("Наименование", ИменаИспользуемыхРеглЗаданий.ВыгрузкаКартинок
			+ " " + Наименование);
			ПараметрыЗадания.Вставить("Ключ", ИменаИспользуемыхРеглЗаданий.ВыгрузкаКартинок
			+ " " + Наименование);
			ПараметрыЗадания.Вставить("Использование", Истина);
			ПараметрыЗадания.Вставить("ЭксклюзивноеВыполнение", Ложь);
			ПараметрыЗадания.Вставить("Параметры", ПараметрыЗапуска);
			ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
			ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);
			ПараметрыЗадания.Вставить("Расписание", РасписаниеРегламентногоЗаданияКартинок);
			
			Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
			Если ОбщегоНазначения.РазделениеВключено() Тогда
				//@skip-warning
				ИдентификаторРегламентногоЗаданияКартинок = Задание.Идентификатор.УникальныйИдентификатор();
			Иначе
				ИдентификаторРегламентногоЗаданияКартинок = Задание.УникальныйИдентификатор;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Задание = Неопределено Тогда
			ИдентификаторРегламентногоЗаданияКартинок = Неопределено;
		КонецЕсли;
		
	Иначе
		РегламентныеЗаданияСервер.УдалитьЗадание(ИдентификаторРегламентногоЗаданияКартинок);
		ИдентификаторРегламентногоЗаданияКартинок = Неопределено;
#КонецОбласти
 	КонецЕсли;		 
	
УстановитьПривилегированныйРежим(Ложь);	
		
	НоваяЗаписьНастройки = РегистрыСведений.Б24_СУ_Настройки.СоздатьМенеджерЗаписи();   
    НоваяЗаписьНастройки.НастройкаПодключения = НастройкаПодключения;
    НоваяЗаписьНастройки.КоличествоПовторенийПриОшибках = КоличествоПовторенийПриОшибках;

	лНастройки = РегистрыСведений.Б24_СУ_Настройки.ПолучитьНастройкиПоУмолчанию(); 
	Выборка = РегистрыСведений.Б24_СУ_Настройки.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
	Пока Выборка.Следующий() Цикл
		Если Выборка.Настройки.Получить() <> Неопределено Тогда
			лНастройки = Выборка.Настройки.Получить();	
		КонецЕсли;
	КонецЦикла;
	
	лНастройки.НастройкиТоваров.ИдентификаторКаталогаТоваров 		= ИдентификаторКаталогаТоваров; 
	лНастройки.НастройкиТоваров.ИдентификаторКаталогаПредложений 	= ИдентификаторКаталогаПредложений; 
	
	лНастройки.НастройкиТоваров.Вставить("ВыгружатьКартинки", ВыгружатьКартинки); 
	лНастройки.НастройкиТоваров.Вставить("СпособВыгрузки"	, СпособВыгрузкиКартинок);
	лНастройки.НастройкиТоваров.Вставить("ИдентификаторРегламентногоЗаданияКартинок", ИдентификаторРегламентногоЗаданияКартинок);
	
	
	лНастройки.НастройкиЦен.Прайсы 		= Прайсы.Выгрузить();
	лНастройки.НастройкиОстатков.Склады = Склады.Выгрузить();
	
	лНастройки.НастройкиЦен.Вставить("СпособВыгрузки", СпособВыгрузкиЦен);
	лНастройки.НастройкиЦен.Вставить("ИдентификаторРегламентногоЗаданияЦен", ИдентификаторРегламентногоЗаданияЦен);
	
	лНастройки.НастройкиОстатков.Вставить("СпособВыгрузки", СпособВыгрузкиОстатков);
	лНастройки.НастройкиОстатков.Вставить("ИдентификаторРегламентногоЗаданияОстатков", ИдентификаторРегламентногоЗаданияОстатков);
	
	лНастройки.НастройкииЗаказов.Вставить("РезервироватьТовары"	, РезервироватьТовары);
	лНастройки.НастройкииЗаказов.Вставить("ОтгружатьТовары"		, ОтгружатьТовары);
	
	лНастройки.НастройкииЗаказов.Вставить("АлгоритмЗаполненияЗаказа"	, АлгоритмЗаполненияЗаказа);
	лНастройки.НастройкииЗаказов.Вставить("АлгоритмЗаполненияОтгрузки"	, АлгоритмЗаполненияОтгрузки);
	лНастройки.НастройкииЗаказов.Вставить("Организация"			, Организация);
	лНастройки.НастройкииЗаказов.Вставить("Подразделение"		, Подразделение);
	лНастройки.НастройкииЗаказов.Вставить("Ответственный"		, Ответственный);
	лНастройки.НастройкииЗаказов.Вставить("Соглашение"			, Соглашение);
	лНастройки.НастройкииЗаказов.Вставить("СтатьяДДС"			, СтатьяДДС);
	лНастройки.НастройкииЗаказов.Вставить("НоменклатураДоставка", НоменклатураДоставка);
	
	лНастройки.НастройкииЗаказов.Вставить("СвойствоСлужбаДоставки", СвойствоСлужбаДоставки);
	лНастройки.НастройкииЗаказов.Вставить("СпособыДоставки", СпособыДоставки.Выгрузить());
	
	НоваяЗаписьНастройки.Настройки = Новый ХранилищеЗначения(лНастройки);
		
	Попытка 
			
		НоваяЗаписьНастройки.Записать();
			
		ОбновитьПовторноИспользуемыеЗначения();

		Модифицированность = Ложь;
			                                                           
	Исключение
		Возврат Ложь;	
	КонецПопытки;
		
	Возврат Истина
	
КонецФункции  

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	Если ПроверитьКорректностьНастроек() Тогда
		СохранитьВыгрузитьНастройки(); 
	КонецЕсли;
	
	Если ПредлагатьПодборТовара И НЕ Модифицированность Тогда     
		ПредложитьДобавитьВстройкуТоваров();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)   
	
	Если ПроверитьКорректностьНастроек() Тогда
		СохранитьВыгрузитьНастройки(Истина);
	КонецЕсли;  
	
	Если ПредлагатьПодборТовара И НЕ Модифицированность Тогда     
		ПредложитьДобавитьВстройкуТоваров();
	КонецЕсли;  
	
КонецПроцедуры


&НаКлиенте
Асинх Процедура УдалитьНастройку(Команда)
	
	обещание = ВопросАсинх("Настройки складского учета будут удалены. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Результат = Ждать обещание;
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

	УдалитьНастройкуСервер(НастройкаПодключения);
	
	Модифицированность = Ложь;
	
	Оповестить("СозданаИзмененаНастройка");
	
	Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьНастройкуСервер(НастройкаПодключения)
	
	ЗаписьНастройкиСинхронизации = РегистрыСведений.Б24_СУ_Настройки.СоздатьМенеджерЗаписи();
	ЗаписьНастройкиСинхронизации.НастройкаПодключения = НастройкаПодключения;
	ЗаписьНастройкиСинхронизации.Прочитать();
	ЗаписьНастройкиСинхронизации.Удалить();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры


&НаКлиенте
Процедура ОчиститьНастройкиВыгрузки(Команда)
	
	ОчиститьНастройкиНаСервере();
	
	СохранитьВыгрузитьНастройки();

	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНастройкиНаСервере()
	
	НастройкаСинхронизации = РегистрыСведений.Б24_СУ_Настройки.ПолучитьНастройкиПоУмолчанию();
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, НастройкаСинхронизации); 
	
	Прайсы.Очистить();
	Склады.Очистить();
	
	Выборка = РегистрыСведений.Б24_СУ_Настройки.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
	Пока Выборка.Следующий() Цикл
		Выборка.ПолучитьМенеджерЗаписи().Удалить();
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Асинх Процедура НастроитьРасписаниеОбменаОстатков(Команда)
		
	Если РасписаниеРегламентногоЗаданияОстатков = НеОпределено Тогда
		РасписаниеРегламентногоЗаданияОстатков = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗаданияОстатков);

	Обещание = Ждать Диалог.ОткрытьАсинх();
	
	Если Обещание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РасписаниеРегламентногоЗаданияОстатков = Обещание;
	
	УстановитьНадписьРасписанияОбмена();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура НастроитьРасписаниеОбменаЦен(Команда)
		
	Если РасписаниеРегламентногоЗаданияЦен = НеОпределено Тогда
		РасписаниеРегламентногоЗаданияЦен = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗаданияЦен);

	Обещание = Ждать Диалог.ОткрытьАсинх();
	
	Если Обещание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РасписаниеРегламентногоЗаданияЦен = Обещание;
	
	УстановитьНадписьРасписанияОбмена();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура НастроитьРасписаниеОбменаКартинок(Команда)
		
	Если РасписаниеРегламентногоЗаданияКартинок = НеОпределено Тогда
		РасписаниеРегламентногоЗаданияКартинок = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗаданияКартинок);

	Обещание = Ждать Диалог.ОткрытьАсинх();
	
	Если Обещание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РасписаниеРегламентногоЗаданияКартинок = Обещание;
	
	УстановитьНадписьРасписанияОбмена();
	
	Модифицированность = Истина;
	
КонецПроцедуры


&НаКлиенте
Процедура ЗагрузитьПрайсы(Команда)     
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка прайсов.";
	
	Прайсы.Очистить();
	
	ТелоHTTPЗапроса = "&filter[active]=Y";	
	ДанныеПрайсов = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/catalog.pricetype.list", "id", ТелоHTTPЗапроса, "priceTypes");	
	Для каждого ТекЭлемент Из ДанныеПрайсов Цикл
		
		ИдЭлемента 	= Формат(ТекЭлемент.Получить("id"), "ЧГ=0");
		Базовый 	= ТекЭлемент.Получить("base") = "Y";          
		
		СтрокаПрайса = Прайсы.Добавить();
		СтрокаПрайса.НаименованиеПрайсаБ24 = ТекЭлемент.Получить("name");
		СтрокаПрайса.ИдПрайсаБ24 = ИдЭлемента;
		СтрокаПрайса.Базовый 	 = Базовый;        
		
		//Если НЕ ЗначениеЗаполнено(СтрокаПрайса.Прайс1С) Тогда
		//	СтрокаПрайса.Прайс1С = ЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Прайс, ИдЭлемента);
		//КонецЕсли;
		
	КонецЦикла;    
	
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПрайсы(Команда)  
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Выгрузка прайс листов";

	ВыгрузитьПрайсыСервер(СтруктураНастроек);
	
	ЗагрузитьПрайсы(Неопределено);	
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьПрайсыСервер(СтруктураНастроек)

	СформированныеЗапросы = Новый Массив;
	
	Для каждого ТекСтрока Из Прайсы Цикл
		
		Прайс1С = ТекСтрока.Прайс1С;
		Если НЕ ЗначениеЗаполнено(Прайс1С) Тогда
			Продолжить;	
		КонецЕсли;
		
		КлючПрайса	= XMLСтрока(Прайс1С);	
	    ИмяПрайса 	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Прайс1С, "Наименование");
		
		Поля = Новый Структура;
		Если НЕ ЗначениеЗаполнено(ИмяПрайса) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "У прайса: " + ИмяПрайса + ". Пустое Наименование, прайс не будет выгружен в Битрикс24");
			Продолжить;	
		КонецЕсли;
	
		Поля.Вставить("name"	, ИмяПрайса);  	
		Поля.Вставить("xmlId"	, КлючПрайса);
	
		ТелоЗапроса = "";
		Для каждого ТекПоле Из Поля Цикл
			ТелоЗапроса = ТелоЗапроса + ?(СтрДлина(ТелоЗапроса) >0, "&", "") +"fields["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
		КонецЦикла;  
	
		ИдПрайсаБ24 = ТекСтрока.ИдПрайсаБ24;	
		Если НЕ ЗначениеЗаполнено(ИдПрайсаБ24) Тогда
			СформированныеЗапросы.Добавить("cmd["+КлючПрайса+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("catalog.pricetype.add?" + ТелоЗапроса));	
		Иначе
			СформированныеЗапросы.Добавить("cmd["+КлючПрайса+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("catalog.pricetype.update?id=" + ИдПрайсаБ24 + "&"  + ТелоЗапроса));	
		КонецЕсли;
		
	КонецЦикла;
	
	КоличествоВЗапросе 	= 0;	
	ТелоHTTPЗапроса		= "";
	Для каждого ТекЭлемент Из СформированныеЗапросы Цикл
		
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + ТекЭлемент;	
		
		КоличествоВЗапросе = КоличествоВЗапросе + 1;
		Если КоличествоВЗапросе = 50 Тогда
			
			ДанныеБитрикс24 = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
			
			ОбработатьДанныеБитрикс24(СтруктураНастроек, ДанныеБитрикс24, "Прайсы");
			
			КоличествоВЗапросе 	= 0;	
			ТелоHTTPЗапроса 	= "";     
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если КоличествоВЗапросе > 0 Тогда
		
		ДанныеБитрикс24 = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);  
		
		ОбработатьДанныеБитрикс24(СтруктураНастроек, ДанныеБитрикс24, "Прайсы");
			
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ЗагрузитьСклады(Команда)
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка складов.";
	
	Склады.Очистить();
	
	ТелоHTTPЗапроса = "&filter[active]=Y";	
	ДанныеСкладов = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/catalog.store.list", "id", ТелоHTTPЗапроса, "stores");	
	Для каждого ТекЭлемент Из ДанныеСкладов Цикл
		
		ИдЭлемента = Формат(ТекЭлемент.Получить("id"), "ЧГ=0");
		
		СтрокаСклада = Склады.Добавить();
		СтрокаСклада.НаименованиеСкладаБ24 = ТекЭлемент.Получить("title");
		СтрокаСклада.ИдСкладаБ24 = ИдЭлемента;
		
		Если НЕ ЗначениеЗаполнено(СтрокаСклада.Склад1С) Тогда
			СтрокаСклада.Склад1С = ЗначениеПоИдБ24Объекта(СтруктураНастроек.Портал, СтруктураНастроек.ТипыОбъектовОбмена.Склад, ИдЭлемента);
		КонецЕсли;
		
	КонецЦикла;           

	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьСклады(Команда)
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Выгрузка складов";

	ВыгрузитьСкладыСервер(СтруктураНастроек);
	
	ЗагрузитьСклады(Неопределено);	
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьСкладыСервер(СтруктураНастроек)

	СформированныеЗапросы = Новый Массив;
	
	Для каждого ТекСтрока Из Склады Цикл
		
		Склад1С = ТекСтрока.Склад1С;
		Если НЕ ЗначениеЗаполнено(Склад1С) Тогда
			Продолжить;	
		КонецЕсли;
		
		КлючСклада		= XMLСтрока(Склад1С);	
		РеквизитыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Склад1С, "Наименование, КонтактнаяИнформация");
		АдресСклада 	= "1С";
		
		КонтактнаяИнформация = РеквизитыСклада.КонтактнаяИнформация.Выгрузить();
		НайденнаяСтрока = КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.АдресСклада);
		Если НайденнаяСтрока <> Неопределено И ЗначениеЗаполнено(НайденнаяСтрока.Представление) Тогда
			АдресСклада = НайденнаяСтрока.Представление;	
		КонецЕсли;
		
		Поля = Новый Структура;
		Если НЕ ЗначениеЗаполнено(РеквизитыСклада.Наименование) Тогда
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "У склада: " + РеквизитыСклада.Наименование + ". Пустое Наименование, склад не будет выгружен в Битрикс24");
			Продолжить;	
		КонецЕсли;
	
		Поля.Вставить("title"	, РеквизитыСклада.Наименование);  	
		Поля.Вставить("xmlId"	, КлючСклада);
		Поля.Вставить("address"	, АдресСклада);
	
		ТелоЗапроса = "";            
		Для каждого ТекПоле Из Поля Цикл
			ТелоЗапроса = ТелоЗапроса + ?(СтрДлина(ТелоЗапроса) >0, "&", "") +"fields["+ ТекПоле.Ключ + "]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекПоле.Значение); 
		КонецЦикла;  
	
		ИдСкладаБ24 = ТекСтрока.ИдСкладаБ24;	
		Если НЕ ЗначениеЗаполнено(ИдСкладаБ24) Тогда
			СформированныеЗапросы.Добавить("cmd["+КлючСклада+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("catalog.store.add?" + ТелоЗапроса));	
		Иначе
			СформированныеЗапросы.Добавить("cmd["+КлючСклада+"]=" +  Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("catalog.store.update?id=" + ИдСкладаБ24 + "&"  + ТелоЗапроса));	
		КонецЕсли;
		
	КонецЦикла;
	
	КоличествоВЗапросе 	= 0;	
	ТелоHTTPЗапроса		= "";
	Для каждого ТекЭлемент Из СформированныеЗапросы Цикл
		
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + ТекЭлемент;	
		
		КоличествоВЗапросе = КоличествоВЗапросе + 1;
		Если КоличествоВЗапросе = 50 Тогда
			
			ДанныеБитрикс24 = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
			
			ОбработатьДанныеБитрикс24(СтруктураНастроек, ДанныеБитрикс24, "Склады");
			
			КоличествоВЗапросе 	= 0;	
			ТелоHTTPЗапроса 	= "";     
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если КоличествоВЗапросе > 0 Тогда
		
		ДанныеБитрикс24 = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);  
		
		ОбработатьДанныеБитрикс24(СтруктураНастроек, ДанныеБитрикс24, "Склады");
			
	КонецЕсли;
	
КонецПроцедуры



&НаКлиенте
Процедура ЗагрузитьСлужбыДоставки(Команда)
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка доставок.";
	
	ТелоHTTPЗапроса = "";	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/sale.deliveryservices.getactivelist", ТелоHTTPЗапроса); 
	
	Если СтруктураОтвета <> Неопределено Тогда
		
		result = СтруктураОтвета.Получить("result");
		
		Если result <> Неопределено Тогда
			
			deliveryServices =  result.Получить("deliveryServices");
			
			Если deliveryServices <> Неопределено Тогда
				
				Для Каждого delivery Из deliveryServices Цикл 
					
					НазваниеСлужбыБ24 	= delivery.Получить("name");
					ИдСлужбыБ24			= Формат(delivery.Получить("id"),"ЧГ=0");
					
					НайденныеСтроки = СпособыДоставки.НайтиСтроки(Новый Структура("ИдСлужбыБ24", ИдСлужбыБ24));
					Если НайденныеСтроки.количество() = 0 Тогда
						НоваяЗапись = СпособыДоставки.Добавить();
						НоваяЗапись.ИдСлужбыБ24			= ИдСлужбыБ24;
						НоваяЗапись.НазваниеСлужбыБ24 	= НазваниеСлужбыБ24;
					Иначе
						Для Каждого ТекСтрокаТаблицы Из СпособыДоставки Цикл
							Если ТекСтрокаТаблицы.ИдСлужбыБ24 		= ИдСлужбыБ24 Тогда
								ТекСтрокаТаблицы.НазваниеСлужбыБ24 	= НазваниеСлужбыБ24; 
								Прервать;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли; 						
					
				КонецЦикла;

			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Модифицированность = Истина;

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы      

&НаКлиенте
Процедура СпособВыгрузкиЦенПриИзменении(Элемент)   
	
	ВыгружатьНастройкиНаПортал = Истина;  
	
	ОбновитьОтображениеЭлементовФормы(); 
	
КонецПроцедуры

&НаКлиенте
Процедура СпособВыгрузкиОстатковПриИзменении(Элемент)
	
	ВыгружатьНастройкиНаПортал = Истина;  
	
	ОбновитьОтображениеЭлементовФормы(); 
	
КонецПроцедуры 

&НаКлиенте
Процедура ПрайсыПриИзменении(Элемент)

	ОбновитьОтображениеЗаголовокСвернутогоОтображения();

КонецПроцедуры

&НаКлиенте
Процедура СкладыПриИзменении(Элемент)

	ОбновитьОтображениеЗаголовокСвернутогоОтображения();

КонецПроцедуры

&НаКлиенте
Процедура ЗадатьАлгоритмЗаполненияОтгрузкиНажатие(Элемент)
	
	КлючиПортала = Новый СписокЗначений;
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка полей";
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.deal.fields", ""); 
	Если СтруктураОтвета <> Неопределено Тогда
		
		result = СтруктураОтвета.Получить("result"); 
		Если result <> Неопределено Тогда
			
			Для каждого ТекЭлемент Из result Цикл    
				
				Если Лев(ТекЭлемент.Ключ, 6) = "UF_CRM" Тогда
					НаименованиеКлюча = ТекЭлемент.Значение.Получить("formLabel");
				Иначе
					НаименованиеКлюча = ТекЭлемент.Значение.Получить("title"); 
				КонецЕсли;
				
				КлючиПортала.Добавить("Проект.Получить("""+ТекЭлемент.Ключ+""")", "(Проект) " + НаименованиеКлюча);	
				
			КонецЦикла;  
			
		КонецЕсли;	
		
	КонецЕсли;
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/sale.shipment.getFields", ""); 
	Если СтруктураОтвета <> Неопределено Тогда
		
		result = СтруктураОтвета.Получить("result"); 
		Если result <> Неопределено Тогда
			
			shipment = result.Получить("shipment");
			Если shipment <> Неопределено Тогда
			
				Для каждого ТекЭлемент Из shipment Цикл    
					КлючиПортала.Добавить("Отгрузка.Получить("""+ТекЭлемент.Ключ+""")", "(Отгрузка) " + ТекЭлемент.Ключ);	
				КонецЦикла;  
			
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЕсли;

	Если ЗначениеЗаполнено(АлгоритмЗаполненияОтгрузки) Тогда
		ПередаваемыйАлгоритм = АлгоритмЗаполненияОтгрузки;	
	Иначе
		ПередаваемыйАлгоритм = "//  Пример алгоритма:
		|
		|//[Объект1С].Комментарий = [ОбъектПортала].Сделка.Получить(""COMMENTS"");   
		|";
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();     
	ПараметрыФормы.Вставить("ВидАлгоритма"	, "Отгрузка");	
	ПараметрыФормы.Вставить("Тип"			, "Процедура");	 
	ПараметрыФормы.Вставить("Назначение"	, "Загрузка");	
	ПараметрыФормы.Вставить("Алгоритм"		, ПередаваемыйАлгоритм);	

	ПараметрыФормы.Вставить("СоставныеКлючи", Истина);	 
	ПараметрыФормы.Вставить("КлючиПортала"	, КлючиПортала);	
	
	ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПараметрыФормы,,,,,Новый ОписаниеОповещения("ПослеУказанияАлгоритма", ЭтотОбъект, ПараметрыФормы), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры  

&НаКлиенте
Процедура ЗадатьАлгоритмЗаполненияЗаказаНажатие(Элемент)

	КлючиПортала = Новый СписокЗначений;
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка полей Проекты";
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.deal.fields", ""); 
	Если СтруктураОтвета <> Неопределено Тогда
		
		result = СтруктураОтвета.Получить("result"); 
		Если result <> Неопределено Тогда
			
			Для каждого ТекЭлемент Из result Цикл    
				
				Если Лев(ТекЭлемент.Ключ, 6) = "UF_CRM" Тогда
					НаименованиеКлюча = ТекЭлемент.Значение.Получить("formLabel");
				Иначе
					НаименованиеКлюча = ТекЭлемент.Значение.Получить("title"); 
				КонецЕсли;
				
				КлючиПортала.Добавить(ТекЭлемент.Ключ, НаименованиеКлюча);
				
			КонецЦикла;  
			
		КонецЕсли;	
		
	КонецЕсли;
	
	
	Если ЗначениеЗаполнено(АлгоритмЗаполненияЗаказа) Тогда
		ПередаваемыйАлгоритм = АлгоритмЗаполненияЗаказа;	
	Иначе
		ПередаваемыйАлгоритм = "//  Пример алгоритма:
		|
		|//[Объект1С].ПометкаУдаления = [ОбъектПортала].Получить(""UF_CRM_DEAL_3835839993709"")=""Y"";   
		|";
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ВидАлгоритма"	, "Заказ");	
	ПараметрыФормы.Вставить("Тип"			, "Процедура");	
	ПараметрыФормы.Вставить("Назначение"	, "Загрузка");	
	ПараметрыФормы.Вставить("Алгоритм"		, ПередаваемыйАлгоритм);	
	ПараметрыФормы.Вставить("КлючиПортала"	, КлючиПортала);	
	
	ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПараметрыФормы,,,,,Новый ОписаниеОповещения("ПослеУказанияАлгоритма", ЭтотОбъект, ПараметрыФормы), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеУказанияАлгоритма(Результат, Параметры) Экспорт
	
	ОтветФормы = Результат;
	Если ОтветФормы <> Неопределено Тогда

		Если Параметры.ВидАлгоритма = "Отгрузка" Тогда   
			АлгоритмЗаполненияОтгрузки = ОтветФормы;
		ИначеЕсли Параметры.ВидАлгоритма = "Заказ" Тогда
			АлгоритмЗаполненияЗаказа = ОтветФормы;
		КонецЕсли;
		
		ОбновитьОтображениеЭлементовФормы();

		Модифицированность = Истина;
		
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура РезервироватьТоварыПриИзменении(Элемент)      
	
	ВыгружатьНастройкиНаПортал = Истина;
	
	ОбновитьОтображениеЗаголовокСвернутогоОтображения();   
	
	ОбновитьОтображениеЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтгружатьТоварыПриИзменении(Элемент)    
	
	ВыгружатьНастройкиНаПортал = Истина;
	
	ОбновитьОтображениеЗаголовокСвернутогоОтображения(); 
	
	ОбновитьОтображениеЭлементовФормы();
	
КонецПроцедуры   

&НаКлиенте
Процедура СпособыДоставкиСлужба1СНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(СвойствоСлужбаДоставки) Тогда СтандартнаяОбработка = Ложь КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ВыгрузкаНастроек

&НаКлиенте
Процедура ВыгрузкаНастройки(ЗакрытьПоОкончании)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ТекстСообщения             = "Выполняется выгрузка настроек..";
	ПараметрыОжидания.ВыводитьОкноОжидания       = Ложь;
	
	НачатьВыгрузкуНастройки(НастройкаПодключения);
	
	Если ЗакрытьПоОкончании Тогда
		Закрыть();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьВыгрузкуНастройки(НастройкаПодключения)
	
	УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка регистрации изменений в Битрикс24";
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыВыгрузки.Вставить("Наименование"			, Строка(НастройкаПодключения));
	ПараметрыВыгрузки.Вставить("РезервироватьТовары"	, РезервироватьТовары);
	ПараметрыВыгрузки.Вставить("ОтгружатьТовары"		, ОтгружатьТовары);
	                                 
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.Б24_СУ_Настройки.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти


#Область Прочие

&НаКлиенте
Процедура ОбновитьОтображениеЭлементовФормы()

	Элементы.ГруппаНастройкаОбменаПоРасписаниюЦен.Видимость 		= СпособВыгрузкиЦен = "ПоРасписанию";
	Элементы.ГруппаНастройкаОбменаПоРасписаниюОстатков.Видимость 	= СпособВыгрузкиОстатков = "ПоРасписанию";
	Элементы.ГруппаНастройкаОбменаПоРасписаниюКартинок.Видимость 	= СпособВыгрузкиКартинок = "ПоРасписанию";

	Элементы.ГруппаСпособВыгрузкиКартинок.Видимость 				= ВыгружатьКартинки;
	
	Если ЗначениеЗаполнено(АлгоритмЗаполненияЗаказа) Тогда
		Элементы.ЗадатьАлгоритмЗаполненияЗаказа.Заголовок = "Изменить алгоритм заполнения заказа";	
	Иначе
		Элементы.ЗадатьАлгоритмЗаполненияЗаказа.Заголовок = "Задать алгоритм заполнения заказа";	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АлгоритмЗаполненияОтгрузки) Тогда
		Элементы.ЗадатьАлгоритмЗаполненияОтгрузки.Заголовок = "Изменить алгоритм заполнения отгрузки";	
	Иначе
		Элементы.ЗадатьАлгоритмЗаполненияОтгрузки.Заголовок = "Задать алгоритм заполнения отгрузки";	
	КонецЕсли;
	
	Элементы.ОтгружатьТовары.Доступность = РезервироватьТовары;
	
	Элементы.ГруппаЗаполнениеПоУмолчанию.Видимость = РезервироватьТовары ИЛИ ОтгружатьТовары;
	
	Элементы.ГруппаСлужбыДоставки.Видимость = ОтгружатьТовары И РезервироватьТовары;
	Элементы.НоменклатураДоставка.Видимость = ОтгружатьТовары ИЛИ РезервироватьТовары; 
	
	Элементы.ЗадатьАлгоритмЗаполненияЗаказа.Видимость = РезервироватьТовары;
	Элементы.ЗадатьАлгоритмЗаполненияОтгрузки.Видимость = ОтгружатьТовары И РезервироватьТовары;
	                                                                       
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеЗаголовокСвернутогоОтображения()
	
	Если Прайсы.Количество() = 0 Тогда
		Элементы.ГруппаЦены.ЗаголовокСвернутогоОтображения = "Настроить выгрузку цены"; 
	Иначе
		Элементы.ГруппаЦены.ЗаголовокСвернутогоОтображения = "Цены"; 
	КонецЕсли; 
	
	Если Склады.Количество() = 0 Тогда
		Элементы.ГруппаОстатки.ЗаголовокСвернутогоОтображения = "Настроить выгрузки остатков"; 
	Иначе
		Элементы.ГруппаОстатки.ЗаголовокСвернутогоОтображения = "Остатки"; 
	КонецЕсли;
	
	Если РезервироватьТовары ИЛИ ОтгружатьТовары Тогда
		Элементы.ГруппаОтгрузки.ЗаголовокСвернутогоОтображения = "Отгрузки";	
	Иначе
		Элементы.ГруппаОтгрузки.ЗаголовокСвернутогоОтображения = "Настроить отгрузки";	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНадписьРасписанияОбмена()
	
	Если РасписаниеРегламентногоЗаданияЦен = НеОпределено Тогда
		ТекстЗаголовка = НСтр("ru='Настроить расписание выгрузки'");
	Иначе
		ТекстЗаголовка = РасписаниеРегламентногоЗаданияЦен;
	КонецЕсли;
	
	Элементы.НастроитьРасписаниеОбменаЦен.Заголовок = ТекстЗаголовка;
	
	Если РасписаниеРегламентногоЗаданияОстатков = НеОпределено Тогда
		ТекстЗаголовка = НСтр("ru='Настроить расписание выгрузки'");
	Иначе
		ТекстЗаголовка = РасписаниеРегламентногоЗаданияОстатков;
	КонецЕсли;
	
	Элементы.НастроитьРасписаниеОбменаОстатков.Заголовок = ТекстЗаголовка;
	
	Если РасписаниеРегламентногоЗаданияКартинок = НеОпределено Тогда
		ТекстЗаголовка = НСтр("ru='Настроить расписание выгрузки'");
	Иначе
		ТекстЗаголовка = РасписаниеРегламентногоЗаданияКартинок;
	КонецЕсли;
	
	Элементы.НастроитьРасписаниеОбменаКартинок.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаСервере
Функция ЗначениеПоИдБ24Объекта(Портал, ТипДанных, Идентификатор)
	
	Результат = Неопределено;
	
	ДанныеИд = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьЗначениеПоИдБ24Объекта(Портал, ТипДанных, Идентификатор);
	Если ДанныеИд <> Неопределено Тогда  
		Результат = ДанныеИд.Объект;	
	КонецЕсли;   
	
	Возврат Результат; 

КонецФункции

&НаСервере
Процедура ОбработатьДанныеБитрикс24(СтруктураНастроек, ДанныеБитрикс24, ТипОбъекта)

	БатчДанные = ДанныеБитрикс24.Получить("result");
	Если БатчДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	result = БатчДанные.Получить("result");
	
	Если ТипОбъекта = "Прайсы" Тогда
		
		Для Каждого ТекЭлемент Из result Цикл
			
			Прайс = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.ВидыЦен"), ТекЭлемент.Ключ);
			Если ЗначениеЗаполнено(Прайс) Тогда
				
				priceType = ТекЭлемент.Значение.Получить("priceType");
				Если priceType <> Неопределено Тогда
					ИдЭлемента = Формат(priceType.Получить("id"), "ЧГ=0");
					РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Прайс, Прайс, ИдЭлемента);  
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипОбъекта = "Склады" Тогда
		
		Для Каждого ТекЭлемент Из result Цикл
			
			Склад = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеПоXML(Тип("СправочникСсылка.Склады"), ТекЭлемент.Ключ);
			Если ЗначениеЗаполнено(Склад) Тогда
				
				ИдЭлемента = Формат(ТекЭлемент.Значение, "ЧГ=0");
				Если ИдЭлемента <> Неопределено Тогда
					РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.Склад, Склад, ИдЭлемента);  
				КонецЕсли; 
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаСпискаИнфоблоков()
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка каталогов на портале.";
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/catalog.catalog.list", ""); 
	Если СтруктураОтвета <> Неопределено Тогда
		
		result = СтруктураОтвета.Получить("result"); 
		Если result <> Неопределено Тогда
			
			Каталоги = result.Получить("catalogs"); 
			Если Каталоги <> Неопределено Тогда

				Элементы.ИдентификаторКаталогаТоваров.СписокВыбора.Очистить();
				Элементы.ИдентификаторКаталогаПредложений.СписокВыбора.Очистить(); 
				
				Для каждого ТекЭлемент Из Каталоги Цикл     
					Элементы.ИдентификаторКаталогаТоваров.СписокВыбора.Добавить(Формат(ТекЭлемент.Получить("id"),"ЧГ=0"), ТекЭлемент.Получить("name"));
					Элементы.ИдентификаторКаталогаПредложений.СписокВыбора.Добавить(Формат(ТекЭлемент.Получить("id"),"ЧГ=0"), ТекЭлемент.Получить("name"));
				КонецЦикла;  
				
			КонецЕсли;
			
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнитьИдентификаторыИнфоблоковПоУмолчанию() 
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторКаталогаТоваров) Тогда 
		Для каждого ТекЭлемент Из Элементы.ИдентификаторКаталогаТоваров.СписокВыбора Цикл
			Если СтрНайти(ТекЭлемент.Представление, "(предложения)") = 0 Тогда
				ИдентификаторКаталогаТоваров = ТекЭлемент.Значение;
				Модифицированность = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторКаталогаПредложений) Тогда
		Для каждого ТекЭлемент Из Элементы.ИдентификаторКаталогаПредложений.СписокВыбора Цикл
			Если СтрНайти(ТекЭлемент.Представление, "(предложения)") > 0 Тогда
				ИдентификаторКаталогаПредложений = ТекЭлемент.Значение;   
				Модифицированность = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры   

&НаСервере
Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
КонецФункции

&НаКлиенте
Функция ПроверитьКорректностьНастроек()
	
	Результат = Истина;
	
	//Если НЕ ЗначениеЗаполнено(Соглашение) И РезервироватьТовары Тогда
	//	ОбщегоНазначенияКлиент.СообщитьПользователю( "Не заполнено соглашение",,, "Соглашение");
	//	Результат = Ложь;
	//КонецЕсли;
	//
	//Если НЕ ЗначениеЗаполнено(Подразделение) И РезервироватьТовары Тогда
	//	ОбщегоНазначенияКлиент.СообщитьПользователю( "Не заполнено подразделение",,, "Подразделение");
	//	Результат = Ложь;
	//КонецЕсли;
	//
	//Если НЕ ЗначениеЗаполнено(Склады) Тогда
	//	ОбщегоНазначенияКлиент.СообщитьПользователю( "Не заполнено сопоставление складов",,, "Склады");
	//	Результат = Ложь;
	//КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаСервере
Функция ПредлагатьПодборТовара(НастройкаПодключения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СУ_Настройки.НастройкаПодключения КАК НастройкаПодключения,
	|	Б24_СУ_Настройки.КоличествоПовторенийПриОшибках КАК КоличествоПовторенийПриОшибках,
	|	Б24_СУ_Настройки.Настройки КАК Настройки
	|ИЗ
	|	РегистрСведений.Б24_СУ_Настройки КАК Б24_СУ_Настройки
	|ГДЕ
	|	Б24_СУ_Настройки.НастройкаПодключения = &НастройкаПодключения";
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Возврат ВыполненныйЗапрос.Пустой();

КонецФункции

#КонецОбласти


&НаКлиенте
Процедура ДобавитьВстройкуТоваров(Команда)
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	АдресДоПриложения = Б24_К_RestApiВызовСервера.ПолучитьАдресДоПриложенияБитрикс24()+"?idAdress="+ЗначениеРеквизитаОбъекта(НастройкаПодключения, "ИдентификаторПодключения");
	
	//ПараметрыЗапроса = "PLACEMENT=CATALOG_PRODUCT_SEARCH&HANDLER="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(АдресДоПриложения+"&seach");
	ПараметрыЗапроса = "PLACEMENT=CATALOG_EXTERNAL_PRODUCT&HANDLER="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(АдресДоПриложения);
	ДанныеПортала = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/placement.bind", ПараметрыЗапроса); 
	Если ДанныеПортала = Неопределено Тогда   
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не удалось получить ответ портала");
		Возврат;
	КонецЕсли;
	
	result = ДанныеПортала.Получить("result");
	Если result <> Истина Тогда
		error_description = ДанныеПортала.Получить("error_description");
		Если  error_description = "Unable to set placement handler: Handler already binded" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю("Встройка для товаров уже выгружена");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПредлагатьПодборТовара = Ложь;
	
	ОбщегоНазначенияКлиент.СообщитьПользователю("Встройка добавлена"); 
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВстройкуТоваров(Команда)
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ДанныеПортала = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/placement.get", ""); 
	Если ДанныеПортала <> Неопределено Тогда
	
		мВстройкиПортала = ДанныеПортала.Получить("result");
		Если мВстройкиПортала <> Неопределено Тогда     
			Для каждого ТекЭлемент Из мВстройкиПортала Цикл   
				
				МестоВстройки = ТекЭлемент.Получить("placement");
				
				Если МестоВстройки = "CATALOG_EXTERNAL_PRODUCT" Тогда
					
					Хэндлер = ТекЭлемент.Получить("handler");
					
					ПараметрыЗапроса = "PLACEMENT=CATALOG_EXTERNAL_PRODUCT&HANDLER="+Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Хэндлер);
					ДанныеПортала = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/placement.unbind", ПараметрыЗапроса); 
					Если ДанныеПортала = Неопределено Тогда   
						ОбщегоНазначенияКлиент.СообщитьПользователю("Не удалось получить ответ портала");
						Возврат;
					КонецЕсли;

					result = ДанныеПортала.Получить("result");
					Если result <> Неопределено Тогда      
						
						count = result.Получить("count");
						Если count <> Неопределено И count > 0 Тогда
							ОбщегоНазначенияКлиент.СообщитьПользователю("Встройка удалена");    
						Иначе
							ОбщегоНазначенияКлиент.СообщитьПользователю("Не найдены встройки");    
						КонецЕсли;   
						
					КонецЕсли;  
					
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;  
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьКартинкиПриИзменении(Элемент)

	ОбновитьОтображениеЭлементовФормы();

КонецПроцедуры



