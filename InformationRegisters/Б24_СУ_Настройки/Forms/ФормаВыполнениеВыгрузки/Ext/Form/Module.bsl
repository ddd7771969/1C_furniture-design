
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаПодключения 	= Параметры.НастройкаПодключения;
	ПолнаяВыгрузка 			= Параметры.ПолнаяВыгрузка;
	
	ВыполнятьЗагрузку 		= Истина;
	ВыполнятьВыгрузку 		= Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//Если НЕ Б24_К_СлужебныйВызовСервера.МодульСкладскойУчетДоступен(НастройкаПодключения) Тогда
	//	Б24_К_СлужебныйКлиент.ОповеститьОбНедоступностиФункционалаСкладскойУчетПоТарифномуПлану();
	//	Закрыть();	
	//КонецЕсли;
	
	ПодключитьОбработчикОжидания("ЗапуститьВыполнениеОбмена", 0.1, Истина);
	
	ПодключитьОбработчикОжидания("ОбновлениеИнформацииОбмена", 1);

КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьВыполнениеОбмена()
	
	ДобавитьЕдиничнуюЗаписьВПротокол("Начало обмена");
	
	ДлительнаяОперация = ВыполнитьВыгрузкуБитрикс24НаСервере();
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииОперацииЗагрузки", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеИнформацииОбмена()
	
	ОбновлениеИнформацииОбменаСервер();	
	
	ДлинаПротокола = СтрДлина(Протокол);
	
	Если ДлинаПротокола > 0 Тогда           
		Элементы.Протокол.ТекущаяОбласть  = ТабличныйПротокол.Области.Найти("Информация");
	КонецЕсли;
	
	Если ЗавершенОбмен Тогда
		ДобавитьЕдиничнуюЗаписьВПротокол("Завершение обмена");
		ОтключитьОбработчикОжидания("ОбновлениеИнформацииОбмена");
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ОбновлениеИнформацииОбменаСервер(Сообщения = Неопределено)
	
	Макет = РегистрыСведений.Б24_СУ_Настройки.ПолучитьМакет("МакетЛога");
	
	Если Сообщения = Неопределено Тогда
		
		Задания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("УникальныйИдентификатор", ИдентификаторЗадания));
		
		Если Задания.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Задание = Задания[0];
		
		Сообщения = Задание.ПолучитьСообщенияПользователю(Истина);
		
	КонецЕсли;
	
	Для Каждого Сообщение Из Сообщения Цикл
		
		Если Лев(Сообщение.Текст, 26)= "Тело запроса HTTP запроса:" Тогда
			Продолжить;
		КонецЕсли;
		
		Если Лев(Сообщение.Текст, 16)= "Ответ с портала:" Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСообщения = Сообщение.Поле + ?(ЗначениеЗаполнено(Сообщение.Поле), " -- ", "") + Сообщение.Текст;
		
		Протокол = Протокол + ТекстСообщения + Символы.ПС; 
		
		Если (Сообщение.ПутьКДанным = "КритическаяОшибка" ИЛИ Сообщение.ПутьКДанным = "Критическая ошибка")
			И ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("КритическаяОшибкаСсылка");                                                                                                                                                                                                                                                                                                                                                
		ИначеЕсли (Сообщение.ПутьКДанным = "КритическаяОшибка" ИЛИ Сообщение.ПутьКДанным = "Критическая ошибка")
			И НЕ ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("КритическаяОшибка");                                                                                                                                                                                                                                                                                                                                               
			
		ИначеЕсли Сообщение.ПутьКДанным = "Ошибка" И ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ОшибкаСсылка");                                                                                                                                                                                                                                                                                                                                                
		ИначеЕсли Сообщение.ПутьКДанным = "Ошибка" И НЕ ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Ошибка");  
			
		ИначеЕсли Сообщение.ПутьКДанным = "Информация" И ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ИнформацияСсылка");                                                                                                                                                                                                                                                                                                                                                
		ИначеЕсли Сообщение.ПутьКДанным = "Информация" И НЕ ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Информация"); 
		Иначе
			ОбластьМакета = Макет.ПолучитьОбласть("Информация");                                                                                                                                                                                                                                                                                                                                                
		КонецЕсли;
		
		ОбластьМакета.Параметры.ТекстСообщения = ТекстСообщения; 
		ОбластьМакета.Параметры.СсылкаНаОбъект = Сообщение.КлючДанных; 
		ТабличныйПротокол.Вывести(ОбластьМакета);
	
	КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Процедура ДобавитьЕдиничнуюЗаписьВПротокол(Сообщение)
	
	Макет = РегистрыСведений.Б24_СУ_Настройки.ПолучитьМакет("МакетЛога");
	
	ТекстСообщения = Формат(ТекущаяДатаСеанса(),"ДЛФ=T") + " -- " + Сообщение;
	
	Протокол = Протокол + ТекстСообщения + Символы.ПС; 
	
	ОбластьМакета= Макет.ПолучитьОбласть("Информация");                                                                                                                                                                                                                                                                                                                                                
	ОбластьМакета.Параметры.ТекстСообщения = ТекстСообщения; 
	ТабличныйПротокол.Вывести(ОбластьМакета);
	
КонецПроцедуры	


&НаКлиенте
Процедура ПриЗавершенииОперацииЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.ГруппаКнопки.Видимость 	= Истина;
	Элементы.КнопкаЗакрыть.Видимость 	= Истина;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	             
	Если Протокол = "" Тогда
		
		Данные = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		Если ТипЗнч(Данные) = Тип("ФиксированныйМассив") Тогда
			ОбновлениеИнформацииОбменаСервер(Данные);	
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.ПодробноеПредставлениеОшибки) Тогда
		ОписаниеОшибки 										= Результат.ПодробноеПредставлениеОшибки; 
		Элементы.СтраницыФормы.ТекущаяСтраница 				= Элементы.СтраницаОшибка;
		Элементы.НаписатьВТехподдержку.Видимость 			= Истина;
		Элементы.НаписатьВТехподдержку.КнопкаПоУмолчанию 	= Истина;
	Иначе
		
		ПоказатьОповещениеПользователя(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 ""%2""'"),
		Формат(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса(), "ДЛФ=DT"),
		НастройкаПодключения) 
		,,
		НСтр("ru = 'Синхронизация с Битрикс24 завершена'"),
		БиблиотекаКартинок.Информация32);
		
		Элементы.НадписьВыполняетсяОбмен.Заголовок 	= "Синхронизация завершена";
		Элементы.КартинкаВыполняетсяОбмен.Видимость = Ложь;
		Элементы.КнопкаЗакрыть.КнопкаПоУмолчанию 	= Истина;
		
	КонецЕсли;
	
	ОбновлениеИнформацииОбмена();
	
	ЗавершенОбмен = Истина;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьВыгрузкуБитрикс24НаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипыОпераций = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыОпераций();
	
	мТипыОпераций = Новый Массив;
	мТипыОпераций.Добавить(ТипыОпераций.Цены);   
	мТипыОпераций.Добавить(ТипыОпераций.Остатки);
	мТипыОпераций.Добавить(ТипыОпераций.Отгрузки);
	мТипыОпераций.Добавить(ТипыОпераций.КартинкиФайлыТоваров);   
	мТипыОпераций.Добавить(ТипыОпераций.КартинкиФайлыПредложений);   
	
	ПараметрыВыгрузки = Б24_СУ_ОбщегоНазначенияВызовСервера.ПараметрыВыполненияОбменаВФоне(НастройкаПодключения, мТипыОпераций, ВыполнятьВыгрузку, ВыполнятьЗагрузку, ПолнаяВыгрузка);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Выполнение выгрузки в Битрикс24'");
	
	Результат = ДлительныеОперации.ВыполнитьВФоне("Б24_СУ_ОбщегоНазначенияВызовСервера.ВыполнитьОбменВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
														 
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура НаписатьВТехподдержку(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОписаниеОшибки", ОписаниеОшибки);	
	ПараметрыФормы.Вставить("Протокол"		, Протокол);	
	ОткрытьФорму("Обработка.Б24_К_Администрирование.Форма.НаписатьТикетВТехподдержку", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокНеотправленныхПакетов(Команда)
	ОткрытьФорму("РегистрСведений.Б24_СУ_Настройки.ФормаСписка");
КонецПроцедуры

