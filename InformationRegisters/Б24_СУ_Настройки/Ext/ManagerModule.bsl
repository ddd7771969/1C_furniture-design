
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область РаботаСНастройками

Функция ПолучитьМассивНастроекВыгрузки(КлючНастройки = "", ЗначениеНастройки = "") Экспорт

	Возврат Б24_СУ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьМассивНастроекВыгрузки(КлючНастройки, ЗначениеНастройки);
	
КонецФункции

Функция ПолучитьКоличествоНастроекПодключения(КлючНастройки = "", ЗначениеНастройки = "") Экспорт
	
УстановитьПривилегированныйРежим(Истина);
	
	Результат = 0;
	
	Запрос = Новый Запрос;
	Если НЕ КлючНастройки = "" Тогда
		Запрос.УстановитьПараметр("ПараметрПоиска", ЗначениеНастройки);
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Б24_СУ_Настройки.НастройкаПодключения) КАК Количество
	|ИЗ
	|	РегистрСведений.Б24_СУ_Настройки КАК Б24_СУ_Настройки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_СУ_Настройки.НастройкаПодключения = Б24_К_НастройкиПодключения.Ссылка
	|ГДЕ
	|	Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ";
	
	Если НЕ КлючНастройки = "" Тогда
		Запрос.Текст = Запрос.Текст + "
		|" + " И Б24_СУ_Настройки."+КлючНастройки+" = &ПараметрПоиска";
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат = Выборка.Количество; 
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкуПодключенияПоУмолчанию(КлючНастройки = "", ЗначениеНастройки = "") Экспорт
	
УстановитьПривилегированныйРежим(Истина);
	
	Результат = Справочники.Б24_К_НастройкиПодключения.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Если НЕ КлючНастройки = "" Тогда
		Запрос.УстановитьПараметр("ПараметрПоиска", ЗначениеНастройки);
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СУ_Настройки.НастройкаПодключения КАК НастройкаПодключения
	|ИЗ
	|	РегистрСведений.Б24_СУ_Настройки КАК Б24_СУ_Настройки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_СУ_Настройки.НастройкаПодключения = Б24_К_НастройкиПодключения.Ссылка
	|ГДЕ
	|	Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ";
	
	Если НЕ КлючНастройки = "" Тогда
		Запрос.Текст = Запрос.Текст + "
		|" + " И Б24_СУ_Настройки."+КлючНастройки+" = &ПараметрПоиска";
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.НастройкаПодключения; 	
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


#Область Прочее

Функция ПолучитьНастройкиПоУмолчанию() Экспорт
	
	УникальныйИдентификатор = Новый УникальныйИдентификатор;
	
	НастройкиОбмена = Новый Структура;
	
	///////////////////////////////////////////
	// 			ТОВАРЫ						//
	/////////////////////////////////////////
	
		НастройкиТоваров = Новый Структура;    
		
		НастройкиТоваров.Вставить("ИдентификаторКаталогаТоваров" 		, "");
		НастройкиТоваров.Вставить("ИдентификаторКаталогаПредложений" 	, "");
		НастройкиТоваров.Вставить("ВыгружатьКартинки" 					, Ложь);
		НастройкиТоваров.Вставить("СпособВыгрузки"						, "ТолькоРучнойРежим");     
		
	НастройкиОбмена.Вставить("НастройкиТоваров", НастройкиТоваров);
	
	///////////////////////////////////////////
	// 			ЦЕНЫ						//
	/////////////////////////////////////////
	
		НастройкиЦен = Новый Структура;
		
		НастройкиЦен.Вставить("СпособВыгрузки", "ВРежимеРеальногоВремени");
		
		тзнПрайсы = Новый ТаблицаЗначений;
		тзнПрайсы.Колонки.Добавить("ИдПрайсаБ24"			, Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(40)));
		тзнПрайсы.Колонки.Добавить("НаименованиеПрайсаБ24"	, Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(40))); 
		тзнПрайсы.Колонки.Добавить("Базовый"				, Новый ОписаниеТипов("Булево"));
		//тзнПрайсы.Колонки.Добавить("Прайс1С"				, Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
		
		НастройкиЦен.Вставить("Прайсы", тзнПрайсы);
		
	НастройкиОбмена.Вставить("НастройкиЦен", НастройкиЦен);
	
	///////////////////////////////////////////
	// 			ОСТАТКИ						//
	/////////////////////////////////////////
	
		НастройкиОстатков = Новый Структура;
		
		НастройкиОстатков.Вставить("СпособВыгрузки", "ВРежимеРеальногоВремени");
		
		тзнСкладов = Новый ТаблицаЗначений;
		тзнСкладов.Колонки.Добавить("ИдСкладаБ24"			, Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(40)));
		тзнСкладов.Колонки.Добавить("НаименованиеСкладаБ24"	, Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(40)));
		тзнСкладов.Колонки.Добавить("Склад1С"				, Новый ОписаниеТипов("СправочникСсылка.Склады"));
		
		НастройкиОстатков.Вставить("Склады", тзнСкладов);
		
	НастройкиОбмена.Вставить("НастройкиОстатков", НастройкиОстатков);
	
	///////////////////////////////////////////
	// 				ЗАКАЗЫ			 		//
	/////////////////////////////////////////
	
		НастройкииЗаказов = Новый Структура;
		
		НастройкииЗаказов.Вставить("РезервироватьТовары", Истина);
		НастройкииЗаказов.Вставить("ОтгружатьТовары"	, Ложь);
		
		//НастройкииЗаказов.Вставить("Подразделение"		, Справочники.СтруктураПредприятия.ПустаяСсылка());
		//НастройкииЗаказов.Вставить("Организация"		, Справочники.Организации.ОрганизацияПоУмолчанию());
		НастройкииЗаказов.Вставить("Ответственный"		, Справочники.Пользователи.ПустаяСсылка());	
		НастройкииЗаказов.Вставить("НоменклатураДоставка", Справочники.Номенклатура.ПустаяСсылка());		
		//НастройкииЗаказов.Вставить("СтатьяДДС"			, Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка());		
		//НастройкииЗаказов.Вставить("Соглашение"			, Справочники.СоглашенияСКлиентами.ПустаяСсылка());		
		НастройкииЗаказов.Вставить("АлгоритмЗаполненияЗаказа"	, "");		
		НастройкииЗаказов.Вставить("АлгоритмЗаполненияОтгрузки"	, "");	
		
		НастройкииЗаказов.Вставить("СвойствоСлужбаДоставки"	, ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка());	
		
		тзнСпособыДоставки = Новый ТаблицаЗначений;
		тзнСпособыДоставки.Колонки.Добавить("ИдСлужбыБ24"			, Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(40)));
		тзнСпособыДоставки.Колонки.Добавить("НазваниеСлужбыБ24"		, Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(40)));
 		НастройкииЗаказов.Вставить("СпособыДоставки", тзнСпособыДоставки);
		
	НастройкиОбмена.Вставить("НастройкииЗаказов", НастройкииЗаказов);
	
	Возврат НастройкиОбмена;
	
КонецФункции

#КонецОбласти


#Область ВыгрузкаНастроек

Процедура ВыгрузитьНастройкиВФоне(ПараметрыВыгрузки, АдресРезультата) Экспорт
	
	НастройкаПодключения 		= ПараметрыВыгрузки.НастройкаПодключения;
	Если ПараметрыВыгрузки.Свойство("НастройкаУдалена") Тогда
		НастройкаУдалена = ПараметрыВыгрузки.НастройкаУдалена;	
	Иначе
	 	Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.СформироватьСтруктуруСвойств(НастройкаПодключения);
		НастройкаУдалена = Ложь;	
	КонецЕсли;
	
	СтруктураНастроек =  Б24_СУ_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;       
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Добавление/удаление подписок на события";
	
	СобытияБитрикс24 				= СобытияБитрикс24();
	ИспользуемыеСобытияБитрикс24 	= ИспользуемыеСобытияБитрикс24();
	
	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Удаление подписок на события");  
	
#Область УдалениеПодписокНаСобытия   
	СформированныеЗапросы = Новый Массив;

	ДанныеПортала = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/event.get", ""); 
	Если ДанныеПортала <> Неопределено Тогда
	
		СобытияПортала = ДанныеПортала.Получить("result");
		Если СобытияПортала <> Неопределено Тогда     
			Для каждого ТекЭлемент Из СобытияПортала Цикл   
				Если Строка(ТекЭлемент.Получить("offline")) = "1" И ТекЭлемент.Получить("connector_id") = СтруктураНастроек.ИдентификаторПодключения 
					И ИспользуемыеСобытияБитрикс24.Найти(ВРег(ТекЭлемент.Получить("event"))) <> Неопределено Тогда

					Запрос = "event.unbind?event=" + ТекЭлемент.Получить("event") + "&event_type=offline&" + СтруктураНастроек.ПараметрКоннектора;
					СформированныеЗапросы.Добавить("cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Запрос));      
					
				КонецЕсли;
			КонецЦикла;  
			
			Для каждого ТекЭлемент Из СобытияПортала Цикл  
				Если ТекЭлемент.Получить("event") = "ONOFFLINEEVENT" Тогда 
					
					АдресХэндлера 	= ТекЭлемент.Получить("handler");      
					СтрокаПоиска 	= "idAdress=" + СтруктураНастроек.ИдентификаторПодключения;       
					НайденнаяПозиция= СтрНайти(АдресХэндлера, СтрокаПоиска);
					
					Если НайденнаяПозиция > 0 И НайденнаяПозиция = СтрДлина(АдресХэндлера)-СтрДлина(СтрокаПоиска)+1 Тогда 
						Запрос = "event.unbind?event="+СобытияБитрикс24.ОфлайнСобытие+"&handler="+АдресХэндлера;
						СформированныеЗапросы.Добавить("cmd[]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Запрос));
					КонецЕсли; 
					
				КонецЕсли;   
			КонецЦикла;
			
		КонецЕсли;  
		
	КонецЕсли;
	
	ТелоHTTPЗапроса= "";
	Для каждого ТекЭлемент Из СформированныеЗапросы Цикл
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + ТекЭлемент;	
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТелоHTTPЗапроса) Тогда
		Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	КонецЕсли;
#КонецОбласти

	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Начало добавления подписок на события");   
	
#Область ДобавлениеПодписокНаСобытия   

	СформированныеДанные = Новый Массив;
	
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////	
	Если ПараметрыВыгрузки.РезервироватьТовары Тогда
		СформированныеДанные.Добавить("event.bind?event="+СобытияБитрикс24.ИзменениеЗаказа+"&event_type=offline&" + СтруктураНастроек.ПараметрКоннектора);
		СформированныеДанные.Добавить("event.bind?event="+СобытияБитрикс24.УдалениеЗаказа+"&event_type=offline&" + СтруктураНастроек.ПараметрКоннектора);
	КонецЕсли; 
	
	Если ПараметрыВыгрузки.РезервироватьТовары И ПараметрыВыгрузки.ОтгружатьТовары Тогда
		СформированныеДанные.Добавить("event.bind?event="+СобытияБитрикс24.ИзменениеОтгрузки+"&event_type=offline&" + СтруктураНастроек.ПараметрКоннектора);
		СформированныеДанные.Добавить("event.bind?event="+СобытияБитрикс24.УдалениеОтгрузки+"&event_type=offline&" + СтруктураНастроек.ПараметрКоннектора);
	КонецЕсли;
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////	

	СпособВзаимодействияСБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "СпособВзаимодействияСБитрикс24");
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() ИЛИ СпособВзаимодействияСБитрикс24 = "Через http-сервисы" Тогда
		
		Если Б24_К_RestApiВызовСервера.РеалтаймИспользуется(НастройкаПодключения) Тогда         
			АдресДоХэндлераОфлайнСобытий = Б24_К_RestApiВызовСервера.ПолучитьАдресДоХэндлераСработкиОфлайнСобытияПриложенияБитрикс24() + "?idAdress="+СтруктураНастроек.НастройкаПодключения.ИдентификаторПодключения;
			СформированныеДанные.Добавить("event.bind?event="+СобытияБитрикс24.ОфлайнСобытие+"&minTimeout=5&handler="+АдресДоХэндлераОфлайнСобытий);
		КонецЕсли; 
		
	КонецЕсли;
	
	Если СформированныеДанные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Итератор 			= 0;
	ТелоHTTPЗапроса		= "";
	Для каждого ТекЭлемент Из СформированныеДанные Цикл
		Итератор			= Итератор + 1;
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + "cmd["+Формат(Итератор,"ЧГ=0")+"]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ТекЭлемент);	
	КонецЦикла;   
	
	Если ЗначениеЗаполнено(ТелоHTTPЗапроса) Тогда
		Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса); 
	КонецЕсли;   
#КонецОбласти  

КонецПроцедуры               

Функция ИспользуемыеСобытияБитрикс24()
	
	Результат = Новый Массив;
	
	СобытияБитрикс24 = СобытияБитрикс24();
	
	Результат.Добавить(ВРег(СобытияБитрикс24.ИзменениеЗаказа));
	Результат.Добавить(ВРег(СобытияБитрикс24.ИзменениеОтгрузки));
	
	Результат.Добавить(ВРег(СобытияБитрикс24.УдалениеЗаказа));
	Результат.Добавить(ВРег(СобытияБитрикс24.УдалениеОтгрузки));
	
	Возврат Результат;	
	
КонецФункции

Функция СобытияБитрикс24() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ОфлайнСобытие"		, "onOfflineEvent");
	Результат.Вставить("ИзменениеЗаказа"	, "OnOrderEntitySaved");
	Результат.Вставить("ИзменениеОтгрузки"	, "OnShipmentEntitySaved");
	
	Результат.Вставить("УдалениеЗаказа"		, "OnOrderDeleted");
	Результат.Вставить("УдалениеОтгрузки"	, "OnShipmentDeleted");
	
	Возврат Результат;	
	
КонецФункции

#КонецОбласти

#КонецЕсли
