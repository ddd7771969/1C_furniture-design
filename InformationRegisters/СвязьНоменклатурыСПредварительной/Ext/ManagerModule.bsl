&НаСервере
Функция СвязатьНоменклатуруСПредворительной(Номенклатура, мПредварительнаяНоменклатура) Экспорт
	
	Попытка
		
		НачатьТранзакцию();
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СвязьНоменклатурыСПредварительной");
		ЭлементБлокировки.УстановитьЗначение("Номенклатура", Номенклатура);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();  
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СвязьНоменклатурыСПредварительной.ИндексНоменклатуры КАК ИндексНоменклатуры,
		|	СвязьНоменклатурыСПредварительной.ПредварительнаяНоменклатура КАК ПредварительнаяНоменклатура,
		|	0 КАК Действие
		|ИЗ
		|	РегистрСведений.СвязьНоменклатурыСПредварительной КАК СвязьНоменклатурыСПредварительной
		|ГДЕ
		|	СвязьНоменклатурыСПредварительной.Номенклатура = &Номенклатура";
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		
		тзСуществующаяНоменклатура = Запрос.Выполнить().Выгрузить();
		тзСуществующаяНоменклатура.Индексы.Добавить("ПредварительнаяНоменклатура,ИндексНоменклатуры");
		
		стОтбор = Новый Структура("ПредварительнаяНоменклатура,ИндексНоменклатуры", );
		ИндексНоменклатуры = 0; 
		Для каждого ПредварительнаяНоменклатура Из мПредварительнаяНоменклатура Цикл
			
			стОтбор.ИндексНоменклатуры 			= ИндексНоменклатуры;
			стОтбор.ПредварительнаяНоменклатура = ПредварительнаяНоменклатура; 
			
			НайденыеСтроки = тзСуществующаяНоменклатура.НайтиСтроки(стОтбор);
			Если НайденыеСтроки.Количество() = 0 Тогда
				НС = тзСуществующаяНоменклатура.Добавить();
				НС.ПредварительнаяНоменклатура 	= ПредварительнаяНоменклатура;
				НС.ИндексНоменклатуры 			= ИндексНоменклатуры;
				НС.Действие 					= 2;
			Иначе
				НайденыеСтроки[0].Действие 	= 1;
			КонецЕсли;
			ИндексНоменклатуры = ИндексНоменклатуры + 1; 
		КонецЦикла;
		
		//Действие = 0 удалить, 1 не изменять, 2 изменить/добавить
		
		
		Для каждого СуществующаяНоменклатура Из тзСуществующаяНоменклатура Цикл
			Если СуществующаяНоменклатура.Действие = 1 Тогда
				Продолжить;	
			КонецЕсли; 
			НаборЗаписей = РегистрыСведений.СвязьНоменклатурыСПредварительной.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
			НаборЗаписей.Отбор.ИндексНоменклатуры.Установить(СуществующаяНоменклатура.ИндексНоменклатуры); 
			
			Если СуществующаяНоменклатура.Действие = 2 Тогда
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись,СуществующаяНоменклатура);
				НоваяЗапись.Номенклатура = Номенклатура;
			КонецЕсли; 
			
			НаборЗаписей.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение 
		
		ОтменитьТранзакцию();
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции // СвязатьНоменклатуруСПредворительной(Номенклатура, Предварительная)


&НаСервере
Функция СвязатьПредварительнуюСНоменклатурой(ПредварительнаяНоменклатура,мНоменклатура) Экспорт
	
	Попытка
		
		НачатьТранзакцию();
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СвязьНоменклатурыСПредварительной");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();  
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СвязьНоменклатурыСПредварительной.Номенклатура КАК Номенклатура,
		|	СвязьНоменклатурыСПредварительной.ИндексНоменклатуры КАК ИндексНоменклатуры,
		|	0 КАК Действие
		|ИЗ
		|	РегистрСведений.СвязьНоменклатурыСПредварительной КАК СвязьНоменклатурыСПредварительной
		|ГДЕ
		|	СвязьНоменклатурыСПредварительной.ПредварительнаяНоменклатура = &ПредварительнаяНоменклатура";
		
		Запрос.УстановитьПараметр("ПредварительнаяНоменклатура", ПредварительнаяНоменклатура);
		
		тзСуществующаяНоменклатура = Запрос.Выполнить().Выгрузить();
		тзСуществующаяНоменклатура.Индексы.Добавить("Номенклатура");
		
		стОтбор = Новый Структура("Номенклатура", );
		
		мПолучитьИндексНоменклатуры = Новый Массив;
		Для каждого Номенклатура Из мНоменклатура Цикл
			
			стОтбор.Номенклатура = Номенклатура; 
			
			НайденыеСтроки = тзСуществующаяНоменклатура.НайтиСтроки(стОтбор);
			Если НайденыеСтроки.Количество() = 0 Тогда
				НС = тзСуществующаяНоменклатура.Добавить();
				НС.Номенклатура 	= Номенклатура;
				НС.Действие 		= 2;
				
				мПолучитьИндексНоменклатуры.Добавить(Номенклатура);
			Иначе
				Для каждого х Из НайденыеСтроки Цикл
					х.Действие 		= 1;
				КонецЦикла; 
			КонецЕсли;
		КонецЦикла;
		
		//Действие = 0 удалить, 1 не изменять, 2 изменить/добавить
		
		Если мПолучитьИндексНоменклатуры.Количество() > 0 Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СвязьНоменклатурыСПредварительной.Номенклатура КАК Номенклатура,
			|	ЕСТЬNULL(МАКСИМУМ(СвязьНоменклатурыСПредварительной.ИндексНоменклатуры), -1) + 1 КАК ИндексНоменклатуры
			|ИЗ
			|	РегистрСведений.СвязьНоменклатурыСПредварительной КАК СвязьНоменклатурыСПредварительной
			|ГДЕ
			|	СвязьНоменклатурыСПредварительной.Номенклатура В(&мПолучитьИндексНоменклатуры)
			|
			|СГРУППИРОВАТЬ ПО
			|	СвязьНоменклатурыСПредварительной.Номенклатура";
			
			Запрос.УстановитьПараметр("мПолучитьИндексНоменклатуры", мПолучитьИндексНоменклатуры);
			
			тзПолучитьИндексНоменклатуры = Запрос.Выполнить().Выгрузить(); 
			тзПолучитьИндексНоменклатуры.Индексы.Добавить("Номенклатура");
			
		КонецЕсли;
		
		
		Для каждого СуществующаяНоменклатура Из тзСуществующаяНоменклатура Цикл
			Если СуществующаяНоменклатура.Действие = 1 Тогда
				Продолжить;	
			КонецЕсли; 
			НаборЗаписей = РегистрыСведений.СвязьНоменклатурыСПредварительной.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Номенклатура.Установить(СуществующаяНоменклатура.Номенклатура);
			Если СуществующаяНоменклатура.Действие = 2 Тогда
				НайденаяСтрока 			= тзПолучитьИндексНоменклатуры.Найти(СуществующаяНоменклатура.Номенклатура,"Номенклатура");
				ИндексНоменклатуры = ?(НайденаяСтрока = Неопределено, 0, НайденаяСтрока.ИндексНоменклатуры);
				НаборЗаписей.Отбор.ИндексНоменклатуры.Установить(ИндексНоменклатуры); 
			Иначе
				НаборЗаписей.Отбор.ИндексНоменклатуры.Установить(СуществующаяНоменклатура.ИндексНоменклатуры); 
			КонецЕсли;
			
			Если СуществующаяНоменклатура.Действие = 2 Тогда
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.ПредварительнаяНоменклатура = ПредварительнаяНоменклатура;
				НоваяЗапись.Номенклатура 				= СуществующаяНоменклатура.Номенклатура;
				НоваяЗапись.ИндексНоменклатуры 			= ИндексНоменклатуры;
			КонецЕсли; 
			
			НаборЗаписей.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение 
		
		ОтменитьТранзакцию();
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции // СвязатьПредварительнуюСНоменклатурой(ПредварительнаяНоменклатура,мНоменклатура)
