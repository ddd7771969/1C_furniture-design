&НаКлиенте
Перем АдресВХ, стДоступныеПрава;

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектТЗ = Параметры.ОбъектТЗ;
	
	Статус 			= -1;
	АвторСоздания 	= ПараметрыСеанса.ТекущийПользователь;
	Комментарий		= "";
	ДатаСоздания 	= ТекущаяДата();

	ЗаполнитьСписокВерсий();
	
	Элементы.ФормаДобавитьТЗ.Видимость 	= РольДоступна("Дизайнер");
	Элементы.ФормаСохранить.Видимость 	= Элементы.ФормаДобавитьТЗ.Видимость;
	
КонецПроцедуры

Процедура ЗаполнитьСписокВерсий()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗ_Изделий.Версия КАК Версия
		|ИЗ
		|	РегистрСведений.ТЗ_Изделий КАК ТЗ_Изделий
		|ГДЕ
		|	ТЗ_Изделий.ОбъектТЗ = &ОбъектТЗ
		|	И ТЗ_Изделий.Версия > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Версия";
	
	Запрос.УстановитьПараметр("ОбъектТЗ", ОбъектТЗ);
	Элементы.Версия.СписокВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Версия"));
	
	КоличествоВерсий = Элементы.Версия.СписокВыбора.Количество();
	
	Если КоличествоВерсий = 0 Тогда
		Статус = -1;
	Иначе 	 
	
		Версия = КоличествоВерсий;
		ВерсияПриИзмененииНаСервере();
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ДобавитьТЗ(Команда)
	
	ИмяФайлаПДФ = Строка(ОбъектТЗ) + ".ТЗ.pdf"; 
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Фильтр = "Файл pdf|" + ИмяФайлаПДФ; 
	ПомещенныйФайл = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога);
	
	Если ПомещенныйФайл <> Неопределено И НЕ ПомещенныйФайл.ПомещениеФайлаОтменено Тогда
		
		Статус = -1;
		
		АдресВХ = ПомещенныйФайл.Адрес;
		ИмяФайла = ПомещенныйФайл.СсылкаНаФайл.Файл.ИмяБезРасширения;
		
		Элементы.ОткрытьФайл.Доступность 	= Истина;
		Элементы.ФормаСохранить.Доступность = Элементы.ОткрытьФайл.Доступность;
		
		НомерВерсии = Элементы.Версия.СписокВыбора.Количество() + 1;
		
		Элементы.Версия.СписокВыбора.Добавить(НомерВерсии);
		
		Версия = НомерВерсии;
		
		ВидимостьСтатуса();
		
	КонецЕсли;
	
	Элементы.ФормаДобавитьТЗ.Доступность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	ЗаписатьНаСервере(АдресВХ);
	Закрыть();
КонецПроцедуры

Функция ЗаписатьНаСервере(АдресВХ)

	
	МенеджерЗаписи = РегистрыСведений.ТЗ_Изделий.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.ОбъектТЗ 		= ОбъектТЗ;
	МенеджерЗаписи.ИмяФайла 		= ИмяФайла;
	МенеджерЗаписи.ФайлТЗ 			= Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресВХ));
	МенеджерЗаписи.Статус 			= 0;
	МенеджерЗаписи.Комментарий		= Комментарий;
	МенеджерЗаписи.АвторСоздания 	= АвторСоздания;
	МенеджерЗаписи.ДатаСоздания		= ТекущаяДата();
	МенеджерЗаписи.Версия			= Версия;
	
	МенеджерЗаписи.Записать();
	
	
	
	ЗадачаКонтрольТЗ = Задачи.КонтрольТЗ.СоздатьЗадачу();
	ЗадачаКонтрольТЗ.Наименование 			= Строка(ОбъектТЗ) + " проверить ТЗ";
	ЗадачаКонтрольТЗ.Дата 					= МенеджерЗаписи.ДатаСоздания;
	ЗадачаКонтрольТЗ.ОбъектТЗ 				= ОбъектТЗ;
	ЗадачаКонтрольТЗ.Версия 				= МенеджерЗаписи.Версия;
	ЗадачаКонтрольТЗ.Проект 				= ОбъектТЗ.Проект;
	ЗадачаКонтрольТЗ.Статус 				= Статус;
	ЗадачаКонтрольТЗ.КрайняяДатаИсполнения 	= ЗадачаКонтрольТЗ.Дата + 24 * 3600 * 3;
	ЗадачаКонтрольТЗ.Исполнитель 			= омСотрудники.СотрудникиОбъекта(ОбъектТЗ).ВедущийКонструктор;
	
	ЗадачаКонтрольТЗ.Записать();
	
	омКанбанТЗ.ПереместитьРеквизитВОжидание(ОбъектТЗ, "Версия " + МенеджерЗаписи.Версия);
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	ПолноеИмяФайла = КаталогВременныхФайлов()  + ИмяФайла + ".pdf";
	
	Файл = Новый Файл(ПолноеИмяФайла);
	
	Если Файл.Существует() Тогда
	
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытиеФайлаПродолжение", ЭтаФорма, ПолноеИмяФайла);
		
		Попытка
		
			НачатьУдалениеФайлов(ОписаниеОповещения, ПолноеИмяФайла);
			
		Исключение
			
			ПоказатьПредупреждение(, "Файл уже открыт");
			Возврат;
		
		КонецПопытки;
		
	Иначе
		
		ОткрытиеФайлаПродолжение(ПолноеИмяФайла);
	
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытиеФайлаПродолжение(ПолноеИмяФайла) Экспорт

	стРезультат = омТЗКлиент.СохранитьФайлТЗВоВременномКаталоге(ОбъектТЗ, Версия, УникальныйИдентификатор, "Открыть");
	
	Если НЕ ПустаяСтрока(стРезультат.Ошибка) Тогда
	
		ПоказатьПредупреждение(, стРезультат.Ошибка);	
	
	КонецЕсли;

КонецПроцедуры // ОткрытиеФайлаПродолжение()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Версия = 1 Тогда
	
		ОткрытьФайл(Неопределено);	
	    Закрыть();
		
	Иначе
		ВидимостьСтатуса();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВерсияПриИзмененииНаСервере()
	
	Статус 			= -1;
	АвторСоздания 	= ПараметрыСеанса.ТекущийПользователь;
	Комментарий		= "";
	ДатаСоздания 	= ТекущаяДата();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗ_Изделий.АвторСоздания КАК АвторСоздания,
		|	ТЗ_Изделий.ДатаСоздания КАК ДатаСоздания,
		|	ТЗ_Изделий.Комментарий КАК Комментарий,
		|	ТЗ_Изделий.Статус КАК Статус,
		|	ТЗ_Изделий.ИмяФайла КАК ИмяФайла
		|ИЗ
		|	РегистрСведений.ТЗ_Изделий КАК ТЗ_Изделий
		|ГДЕ
		|	ТЗ_Изделий.ОбъектТЗ = &ОбъектТЗ
		|	И ТЗ_Изделий.Версия = &Версия";
	
	Запрос.УстановитьПараметр("Версия", Версия);
	Запрос.УстановитьПараметр("ОбъектТЗ", ОбъектТЗ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Статус 			= Выборка.Статус;
		АвторСоздания 	= Выборка.АвторСоздания;
		ДатаСоздания 	= Выборка.ДатаСоздания;
		ИмяФайла		= Выборка.ИмяФайла;
	КонецЦикла;
	

КонецПроцедуры 

&НаКлиенте
Процедура ВидимостьСтатуса()
	
	//1 принят, 2 отказ, 0 Ожидание, -1 новый
	Элементы.ТЗОжидание.Видимость 		= Статус = 0;
	Элементы.ТЗОтказ.Видимость 			= Статус = 2;
	Элементы.ТЗПринято.Видимость 		= Статус = 1;
	Элементы.СозданиеНовогоТЗ.Видимость = Статус = -1;
	
	Элементы.Комментарий.ТолькоПросмотр = НЕ Элементы.СозданиеНовогоТЗ.Видимость;
	
	Элементы.ОткрытьФайл.Доступность 	= НЕ ПустаяСтрока(ИмяФайла);
	
	Элементы.ФормаСохранить.Доступность = Элементы.СозданиеНовогоТЗ.Видимость И Элементы.ОткрытьФайл.Доступность;
	
КонецПроцедуры // ВидимостьСтатуса()

&НаКлиенте
Процедура ВерсияПриИзменении(Элемент)
	ВерсияПриИзмененииНаСервере();
	ВидимостьСтатуса();
КонецПроцедуры



