
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область РаботаСНастройками

Функция ПолучитьМассивНастроекСинхронизации(КлючНастройки = "", ЗначениеНастройки = "") Экспорт

	Возврат Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьМассивНастроекСинхронизации(КлючНастройки, ЗначениеНастройки); 
	
КонецФункции

Функция ПолучитьКоличествоНастроекПодключенияСинхронизации(КлючНастройки = "", ЗначениеНастройки = "") Экспорт
	
УстановитьПривилегированныйРежим(Истина);
	
	Результат = 0;
	
	Запрос = Новый Запрос;
	Если НЕ КлючНастройки = "" Тогда
		Запрос.УстановитьПараметр("ПараметрПоиска", ЗначениеНастройки);
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Б24_СМ_НастройкиСмартПроцессов.НастройкаПодключения) КАК Количество
	|ИЗ
	|	РегистрСведений.Б24_СМ_НастройкиСмартПроцессов КАК Б24_СМ_НастройкиСмартПроцессов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_СМ_НастройкиСмартПроцессов.НастройкаПодключения = Б24_К_НастройкиПодключения.Ссылка
	|ГДЕ
	|	Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ";
	
	Если НЕ КлючНастройки = "" Тогда
		Запрос.Текст = Запрос.Текст + "
		|" + " И Б24_СМ_НастройкиСмартПроцессов."+КлючНастройки+" = &ПараметрПоиска";
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат = Выборка.Количество; 
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкуПодключенияПоУмолчаниюСинхронизации(КлючНастройки = "", ЗначениеНастройки = "") Экспорт
	
УстановитьПривилегированныйРежим(Истина);
	
	Результат = Справочники.Б24_К_НастройкиПодключения.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Если НЕ КлючНастройки = "" Тогда
		Запрос.УстановитьПараметр("ПараметрПоиска", ЗначениеНастройки);
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СМ_НастройкиСмартПроцессов.НастройкаПодключения КАК НастройкаПодключения
	|ИЗ
	|	РегистрСведений.Б24_СМ_НастройкиСмартПроцессов КАК Б24_СМ_НастройкиСмартПроцессов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Б24_К_НастройкиПодключения КАК Б24_К_НастройкиПодключения
	|		ПО Б24_СМ_НастройкиСмартПроцессов.НастройкаПодключения = Б24_К_НастройкиПодключения.Ссылка
	|ГДЕ
	|	Б24_К_НастройкиПодключения.ПометкаУдаления = ЛОЖЬ";
	
	Если НЕ КлючНастройки = "" Тогда
		Запрос.Текст = Запрос.Текст + "
		|" + " И Б24_СМ_НастройкиСмартПроцессов."+КлючНастройки+" = &ПараметрПоиска";
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.НастройкаПодключения; 	
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения) Экспорт

	Возврат Б24_СМ_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения); 

КонецФункции

#КонецОбласти


#Область ВыгрузкаНастроек

Процедура ВыгрузитьНастройкиВФоне(ПараметрыВыгрузки, АдресРезультата) Экспорт
	
	НастройкаПодключения 		= ПараметрыВыгрузки.НастройкаПодключения;
	Если ПараметрыВыгрузки.Свойство("НастройкаУдалена") Тогда
		НастройкаУдалена = ПараметрыВыгрузки.НастройкаУдалена;	
	Иначе
		НастройкаУдалена = Ложь;	
	КонецЕсли;
	
	СтруктураНастроек =  Б24_СМ_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.ПараметрКоннектора = "auth_connector=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(СтруктураНастроек.ИдентификаторПодключения);
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Добавление/удаление подписок на события";
	
	Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек, СтруктураНастроек.ТипыСообщений.Информация, "Начало добавления подписок на события");
	
	СформированныеДанные = Новый Массив;
	
	СобытияБитрикс24 = СобытияБитрикс24();
	СформированныеДанные.Добавить("cmd[1]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("event.bind?event=" + СобытияБитрикс24.ДобавлениеСП + "&event_type=offline&" + СтруктураНастроек.ПараметрКоннектора));
	СформированныеДанные.Добавить("cmd[2]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("event.bind?event=" + СобытияБитрикс24.ОбновлениеСП + "&event_type=offline&" + СтруктураНастроек.ПараметрКоннектора));
	СформированныеДанные.Добавить("cmd[3]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("event.bind?event=" + СобытияБитрикс24.УдалениеСП + "&event_type=offline&" + СтруктураНастроек.ПараметрКоннектора));
	
	АдресДоХэндлераОфлайнСобытий = Б24_К_RestApiВызовСервера.ПолучитьАдресДоХэндлераСработкиОфлайнСобытияПриложенияБитрикс24() + "?idAdress="+СтруктураНастроек.НастройкаПодключения.ИдентификаторПодключения;
	
	СформированныеДанные.Добавить("cmd[4]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("event.unbind?event="+СобытияБитрикс24.ОфлайнСобытие+"&handler="+АдресДоХэндлераОфлайнСобытий));
	
	СпособВзаимодействияСБитрикс24 = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "СпособВзаимодействияСБитрикс24");
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() ИЛИ СпособВзаимодействияСБитрикс24 = "Через http-сервисы" Тогда
		Если Б24_К_RestApiВызовСервера.РеалтаймИспользуется(НастройкаПодключения) Тогда
			СформированныеДанные.Добавить("cmd[5]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("event.bind?event="+СобытияБитрикс24.ОфлайнСобытие+"&handler="+АдресДоХэндлераОфлайнСобытий));
		КонецЕсли;
	КонецЕсли;
	
	ТелоHTTPЗапроса= "";
	Для каждого ТекЭлемент Из СформированныеДанные Цикл
		ТелоHTTPЗапроса = ТелоHTTPЗапроса + "&" + ТекЭлемент;	
	КонецЦикла;
	
	Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ТелоHTTPЗапроса);
	
КонецПроцедуры

#КонецОбласти


#Область Прочее

Процедура ДобавитьНастройкуВСтруктуру(СтруктураНастроек, НазваниеНастройки, ЗначениеНастройки) Экспорт
	
	Если СтруктураНастроек.Свойство(НазваниеНастройки) Тогда
		СтруктураНастроек[НазваниеНастройки] = ЗначениеНастройки;	
	Иначе
		СтруктураНастроек.Вставить(НазваниеНастройки, ЗначениеНастройки);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСтруктуруСхемКомпоновки() Экспорт
	
	Результат = Новый Структура();
	
	Результат.Вставить("МакетСмартПроцессов", РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьМакет("МакетСмартПроцессов")); 	
		
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкиСинхронизацииПоУмолчанию() Экспорт
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Ид");
	Результат.Колонки.Добавить("ТипВладельца");
	Результат.Колонки.Добавить("Наименование");
	Результат.Колонки.Добавить("Данные");
	
	Возврат Результат;
	
КонецФункции

Функция СобытияБитрикс24() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ОфлайнСобытие"	, "onOfflineEvent");
	Результат.Вставить("ДобавлениеСП"	, "onCrmDynamicItemAdd");
	Результат.Вставить("ОбновлениеСП"	, "onCrmDynamicItemUpdate");
	Результат.Вставить("УдалениеСП"		, "onCrmDynamicItemDelete");
	
	Возврат Результат;	
	
КонецФункции

#КонецОбласти

#КонецЕсли
