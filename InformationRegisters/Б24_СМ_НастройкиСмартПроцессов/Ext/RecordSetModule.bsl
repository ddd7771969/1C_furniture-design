
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка И Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 0 Тогда
		
		СтарыйНаборЗаписей = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.СоздатьНаборЗаписей();
		
		Для Каждого ЗначениеОтбора Из Отбор Цикл
			
			Если ЗначениеОтбора.Использование = Ложь Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаОтбора = СтарыйНаборЗаписей.Отбор.Найти(ЗначениеОтбора.Имя);
			СтрокаОтбора.Значение = ЗначениеОтбора.Значение;
			СтрокаОтбора.Использование = Истина;
			
		КонецЦикла;
		
		СтарыйНаборЗаписей.Прочитать();
		
		Для каждого ТекЗапись Из СтарыйНаборЗаписей Цикл
			
				
			УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
			ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
			ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка регистрации изменений в Битрикс24";
			ПараметрыВыполнения.ЗапуститьВФоне = Истина;
				
			ПараметрыВыгрузки = Новый Структура;
			ПараметрыВыгрузки.Вставить("НастройкаПодключения"	, ТекЗапись.НастройкаПодключения);
			ПараметрыВыгрузки.Вставить("НастройкаУдалена"		, Истина);
				                                 
			ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли 