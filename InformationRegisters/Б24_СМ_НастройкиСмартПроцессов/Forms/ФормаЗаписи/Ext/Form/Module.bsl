
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаПодключения 	= Параметры.НастройкаПодключения;
	
	НастройкаСинхронизации = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкаСинхронизации.НастройкаПодключения = НастройкаПодключения;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, НастройкаСинхронизации); 
	
	
	Портал = НастройкаПодключения.Портал;
	
	ПараметрПортал = СписокПользователей.Параметры.Элементы.Найти("Портал");
	ПараметрПортал.Использование 	= Истина;
	ПараметрПортал.Значение 		= Портал;
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Истина);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтруктураНастроек.Логирование.Измерение2 = "Настройки смарт процессов";
	
	ЗаполнитьТаблицуСмартПроцессов(СтруктураНастроек);
	
	СохраненныеНастройки = НастройкаСинхронизации.Настройки.Получить();
	Если СохраненныеНастройки <> Неопределено Тогда
		Для каждого ТекСтрока Из СмартПроцессы Цикл
			Для каждого ТекСтрокаНастроек Из СохраненныеНастройки Цикл
				Если ТекСтрока.Ид = ТекСтрокаНастроек.Ид И ТекСтрока.ТипВладельца = ТекСтрокаНастроек.ТипВладельца Тогда
					Если ТекСтрокаНастроек.Данные <> Неопределено Тогда
						ТекСтрока.ВременныйАдресНастроекСмартПроцесса = ПоместитьВоВременноеХранилище(ТекСтрокаНастроек.Данные, Новый УникальныйИдентификатор);	
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
УстановитьПривилегированныйРежим(Истина);	 //Для возможности всем пользователям читать расписание		
		ИдентификаторРегламентногоЗадания 	= НастройкаСинхронизации.ИдентификаторРегламентногоЗадания; 
		
		НайденноеРегламентноеЗадание = Неопределено;
		Если ЗначениеЗаполнено(ИдентификаторРегламентногоЗадания) Тогда			
			НайденноеРегламентноеЗадание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗадания);
		КонецЕсли;			
	
		Если НайденноеРегламентноеЗадание 	= Неопределено Тогда
			ИдентификаторРегламентногоЗадания	= Неопределено;
			РасписаниеРегламентногоЗадания 	= Новый РасписаниеРегламентногоЗадания;
		Иначе
			РасписаниеРегламентногоЗадания 	= НайденноеРегламентноеЗадание.Расписание;
		КонецЕсли;		
УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//Если НЕ Б24_К_СлужебныйВызовСервера.МодульСмартПроцессовДоступен(НастройкаПодключения) Тогда
	//	Б24_К_СлужебныйКлиент.ОповеститьОбНедоступностиФункционалаСинхронизацииПоТарифномуПлану();
	//	Закрыть();	
	//КонецЕсли;
	
	Заголовок = Строка(НастройкаПодключения);
	
	ОбновитьОтображениеЭлементовФормы();
	
	УстановитьНадписьРасписанияОбмена(); 
	
КонецПроцедуры


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НужноЗакрытьОкно = Истина;
		
		СохранитьВыгрузитьНастройки(Истина);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		НужноЗакрытьОкно = Истина;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СохранитьВыгрузитьНастройки(ЗакрытьПоОкончании = Ложь)
	
	НастройкаСохранена = СохранитьНастройкуСервер();
	
	Если НастройкаСохранена И ВыгружатьНастройкиНаПортал Тогда
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НастройкаПодключения, "ПометкаУдаления") = Ложь Тогда
			
			СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
			
			Если СтруктураНастроек = Неопределено Тогда
				ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
				Возврат Ложь;
			КонецЕсли;
			
			ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании);
		Иначе
			Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
	КонецЕсли;
	
	Оповестить("СозданаИзмененаНастройка");
	
	Возврат НастройкаСохранена
	
КонецФункции  

&НаСервере
Функция СохранитьНастройкуСервер()
	
	Если Модифицированность Тогда
		
УстановитьПривилегированныйРежим(Истина); //Для возможности всем пользователям с правами устанавливать расписания		
		Если СпособСинхронизацииДанных = "ПоРасписанию"  И НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда

			Наименование = Строка(НастройкаПодключения);
			
			ИменаИспользуемыхРеглЗаданий 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьИменаИспользуемыхРеглЗаданий();
			СтруктураРеглЗаданий 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьИспользуемыеРеглЗадания();

			ПараметрыЗапуска = Новый Массив();
			ПараметрыЗапуска.Добавить(СтруктураРеглЗаданий.СинхронизацияДанныхСМ);
			ПараметрыЗапуска.Добавить(НастройкаПодключения);

			Задание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗадания);
			Если Задание <> Неопределено Тогда

				ПараметрыИзмененияЗадания = Новый Структура();
				ПараметрыИзмененияЗадания.Вставить("Использование", Истина);
				ПараметрыИзмененияЗадания.Вставить("Параметры", ПараметрыЗапуска);
				ПараметрыИзмененияЗадания.Вставить("Расписание", РасписаниеРегламентногоЗадания);
				ПараметрыИзмененияЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
				ПараметрыИзмененияЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);

				РегламентныеЗаданияСервер.ИзменитьЗадание(ИдентификаторРегламентногоЗадания, ПараметрыИзмененияЗадания);

			Иначе

				ПараметрыЗадания = Новый Структура();
				ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ЗапускДополнительныхОбработок);
				ПараметрыЗадания.Вставить("Наименование", ИменаИспользуемыхРеглЗаданий.СинхронизацияДанныхСМ
					+ Наименование);
				ПараметрыЗадания.Вставить("Ключ", ИменаИспользуемыхРеглЗаданий.СинхронизацияДанныхСМ
					+ Наименование);
				ПараметрыЗадания.Вставить("Использование", Истина);
				ПараметрыЗадания.Вставить("ЭксклюзивноеВыполнение", Ложь);
				ПараметрыЗадания.Вставить("Параметры", ПараметрыЗапуска);
				ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
				ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);
				ПараметрыЗадания.Вставить("Расписание", РасписаниеРегламентногоЗадания);

				Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
				Если ОбщегоНазначения.РазделениеВключено() Тогда
					//@skip-warning
					ИдентификаторРегламентногоЗадания = Задание.Идентификатор.УникальныйИдентификатор();
				Иначе
					ИдентификаторРегламентногоЗадания = Задание.УникальныйИдентификатор;
				КонецЕсли;

			КонецЕсли;

			Если Задание = Неопределено Тогда
				ИдентификаторРегламентногоЗадания = Неопределено;
			КонецЕсли;

		Иначе
			РегламентныеЗаданияСервер.УдалитьЗадание(ИдентификаторРегламентногоЗадания);
			ИдентификаторРегламентногоЗадания = Неопределено;
		КонецЕсли;		
	
УстановитьПривилегированныйРежим(Ложь);	
		
		НоваяЗаписьНастройки = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(НоваяЗаписьНастройки, ЭтаФорма);
	
		тзнНастроек = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьНастройкиСинхронизацииПоУмолчанию(); 
		Выборка = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
		Пока Выборка.Следующий() Цикл
			Если Выборка.Настройки.Получить() <> Неопределено Тогда
				тзнНастроек = Выборка.Настройки.Получить();	
			КонецЕсли;
		КонецЦикла;
		
		Для каждого ТекСтрока Из СмартПроцессы Цикл
			
			ЕстьНастройкаПроцесса = Ложь;
			Для каждого ТекСтрокаНастроек Из тзнНастроек Цикл
				Если ТекСтрока.Ид = ТекСтрокаНастроек.Ид И ТекСтрока.ТипВладельца = ТекСтрокаНастроек.ТипВладельца Тогда
					ЕстьНастройкаПроцесса = Истина;
					Если ЭтоАдресВременногоХранилища(ТекСтрока.ВременныйАдресНастроекСмартПроцесса) Тогда
						ТекСтрокаНастроек.Данные 		= ПолучитьИзВременногоХранилища(ТекСтрока.ВременныйАдресНастроекСмартПроцесса);
						ТекСтрокаНастроек.Наименование 	= ТекСтрока.Наименование;
					Иначе
						ТекСтрокаНастроек.Данные = Неопределено			
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если НЕ ЕстьНастройкаПроцесса И ЭтоАдресВременногоХранилища(ТекСтрока.ВременныйАдресНастроекСмартПроцесса) Тогда
				НоваяСтрока = тзнНастроек.Добавить();
				НоваяСтрока.Ид 				= ТекСтрока.Ид;
				НоваяСтрока.ТипВладельца 	= ТекСтрока.ТипВладельца;
				НоваяСтрока.Наименование 	= ТекСтрока.Наименование;
				НоваяСтрока.Данные 			= ПолучитьИзВременногоХранилища(ТекСтрока.ВременныйАдресНастроекСмартПроцесса);
			КонецЕсли;
			
		КонецЦикла;
		
		НоваяЗаписьНастройки.Настройки = Новый ХранилищеЗначения(тзнНастроек);
		
		Попытка 
			
			НоваяЗаписьНастройки.Записать();
			
			ОбновитьПовторноИспользуемыеЗначения();

			Модифицированность = Ложь;
			                                                           
		Исключение
			Возврат Ложь;	
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции  

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	СохранитьВыгрузитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	СохранитьВыгрузитьНастройки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНастройку(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОбУдалении", ЭтотОбъект, Параметры);
	ПоказатьВопрос(Оповещение, "Настройки синхронизации будут удалены. Продолжить?", РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОбУдалении(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

	УдалитьНастройкуСервер();
	
	Модифицированность = Ложь;
	
	Оповестить("СозданаИзмененаНастройка");
	
	Закрыть();

КонецПроцедуры

&НаСервере
Процедура УдалитьНастройкуСервер()
	
	ЗаписьНастройкиСинхронизации = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.СоздатьМенеджерЗаписи();
	ЗаписьНастройкиСинхронизации.НастройкаПодключения = НастройкаПодключения;
	ЗаписьНастройкиСинхронизации.Прочитать();
	ЗаписьНастройкиСинхронизации.Удалить();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры


&НаКлиенте
Процедура НастроитьРасписаниеОбмена(Команда)
		
	Если РасписаниеРегламентногоЗадания = НеОпределено Тогда
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьИзменятьРасписание", ЭтотОбъект);
	
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИзменятьРасписание(РасписаниеЗадания, ДополнительныеПараметры) Экспорт
	
	Если РасписаниеЗадания = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РасписаниеРегламентногоЗадания = РасписаниеЗадания;
	
	УстановитьНадписьРасписанияОбмена();
	Модифицированность = Истина;
	
КонецПроцедуры


&НаКлиенте
Процедура ЗагрузитьПользователейСПортала(Команда)
		
	Если НЕ СохранитьВыгрузитьНастройки() Тогда
		Возврат;
	КонецЕсли;
		
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Загрузка пользователей с портала.";
	
	ЗагрузитьСБитрикс24Сервер(СтруктураНастроек);
	
	Элементы.СписокПользователей.Обновить();

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСБитрикс24Сервер(СтруктураНастроек)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Портал", Портал);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ПользователиБитрикс24.Портал КАК Портал,
	|	Б24_К_ПользователиБитрикс24.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрСведений.Б24_К_ПользователиБитрикс24 КАК Б24_К_ПользователиБитрикс24
	|ГДЕ
	|	Б24_К_ПользователиБитрикс24.Портал = &Портал";
	Запрос.Выполнить();
	
	СтруктураНастроек.Логирование.Измерение2 = "Получение сотрудников";          
	
	ДанныеПользователей = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/user.get.json", "ID");	
	Для каждого ТекЭлемент Из ДанныеПользователей Цикл
		
		Если ТекЭлемент.Получить("ACTIVE") <> Истина Тогда
			Продолжить;
		КонецЕсли;
		
		Идентификатор = ТекЭлемент.Получить("ID");

		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_Данные.Портал КАК Портал,
		|	ВТ_Данные.Идентификатор КАК Идентификатор
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|ГДЕ
		|	ВТ_Данные.Идентификатор = &Идентификатор";
		
		ВыполненныйЗапрос = Запрос.Выполнить();
		Если ВыполненныйЗапрос.Пустой() Тогда
			
			НоваяЗапись = РегистрыСведений.Б24_К_ПользователиБитрикс24.СоздатьМенеджерЗаписи();
			НоваяЗапись.Портал 			= Портал;
			НоваяЗапись.Идентификатор 	= Идентификатор;
			НоваяЗапись.Наименование 	= Строка(ТекЭлемент.Получить("NAME")) + " " + Строка(ТекЭлемент.Получить("LAST_NAME"));
			
			НоваяЗапись.Записать();
			
		Иначе      
			
			НоваяЗапись = РегистрыСведений.Б24_К_ПользователиБитрикс24.СоздатьМенеджерЗаписи();
			НоваяЗапись.Портал 			= Портал;
			НоваяЗапись.Идентификатор 	= Идентификатор;
			НоваяЗапись.Прочитать();
			
			НоваяЗапись.Наименование 	= Строка(ТекЭлемент.Получить("NAME")) + " " + Строка(ТекЭлемент.Получить("LAST_NAME"));
			
			НоваяЗапись.Записать();
			
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры	

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СпособСинхронизацииДанныхПриИзменении(Элемент)
	
	ВыгружатьНастройкиНаПортал = Истина;
	Если НЕ СохранитьВыгрузитьНастройки() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось сохранить настройку");
		Возврат;
	КонецЕсли;
	
	ОбновитьОтображениеЭлементовФормы();

КонецПроцедуры

&НаКлиенте
Процедура СмартПроцессыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СмартПроцессыСсылкаНастроить" Тогда
		ОткрытьФормуНастройкиСмартПроцесса();
	ИначеЕсли Поле.Имя = "СмартПроцессыОчиститьСмартПроцесс" Тогда
		Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.ВременныйАдресНастроекСмартПроцесса) Тогда
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаНаУдалениеСмартПроцесса", ЭтотОбъект, Параметры);
			ПоказатьВопрос(Оповещение, "Будут удалены настройки по выбранному смарт процессу. Продолжить?", РежимДиалогаВопрос.ДаНет, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаНаУдалениеСмартПроцесса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
	
	Элементы.СмартПроцессы.ТекущиеДанные.ВременныйАдресНастроекСмартПроцесса = "";	
	
	Модифицированность 			= Истина;
	ВыгружатьНастройкиНаПортал 	= Истина;  

КонецПроцедуры	

#КонецОбласти


#Область ВыгрузкаНастроек

&НаКлиенте
Процедура ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ТекстСообщения             = "Выполняется выгрузка настроек..";
	ПараметрыОжидания.ВыводитьОкноОжидания       = Ложь;
	
	НачатьВыгрузкуНастройки(СтруктураНастроек);
	
	Если ЗакрытьПоОкончании Тогда
		Закрыть();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьВыгрузкуНастройки(СтруктураНастроек)
	
	УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка регистрации изменений в Битрикс24";
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("НастройкаПодключения"		, НастройкаПодключения);
	ПараметрыВыгрузки.Вставить("Наименование"				, Строка(НастройкаПодключения));
	ПараметрыВыгрузки.Вставить("СпособСинхронизацииДанных"	, СпособСинхронизацииДанных);
	                                 
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти


#Область Прочие

&НаКлиенте
Процедура ОбновитьОтображениеЭлементовФормы()
	
	Если СпособСинхронизацииДанных = "ВРежимеРеальногоВремени" Тогда
		Элементы.ГруппаНастройкаОбменаПоРасписанию.Видимость = Ложь;
	ИначеЕсли СпособСинхронизацииДанных = "ПоРасписанию" И НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		Элементы.ГруппаНастройкаОбменаПоРасписанию.Видимость = Истина;
	Иначе
		Элементы.ГруппаНастройкаОбменаПоРасписанию.Видимость = Ложь;
	КонецЕсли;
	
	//Элементы.СписокПользователей.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНадписьРасписанияОбмена()
	
	Если РасписаниеРегламентногоЗадания = НеОпределено Тогда
		ТекстЗаголовка = НСтр("ru='Настроить расписание обмена'");
	Иначе
		ТекстЗаголовка = РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Элементы.НастроитьРасписаниеОбмена.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСмартПроцессов(СтруктураНастроек)
	
	ДанныеПортала = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/crm.type.list", "id", "", "types");
	
	Для каждого ТекЭлемент Из ДанныеПортала Цикл
		НоваяСтрока = СмартПроцессы.Добавить();
		НоваяСтрока.Ид 					= Формат(ТекЭлемент.Получить("id"), "ЧГ=0");
		НоваяСтрока.Наименование 		= ТекЭлемент.Получить("title"); 
		НоваяСтрока.ОчиститьСмартПроцесс= БиблиотекаКартинок.Удалить; 
		НоваяСтрока.ТипВладельца 			= Формат(ТекЭлемент.Получить("entityTypeId"), "ЧГ=0"); 
		НоваяСтрока.УказываетсяИсточник 	= ТекЭлемент.Получить("isSourceEnabled") = "Y" ИЛИ ТекЭлемент.Получить("isSourceEnabled") = Истина; 
		НоваяСтрока.УказываютсяТовары 		= ТекЭлемент.Получить("isLinkWithProductsEnabled") = "Y" ИЛИ ТекЭлемент.Получить("isLinkWithProductsEnabled") = Истина; 
		
		НоваяСтрока.ИспользуютсяНаправления = ТекЭлемент.Получить("isCategoriesEnabled") = "Y" ИЛИ ТекЭлемент.Получить("isCategoriesEnabled") = Истина; 
		НоваяСтрока.ИспользуютсяСтадии 		= ТекЭлемент.Получить("isStagesEnabled") = "Y" ИЛИ ТекЭлемент.Получить("isStagesEnabled") = Истина; 
		
		НоваяСтрока.УказываетсяОрганизация	= ТекЭлемент.Получить("isMycompanyEnabled") = "Y" ИЛИ ТекЭлемент.Получить("isMycompanyEnabled") = Истина; 
		НоваяСтрока.УказываетсяКлиент 		= ТекЭлемент.Получить("isClientEnabled") = "Y" ИЛИ ТекЭлемент.Получить("isClientEnabled") = Истина; 
		
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьФормуНастройкиСмартПроцесса()
	
	ТекущийСмартПроцесс = Элементы.СмартПроцессы.ТекущиеДанные;
	Если ТекущийСмартПроцесс = Неопределено Тогда
		Возврат;
	КонецЕсли;                                  
	
	ПараметрыСмартПроцесса = Новый Структура;
	ПараметрыСмартПроцесса.Вставить("НастройкаПодключения"		, НастройкаПодключения);
	ПараметрыСмартПроцесса.Вставить("ВременныйАдресНастроек"	, ТекущийСмартПроцесс.ВременныйАдресНастроекСмартПроцесса);
	ПараметрыСмартПроцесса.Вставить("ИдСмартПроцесса"			, ТекущийСмартПроцесс.Ид);
	ПараметрыСмартПроцесса.Вставить("НаименованиеСмартПроцесса"	, ТекущийСмартПроцесс.Наименование);
	ПараметрыСмартПроцесса.Вставить("ТипВладельцаСмартПроцесса"	, ТекущийСмартПроцесс.ТипВладельца);
	ПараметрыСмартПроцесса.Вставить("УказываетсяИсточник"		, ТекущийСмартПроцесс.УказываетсяИсточник);
	ПараметрыСмартПроцесса.Вставить("УказываютсяТовары"			, ТекущийСмартПроцесс.УказываютсяТовары);
	ПараметрыСмартПроцесса.Вставить("ИспользуютсяСтадии"		, ТекущийСмартПроцесс.ИспользуютсяСтадии);
	ПараметрыСмартПроцесса.Вставить("ИспользуютсяНаправления"	, ТекущийСмартПроцесс.ИспользуютсяНаправления);
	ПараметрыСмартПроцесса.Вставить("УказываетсяОрганизация"	, ТекущийСмартПроцесс.УказываетсяОрганизация);
	ПараметрыСмартПроцесса.Вставить("УказываетсяКлиент"			, ТекущийСмартПроцесс.УказываетсяКлиент);
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияНастройкиСмартПроцесса", ЭтотОбъект, ПараметрыСмартПроцесса);
	
	ОткрытьФорму("РегистрСведений.Б24_СМ_НастройкиСмартПроцессов.Форма.ФормаНастройкиСмартПроцесса",ПараметрыСмартПроцесса,ЭтаФорма,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияНастройкиСмартПроцесса(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Элементы.СмартПроцессы.ТекущиеДанные.ВременныйАдресНастроекСмартПроцесса = Результат; 
		Модифицированность 			= Истина; 
		ВыгружатьНастройкиНаПортал 	= Истина;
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти




