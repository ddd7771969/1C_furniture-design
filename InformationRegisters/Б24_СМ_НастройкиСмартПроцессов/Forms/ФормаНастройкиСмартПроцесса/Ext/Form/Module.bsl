
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаПодключения 		= Параметры.НастройкаПодключения;
	ИдСмартПроцесса 			= Параметры.ИдСмартПроцесса;
	НаименованиеСмартПроцесса	= Параметры.НаименованиеСмартПроцесса;
	ТипВладельцаСмартПроцесса 	= Параметры.ТипВладельцаСмартПроцесса;
	УказываетсяИсточник 		= Параметры.УказываетсяИсточник;
	УказываютсяТовары 			= Параметры.УказываютсяТовары;
	ИспользуютсяСтадии 			= Параметры.ИспользуютсяСтадии;
	ИспользуютсяНаправления 	= Параметры.ИспользуютсяНаправления;
	УказываетсяКлиент 			= Параметры.УказываетсяКлиент;
	УказываетсяОрганизация 		= Параметры.УказываетсяОрганизация;
	ВременныйАдресНастроек 		= Параметры.ВременныйАдресНастроек;
	
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		
		Элементы.ДекорацияИнфо1.Видимость = Истина;
		Элементы.ДекорацияИнфо2.Видимость = Истина;
		
		Элементы.ГруппаАлгоритмВыгрузкиНаПортал.Видимость 	= Ложь;
		Элементы.ГруппаАлгоритмЗагрузкиВ1С.Видимость 		= Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтруктураНастроек.Логирование.Измерение2 = "Настройки смарт процессов";
	
	ПолучитьДанныеСмартПроцесса(СтруктураНастроек);
	
	ТаблицаСоответствийВыгрузки.Очистить();
	Для каждого ТекСтрока Из ПоляСмартПроцесса Цикл
		Если НЕ ТекСтрока.ТолькоДляЧтения = Истина Тогда
			НоваяСтрока = ТаблицаСоответствийВыгрузки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока); 
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекСтрока Из ПользовательскиеПоляПортала Цикл
		НайденныеСтроки = ТаблицаСоответствийВыгрузки.НайтиСтроки(Новый Структура("КлючЗапроса", ТекСтрока.КлючЗапросаСлужебный));
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденныеСтроки[0].Идентификатор = ТекСтрока.Идентификатор;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаСоответствийВыгрузкиТабЧасть.Очистить();
	Для каждого ТекСтрока Из ПоляТЧСмартПроцесса Цикл
		Если НЕ ТекСтрока.ТолькоДляЧтения = Истина Тогда
			НоваяСтрока = ТаблицаСоответствийВыгрузкиТабЧасть.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока); 
		КонецЕсли;
	КонецЦикла;
	
	НастройкиСмартПроцессаПоУмолчаниюСервер();  //заполняем по умолчанию
	
	ЗаполнитьНастройкиПоСохраненнымДанным(ВременныйАдресНастроек, Неопределено);
	
	ПредТипСущности1С = ТипСущности1С;
	
	ОбновлениеОтображенияКомандЭлементовФормы();
	ОбновлениеОтображенияЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти


#Область КомандыФормы

&НаКлиенте
Процедура КомандаДалее(Команда)
	
	Если Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыборИсточника1С Тогда
		
		Если Сущность1С.Количество() > 0 Тогда
			Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталОбщее;	
			Заголовок = "Можете установить фильтры по выгружаемым данных из 1С";
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана сущность 1С");
			Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыборИсточника1С;	
			Заголовок = "Выберите сущность 1С для связи с смарт процессом";
		КонецЕсли;
		
	ИначеЕсли Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталОбщее Тогда
		
		Если ВыгружатьНаПортал Тогда
			Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталПодробно;	
			Заголовок = "Установите сопоставление полей смарт процесса к реквизитам сущности 1С";
		Иначе
			Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1СОбщее;	
			Заголовок = "Можете установить фильтры по загружаемым данным в 1С";
		КонецЕсли;
		
	ИначеЕсли Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталПодробно Тогда
		Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталАлгоритм;	
		Заголовок = "Можете указать алгоритм, выполняющейся перед выгрузкой данных в Битрикс24";
	ИначеЕсли Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталАлгоритм Тогда
		Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1СОбщее;	
		Заголовок = "Можете установить фильтры по загружаемым данным в 1С";
	ИначеЕсли Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1СОбщее Тогда
		
		Если ЗагружатьВ1С Тогда
			Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1СПодробно;
			Заголовок = "Установите сопоставление реквизитов сущности 1С к полям смарт процесса";
		КонецЕсли;
		
	ИначеЕсли Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1СПодробно Тогда
		Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1САлгоритм;	
		Заголовок = "Можете указать алгоритм, который будет выполняться перед загрузкой данных в 1С";
	КонецЕсли;	
	
	ОбновлениеОтображенияКомандЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНазад(Команда)
	
	Если Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1САлгоритм Тогда
		Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1СПодробно;
		Заголовок = "Установите сопоставление реквизитов сущности 1С к полям смарт процесса";
	ИначеЕсли Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1СПодробно Тогда
		Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1СОбщее;	
		Заголовок = "Можете установить фильтры по загружаемым данным в 1С";
	ИначеЕсли Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1СОбщее Тогда
		
		Если ВыгружатьНаПортал Тогда
			Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталАлгоритм;	
			Заголовок = "Можете указать алгоритм, выполняющейся перед выгрузкой данных в Битрикс24";
		Иначе
			Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталОбщее;
			Заголовок = "Можете установить фильтры по выгружаемым данных из 1С";
		КонецЕсли;
		
	ИначеЕсли Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталАлгоритм Тогда
		Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталПодробно;
		Заголовок = "Установите сопоставление полей смарт процесса к реквизитам сущности 1С";
	ИначеЕсли Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталПодробно Тогда
		Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталОбщее;	
		Заголовок = "Можете установить фильтры по выгружаемым данных из 1С";
	ИначеЕсли Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанныхНаПорталОбщее Тогда
		Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница = Элементы.СтраницаВыборИсточника1С;	
		Заголовок = "Выберите сущность 1С для связи с смарт процессом";
	КонецЕсли;	
	
	ОбновлениеОтображенияКомандЭлементовФормы();
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаЗавершить(Команда)
	Закрыть(ПоместитьВоВременноеХранилищеДанныеНастроек());
КонецПроцедуры


&НаСервере
Функция ПоместитьДанныеДляЭкспортаВоВременноеХранилище()
	
    Данные = ПолучитьИзВременногоХранилища(ПоместитьВоВременноеХранилищеДанныеНастроек());
	
	ВременныйФайл 	= ПолучитьИмяВременногоФайла();
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ВременныйФайл);

	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, Данные);   	
	ЗаписьXML.Закрыть();
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ВременныйФайл);
	Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
	
	УдалитьФайлы(ВременныйФайл);
	
    Возврат Адрес;
	
КонецФункции

&НаКлиенте
Процедура ВыгрузкаВФайл(Команда)

	Адрес 			= ПоместитьДанныеДляЭкспортаВоВременноеХранилище();
	ИмяСРасширением = НаименованиеСмартПроцесса + ".xml";
	
	ПолучаемыеФайлы = Новый Массив;
	ОписаниеПередаваемогоФайла = Новый ОписаниеПередаваемогоФайла(ИмяСРасширением, Адрес);
	ПолучаемыеФайлы.Добавить(ОписаниеПередаваемогоФайла);
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПолученияФайлов;
	ПараметрыДиалога.Заголовок = "Выберите путь для сохранения настроек";
	
	НачатьПолучениеФайловССервера(ПолучаемыеФайлы, ПараметрыДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)

	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗагрузкеДанныхИзФайла", ЭтотОбъект, Параметры);
	ПоказатьВопрос(Оповещение, "Будут удалены все настройки по смарт процессу. Продолжить?", РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗагрузкеДанныхИзФайла(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
	
    ОписаниеОповещения = Новый ОписаниеОповещения("ИмпортИзФайлаЗавершение", ЭтаФорма);
    НачатьПомещениеФайлаНаСервер(ОписаниеОповещения);
	
КонецПроцедуры	

&НаСервере
Процедура ЗагрузитьИзФайлаСервер(ДвоичныеДанные)

	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьПоток(ДвоичныеДанные.ОткрытьПотокДляЧтения());
	СохраненныеДанные = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
	ЧтениеXML.Закрыть();
	
	НастройкиСмартПроцессаПоУмолчаниюСервер();     //очищаем по умолчанию
	
	ЗаполнитьНастройкиПоСохраненнымДанным(Неопределено, СохраненныеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмпортИзФайлаЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	
    Если ОписаниеПомещенногоФайла = Неопределено Тогда
        Возврат;
    КонецЕсли;
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(ОписаниеПомещенногоФайла.Адрес);
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтруктураНастроек.Логирование.Измерение2 = "Настройки смарт процессов";
	ПолучитьДанныеСмартПроцесса(СтруктураНастроек);
	
	ТаблицаСоответствийВыгрузки.Очистить();
	Для каждого ТекСтрока Из ПоляСмартПроцесса Цикл
		Если НЕ ТекСтрока.ТолькоДляЧтения = Истина Тогда
			НоваяСтрока = ТаблицаСоответствийВыгрузки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока); 
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаСоответствийВыгрузкиТабЧасть.Очистить();
	Для каждого ТекСтрока Из ПоляТЧСмартПроцесса Цикл
		Если НЕ ТекСтрока.ТолькоДляЧтения = Истина Тогда
			НоваяСтрока = ТаблицаСоответствийВыгрузкиТабЧасть.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока); 
		КонецЕсли;
	КонецЦикла;
	
	ЗагрузитьИзФайлаСервер(ДвоичныеДанные);
	
	Модифицированность = Истина;
	ОбновлениеОтображенияЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти


#Область ВыборИсточника1С

&НаКлиенте
Процедура ТипСущности1СПриИзменении(Элемент)
	
	Если Сущность1С.Количество() > 0 Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПриСменеТипаСущности", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, "Будут удалены настройки по выбранному смарт процессу. Продолжить?", РежимДиалогаВопрос.ДаНет, 0);
	Иначе
		ПослеЗакрытияВопросаПриСменеТипаСущности(КодВозвратаДиалога.Да, Неопределено);	
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПослеЗакрытияВопросаПриСменеТипаСущности(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ТипСущности1С = ПредТипСущности1С;
        Возврат;
    КонецЕсли;
	
	ПредТипСущности1С = ТипСущности1С;     //чтобы мог быть откат
	
	Сущность1С.Очистить();
	ТабличнаяЧасть1С.Очистить();
	
	НастройкиСмартПроцессаПоУмолчаниюСервер(Ложь);
	
КонецПроцедуры	


&НаКлиенте
Процедура Сущность1СНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Сущность1С.Количество() > 0  Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПриСменеСущности", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, "Будут удалены настройки по выбранному смарт процессу. Продолжить?", РежимДиалогаВопрос.ДаНет, 0);
	Иначе
		ПослеЗакрытияВопросаПриСменеСущности(КодВозвратаДиалога.Да, Неопределено);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПриСменеСущности(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
	
	Сущность1С 			= Сущность1С;
	СписокСущностей1С 	= ПолучитьСписокСущностей1С();
	
	Если Сущность1С.Количество() > 0 Тогда
		ВыбраннаяСущность = СписокСущностей1С.НайтиПоЗначению(Сущность1С[0].Значение);	
	Иначе
		ВыбраннаяСущность = Неопределено;	
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораСущности", ЭтотОбъект, Параметры);
	ПоказатьВыборИзСписка(Оповещение, СписокСущностей1С, Элементы.Сущность1С, ВыбраннаяСущность);

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСущности(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
        Возврат;
	КонецЕсли;
	
	Сущность1С.Очистить();
	ТабличнаяЧасть1С.Очистить();
	Сущность1С.Добавить(Результат.Значение, Результат.Представление);
	
	НастройкиСмартПроцессаПоУмолчаниюСервер(Ложь);
	
КонецПроцедуры


&НаКлиенте
Процедура Сущность1СОчистка(Элемент, СтандартнаяОбработка)
	
	Сущность1С.Очистить();
	ТабличнаяЧасть1С.Очистить();
	НастройкиСмартПроцессаПоУмолчаниюСервер(Ложь);
	
КонецПроцедуры


&НаКлиенте
Процедура ТабличнаяЧасть1СНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТабличнаяЧасть1С.Количество() > 0  Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПриСменеТабличнойЧастиСущности", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, "Будут удалены настройки по выбранному смарт процессу. Продолжить?", РежимДиалогаВопрос.ДаНет, 0);
	Иначе
		ПослеЗакрытияВопросаПриСменеТабличнойЧастиСущности(КодВозвратаДиалога.Да, Неопределено);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПриСменеТабличнойЧастиСущности(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
	
	СписокТЧСущностей1С 	= ПолучитьСписокТЧСущности1С();
	
	Если ТабличнаяЧасть1С.Количество() > 0 Тогда
		ВыбраннаяТЧСущности = СписокТЧСущностей1С.НайтиПоЗначению(ТабличнаяЧасть1С[0].Значение);	
	Иначе
		ВыбраннаяТЧСущности = Неопределено;	
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораТабличнойЧастиСущности", ЭтотОбъект, Параметры);
	ПоказатьВыборИзСписка(Оповещение, СписокТЧСущностей1С, Элементы.ТабличнаяЧасть1С, ВыбраннаяТЧСущности);

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораТабличнойЧастиСущности(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
        Возврат;
	КонецЕсли;
	
	ТабличнаяЧасть1С.Очистить();
	ТабличнаяЧасть1С.Добавить(Результат.Значение, Результат.Представление);
	
	НастройкиСмартПроцессаПоУмолчаниюСервер(Ложь);
	
КонецПроцедуры


&НаКлиенте
Процедура ТабличнаяЧасть1СОчистка(Элемент, СтандартнаяОбработка)
	
	ТабличнаяЧасть1С.Очистить();
	
	НастройкиСмартПроцессаПоУмолчаниюСервер(Ложь);
	
КонецПроцедуры


&НаСервере
Функция ПолучитьСписокСущностей1С()
	
	Результат = Новый СписокЗначений;
	
	Если ТипСущности1С = "Справочник" Тогда
		
		Для каждого ТекЭлемент Из Метаданные.Справочники Цикл
			Результат.Добавить(ТекЭлемент.Имя, ТекЭлемент.Синоним); 
		КонецЦикла;
		
	ИначеЕсли ТипСущности1С = "Документ" Тогда
		
		Для каждого ТекЭлемент Из Метаданные.Документы Цикл
			Результат.Добавить(ТекЭлемент.Имя, ТекЭлемент.Синоним); 
		КонецЦикла;
		
	КонецЕсли;
	
	Результат.СортироватьПоПредставлению();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокТЧСущности1С()
	
	Результат = Новый СписокЗначений;
	
	Если Сущность1С.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ТипСущности1С = "Справочник" Тогда
		ТекМетаданные = Метаданные.Справочники;
	Иначе
		ТекМетаданные = Метаданные.Документы;
	КонецЕсли;
	
	Для каждого ТекЭлемент Из ТекМетаданные[Сущность1С[0].Значение].ТабличныеЧасти Цикл
		Результат.Добавить(ТекЭлемент.Имя, ТекЭлемент.Синоним); 
	КонецЦикла;
		
	
	Возврат Результат;
	
КонецФункции


&НаКлиенте
Процедура НастроитьСтадии(Команда)
	
	Если ЗначениеЗаполнено(ТипСущности1С) И Сущность1С.Количество() > 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияНастройкиСтатусов", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВременныйАдресНастроекСтадий"		, ВременныйАдресНастроекСтадий);
		ПараметрыФормы.Вставить("НастройкаПодключения"				, НастройкаПодключения);
		ПараметрыФормы.Вставить("ИдСмартПроцесса"					, ИдСмартПроцесса);
		ПараметрыФормы.Вставить("ТипВладельцаСмартПроцесса"			, ТипВладельцаСмартПроцесса);
		
		ПараметрыФормы.Вставить("ТипСущности1С"						, ТипСущности1С);
		ПараметрыФормы.Вставить("Сущность1С"						, Сущность1С[0].Значение);
		
		ОткрытьФорму("РегистрСведений.Б24_СМ_НастройкиСмартПроцессов.Форма.ФормаСопоставленияСтадий", ПараметрыФормы, ЭтаФорма,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияНастройкиСтатусов(Результат, Параметры) Экспорт

	Если Результат <> Неопределено Тогда
		ВременныйАдресНастроекСтадий = Результат.ВременныйАдресНастроекСтадий;
	КонецЕсли;
	
КонецПроцедуры	


&НаКлиенте
Процедура НастроитьНаправления(Команда)
	
	Если ЗначениеЗаполнено(ТипСущности1С) И Сущность1С.Количество() > 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияНастройкиНаправлений", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВременныйАдресНастроекНаправлений"	, ВременныйАдресНастроекНаправлений);
		ПараметрыФормы.Вставить("НастройкаПодключения"				, НастройкаПодключения);
		ПараметрыФормы.Вставить("ИдСмартПроцесса"					, ИдСмартПроцесса);
		ПараметрыФормы.Вставить("ТипВладельцаСмартПроцесса"			, ТипВладельцаСмартПроцесса);
		
		ПараметрыФормы.Вставить("ТипСущности1С"						, ТипСущности1С);
		ПараметрыФормы.Вставить("Сущность1С"						, Сущность1С[0].Значение);
		
		ОткрытьФорму("РегистрСведений.Б24_СМ_НастройкиСмартПроцессов.Форма.ФормаСопоставленияНаправлений", ПараметрыФормы, ЭтаФорма,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияНастройкиНаправлений(Результат, Параметры) Экспорт

	Если Результат <> Неопределено Тогда
		ВременныйАдресНастроекНаправлений = Результат.ВременныйАдресНастроекНаправлений;
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти


#Область ВыгрузкаНаПорталОбщее

&НаКлиенте
Процедура ВыгружатьНаПорталПриИзменении(Элемент)
	
	Если НЕ ВыгружатьНаПортал Тогда ОбновлятьНаПортале = Ложь КонецЕсли;
	
	ОбновлениеОтображенияЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти


#Область ЗагрузкаВ1СОбщее

&НаКлиенте
Процедура ЗагружатьВ1СПриИзменении(Элемент)
	
	Если НЕ ЗагружатьВ1С Тогда 
		ОбновлятьВ1С = Ложь;
		ПроводитьВ1С = Ложь;
	КонецЕсли;
	
	ОбновлениеОтображенияЭлементовФормы();
	ОбновлениеОтображенияКомандЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОтборовПоЗагрузкеДанныхВ1СПриАктивизацииЯчейки(Элемент)
		
	ТекущиеДанные = Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1С.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	лЭлемент = Элемент.ТекущийЭлемент;
	
	Если лЭлемент.Имя = "ТаблицаОтборовПоЗагрузкеДанныхВ1СЗначение" Тогда
		
		лЭлемент.ВыбиратьТип 		  		= Ложь;
		лЭлемент.РедактированиеТекста 		= Истина;
		лЭлемент.КнопкаВыбора 				= Ложь;
		лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
		лЭлемент.ТолькоПросмотр		  		= Ложь;
		лЭлемент.ТолькоПросмотр		  		= Ложь;
		
		Если ТипЗнч(ТекущиеДанные.Значение) = Тип("СписокЗначений") Тогда
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("СписокЗначений");
		КонецЕсли;
		
		
		Если ТекущиеДанные.ВидСравнения = "В списке" ИЛИ ТекущиеДанные.ВидСравнения = "Не в списке" Тогда
				
			лЭлемент.КнопкаВыбора 	= Истина;
			
			Если ТипЗнч(ТекущиеДанные.Значение) <> Тип("СписокЗначений") Тогда
				
				лЭлемент.ОграничениеТипа	= Новый ОписаниеТипов("СписокЗначений");
				         
				спкЗаглушка = Новый СписокЗначений;
				
				НайденныеДанные = ТаблицаСоответствийВыгрузки.НайтиСтроки(Новый Структура("КлючЗапроса", ТекущиеДанные.КлючЗапроса));
				Если НайденныеДанные.Количество() > 0 Тогда
					спкЗаглушка.ТипЗначения = ПолучитьОписаниеТипКлючаСмартПроцесса(НайденныеДанные[0].ТипЗначения);
				КонецЕсли;
				ТекущиеДанные.Значение 	= спкЗаглушка;
				
			КонецЕсли;
			
		Иначе	
			
			НайденныеСтроки = ПоляСмартПроцесса.НайтиСтроки(Новый Структура("КлючЗапроса", ТекущиеДанные.КлючЗапроса));
			Если НайденныеСтроки.Количество() > 0 Тогда
				ПодготовитьЭлементПриСменеТипаКлючаСмартПроцесса(ТекущиеДанные.КлючЗапроса, НайденныеСтроки[0].ТипЗначения, лЭлемент);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	

КонецПроцедуры

#КонецОбласти


#Область ВыгрузкаНаПорталПодробно

&НаКлиенте
Процедура ТаблицаСоответствийВыгрузкиПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийВыгрузки.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		мИсточникДанных = Новый Массив;   
		мИсточникДанных.Добавить("Не изменять");		
		мИсточникДанных.Добавить("Фиксированное значение");
		мИсточникДанных.Добавить("Реквизит объекта");
		
		Если ДополнительныеРеквизитыИспользуются() Тогда
			мИсточникДанных.Добавить("Из дополнительного реквизита");
		КонецЕсли;
		
		Если ДополнительныеСведенияИспользуются() Тогда
			мИсточникДанных.Добавить("Из дополнительного сведения");
		КонецЕсли;
		
		Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
			мИсточникДанных.Добавить("Произвольный алгоритм");
		КонецЕсли;
		
		мИсточникДанных.Добавить("Предопределенный алгоритм 'Компания'");
		мИсточникДанных.Добавить("Предопределенный алгоритм 'Контакт'");
		
		мИсточникДанных.Добавить("");
		
		Элементы.ТаблицаСоответствийВыгрузкиЗначение.ТолькоПросмотр			= Ложь;
		Элементы.ТаблицаСоответствийВыгрузкиИсточникДанных.ТолькоПросмотр 	= Ложь;
		
		Элементы.ТаблицаСоответствийВыгрузкиИсточникДанных.СписокВыбора.ЗагрузитьЗначения(мИсточникДанных);	
		
		лЭлемент = Элементы.ТаблицаСоответствийВыгрузки.ПодчиненныеЭлементы.ТаблицаСоответствийВыгрузкиЗначение;
		
		Если ТекущиеДанные.ИсточникДанных = "Фиксированное значение" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.СписокВыбора.Очистить();
			
			ПодготовитьЭлементПриСменеТипаКлючаСмартПроцесса(ТекущиеДанные.КлючЗапроса, ТекущиеДанные.ТипЗначения, лЭлемент);
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Произвольный алгоритм" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Из дополнительного реквизита" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Истина;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения");
			
			МассивПараметров = Новый Массив;
			МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ЭтоДополнительноеСведение", Ложь));	
			лЭлемент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Из дополнительного сведения" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Истина;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения");
			
			МассивПараметров = Новый Массив;
			МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ЭтоДополнительноеСведение", Истина));	
			лЭлемент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Реквизит объекта" 
			ИЛИ ТекущиеДанные.ИсточникДанных = "Предопределенный алгоритм 'Компания'" ИЛИ ТекущиеДанные.ИсточникДанных = "Предопределенный алгоритм 'Контакт'"  Тогда
			
 			лЭлемент.ВыбиратьТип 		  		= Истина;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
			лЭлемент.СписокВыбора.Очистить();
			
			СписокРеквизитов = ПолучитьСписокРеквизитов();
			Для каждого ТекЭлемент Из СписокРеквизитов Цикл
				лЭлемент.СписокВыбора.Добавить(ТекЭлемент.Значение, ТекЭлемент.Представление);
			КонецЦикла;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Не изменять" Тогда      
			
			лЭлемент.СписокВыбора.Очистить();
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Истина;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "" Тогда
			
			лЭлемент.СписокВыбора.Очистить();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийВыгрузкиЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
			
	ЭлементРодитель = Элемент.Родитель;
	
	Если ЭлементРодитель.ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные = ЭлементРодитель.ТекущиеДанные;
	
	Если ТекущиеДанные.ИсточникДанных = "Произвольный алгоритм" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОО = Новый Структура;
		ПараметрыОО.Вставить("Элемент", ЭлементРодитель);
		ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаЗаполнения", ЭтаФорма, ПараметрыОО);
			
			
		ПередаваемыеПараметры = Новый Структура;
		ПередаваемыеПараметры.Вставить("Тип"		, "Функция");
		ПередаваемыеПараметры.Вставить("Назначение"	, "Выгрузка");
		ПередаваемыеПараметры.Вставить("ЭтоСтрокаТЧ", Истина);
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Значение) Тогда
			ПередаваемыеПараметры.Вставить("Алгоритм", ТекущиеДанные.Значение);
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаАлгоритмаЗаполнения(Алгоритм, пПараметры) Экспорт
	
	Если НЕ Алгоритм = Неопределено Тогда
		пПараметры.Элемент.ТекущиеДанные.Значение = Алгоритм;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаСоответствийВыгрузкиТабЧастьПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийВыгрузкиТабЧасть.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		мИсточникДанных = Новый Массив;   
		мИсточникДанных.Добавить("Не изменять");		
		мИсточникДанных.Добавить("Предопределенный алгоритм");
		мИсточникДанных.Добавить("Фиксированное значение");
		мИсточникДанных.Добавить("Реквизит объекта");
		
		Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
			мИсточникДанных.Добавить("Произвольный алгоритм");
		КонецЕсли;
		
		мИсточникДанных.Добавить("");
		
		Элементы.ТаблицаСоответствийВыгрузкиТабЧастьЗначение.ТолькоПросмотр			= Ложь;
		Элементы.ТаблицаСоответствийВыгрузкиТабЧастьИсточникДанных.ТолькоПросмотр 	= Ложь;
		          
		Элементы.ТаблицаСоответствийВыгрузкиТабЧастьИсточникДанных.СписокВыбора.ЗагрузитьЗначения(мИсточникДанных);	
		
		лЭлемент = Элементы.ТаблицаСоответствийВыгрузкиТабЧасть.ПодчиненныеЭлементы.ТаблицаСоответствийВыгрузкиТабЧастьЗначение;
		
		Если ТекущиеДанные.ИсточникДанных = "Фиксированное значение" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.ТолькоПросмотр 			= Ложь;
			
			ПодготовитьЭлементПриСменеТипаКлючаСмартПроцесса(ТекущиеДанные.КлючЗапроса, ТекущиеДанные.ТипЗначения, лЭлемент);
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Предопределенный алгоритм" Тогда
			
 			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.СписокВыбора.Добавить("Идентификатор товара"			, "Идентификатор товара");
			лЭлемент.СписокВыбора.Добавить("Описание товарной позиции"		, "Описание товарной позиции");
			лЭлемент.СписокВыбора.Добавить("Код единицы измерения"			, "Код единицы измерения");
			лЭлемент.СписокВыбора.Добавить("Наименование единицы измерения"	, "Наименование единицы измерения");
			лЭлемент.СписокВыбора.Добавить("Цена"				, "Цена");
			лЭлемент.СписокВыбора.Добавить("Количество"			, "Количество");
			лЭлемент.СписокВыбора.Добавить("Ставка НДС"			, "Ставка НДС");
			лЭлемент.СписокВыбора.Добавить("НДС включён в цену"	, "НДС включён в цену");
			лЭлемент.СписокВыбора.Добавить("Процент скидки"		, "Процент скидки");
			лЭлемент.СписокВыбора.Добавить("Сумма скидки"		, "Сумма скидки");
			лЭлемент.СписокВыбора.Добавить("Тип скидки"			, "Тип скидки");
			лЭлемент.СписокВыбора.Добавить("Изменен"			, "Изменен");
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Произвольный алгоритм" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Реквизит объекта" Тогда
			
 			лЭлемент.ВыбиратьТип 		  		= Истина;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
			лЭлемент.СписокВыбора.Очистить();
			
			СписокРеквизитов = ПолучитьСписокРеквизитовТЧ();
			Для каждого ТекЭлемент Из СписокРеквизитов Цикл
				лЭлемент.СписокВыбора.Добавить(ТекЭлемент.Значение, ТекЭлемент.Представление);
			КонецЦикла;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Не изменять" Тогда      
			
			лЭлемент.СписокВыбора.Очистить();
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Истина;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "" Тогда
			
			лЭлемент.СписокВыбора.Очистить();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Функция ПолучитьСписокРеквизитов()
	
	Результат = Новый СписокЗначений;
	
	Если ТипСущности1С = "Справочник" Тогда
		
		ТекущиеМетаданные = Метаданные.Справочники;
		
		Результат.Добавить("Код"			, "Код");
		Результат.Добавить("Наименование"	, "Наименование");
		Результат.Добавить("Владелец"		, "Владелец");
		
	ИначеЕсли ТипСущности1С = "Документ" Тогда
		
		ТекущиеМетаданные = Метаданные.Документы;
		
		Результат.Добавить("Номер"			, "Номер");
		Результат.Добавить("Дата"			, "Дата");
		
	КонецЕсли;
	
	Если Сущность1С.Количество() > 0 Тогда
		Для каждого ТекЭлемент Из ТекущиеМетаданные[Сущность1С[0].Значение].Реквизиты Цикл 
			Результат.Добавить(ТекЭлемент.Имя, ТекЭлемент.Синоним); 
		КонецЦикла;
	КонецЕсли; 
	
	Результат.Добавить("ПометкаУдаления", "Пометка удаления");
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокРеквизитовТЧ()
	
	Результат = Новый СписокЗначений;
	
	Если ТипСущности1С = "Справочник" Тогда
		ТекущиеМетаданные = Метаданные.Справочники;
	ИначеЕсли ТипСущности1С = "Документ" Тогда
		ТекущиеМетаданные = Метаданные.Документы;
	КонецЕсли;
	
	Для каждого ТекЭлемент Из ТекущиеМетаданные[Сущность1С[0].Значение].ТабличныеЧасти[ТабличнаяЧасть1С[0].Значение].Реквизиты Цикл 
		Результат.Добавить(ТекЭлемент.Имя, ТекЭлемент.Синоним); 
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции


&НаСервере
Функция ДополнительныеРеквизитыИспользуются()
	
	Результат = Ложь;
	
	Если Сущность1С.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	МетаданныеОбъекта = Неопределено;
	Если ТипСущности1С = "Справочник" Тогда
		МетаданныеОбъекта 	= Метаданные.Справочники[Сущность1С[0].Значение];
	ИначеЕсли ТипСущности1С = "Документ" Тогда
		МетаданныеОбъекта = Метаданные.Документы[Сущность1С[0].Значение];
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения") Тогда
		Если МетаданныеОбъекта.ТабличныеЧасти.Найти("ДополнительныеРеквизиты") <> Неопределено Тогда
			Результат = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ДополнительныеСведенияИспользуются()
	
	Результат = Ложь;
	
	Если Сущность1С.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	МетаданныеОбъекта = Неопределено;
	Если ТипСущности1С = "Справочник" Тогда
		МетаданныеОбъекта 	= Метаданные.Справочники[Сущность1С[0].Значение];
		ТипОбъекта 			= Тип("СправочникСсылка." + Сущность1С[0].Значение);
	ИначеЕсли ТипСущности1С = "Документ" Тогда
		МетаданныеОбъекта = Метаданные.Документы[Сущность1С[0].Значение];
		ТипОбъекта 			= Тип("ДокументСсылка." + Сущность1С[0].Значение);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения") Тогда
		НаборСвойств1С = ПолучитьНаборСвойств1С(МетаданныеОбъекта);
		Если ЗначениеЗаполнено(НаборСвойств1С) Тогда
			Результат = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


&НаКлиенте
Процедура ТаблицаСоответствийВыгрузкиИсточникДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
		
	ТекущиеДанные = Элементы.ТаблицаСоответствийВыгрузки.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.ИсточникДанных <> ВыбранноеЗначение Тогда
			ТекущиеДанные.Значение = "";	
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийВыгрузкиТабЧастьИсточникДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийВыгрузкиТабЧасть.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.ИсточникДанных <> ВыбранноеЗначение Тогда
			ТекущиеДанные.Значение = "";	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ЗагрузкаНаПорталПодробно

&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийЗагрузки.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		мИсточникДанных = Новый Массив;   
		мИсточникДанных.Добавить("Не изменять");		
		мИсточникДанных.Добавить("Фиксированное значение");
		мИсточникДанных.Добавить("Из пользовательского поля");
		мИсточникДанных.Добавить("Ключ JSON");
		Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
			мИсточникДанных.Добавить("Произвольный алгоритм");
		КонецЕсли;
		
		мИсточникДанных.Добавить("Предопределенный алгоритм 'Клиент'");
		мИсточникДанных.Добавить("Предопределенный алгоритм 'Реквизит'"); 
		мИсточникДанных.Добавить("Предопределенный алгоритм 'НДС включен в цену'");
		
		мИсточникДанных.Добавить("");
		
		Элементы.ТаблицаСоответствийЗагрузкиИсточникДанных.СписокВыбора.ЗагрузитьЗначения(мИсточникДанных);	
		
		лЭлемент = Элементы.ТаблицаСоответствийЗагрузки.ПодчиненныеЭлементы.ТаблицаСоответствийЗагрузкиЗначение;
		
		лЭлемент.СписокВыбора.Очистить();

		Если ТекущиеДанные.ИсточникДанных = "Фиксированное значение" Тогда
			
			Если ТекущиеДанные.ТипРеквизита = "ДополнительныйРеквизит" ИЛИ ТекущиеДанные.ТипРеквизита = "ДополнительноеСведение" Тогда
				
				ОписаниеСвойства = ПолучитьОписаниеСвойстваДляУказанияЗначения(ТекущиеДанные.Реквизит1С);

				Если ЗначениеЗаполнено(ОписаниеСвойства.Свойство) Тогда
					
					лЭлемент.ОграничениеТипа 			= ОписаниеСвойства.ТипЗначения;
					лЭлемент.ВыбиратьТип 				= Истина;
					лЭлемент.КнопкаВыбора 				= Истина;
					лЭлемент.РедактированиеТекста 		= Истина;
					лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
					
					Если ОписаниеСвойства.ДополнительныеЗначенияИспользуются Тогда
						Элементы.ТаблицаСоответствийЗагрузкиЗначение.ПараметрыВыбора = ПолучитьПараметрыВыбораЗначенийСвойств(ОписаниеСвойства.Свойство);
					КонецЕсли;
							
				Иначе
					лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
					лЭлемент.ВыбиратьТип 		  		= Ложь;
					лЭлемент.РедактированиеТекста 		= Ложь;
					лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
					лЭлемент.КнопкаВыбора 				= Ложь;
					лЭлемент.ТолькоПросмотр		  		= Истина;
				КонецЕсли;
				
			Иначе
				
				лЭлемент.ОграничениеТипа 			= ПолучитьОграничениеТипаРеквизита1С(ТекущиеДанные.Реквизит1С, ТекущиеДанные.ТипРеквизита);
				лЭлемент.ВыбиратьТип 				= Истина;
				лЭлемент.РедактированиеТекста 		= Истина;
				лЭлемент.КнопкаВыпадающегоСписка	= Истина;
				лЭлемент.КнопкаВыбора 				= Истина;
				лЭлемент.ТолькоПросмотр 			= Ложь;
				
				Если ТекущиеДанные.Реквизит1С = "Родитель" Тогда
					лЭлемент.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;	
				КонецЕсли;
				
			КонецЕсли;

		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Ключ JSON" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			Для каждого ТекСтрока Из ПоляСмартПроцесса Цикл
				
				Если ПользовательскиеПоляПортала.НайтиСтроки(Новый Структура("КлючЗапросаСлужебный", ТекСтрока.КлючЗапроса)).Количество() = 0 Тогда 
					лЭлемент.СписокВыбора.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.Описание);
				КонецЕсли;
				
			КонецЦикла;
	                   
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Произвольный алгоритм" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Из пользовательского поля" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			СпкПоля = ПолучитьСпкПользовательскихПолейПортала();
			Для каждого ТекЭлемент Из СпкПоля Цикл 
				лЭлемент.СписокВыбора.Добавить(ТекЭлемент.Значение, ТекЭлемент.Представление);
			КонецЦикла;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Предопределенный алгоритм 'Клиент'"    
			ИЛИ ТекущиеДанные.ИсточникДанных = "Предопределенный алгоритм 'Реквизит'"
			ИЛИ ТекущиеДанные.ИсточникДанных = "Предопределенный алгоритм 'НДС включен в цену'" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.ТолькоПросмотр		  		= Истина;
			
			лЭлемент.СписокВыбора.Очистить();
			ТекущиеДанные.Значение = "";	
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Не изменять" Тогда      
			
			лЭлемент.СписокВыбора.Очистить();
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Истина;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "" Тогда
			
			лЭлемент.СписокВыбора.Очистить();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
			
	ЭлементРодитель = Элемент.Родитель;
	
	Если ЭлементРодитель.ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные = ЭлементРодитель.ТекущиеДанные;
	
	Если ТекущиеДанные.ИсточникДанных = "Произвольный алгоритм" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		спкКлючей = Новый СписокЗначений;
		Для каждого ТекСтрока Из ПоляСмартПроцесса Цикл
			спкКлючей.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.КлючЗапроса);
		КонецЦикла;
		
		ПараметрыОО = Новый Структура;
		ПараметрыОО.Вставить("Элемент", ЭлементРодитель);
		ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаЗаполнения", ЭтаФорма, ПараметрыОО);
		
		ПередаваемыеПараметры = Новый Структура;
		ПередаваемыеПараметры.Вставить("Тип"			, "Функция");
		ПередаваемыеПараметры.Вставить("Назначение"		, "Загрузка");
		ПередаваемыеПараметры.Вставить("КлючиПортала"	, спкКлючей);
		ПередаваемыеПараметры.Вставить("ЭтоСтрокаТЧ", Истина);
		Если ЗначениеЗаполнено(ТекущиеДанные.Значение) Тогда
			ПередаваемыеПараметры.Вставить("Алгоритм", ТекущиеДанные.Значение);
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиТабЧастьПриАктивизацииЯчейки(Элемент)
		
	ТекущиеДанные = Элементы.ТаблицаСоответствийЗагрузкиТабЧасть.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		мИсточникДанных = Новый Массив;   
		мИсточникДанных.Добавить("Не изменять");		
		мИсточникДанных.Добавить("Фиксированное значение");
		мИсточникДанных.Добавить("Предопределенный алгоритм");
		
		Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
			мИсточникДанных.Добавить("Произвольный алгоритм");
		КонецЕсли;
		
		мИсточникДанных.Добавить("");
		
		Элементы.ТаблицаСоответствийЗагрузкиТабЧастьЗначение.ТолькоПросмотр			= Ложь;
		Элементы.ТаблицаСоответствийЗагрузкиТабЧастьИсточникДанных.ТолькоПросмотр 	= Ложь;
		          
		Элементы.ТаблицаСоответствийЗагрузкиТабЧастьИсточникДанных.СписокВыбора.ЗагрузитьЗначения(мИсточникДанных);	
		
		лЭлемент = Элементы.ТаблицаСоответствийЗагрузкиТабЧасть.ПодчиненныеЭлементы.ТаблицаСоответствийЗагрузкиТабЧастьЗначение;
		
		Если ТекущиеДанные.ИсточникДанных = "Фиксированное значение" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.ТолькоПросмотр 			= Ложь;
			
			лЭлемент.ОграничениеТипа 			= ПолучитьОграничениеТипаРеквизита1С(ТекущиеДанные.Реквизит1С, "ТабличнаяЧасть");
			
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Предопределенный алгоритм" Тогда
			
 			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.СписокВыбора.Добавить("Номенклатура"			, "Номенклатура");
			лЭлемент.СписокВыбора.Добавить("Характеристика"			, "Характеристика");
			лЭлемент.СписокВыбора.Добавить("Содержание"				, "Содержание");
			лЭлемент.СписокВыбора.Добавить("ДатаОтгрузки"			, "Дата отгрузки");
			лЭлемент.СписокВыбора.Добавить("Единица измерения"		, "Единица измерения");
			лЭлемент.СписокВыбора.Добавить("Количество"				, "Количество");
			лЭлемент.СписокВыбора.Добавить("Цена"					, "Цена");
			лЭлемент.СписокВыбора.Добавить("Ставка НДС"				, "Ставка НДС");
			лЭлемент.СписокВыбора.Добавить("Сумма"					, "Сумма");
			лЭлемент.СписокВыбора.Добавить("Всего"					, "Всего");
			лЭлемент.СписокВыбора.Добавить("Сумма НДС"				, "Сумма НДС");
			лЭлемент.СписокВыбора.Добавить("Процент скидки/наценки"	, "Процент скидки/наценки");
			лЭлемент.СписокВыбора.Добавить("Сумма скидки/наценки"	, "Сумма скидки/наценки");
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Произвольный алгоритм" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Не изменять" Тогда      
			
			лЭлемент.СписокВыбора.Очистить();
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Истина;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "" Тогда
			
			лЭлемент.СписокВыбора.Очистить();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиТабЧастьЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
			
	ЭлементРодитель = Элемент.Родитель;
	
	Если ЭлементРодитель.ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные = ЭлементРодитель.ТекущиеДанные;
	
	Если ТекущиеДанные.ИсточникДанных = "Произвольный алгоритм" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		спкКлючей = Новый СписокЗначений;
		Для каждого ТекСтрока Из ПоляТЧСмартПроцесса Цикл
			спкКлючей.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.КлючЗапроса);
		КонецЦикла;
		
		ПараметрыОО = Новый Структура;
		ПараметрыОО.Вставить("Элемент", ЭлементРодитель);
		ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаЗаполнения", ЭтаФорма, ПараметрыОО);
			
		ПередаваемыеПараметры = Новый Структура;
		ПередаваемыеПараметры.Вставить("Тип"			, "Функция");
		ПередаваемыеПараметры.Вставить("Назначение"		, "Загрузка");
		ПередаваемыеПараметры.Вставить("ЭтоСтрокаТЧ"	, Истина);
		ПередаваемыеПараметры.Вставить("КлючиПортала"	, спкКлючей);
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Значение) Тогда
			ПередаваемыеПараметры.Вставить("Алгоритм", ТекущиеДанные.Значение);
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Функция ПолучитьОграничениеТипаРеквизита1С(Реквизит1С, ТипРеквизита)
	
	Результат = Новый ОписаниеТипов("Строка");
	
	Попытка 
			
		МетаданныеОбъекта = ПолучитьМетаданныеОбъекта();
		Если МетаданныеОбъекта = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	
		Если ТипРеквизита = "ДополнительныйРеквизит" Тогда
			
		ИначеЕсли ТипРеквизита = "ДополнительноеСведение" Тогда
			
		ИначеЕсли ТипРеквизита = "СтандартныйРеквизит" Тогда
			
			Для каждого ТекРеквизит Из МетаданныеОбъекта.СтандартныеРеквизиты Цикл
				Если ТекРеквизит.Имя = Реквизит1С Тогда
					Результат = ТекРеквизит.Тип;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ТипРеквизита = "Реквизиты" Тогда
			
			Для каждого ТекРеквизит Из МетаданныеОбъекта.Реквизиты Цикл
				Если ТекРеквизит.Имя = Реквизит1С Тогда
					Результат = ТекРеквизит.Тип;
				КонецЕсли;
			КонецЦикла;
		
		ИначеЕсли ТипРеквизита = "ТабличнаяЧасть" Тогда
			
			Если ТабличнаяЧасть1С.Количество() > 0 Тогда
				Для каждого ТекРеквизит Из МетаданныеОбъекта.ТабличныеЧасти[ТабличнаяЧасть1С[0].Значение].Реквизиты Цикл
					Если ТекРеквизит.Имя = Реквизит1С Тогда
						Результат = ТекРеквизит.Тип;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	Исключение
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьОписаниеСвойстваДляУказанияЗначения(НаименованиеСвойства)
	
	Результат = Новый Структура;
	Результат.Вставить("Свойство"							, ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка());
	Результат.Вставить("ТипЗначения"						, Новый ОписаниеТипов);
	Результат.Вставить("ЭтоДополнительноеСведение"			, Ложь);
	Результат.Вставить("ДополнительныеЗначенияИспользуются"	, Ложь);
	
	МенеджерВременныхТаблиц = ПолучитьМенеджерВременныхТаблицСвойств();

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Наименование", НаименованиеСвойства);
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_Свойства.Ссылка КАК Ссылка,
	|	ДополнительныеРеквизитыИСведения.ТипЗначения КАК ТипЗначения,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	ДополнительныеРеквизитыИСведения.ДополнительныеЗначенияИспользуются КАК ДополнительныеЗначенияИспользуются
	|ИЗ
	|	ВТ_Свойства КАК ВТ_Свойства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО ВТ_Свойства.Ссылка = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	ДополнительныеРеквизитыИСведения.Заголовок = &Наименование";
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Выборка.Следующий();
		
		Результат.Свойство 								= Выборка.Ссылка; 
		Результат.ТипЗначения 							= Выборка.ТипЗначения; 
		Результат.ЭтоДополнительноеСведение 			= Выборка.ЭтоДополнительноеСведение; 
		Результат.ДополнительныеЗначенияИспользуются 	= Выборка.ДополнительныеЗначенияИспользуются; 
	                    
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаСервере
Функция ПолучитьМенеджерВременныхТаблицСвойств()
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; 
	
	МетаданныеОбъекта = ПолучитьМетаданныеОбъекта();
	НаборСвойств = ПолучитьНаборСвойств1С(МетаданныеОбъекта);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НаборСвойств", НаборСвойств);
	Запрос.Текст = "ВЫБРАТЬ
	|	ДополнительныеРеквизиты.Ссылка КАК Набор,
	|	ДополнительныеРеквизиты.Свойство КАК Свойство
	|ПОМЕСТИТЬ ВТ_ВсеНаборы
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДополнительныеСведения.Ссылка,
	|	ДополнительныеСведения.Свойство
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВсеНаборы.Набор КАК НаборСвойств,
	|	ВТ_ВсеНаборы.Свойство КАК Свойство
	|ПОМЕСТИТЬ ВТ_СвойстваНаборы
	|ИЗ
	|	ВТ_ВсеНаборы КАК ВТ_ВсеНаборы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВсеНаборы.Набор,
	|	ВТ_ВсеНаборы.Свойство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка,
	|	ДополнительныеРеквизитыИСведения.Заголовок КАК Заголовок,
	|	ДополнительныеРеквизитыИСведения.Наименование КАК Наименование,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	ВЫРАЗИТЬ(ДополнительныеРеквизитыИСведения.Комментарий КАК СТРОКА(100)) КАК Комментарий
	|ПОМЕСТИТЬ ВТ_СвойстваПром
	|ИЗ
	|	ВТ_СвойстваНаборы КАК ВТ_СвойстваНаборы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО ВТ_СвойстваНаборы.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	ВТ_СвойстваНаборы.НаборСвойств В ИЕРАРХИИ(&НаборСвойств)
	|	И ДополнительныеРеквизитыИСведения.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СвойстваПром.Ссылка КАК Ссылка,
	|	ВТ_СвойстваПром.Заголовок КАК Заголовок,
	|	ВТ_СвойстваПром.Наименование КАК Наименование,
	|	ВТ_СвойстваПром.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	ВТ_СвойстваПром.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ВТ_Свойства
	|ИЗ
	|	ВТ_СвойстваПром КАК ВТ_СвойстваПром
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СвойстваПром.Комментарий,
	|	ВТ_СвойстваПром.Ссылка,
	|	ВТ_СвойстваПром.Заголовок,
	|	ВТ_СвойстваПром.ЭтоДополнительноеСведение,
	|	ВТ_СвойстваПром.Наименование";
	
	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;	
	
КонецФункции

&НаСервере
Функция ПолучитьПараметрыВыбораЗначенийСвойств(Свойство)
	
	МассивПараметров 	= Новый Массив;
	
	Если ЗначениеЗаполнено(Свойство) Тогда
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Владелец", Свойство));	
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(МассивПараметров);
	
КонецФункции

&НаКлиенте
Функция ПолучитьСпкПользовательскихПолейПортала()
	
	Результат = Новый СписокЗначений;
	
	Для каждого ТекСтрока Из ТаблицаСоответствийВыгрузки Цикл
		Если Лев(ТекСтрока.КлючЗапроса,5)= "ufCrm" Тогда
			Результат.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.Описание);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции


&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиИсточникДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
		
	ТекущиеДанные = Элементы.ТаблицаСоответствийЗагрузки.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.ИсточникДанных <> ВыбранноеЗначение Тогда
			ТекущиеДанные.Значение = "";	
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиТабЧастьИсточникДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийЗагрузкиТабЧасть.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.ИсточникДанных <> ВыбранноеЗначение Тогда
			ТекущиеДанные.Значение = "";	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийЗагрузки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные.Идентификатор = "";
		
		Если ТекущиеДанные.ИсточникДанных = "Из пользовательского поля" Тогда

			НайденныеСтроки = ПользовательскиеПоляПортала.НайтиСтроки(Новый Структура("КлючЗапросаСлужебный", ТекущиеДанные.Значение));
			Если НайденныеСтроки.Количество() > 0 Тогда
				ТекущиеДанные.Идентификатор = НайденныеСтроки[0].Идентификатор;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиТабЧастьЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийЗагрузкиТабЧасть.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные.Идентификатор = "";
		
		Если ТекущиеДанные.ИсточникДанных = "Из пользовательского поля" Тогда

			НайденныеСтроки = ПользовательскиеПоляПортала.НайтиСтроки(Новый Структура("КлючЗапросаСлужебный", ТекущиеДанные.Значение));
			Если НайденныеСтроки.Количество() > 0 Тогда
				ТекущиеДанные.Идентификатор = НайденныеСтроки[0].Идентификатор;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ВыгрузкаНаПорталАлгоритм

&НаКлиенте
Процедура УстановкаАлгоритмаПриВыгрузкеНаПортал(Команда)
	
	спкКлючей = Новый СписокЗначений;
	Для каждого ТекСтрока Из ПоляСмартПроцесса Цикл
		спкКлючей.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.КлючЗапроса);
	КонецЦикла;	
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаПриВыгрузкеНаПортал", ЭтаФорма, ПараметрыОО);
	
	ПередаваемыеПараметры = Новый Структура;
	ПередаваемыеПараметры.Вставить("Тип"			, "Процедура");
	ПередаваемыеПараметры.Вставить("Назначение"		, "Выгрузка");
	ПередаваемыеПараметры.Вставить("КлючиПортала"	, спкКлючей);
	
	ПередаваемыеПараметры.Вставить("Алгоритм", АлгоритмПриВыгрузкеДанныхНаПортал);
	
	ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаАлгоритмаПриВыгрузкеНаПортал(Алгоритм, пПараметры) Экспорт
	
	Если НЕ Алгоритм = Неопределено Тогда
		АлгоритмПриВыгрузкеДанныхНаПортал = Алгоритм;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ЗагрузкаВ1САлгоритм

&НаКлиенте
Процедура УстановкаАлгоритмаПриЗагрузкеВ1С(Команда)
	
	спкКлючей = Новый СписокЗначений;
	Для каждого ТекСтрока Из ПоляСмартПроцесса Цикл
		спкКлючей.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.КлючЗапроса);
	КонецЦикла;
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаПриЗагрузкеВ1С", ЭтаФорма, ПараметрыОО);
	
	ПередаваемыеПараметры = Новый Структура;
	ПередаваемыеПараметры.Вставить("Тип"			, "Процедура");
	ПередаваемыеПараметры.Вставить("Назначение"		, "Загрузка");
	ПередаваемыеПараметры.Вставить("КлючиПортала"	, спкКлючей);
	
	ПередаваемыеПараметры.Вставить("Алгоритм", АлгоритмПриЗагрузкеДанныхВ1С);
	
	ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаАлгоритмаПриЗагрузкеВ1С(Алгоритм, пПараметры) Экспорт
	
	Если НЕ Алгоритм = Неопределено Тогда
		АлгоритмПриЗагрузкеДанныхВ1С = Алгоритм;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура УстановкаАлгоритмаПослеЗагрузкиВ1С(Команда)
	
	спкКлючей = Новый СписокЗначений;
	Для каждого ТекСтрока Из ПоляСмартПроцесса Цикл
		спкКлючей.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.КлючЗапроса);
	КонецЦикла;
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаПослеЗагрузкиВ1С", ЭтаФорма, ПараметрыОО);
	
	ПередаваемыеПараметры = Новый Структура;
	ПередаваемыеПараметры.Вставить("Тип"			, "Процедура");
	ПередаваемыеПараметры.Вставить("Назначение"		, "Загрузка");
	ПередаваемыеПараметры.Вставить("КлючиПортала"	, спкКлючей);
	
	ПередаваемыеПараметры.Вставить("Алгоритм", АлгоритмПослеЗагрузкиДанныхВ1С);
	
	ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаАлгоритмаПослеЗагрузкиВ1С(Алгоритм, пПараметры) Экспорт
	
	Если НЕ Алгоритм = Неопределено Тогда
		АлгоритмПослеЗагрузкиДанныхВ1С = Алгоритм;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область Прочее

&НаСервере
Функция ПоместитьВоВременноеХранилищеДанныеНастроек()
	
	ДанныеНастроекСтадий = Неопределено;
	Если ЭтоАдресВременногоХранилища(ВременныйАдресНастроекСтадий) Тогда
		ДанныеНастроекСтадий = ПолучитьИзВременногоХранилища(ВременныйАдресНастроекСтадий);
	КонецЕсли;
	
	ДанныеНастроекНаправлений = Неопределено;
	Если ЭтоАдресВременногоХранилища(ВременныйАдресНастроекНаправлений) Тогда
		ДанныеНастроекНаправлений = ПолучитьИзВременногоХранилища(ВременныйАдресНастроекНаправлений);
	КонецЕсли;
	
	СохраненныеДанные = Новый Структура;
	СохраненныеДанные.Вставить("ДанныеНастроекСтадий"		, ДанныеНастроекСтадий);
	СохраненныеДанные.Вставить("ДанныеНастроекНаправлений"	, ДанныеНастроекНаправлений);
	СохраненныеДанные.Вставить("ТипСущности1С"				, ТипСущности1С);
	
	Если Сущность1С.Количество() > 0 Тогда
		СохраненныеДанные.Вставить("Сущность1С", Новый Структура("Значение, Представление", Сущность1С[0].Значение, Сущность1С[0].Представление));
	Иначе
		СохраненныеДанные.Вставить("Сущность1С", Неопределено);
	КонецЕсли;
	
	Если ТабличнаяЧасть1С.Количество() > 0 Тогда
		СохраненныеДанные.Вставить("ТабличнаяЧасть1С", Новый Структура("Значение, Представление", ТабличнаяЧасть1С[0].Значение, ТабличнаяЧасть1С[0].Представление));
	Иначе
		СохраненныеДанные.Вставить("ТабличнаяЧасть1С", Неопределено);
	КонецЕсли;
	
	СохраненныеДанные.Вставить("ВыгружатьНаПортал"					, ВыгружатьНаПортал);
	СохраненныеДанные.Вставить("ОбновлятьНаПортале"					, ОбновлятьНаПортале);
	СохраненныеДанные.Вставить("НастройкиКомпоновкиДанных"			, КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки());
	СохраненныеДанные.Вставить("ТаблицаСоответствийВыгрузки"		, ТаблицаСоответствийВыгрузки.Выгрузить());
	СохраненныеДанные.Вставить("ТаблицаСоответствийВыгрузкиТабЧасть", ТаблицаСоответствийВыгрузкиТабЧасть.Выгрузить());
	СохраненныеДанные.Вставить("АлгоритмПриВыгрузкеДанныхНаПортал"	, АлгоритмПриВыгрузкеДанныхНаПортал);
	
	СохраненныеДанные.Вставить("ЗагружатьВ1С"						, ЗагружатьВ1С);
	СохраненныеДанные.Вставить("ОбновлятьВ1С"						, ОбновлятьВ1С);
	СохраненныеДанные.Вставить("ПроводитьВ1С"						, ПроводитьВ1С);
	СохраненныеДанные.Вставить("ТаблицаОтборовПоЗагрузкеДанныхВ1С"	, ТаблицаОтборовПоЗагрузкеДанныхВ1С.Выгрузить());
	СохраненныеДанные.Вставить("ТаблицаСоответствийЗагрузки"		, ТаблицаСоответствийЗагрузки.Выгрузить());
	СохраненныеДанные.Вставить("ТаблицаСоответствийЗагрузкиТабЧасть", ТаблицаСоответствийЗагрузкиТабЧасть.Выгрузить());
	СохраненныеДанные.Вставить("АлгоритмПриЗагрузкеДанныхВ1С"		, АлгоритмПриЗагрузкеДанныхВ1С);
	СохраненныеДанные.Вставить("АлгоритмПослеЗагрузкиДанныхВ1С"		, АлгоритмПослеЗагрузкиДанныхВ1С);
	
	Возврат ПоместитьВоВременноеХранилище(СохраненныеДанные, Новый УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьНастройкиПоСохраненнымДанным(ВременныйАдрес, ДанныеФайла)
	
	Если ЭтоАдресВременногоХранилища(ВременныйАдрес) Тогда
		СохраненныеДанные = ПолучитьИзВременногоХранилища(ВременныйАдресНастроек);
	Иначе
		СохраненныеДанные = ДанныеФайла;
	КонецЕсли;
	
	Если СохраненныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	
	ТипСущности1С = СохраненныеДанные.ТипСущности1С;
	
	Если СохраненныеДанные.Сущность1С <> Неопределено Тогда
		Сущность1С.Добавить(СохраненныеДанные.Сущность1С.Значение, СохраненныеДанные.Сущность1С.Представление);
	КонецЕсли;
	
	Если СохраненныеДанные.ТабличнаяЧасть1С <> Неопределено Тогда
		ТабличнаяЧасть1С.Добавить(СохраненныеДанные.ТабличнаяЧасть1С.Значение, СохраненныеДанные.ТабличнаяЧасть1С.Представление);
	КонецЕсли;
	
	Если СохраненныеДанные.ДанныеНастроекСтадий <> Неопределено Тогда
		ВременныйАдресНастроекСтадий = ПоместитьВоВременноеХранилище(СохраненныеДанные.ДанныеНастроекСтадий, Новый УникальныйИдентификатор)
	КонецЕсли;
	
	Если СохраненныеДанные.ДанныеНастроекНаправлений <> Неопределено Тогда
		ВременныйАдресНастроекНаправлений = ПоместитьВоВременноеХранилище(СохраненныеДанные.ДанныеНастроекНаправлений, Новый УникальныйИдентификатор)
	КонецЕсли;
	
	Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1СКлючЗапроса.СписокВыбора.Очистить();
	Для каждого ТекСтрока Из ПоляСмартПроцесса Цикл
		Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1СКлючЗапроса.СписокВыбора.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.Описание);
	КонецЦикла;
	
	ЗаполнитьРеквизитыОбъекта1СПоУмолчанию();
	
	ВыгружатьНаПортал 	= СохраненныеДанные.ВыгружатьНаПортал;
	ОбновлятьНаПортале 	= СохраненныеДанные.ОбновлятьНаПортале;
	ЗагружатьВ1С		= СохраненныеДанные.ЗагружатьВ1С;
	ОбновлятьВ1С 		= СохраненныеДанные.ОбновлятьВ1С;
	
	СохраненныеДанные.Свойство("ПроводитьВ1С", ПроводитьВ1С);	
	
	СохраненныеДанные.Свойство("АлгоритмПриВыгрузкеДанныхНаПортал"	, АлгоритмПриВыгрузкеДанныхНаПортал);
	СохраненныеДанные.Свойство("АлгоритмПриЗагрузкеДанныхВ1С"		, АлгоритмПриЗагрузкеДанныхВ1С);
	СохраненныеДанные.Свойство("АлгоритмПослеЗагрузкиДанныхВ1С"		, АлгоритмПослеЗагрузкиДанныхВ1С);
	
	Если Сущность1С.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураСхемКомпоновки = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьСтруктуруСхемКомпоновки();
	Схема = СтруктураСхемКомпоновки.МакетСмартПроцессов;
	ИсточникДанных = ТипСущности1С + "." + Сущность1С[0].Значение; 
	Схема.НаборыДанных.ОсновнойНаборДанных.Запрос = СтрЗаменить(Схема.НаборыДанных.ОсновнойНаборДанных.Запрос, "Справочник.Валюты", ИсточникДанных);
	АдресСхемы = ПоместитьВоВременноеХранилище(Схема, УникальныйИдентификатор);
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы)); 
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СохраненныеДанные.НастройкиКомпоновкиДанных);
	КомпоновщикНастроекКомпоновкиДанных.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	
	
	Для каждого ТекСтрока Из ТаблицаСоответствийВыгрузки Цикл
		НайденнаяСтрока = СохраненныеДанные.ТаблицаСоответствийВыгрузки.Найти(ТекСтрока.КлючЗапроса, "КлючЗапроса");
		Если НайденнаяСтрока <> Неопределено Тогда
			лОписание 		= ТекСтрока.Описание;
			лИдентификатор 	= ТекСтрока.Идентификатор;
			ЗаполнитьЗначенияСвойств(ТекСтрока, НайденнаяСтрока); 
			ТекСтрока.Описание 		= лОписание;
			ТекСтрока.Идентификатор = лИдентификатор;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекСтрока Из ТаблицаСоответствийВыгрузкиТабЧасть Цикл
		НайденнаяСтрока = СохраненныеДанные.ТаблицаСоответствийВыгрузкиТабЧасть.Найти(ТекСтрока.КлючЗапроса, "КлючЗапроса");
		Если НайденнаяСтрока <> Неопределено Тогда
			лОписание = ТекСтрока.Описание;
			ЗаполнитьЗначенияСвойств(ТекСтрока, НайденнаяСтрока); 
			ТекСтрока.Описание = лОписание;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекСтрока Из ТаблицаСоответствийЗагрузки Цикл
		НайденныеСтроки = СохраненныеДанные.ТаблицаСоответствийЗагрузки.НайтиСтроки(Новый Структура("Реквизит1С, ТипРеквизита", ТекСтрока.Реквизит1С, ТекСтрока.ТипРеквизита));
		Если НайденныеСтроки.Количество() > 0 Тогда
			лНаименование = ТекСтрока.Наименование;
			ЗаполнитьЗначенияСвойств(ТекСтрока, НайденныеСтроки[0]); 
			ТекСтрока.Наименование = лНаименование;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекСтрока Из ТаблицаСоответствийЗагрузкиТабЧасть Цикл
		НайденнаяСтрока = СохраненныеДанные.ТаблицаСоответствийЗагрузкиТабЧасть.Найти(ТекСтрока.Реквизит1С, "Реквизит1С");
		Если НайденнаяСтрока <> Неопределено Тогда
			лНаименование = ТекСтрока.Наименование;
			ЗаполнитьЗначенияСвойств(ТекСтрока, НайденнаяСтрока); 
			ТекСтрока.Наименование = лНаименование;
		КонецЕсли;
	КонецЦикла;
	
	тзнСоответствийВыгрузки = ТаблицаСоответствийВыгрузки.Выгрузить();
	Для каждого ТекСтрока Из СохраненныеДанные.ТаблицаОтборовПоЗагрузкеДанныхВ1С Цикл
		//Если тзнСоответствийВыгрузки.Найти(ТекСтрока.КлючЗапроса, "КлючЗапроса") <> Неопределено Тогда
			НоваяСтрока = ТаблицаОтборовПоЗагрузкеДанныхВ1С.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекСтрока); 
		//КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Процедура НастройкиСмартПроцессаПоУмолчаниюСервер(ВТомЧислеСущность1С = Истина)
	
	Если ВТомЧислеСущность1С Тогда
		ТипСущности1С 		= "Справочник";
		Сущность1С.Очистить();
		ТабличнаяЧасть1С.Очистить();
	КонецЕсли;
	
	НастройкиСтатусов		= Неопределено;
	
	ВыгружатьНаПортал 		= Ложь;
	ОбновлятьНаПортале 		= Ложь;
	
	ТаблицаСоответствийЗагрузки.Очистить();
	ОтборыПриВыгрузкеНаПортал = Неопределено;
	
	АлгоритмПриВыгрузкеДанныхНаПортал 	= "";
	
	РежимЗаписи							= "Только записывать";
	ЗагружатьВ1С 						= Ложь;
	ОбновлятьВ1С 						= Ложь;
	ОтборыПриЗагрузкеДанныхВ1С 			= Неопределено;
	АлгоритмПриЗагрузкеДанныхВ1С 		= "";        
	АлгоритмПослеЗагрузкиДанныхВ1С 		= "";
	ТаблицаСоответствийЗагрузкиТабЧасть.Очистить(); 
	ВременныйАдресНастроекСтадий		= "";
	ВременныйАдресНастроекНаправлений	= "";
	
	Если Сущность1С.Количество() > 0 Тогда
		
		СтруктураСхемКомпоновки = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьСтруктуруСхемКомпоновки();
		Схема = СтруктураСхемКомпоновки.МакетСмартПроцессов;
		ИсточникДанных = ТипСущности1С + "." + Сущность1С[0].Значение; 
		Схема.НаборыДанных.ОсновнойНаборДанных.Запрос = СтрЗаменить(Схема.НаборыДанных.ОсновнойНаборДанных.Запрос, "Справочник.Валюты", ИсточникДанных);
		АдресСхемы = ПоместитьВоВременноеХранилище(Схема, УникальныйИдентификатор);
		
		КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы)); 
		КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(Схема.НастройкиПоУмолчанию);
		КомпоновщикНастроекКомпоновкиДанных.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
		
		ЗаполнитьРеквизитыОбъекта1СПоУмолчанию();
		
		Для каждого ТекСтрока Из ТаблицаСоответствийВыгрузкиТабЧасть Цикл
			
			Если ТекСтрока.КлючЗапроса = "discountRate" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Процент скидки";
			ИначеЕсли ТекСтрока.КлючЗапроса = "measureName" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Наименование единицы измерения";
			ИначеЕсли ТекСтрока.КлючЗапроса = "customized" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Изменен";
			ИначеЕсли ТекСтрока.КлючЗапроса = "measureCode" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Код единицы измерения";
			ИначеЕсли ТекСтрока.КлючЗапроса = "quantity" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Количество";
			ИначеЕсли ТекСтрока.КлючЗапроса = "productName" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Описание товарной позиции";
			ИначеЕсли ТекСтрока.КлючЗапроса = "taxRate" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Ставка НДС";
			ИначеЕсли ТекСтрока.КлючЗапроса = "taxIncluded" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "НДС включён в цену";
			ИначеЕсли ТекСтрока.КлючЗапроса = "discountSum" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Сумма скидки";
			ИначеЕсли ТекСтрока.КлючЗапроса = "discountTypeId" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Тип скидки";
			ИначеЕсли ТекСтрока.КлючЗапроса = "productId" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Идентификатор товара";
			ИначеЕсли ТекСтрока.КлючЗапроса = "price" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Цена";
			КонецЕсли;
			
		КонецЦикла;
		
		Для каждого ТекСтрока Из ТаблицаСоответствийЗагрузкиТабЧасть Цикл
			
			Если ТекСтрока.Реквизит1С = "Номенклатура" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Номенклатура";
			ИначеЕсли ТекСтрока.Реквизит1С = "Характеристика" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Характеристика";
			ИначеЕсли ТекСтрока.Реквизит1С = "Содержание" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Содержание";
			ИначеЕсли ТекСтрока.Реквизит1С = "ДатаОтгрузки" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Дата отгрузки";
			ИначеЕсли ТекСтрока.Реквизит1С = "ЕдиницаИзмерения" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Единица измерения";
			ИначеЕсли ТекСтрока.Реквизит1С = "Количество" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Количество";
			ИначеЕсли ТекСтрока.Реквизит1С = "Цена" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Цена";
			ИначеЕсли ТекСтрока.Реквизит1С = "СтавкаНДС" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Ставка НДС";
			ИначеЕсли ТекСтрока.Реквизит1С = "Сумма" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Сумма";
			ИначеЕсли ТекСтрока.Реквизит1С = "Всего" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Всего";
			ИначеЕсли ТекСтрока.Реквизит1С = "СуммаНДС" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Сумма НДС";
			ИначеЕсли ТекСтрока.Реквизит1С = "ПроцентРучнойСкидки" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Процент скидки/наценки";
			ИначеЕсли ТекСтрока.Реквизит1С = "СуммаРучнойСкидки" Тогда
				ТекСтрока.ИсточникДанных 	= "Предопределенный алгоритм";
				ТекСтрока.Значение			= "Сумма скидки/наценки";
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1СКлючЗапроса.СписокВыбора.Очистить();
	Для каждого ТекСтрока Из ПоляСмартПроцесса Цикл
		Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1СКлючЗапроса.СписокВыбора.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.Описание);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыОбъекта1СПоУмолчанию()
	
	ТаблицаСоответствийЗагрузки.Очистить();

	Если Сущность1С.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеОбъекта = ПолучитьМетаданныеОбъекта();
	Если МетаданныеОбъекта = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекЭлемент Из МетаданныеОбъекта.СтандартныеРеквизиты Цикл
		
		Если ТекЭлемент.Имя = "Родитель" ИЛИ ТекЭлемент.Имя = "Наименование" ИЛИ ТекЭлемент.Имя = "ПометкаУдаления" 
			ИЛИ ТекЭлемент.Имя = "Код" ИЛИ ТекЭлемент.Имя = "Дата" ИЛИ ТекЭлемент.Имя = "Номер" ИЛИ ТекЭлемент.Имя = "Владелец" Тогда
			
			НоваяЗапись = ТаблицаСоответствийЗагрузки.Добавить();
			
			НоваяЗапись.Приоритет 		= 10; 
			НоваяЗапись.ТипРеквизита    = "СтандартныйРеквизит";
			НоваяЗапись.Реквизит1С 		= ТекЭлемент.Имя; 
			НоваяЗапись.Наименование 	= ?(ЗначениеЗаполнено(ТекЭлемент.Синоним), ТекЭлемент.Синоним, ТекЭлемент.Имя); 
			
			Если ТекЭлемент.Имя = "ПометкаУдаления" Тогда
				НоваяЗапись.Наименование = "Пометка удаления"; 
			ИначеЕсли ТекЭлемент.Имя = "Родитель" Тогда
				НоваяЗапись.Наименование = "Группа"; 
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ТекЭлемент Из МетаданныеОбъекта.Реквизиты Цикл
		НоваяЗапись = ТаблицаСоответствийЗагрузки.Добавить();
		
		НоваяЗапись.Приоритет 		= 40; 
		НоваяЗапись.ТипРеквизита    = "Реквизиты";
		
		НоваяЗапись.Реквизит1С 		= ТекЭлемент.Имя; 
		НоваяЗапись.Наименование 	= ?(ЗначениеЗаполнено(ТекЭлемент.Синоним), ТекЭлемент.Синоним, ТекЭлемент.Имя); 
	КонецЦикла;
	
	Если ТабличнаяЧасть1С.Количество() > 0 Тогда
		
		НайденнаяТабЧасть = МетаданныеОбъекта.ТабличныеЧасти.Найти(ТабличнаяЧасть1С[0].Значение);

		Если НайденнаяТабЧасть <> Неопределено Тогда
			Для каждого ТекЭлемент Из НайденнаяТабЧасть.Реквизиты Цикл
				НоваяЗапись = ТаблицаСоответствийЗагрузкиТабЧасть.Добавить();
				
				НоваяЗапись.Реквизит1С 		= ТекЭлемент.Имя; 
				НоваяЗапись.Наименование 	= ?(ЗначениеЗаполнено(ТекЭлемент.Синоним), ТекЭлемент.Синоним, ТекЭлемент.Имя); 
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДополнительныеРеквизитыИспользуются() Тогда
		
		НайденныйНабор = ПолучитьНаборСвойств1С(МетаданныеОбъекта);
		Если ЗначениеЗаполнено(НайденныйНабор) Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("НаборСвойств", НайденныйНабор);
			Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство КАК Свойство,
			|	ДополнительныеРеквизитыИСведения.Заголовок КАК Заголовок
			|ИЗ
			|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
			|		ПО НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
			|ГДЕ
			|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка В ИЕРАРХИИ(&НаборСвойств)";
			ВыполненныйЗапрос = Запрос.Выполнить();
			Если НЕ ВыполненныйЗапрос.Пустой() Тогда
				Выборка = ВыполненныйЗапрос.Выбрать();
				Пока Выборка.Следующий() Цикл
					
					НоваяЗапись = ТаблицаСоответствийЗагрузки.Добавить();
					
					НоваяЗапись.Приоритет 		= 20; 
					НоваяЗапись.ТипРеквизита    = "ДополнительныйРеквизит";
					НоваяЗапись.СсылкаНаСвойство= Выборка.Свойство;
					
					НоваяЗапись.Реквизит1С 		= Выборка.Заголовок; 
					НоваяЗапись.Наименование 	= Выборка.Заголовок; 
					
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДополнительныеСведенияИспользуются() Тогда
		
		НайденныйНабор = ПолучитьНаборСвойств1С(МетаданныеОбъекта);
		Если ЗначениеЗаполнено(НайденныйНабор) Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("НаборСвойств", НайденныйНабор);
			Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Свойство КАК Свойство,
			|	ДополнительныеРеквизитыИСведения.Заголовок КАК Заголовок
			|ИЗ
			|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
			|		ПО НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
			|ГДЕ
			|	НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Ссылка В ИЕРАРХИИ(&НаборСвойств)";
			ВыполненныйЗапрос = Запрос.Выполнить();
			Если НЕ ВыполненныйЗапрос.Пустой() Тогда
				Выборка = ВыполненныйЗапрос.Выбрать();
				Пока Выборка.Следующий() Цикл
					
					НоваяЗапись = ТаблицаСоответствийЗагрузки.Добавить();
					
					НоваяЗапись.Приоритет 		= 30; 
					НоваяЗапись.ТипРеквизита    = "ДополнительноеСведение";
					НоваяЗапись.СсылкаНаСвойство= Выборка.Свойство;
					
					НоваяЗапись.Реквизит1С 		= Выборка.Заголовок; 
					НоваяЗапись.Наименование 	= Выборка.Заголовок; 
					
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаСоответствийЗагрузки.Сортировать("Приоритет");
	
КонецПроцедуры



&НаКлиенте
Процедура ОбновлениеОтображенияКомандЭлементовФормы()
	
	ТекущаяСтраница = Элементы.СтраницыЭтапыНастройки.ТекущаяСтраница;
	
	Элементы.ФормаКомандаНазад.Доступность 	= НЕ ТекущаяСтраница = Элементы.СтраницаВыборИсточника1С; 
	
	Если (ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1СОбщее И НЕ ЗагружатьВ1С)
		ИЛИ ТекущаяСтраница = Элементы.СтраницаЗагрузкаДанныхВ1САлгоритм Тогда
		Элементы.ФормаКомандаЗавершить.Видимость 	= Истина;	
		Элементы.ФормаКомандаДалее.Видимость 		= Ложь;	
	Иначе
		Элементы.ФормаКомандаЗавершить.Видимость 	= Ложь;	
		Элементы.ФормаКомандаДалее.Видимость 		= Истина;	
	КонецЕсли;
	
	Элементы.НастроитьСтатусы.Доступность  		= ИспользуютсяСтадии ИЛИ ИспользуютсяСтадии;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеОтображенияЭлементовФормы()
	
	Элементы.ГруппаТабличнаяЧасть1С.Доступность = УказываютсяТовары;
	
	Элементы.ГруппаВыгрузкиОтбор.Доступность 	= ВыгружатьНаПортал;
	Элементы.ОбновлятьНаПортале.Доступность 	= ВыгружатьНаПортал;
	
	Элементы.ГруппаЗагрузкиОтбор.Доступность 	= ЗагружатьВ1С;
	Элементы.ОбновлятьВ1С.Доступность 			= ЗагружатьВ1С;
	Элементы.ПроводитьВ1С.Доступность 			= ЗагружатьВ1С И ТипСущности1С = "Документ";
	
	Элементы.ГруппаТабЧастьВыгрузки.Видимость	= УказываютсяТовары И ТабличнаяЧасть1С.Количество()>0;
	Элементы.ГруппаТабЧастьЗагрузки.Видимость	= УказываютсяТовары И ТабличнаяЧасть1С.Количество()>0;
	
	Элементы.НастроитьКатегории.Доступность 	= ИспользуютсяНаправления;
	Элементы.НастроитьСтатусы.Доступность 		= ИспользуютсяСтадии;
	
	
КонецПроцедуры



&НаКлиенте
Процедура ПолучитьДанныеСмартПроцесса(СтруктураНастроек)

Состояние("Получение данных с портала", 0, "Загрузка полей");	
	
	ПоляСмартПроцесса.Очистить();
	
	ПараметрыЗапроса 	= "&entityTypeId=" + ТипВладельцаСмартПроцесса;
	Метод 				= "/rest/crm.item.fields";	
	
	Результат = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, Метод, ПараметрыЗапроса); 
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	result = Результат.Получить("result");
	Если result <> Неопределено Тогда
		
		fields = result.Получить("fields");
		Если fields <> Неопределено Тогда
				
			Для каждого ТекЭлемент Из fields Цикл
				
				Если ТекЭлемент.Значение.Получить("isMultiple") = Истина Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ПоляСмартПроцесса.Добавить();
				
				НоваяСтрока.КлючЗапроса 	= ТекЭлемент.Ключ;
				НоваяСтрока.Описание 		= ТекЭлемент.Значение.Получить("title");
				НоваяСтрока.ТипЗначения		= ТекЭлемент.Значение.Получить("type");
				НоваяСтрока.Обязательный	= ТекЭлемент.Значение.Получить("isRequired") = "Y" ИЛИ ТекЭлемент.Значение.Получить("isRequired") = Истина;
				
				Если ТекЭлемент.Ключ = "id" ИЛИ ТекЭлемент.Ключ = "xmlId" ИЛИ ТекЭлемент.Ключ = "stageId" ИЛИ ТекЭлемент.Ключ = "categoryId" Тогда  
					НоваяСтрока.ТолькоДляЧтения = Истина;	
				Иначе
					НоваяСтрока.ТолькоДляЧтения	= ТекЭлемент.Значение.Получить("isReadOnly") = "Y" ИЛИ ТекЭлемент.Значение.Получить("isReadOnly") = Истина;
				КонецЕсли;
				
				Если Лев(ТекЭлемент.Ключ,5)= "ufCrm" Тогда
					НоваяСтрока.Приоритет		= 10;
					НоваяСтрока.ТипРеквизита	= "ПользовательскоеПоле";
				Иначе
					НоваяСтрока.Приоритет		= 30;
					НоваяСтрока.ТипРеквизита	= "Ключ";
				КонецЕсли;
					
			КонецЦикла;
			                
		КонецЕсли;                       
	КонецЕсли;                             
	
	ПоляСмартПроцесса.Сортировать("Приоритет, Описание");
	
Состояние("Получение данных с портала", 20, "Загрузка полей ТЧ");	
	
	ПоляТЧСмартПроцесса.Очистить();
	
	ПараметрыЗапроса 	= "&entityTypeId=" + ТипВладельцаСмартПроцесса;
	Метод 				= "/rest/crm.item.productrow.fields";	
	
	Результат = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, Метод, ПараметрыЗапроса); 
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	result = Результат.Получить("result");
	Если result <> Неопределено Тогда
		
		fields = result.Получить("fields");
		Если fields <> Неопределено Тогда
			Для каждого ТекЭлемент Из fields Цикл
				
				Если ТекЭлемент.Ключ = "ownerId" ИЛИ ТекЭлемент.Ключ = "ownerType" Тогда  //служебные
					Продолжить;	
				КонецЕсли;
				
				НоваяСтрока = ПоляТЧСмартПроцесса.Добавить();
				
				НоваяСтрока.КлючЗапроса 	= ТекЭлемент.Ключ;
				НоваяСтрока.Описание 		= ТекЭлемент.Значение.Получить("title");
				НоваяСтрока.ТипЗначения		= ТекЭлемент.Значение.Получить("type");
				НоваяСтрока.ТолькоДляЧтения	= ТекЭлемент.Значение.Получить("isReadOnly") = "Y" ИЛИ ТекЭлемент.Значение.Получить("isReadOnly") = Истина;
				НоваяСтрока.Обязательный	= ТекЭлемент.Значение.Получить("isRequired") = "Y" ИЛИ ТекЭлемент.Значение.Получить("isRequired") = Истина;
				
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ПоляТЧСмартПроцесса.Сортировать("Описание");
	
Состояние("Получение данных с портала", 30, "Загрузка данных пользовательских полей");	
	
	ТелоЗапроса = "&moduleId=crm&filter[entityId]=CRM_"+ИдСмартПроцесса;
	ДанныеПользовательскихПолей = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/userfieldconfig.list", "id", ТелоЗапроса, "fields");	
	
	Для каждого ТекЭлемент Из ДанныеПользовательскихПолей Цикл
		
		НоваяЗапись = ПользовательскиеПоляПортала.Добавить();
		
		НоваяЗапись.КлючЗапроса				= ТекЭлемент.Получить("fieldName");
		НоваяЗапись.ТипПоля					= ТекЭлемент.Получить("userTypeId");
		НоваяЗапись.Идентификатор 			= Формат(ТекЭлемент.Получить("id"), "ЧГ=0");
		НоваяЗапись.КлючЗапросаСлужебный	= "ufCrm" + Прав(НоваяЗапись.КлючЗапроса, СтрДлина(НоваяЗапись.КлючЗапроса)-7);
		
	КонецЦикла;
	
Состояние("Получение данных с портала", 50, "Загрузка значений пользовательских полей");	
	
	//ЗапросыПоЗначениямПолей = "";
	Для каждого ТекСтрока Из ПользовательскиеПоляПортала Цикл
		                                                                                                                                                                                                                
		Если ТекСтрока.ТипПоля = "enumeration" Тогда
			
			//Не работает. может починят
			//ЗапросыПоЗначениямПолей = ЗапросыПоЗначениямПолей + "&cmd["+ТекСтрока.Идентификатор+"]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер("userfieldconfig.get?moduleId=crm&id=" + ТекСтрока.Идентификатор);
			//костыль ниже
			
			ПараметрыЗапроса = "&moduleId=crm&id="+ТекСтрока.Идентификатор;
			ДанныеПортала = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/userfieldconfig.get", ПараметрыЗапроса); 
			Если ДанныеПортала = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			result = ДанныеПортала.Получить("result");
			Если result = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			field = result.Получить("field");
			Если field = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			enum = field.Получить("enum");
			Если enum = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Для каждого ТекЭлемент Из enum Цикл
				
				НоваяЗапись = ЗначенияПользовательскихПолейПортала.Добавить();
				
				НоваяЗапись.Идентификатор			= ТекСтрока.Идентификатор;
				НоваяЗапись.ИдентификаторЗначения 	= Формат(ТекЭлемент.Получить("id"), "ЧГ=0");
				НоваяЗапись.ПредставлениеЗначения	= ТекЭлемент.Получить("value");
						
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	//Тест = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПорталЧерезBatch(СтруктураНастроек, ЗапросыПоЗначениямПолей);
	
Состояние("Получение данных с портала", 100, "");	

КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьЭлементПриСменеТипаКлючаСмартПроцесса(КлючЗапроса, ТипЗначения, лЭлемент)
	
	лЭлемент.СписокВыбора.Очистить();
	
	Если Лев(КлючЗапроса, 5) = "ufCrm" Тогда
		Если ТипЗначения = "enumeration" Тогда
			
			лЭлемент.ТолькоПросмотр				= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			//лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ОграничениеТипа 		= ПолучитьОписаниеТипКлючаСмартПроцесса(ТипЗначения);
			
			НайденныеСтроки = ПользовательскиеПоляПортала.НайтиСтроки(Новый Структура("КлючЗапросаСлужебный", КлючЗапроса));	
			Если НайденныеСтроки.Количество() > 0 Тогда
				Для каждого ТекСтрока Из ЗначенияПользовательскихПолейПортала Цикл
					Если ТекСтрока.Идентификатор = НайденныеСтроки[0].Идентификатор Тогда
						лЭлемент.СписокВыбора.Добавить(ТекСтрока.ИдентификаторЗначения, ТекСтрока.ПредставлениеЗначения);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		ИначеЕсли ТипЗначения = "crm" ИЛИ ТипЗначения = "crm_status" Тогда
			
			лЭлемент.КнопкаВыбора 			= Истина;
			
			лЭлемент.ВыбиратьТип 		  	= Истина;
			лЭлемент.РедактированиеТекста 	= Истина;
			лЭлемент.ТолькоПросмотр 		= Ложь;
			лЭлемент.ОграничениеТипа 		= ПолучитьОписаниеТипКлючаСмартПроцесса(ТипЗначения); 
			
		ИначеЕсли ТипЗначения = "date" ИЛИ ТипЗначения = "datetime" Тогда
			
			лЭлемент.КнопкаВыбора 			= Истина;
			лЭлемент.РедактированиеТекста 	= Истина;
			лЭлемент.ТолькоПросмотр 		= Ложь;
			лЭлемент.ОграничениеТипа 		= ПолучитьОписаниеТипКлючаСмартПроцесса(ТипЗначения);
			
		Иначе
			
			лЭлемент.РедактированиеТекста 	= Истина;
			лЭлемент.ТолькоПросмотр 		= Ложь;
			лЭлемент.ОграничениеТипа 		= ПолучитьОписаниеТипКлючаСмартПроцесса(ТипЗначения);
			
		КонецЕсли;
		
	Иначе
		
		лЭлемент.КнопкаВыбора 			= Истина;
		лЭлемент.ВыбиратьТип 		  	= Истина;
		лЭлемент.РедактированиеТекста 	= Истина;
		лЭлемент.ТолькоПросмотр 		= Ложь;
		лЭлемент.ОграничениеТипа 		= ПолучитьОписаниеТипКлючаСмартПроцесса(ТипЗначения);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьОписаниеТипКлючаСмартПроцесса(ТипЗначения)
	
	лОграничениеТипа = Новый ОписаниеТипов();
	
	Если ТипЗначения = "string" Тогда
		КС = Новый КвалификаторыСтроки(100);
		Массив = Новый Массив;
		Массив.Добавить(Тип("Строка"));
		лОграничениеТипа = Новый ОписаниеТипов(Массив, , КС);				
	ИначеЕсли ТипЗначения = "double" Тогда
		КЧ = Новый КвалификаторыЧисла(10,3);
		Массив = Новый Массив;
		Массив.Добавить(Тип("Число"));
		лОграничениеТипа = Новый ОписаниеТипов(Массив, , КЧ);	
	ИначеЕсли ТипЗначения = "text" Тогда
		лОграничениеТипа = Новый ОписаниеТипов("Строка");
	ИначеЕсли ТипЗначения = "integer" Тогда
		КЧ = Новый КвалификаторыЧисла(12,0);
		Массив = Новый Массив;
		Массив.Добавить(Тип("Число"));
		лОграничениеТипа = Новый ОписаниеТипов(Массив, , КЧ);				
	ИначеЕсли ТипЗначения = "boolean" Тогда
		лОграничениеТипа = Новый ОписаниеТипов("Булево");
	ИначеЕсли ТипЗначения = "datetime" Тогда
		КД = Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя);
		Массив = Новый Массив;
		Массив.Добавить(Тип("Дата"));
		лОграничениеТипа = Новый ОписаниеТипов(Массив,, КД);	
	ИначеЕсли ТипЗначения = "date" Тогда
		КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
		Массив = Новый Массив;
		Массив.Добавить(Тип("Дата"));
		лОграничениеТипа = Новый ОписаниеТипов(Массив,, КД);	
	ИначеЕсли ТипЗначения = "time" Тогда
		КД = Новый КвалификаторыДаты(ЧастиДаты.Время);
		Массив = Новый Массив;
		Массив.Добавить(Тип("Дата"));
		лОграничениеТипа = Новый ОписаниеТипов(Массив,, КД);
	ИначеЕсли ТипЗначения = "crm" ИЛИ ТипЗначения = "crm_status" ИЛИ ТипЗначения = "crm_entity" Тогда
		лОграничениеТипа = Новый ОписаниеТипов();
	ИначеЕсли ТипЗначения = "user" ИЛИ ТипЗначения = "employee" Тогда
		лОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
	ИначеЕсли ТипЗначения = "crm_currency" ИЛИ  ТипЗначения = "money" Тогда
		лОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Валюты");
	ИначеЕсли ТипЗначения = "crm_contact" Тогда
		Массив = Новый Массив;
		Массив.Добавить(Тип("СправочникСсылка.Партнеры"));
		Массив.Добавить(Тип("СправочникСсылка.КонтактныеЛицаПартнеров"));
		лОграничениеТипа = Новый ОписаниеТипов(Массив);				
	ИначеЕсли ТипЗначения = "crm_company" Тогда
		Массив = Новый Массив;
		Массив.Добавить(Тип("СправочникСсылка.Партнеры"));
		Массив.Добавить(Тип("СправочникСсылка.Организации"));
		лОграничениеТипа = Новый ОписаниеТипов(Массив);	
	ИначеЕсли ТипЗначения = "enumeration" Тогда
		КС = Новый КвалификаторыСтроки(100);
		Массив = Новый Массив;
		Массив.Добавить(Тип("Строка"));
		лОграничениеТипа = Новый ОписаниеТипов(Массив, , КС);				
	КонецЕсли;
	
	Возврат лОграничениеТипа;	
	
КонецФункции

&НаСервере
Функция ПолучитьНаборСвойств1С(МетаданныеОбъекта)

	НайденныйНабор = Справочники.НаборыДополнительныхРеквизитовИСведений.НайтиПоНаименованию(МетаданныеОбъекта.Синоним, Истина);
	Если НайденныйНабор.Пустая() Тогда
		НайденныйНабор = Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств((ТипСущности1С + "_" + МетаданныеОбъекта.Имя));		
	КонецЕсли;
	
	Возврат НайденныйНабор;	
	
КонецФункции

&НаСервере
Функция ПолучитьМетаданныеОбъекта()
	
	МетаданныеОбъекта = Неопределено;
	Если ТипСущности1С = "Справочник" Тогда
		МетаданныеОбъекта = Метаданные.Справочники[Сущность1С[0].Значение];
	ИначеЕсли ТипСущности1С = "Документ" Тогда
		МетаданныеОбъекта = Метаданные.Документы[Сущность1С[0].Значение];
	КонецЕсли;
	
	Возврат МетаданныеОбъекта;
	
КонецФункции

#КонецОбласти
