
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаПодключения 	= Параметры.НастройкаПодключения;
	ВидСинхронизации 		= Параметры.ВидСинхронизации;
	ПолнаяСинхронизация 	= Параметры.ПолнаяСинхронизация;
	
	ВыполнятьЗагрузку 		= Истина;
	ВыполнятьВыгрузку 		= Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//Если НЕ Б24_К_СлужебныйВызовСервера.МодульСмартПроцессовДоступен(НастройкаПодключения) Тогда
	//	Б24_К_СлужебныйКлиент.ОповеститьОбНедоступностиФункционалаСинхронизацииПоТарифномуПлану();
	//	Закрыть();	
	//КонецЕсли;
	
	ПодключитьОбработчикОжидания("ЗапуститьВыполнениеОбмена", 0.1, Истина);
	
	ПодключитьОбработчикОжидания("ОбновлениеИнформацииОСинхронизации", 3);

КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьВыполнениеОбмена()
	
	ДобавитьЕдиничнуюЗаписьВПротокол("Начало синхронизации");
	
	ПараметрыСинхронизации = Новый Структура;
	ПараметрыСинхронизации.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыСинхронизации.Вставить("ВидСинхронизации"		, ВидСинхронизации);
	ПараметрыСинхронизации.Вставить("ПолнаяСинхронизация"	, ПолнаяСинхронизация);
	ПараметрыСинхронизации.Вставить("ВыполнятьЗагрузку"		, ВыполнятьЗагрузку);
	ПараметрыСинхронизации.Вставить("ВыполнятьВыгрузку"		, ВыполнятьВыгрузку);
	
	ДлительнаяОперация = ВыполнитьСинхронизациюСБитрикс24НаСервере(ПараметрыСинхронизации);
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииОперацииЗагрузки", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеИнформацииОСинхронизации()
	
	ОбновлениеИнформацииОСинхронизацииСервер();	
	
	ДлинаПротокола = СтрДлина(Протокол);
	
	Если ДлинаПротокола > 0 Тогда           
		Элементы.Протокол.ТекущаяОбласть  = ТабличныйПротокол.Области[ТабличныйПротокол.Области.Количество()-1];
	КонецЕсли;
	
	Если ЗавершенаСинхронизация Тогда
		ДобавитьЕдиничнуюЗаписьВПротокол("Завершение синхронизации");
		ОтключитьОбработчикОжидания("ОбновлениеИнформацииОСинхронизации");
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ОбновлениеИнформацииОСинхронизацииСервер(Сообщения = Неопределено)
	
	Макет = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьМакет("МакетЛога");
	
	Если Сообщения = Неопределено Тогда
		
		Задания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("УникальныйИдентификатор", ИдентификаторЗадания));
		
		Если Задания.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Задание = Задания[0];
		
		Сообщения = Задание.ПолучитьСообщенияПользователю(Истина);
		
	КонецЕсли;
	
	Для Каждого Сообщение Из Сообщения Цикл
		
		Если Лев(Сообщение.Текст, 26)= "Тело запроса HTTP запроса:" Тогда
			Продолжить;
		КонецЕсли;
		
		Если Лев(Сообщение.Текст, 16)= "Ответ с портала:" Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСообщения = Сообщение.Поле + ?(ЗначениеЗаполнено(Сообщение.Поле), " -- ", "") + Сообщение.Текст;
		
		Протокол = Протокол + ТекстСообщения + Символы.ПС; 
		
		Если Сообщение.ПутьКДанным = "КритическаяОшибка" И ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета= Макет.ПолучитьОбласть("КритическаяОшибкаСсылка");                                                                                                                                                                                                                                                                                                                                                
		ИначеЕсли Сообщение.ПутьКДанным = "КритическаяОшибка" И НЕ ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета= Макет.ПолучитьОбласть("КритическаяОшибка");                                                                                                                                                                                                                                                                                                                                                
			
		ИначеЕсли Сообщение.ПутьКДанным = "Ошибка" И ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета= Макет.ПолучитьОбласть("ОшибкаСсылка");                                                                                                                                                                                                                                                                                                                                                
		ИначеЕсли Сообщение.ПутьКДанным = "Ошибка" И НЕ ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета= Макет.ПолучитьОбласть("Ошибка");  
			
		ИначеЕсли Сообщение.ПутьКДанным = "Информация" И ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета= Макет.ПолучитьОбласть("ИнформацияСсылка");                                                                                                                                                                                                                                                                                                                                                
		ИначеЕсли Сообщение.ПутьКДанным = "Информация" И НЕ ЗначениеЗаполнено(Сообщение.КлючДанных) Тогда
			ОбластьМакета= Макет.ПолучитьОбласть("Информация"); 
		Иначе
			ОбластьМакета= Макет.ПолучитьОбласть("Информация");                                                                                                                                                                                                                                                                                                                                                
		КонецЕсли;
		
		ОбластьМакета.Параметры.ТекстСообщения = ТекстСообщения; 
		ОбластьМакета.Параметры.СсылкаНаОбъект = Сообщение.КлючДанных; 
		ТабличныйПротокол.Вывести(ОбластьМакета);
	
	КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Процедура ДобавитьЕдиничнуюЗаписьВПротокол(Сообщение)
	
	Макет = РегистрыСведений.Б24_СМ_НастройкиСмартПроцессов.ПолучитьМакет("МакетЛога");
	
	ТекстСообщения = Формат(ТекущаяДатаСеанса(),"ДЛФ=T") + " -- " + Сообщение;
	
	Протокол = Протокол + ТекстСообщения + Символы.ПС; 
	
	ОбластьМакета= Макет.ПолучитьОбласть("Информация");                                                                                                                                                                                                                                                                                                                                                
	ОбластьМакета.Параметры.ТекстСообщения = ТекстСообщения; 
	ТабличныйПротокол.Вывести(ОбластьМакета);
	
КонецПроцедуры	


&НаКлиенте
Процедура ПриЗавершенииОперацииЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.ГруппаКнопки.Видимость 	= Истина;
	Элементы.КнопкаЗакрыть.Видимость 	= Истина;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	             
	Если Протокол = "" Тогда
		
		Данные = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		Если ТипЗнч(Данные) = Тип("ФиксированныйМассив") Тогда
			ОбновлениеИнформацииОСинхронизацииСервер(Данные);	
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.ПодробноеПредставлениеОшибки) Тогда
		ОписаниеОшибки 										= Результат.ПодробноеПредставлениеОшибки; 
		Элементы.СтраницыФормы.ТекущаяСтраница 				= Элементы.СтраницаОшибка;
		Элементы.НаписатьВТехподдержку.Видимость 			= Истина;
		Элементы.НаписатьВТехподдержку.КнопкаПоУмолчанию 	= Истина;
	Иначе
		
		ПоказатьОповещениеПользователя(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 ""%2""'"),
		Формат(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТекущуюДатуСеанса(), "ДЛФ=DT"),
		НастройкаПодключения) 
		,,
		НСтр("ru = 'Синхронизация с Битрикс24 завершена'"),
		БиблиотекаКартинок.Информация32);
		
		Элементы.НадписьВыполняетсяОбмен.Заголовок 	= "Синхронизация завершена";
		Элементы.КартинкаВыполняетсяОбмен.Видимость = Ложь;
		Элементы.КнопкаЗакрыть.КнопкаПоУмолчанию 	= Истина;
		
	КонецЕсли;
	
	ОбновлениеИнформацииОСинхронизации();
	
	ЗавершенаСинхронизация = Истина;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьСинхронизациюСБитрикс24НаСервере(ПараметрыСинхронизации)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Выполнение синхронизации с Битрикс24'");
	
	Результат = ДлительныеОперации.ВыполнитьВФоне("Б24_СМ_ОбщегоНазначенияВызовСервера.ВыполнитьСинхронизациюПоФоновомуЗаданию", ПараметрыСинхронизации, ПараметрыВыполнения);
	
	//Б24_СМ_ОбщегоНазначенияВызовСервера.ВыполнитьСинхронизациюПоФоновомуЗаданию(ПараметрыСинхронизации,"");
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура НаписатьВТехподдержку(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОписаниеОшибки", ОписаниеОшибки);	
	ПараметрыФормы.Вставить("Протокол"		, Протокол);	
	ОткрытьФорму("Обработка.Б24_К_Администрирование.Форма.НаписатьТикетВТехподдержку", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокНеотправленныхПакетов(Команда)
	ОткрытьФорму("РегистрСведений.Б24_СМ_ПакетыВыгрузки.ФормаСписка");
КонецПроцедуры

