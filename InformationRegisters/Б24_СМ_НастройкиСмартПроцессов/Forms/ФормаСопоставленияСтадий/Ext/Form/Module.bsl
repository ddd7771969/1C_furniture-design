
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	ВременныйАдресНастроекСтадий= Параметры.ВременныйАдресНастроекСтадий;
	НастройкаПодключения 		= Параметры.НастройкаПодключения;	
	ИдСмартПроцесса 			= Параметры.ИдСмартПроцесса;	
	ТипВладельцаСмартПроцесса 	= Параметры.ТипВладельцаСмартПроцесса;	
	
	ТипСущности1С 			= Параметры.ТипСущности1С;	
	Сущность1С 				= Параметры.Сущность1С;	
	       
	МетаданныеОбъекта = Неопределено;
	Если ТипСущности1С = "Справочник" Тогда
		МетаданныеОбъекта 	= Метаданные.Справочники[Сущность1С];
		ТипОбъекта 			= Тип("СправочникСсылка." + Сущность1С);
	ИначеЕсли ТипСущности1С = "Документ" Тогда
		МетаданныеОбъекта = Метаданные.Документы[Сущность1С];
		ТипОбъекта 			= Тип("ДокументСсылка." + Сущность1С);
	КонецЕсли;
	
	Если МетаданныеОбъекта = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(ВременныйАдресНастроекСтадий) Тогда
		
		СохраненныеДанные = ПолучитьИзВременногоХранилища(ВременныйАдресНастроекСтадий);
		
		ИсточникСтадийВ1С 	= СохраненныеДанные.ИсточникСтадийВ1С;
		СвойствоОбъекта 	= СохраненныеДанные.СвойствоОбъекта;
		Алгоритм 			= СохраненныеДанные.Алгоритм;
		Реквизит1С 			= СохраненныеДанные.Реквизит1С;
		
		СоответствияСтадийНаправлений.Загрузить(СохраненныеДанные.СоответствияСтадийНаправлений);
		
	КонецЕсли;
	
	Элементы.ИсточникСтадийВ1С.СписокВыбора.Добавить("Реквизит объекта");
	Если ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения") Тогда
		
		Если МетаданныеОбъекта.ТабличныеЧасти.Найти("ДополнительныеРеквизиты") <> Неопределено Тогда
			Элементы.ИсточникСтадийВ1С.СписокВыбора.Добавить("Дополнительный реквизит");
		КонецЕсли;
		
		Если Метаданные.РегистрыСведений.ДополнительныеСведения.Измерения.Объект.Тип.СодержитТип(ТипОбъекта) Тогда
			Элементы.ИсточникСтадийВ1С.СписокВыбора.Добавить("Дополнительное сведение");
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		Элементы.ИсточникСтадийВ1С.СписокВыбора.Добавить("Произвольный алгоритм");
	КонецЕсли;
	
	
	Если ИсточникСтадийВ1С = "" Тогда
		ИсточникСтадийВ1С = "Реквизит объекта";	
	КонецЕсли;
	
	СтарИсточникСтадийВ1С = ИсточникСтадийВ1С;
	
	ЗагрузитьНаправленияСПортала();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновлениеЭлементовФормы();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
		
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по статусам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		НужноЗакрытьОкно = Истина;
		НаправленияПриАктивизацииСтроки(Неопределено);
        Закрыть(СохранениеНастроек());
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		НужноЗакрытьОкно = Истина;
		Закрыть();
    КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИсточникСтадийВ1СПриИзменении(Элемент)
	
	ЕстьЗаполненныеДанные = Ложь;
	Для каждого ТекСтрока Из СоответствияСтадий Цикл 
		Если ЗначениеЗаполнено(ТекСтрока.Значение1С) Тогда
			ЕстьЗаполненныеДанные = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьЗаполненныеДанные Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПриИзмененииИсточникаСтадийВ1С", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, "Будут удалены соответствия по выбранному смарт процессу. Продолжить?", РежимДиалогаВопрос.ДаНет, 0);
	Иначе
		СтарИсточникСтадийВ1С = ИсточникСтадийВ1С;
		ОбновлениеЭлементовФормы();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПриИзмененииИсточникаСтадийВ1С(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ИсточникСтадийВ1С = СтарИсточникСтадийВ1С;
		ОбновлениеЭлементовФормы();
        Возврат;
    КонецЕсли;
	
	
	Для каждого ТекСтрока Из СоответствияСтадий Цикл 
		ТекСтрока.Значение1С = Неопределено;	
	КонецЦикла;
	
	Для каждого ТекСтрока Из СоответствияСтадийНаправлений Цикл 
		ТекСтрока.Значение1С = Неопределено;	
	КонецЦикла;
	
	ОбновлениеЭлементовФормы();

КонецПроцедуры


&НаКлиенте
Процедура НаправленияПриАктивизацииСтроки(Элемент)
	
	ТекущееНаправление = Элементы.Направления.ТекущиеДанные;
	Если Элементы.Направления.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СоответствияСтадий.Количество() > 0 Тогда
		НайденныеСтроки = СоответствияСтадийНаправлений.НайтиСтроки(Новый Структура("ИдНаправления", ПредИдНаправления));
		Для каждого ТекСтрока Из НайденныеСтроки Цикл
			СоответствияСтадийНаправлений.Удалить(СоответствияСтадийНаправлений.Индекс(ТекСтрока));
		КонецЦикла;                                       
		
		Для каждого ТекСтрока Из СоответствияСтадий Цикл
			НоваяЗапись = СоответствияСтадийНаправлений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
			НоваяЗапись.ИдНаправления = ПредИдНаправления;
		КонецЦикла;
	КонецЕсли;
	
	ПредИдНаправления = ТекущееНаправление.ИдНаправления; 
	
	СоответствияСтадий.Очистить();
	
	Для каждого ТекСтрока Из СоответствияСтадийНаправлений Цикл
		Если ТекСтрока.ИдНаправления = ТекущееНаправление.ИдНаправления Тогда
			НовСтрока = СоответствияСтадий.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, ТекСтрока); 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура Реквизит1СНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	
	Если Реквизит1С.Количество() > 0  Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПриИзмененииРеквизита", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, "Будут удалены соответствия по выбранному смарт процессу. Продолжить?", РежимДиалогаВопрос.ДаНет, 0);
	Иначе
		ПослеЗакрытияВопросаПриИзмененииРеквизита(КодВозвратаДиалога.Да, Неопределено);	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПриИзмененииРеквизита(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
	
	СписокРеквизитов1С 	= ПолучитьСписокРеквизитов();
	
	Если СписокРеквизитов1С.Количество() > 0 Тогда
		ВыбраннаяСущность = СписокРеквизитов1С.НайтиПоЗначению(СписокРеквизитов1С[0].Значение);	
	Иначе
		ВыбраннаяСущность = Неопределено;	
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораРеквизита1С", ЭтотОбъект, Параметры);
	ПоказатьВыборИзСписка(Оповещение, СписокРеквизитов1С, Элементы.Реквизит1С, ВыбраннаяСущность);

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораРеквизита1С(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
        Возврат;
	КонецЕсли;
	
	Для каждого ТекСтрока Из СоответствияСтадий Цикл 
		ТекСтрока.Значение1С = Неопределено;	
	КонецЦикла;
	
	Для каждого ТекСтрока Из СоответствияСтадийНаправлений Цикл 
		ТекСтрока.Значение1С = Неопределено;	
	КонецЦикла;
	
	Реквизит1С.Очистить();
	Реквизит1С.Добавить(Результат.Значение, Результат.Представление);
	
КонецПроцедуры


&НаКлиенте
Процедура Реквизит1СОчистка(Элемент, СтандартнаяОбработка)
	Реквизит1С.Очистить();
КонецПроцедуры


&НаКлиенте
Процедура СвойствоОбъектаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	
	Если НЕ СвойствоОбъекта.Пустая() Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПриИзмененииСвойства", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, "Будут удалены соответствия по выбранному смарт процессу. Продолжить?", РежимДиалогаВопрос.ДаНет, 0);
	Иначе
		ПослеЗакрытияВопросаПриИзмененииСвойства(КодВозвратаДиалога.Да, Неопределено);	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПриИзмененииСвойства(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораСвойства", ЭтотОбъект, Параметры);
	ПоказатьВводЗначения(Оповещение, СвойствоОбъекта, "Выберите свойство", Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСвойства(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
        Возврат;
	КонецЕсли;
	
	Для каждого ТекСтрока Из СоответствияСтадий Цикл 
		ТекСтрока.Значение1С = Неопределено;	
	КонецЦикла;
	
	Для каждого ТекСтрока Из СоответствияСтадийНаправлений Цикл 
		ТекСтрока.Значение1С = Неопределено;	
	КонецЦикла;
	
	СвойствоОбъекта = Результат;
	
КонецПроцедуры


&НаКлиенте
Процедура СоответствияСтадийПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.СоответствияСтадий.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		лЭлемент = Элементы.СоответствияСтадийЗначение1С;
		
		Если ИсточникСтадийВ1С = "Дополнительный реквизит" ИЛИ ИсточникСтадийВ1С = "Дополнительное сведение" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Истина;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			
			МассивПараметров = Новый Массив;
			МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СвойствоОбъекта));	
			лЭлемент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
			
			лЭлемент.ОграничениеТипа = ПолучитьТипВыбранногоСвойства();
			
		ИначеЕсли ИсточникСтадийВ1С = "Реквизит объекта" Тогда
			
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			
			лЭлемент.ОграничениеТипа = ПолучитьТипВыбранногоРеквизита();
			
		ИначеЕсли ИсточникСтадийВ1С = "Произвольный алгоритм" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Истина;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаСервере
Процедура ЗагрузитьНаправленияСПортала()
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Истина);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка статусов смарт процессов.";
	
	ТелоHTTPЗапроса = "&entityTypeId=" + ТипВладельцаСмартПроцесса;
	НаправленияСБ24 = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.category.list", ТелоHTTPЗапроса); 
	Если НаправленияСБ24 = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	result = НаправленияСБ24.Получить("result");
	Если result = Неопределено Тогда
		Возврат;
	КонецЕсли;

	categories = result.Получить("categories");
	Если categories = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекНаправление Из categories Цикл
		
		ИдНаправления			= Формат(ТекНаправление.Получить("id"),"ЧГ=0");
		НаименованиеНаправления = ТекНаправление.Получить("name");
		
		НоваяСтрока = Направления.Добавить();
		НоваяСтрока.ИдНаправления 	= ИдНаправления; 
		НоваяСтрока.Направление 	= НаименованиеНаправления; 
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСтатусыСПортала(Команда)
	
	НаправленияПриАктивизацииСтроки(Неопределено);
	
	ТекущиеДанные = Элементы.Направления.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ЗагрузитьСтатусыСПорталаСервер(); 
	
	ТекущееНаправление = Элементы.Направления.ТекущиеДанные;
	Если Элементы.Направления.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПредИдНаправления = ТекущееНаправление.ИдНаправления; 
	
	СоответствияСтадий.Очистить();
	Для каждого ТекСтрока Из СоответствияСтадийНаправлений Цикл
		Если ТекСтрока.ИдНаправления = ТекущееНаправление.ИдНаправления Тогда
			НовСтрока = СоответствияСтадий.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, ТекСтрока); 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
	
&НаСервере
Процедура ЗагрузитьСтатусыСПорталаСервер()
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Истина);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка статусов смарт процессов.";
	
	мТаблицы = Новый Массив;
	мТаблицы.Добавить(СоответствияСтадийНаправлений);
	
	Для Каждого ТекТаблица Из мТаблицы Цикл
		
		тзнАрхив = ТекТаблица.Выгрузить();
		ТекТаблица.Очистить();
		
		Для каждого ТекНаправление Из Направления Цикл
			
			ДопИдНаправления = "DYNAMIC_" + ТипВладельцаСмартПроцесса + "_STAGE_" + ТекНаправление.ИдНаправления;
			ФильтрПоКатегории 	= "filter[CATEGORY_ID]=" + ТекНаправление.ИдНаправления;
			
			ДанныеНаправления = Б24_К_RestApiВызовСервера.ПолучитьМассивСтатусовCRM(СтруктураНастроек, ФильтрПоКатегории); 
			Для Каждого ТекСтатус Из ДанныеНаправления Цикл
			
				ВладелецСтатуса = Формат(ТекСтатус.Получить("ENTITY_ID"),"ЧГ=0");
				НазваниеСтадии	= ТекСтатус.Получить("NAME");
				ИдСтадии = Формат(ТекСтатус.Получить("STATUS_ID"),"ЧГ=0");
				
				Для каждого ТекНаправление Из Направления Цикл
					
					Если ВладелецСтатуса = ДопИдНаправления Тогда
						
						НоваяЗапись = ТекТаблица.Добавить();
						НоваяЗапись.ИдНаправления 		= ТекНаправление.ИдНаправления;
						НоваяЗапись.НазваниеНаправления = ТекНаправление.Направление;
							
						НоваяЗапись.ИдСтадии		= ИдСтадии;
						НоваяЗапись.НазваниеСтадии 	= НазваниеСтадии;
						
						НайденныеСтроки = ТзнАрхив.НайтиСтроки(Новый Структура("ИдНаправления, ИдСтадии", ТекНаправление.ИдНаправления, ИдСтадии)); 
						Если НайденныеСтроки.Количество()>0 Тогда
							НоваяЗапись.Значение1С = НайденныеСтроки[0].Значение1С;	
						КонецЕсли;
						
					КонецЕсли;  
					
				КонецЦикла;
				
			КонецЦикла;
		
		КонецЦикла;  
		
	КонецЦикла;   
	
КонецПроцедуры


&НаКлиенте
Процедура НастроитьАлгоритм(Команда)
		
	ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритма", ЭтаФорма);
	
	ПередаваемыеПараметры = Новый Структура;
	ПередаваемыеПараметры.Вставить("Алгоритм"	, Алгоритм);
	ПередаваемыеПараметры.Вставить("Тип"		, "Функция");
	
	ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаАлгоритма(НовыйАлгоритм, пПараметры) Экспорт
	
	Если НЕ НовыйАлгоритм = Неопределено Тогда
		Алгоритм = НовыйАлгоритм;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура Применить(Команда)
	
	Если (ИсточникСтадийВ1С = "Дополнительный реквизит" ИЛИ ИсточникСтадийВ1С = "Дополнительное сведение") И НЕ ЗначениеЗаполнено(СвойствоОбъекта) Тогда
		ПоказатьПредупреждение(,"Не указано свойство смарт процесса. Запись настроек невозможна");
	Иначе
		
		НаправленияПриАктивизацииСтроки(Неопределено);
		
		Модифицированность = Ложь;
		
		Закрыть(СохранениеНастроек());
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область Прочее

&НаКлиенте
Процедура ОбновлениеЭлементовФормы()

	Элементы.Алгоритм.Видимость = Ложь;
	
	Если ИсточникСтадийВ1С = "Дополнительный реквизит" ИЛИ ИсточникСтадийВ1С = "Дополнительное сведение" Тогда
		
		Элементы.ГруппаИсточник.ТекущаяСтраница = Элементы.ГруппаСвойство;
		
		МассивПараметров = Новый Массив;
		Если ИсточникСтадийВ1С = "Дополнительный реквизит" Тогда
			МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ЭтоДополнительноеСведение", Ложь));	
		Иначе
			МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ЭтоДополнительноеСведение", Истина));	
		КонецЕсли;
		
		Элементы.СвойствоОбъекта.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
		
	ИначеЕсли ИсточникСтадийВ1С = "Реквизит объекта" Тогда
		Элементы.ГруппаИсточник.ТекущаяСтраница = Элементы.ГруппаРеквизит;
	ИначеЕсли ИсточникСтадийВ1С = "Произвольный алгоритм" Тогда
		Элементы.ГруппаИсточник.ТекущаяСтраница = Элементы.ГруппаАлгоритм;
		Элементы.Алгоритм.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокРеквизитов()
	
	Результат = Новый СписокЗначений;
	
	Если ТипСущности1С = "Справочник" Тогда
		ТекущиеМетаданные = Метаданные.Справочники;
	ИначеЕсли ТипСущности1С = "Документ" Тогда
		ТекущиеМетаданные = Метаданные.Документы;
	КонецЕсли;
	
	Для каждого ТекЭлемент Из ТекущиеМетаданные[Сущность1С].Реквизиты Цикл 
		Результат.Добавить(ТекЭлемент.Имя, ТекЭлемент.Синоним); 
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьТипВыбранногоРеквизита()
	
	Результат = Новый ОписаниеТипов;
	
	Если ЗначениеЗаполнено(Реквизит1С) Тогда
		
		Если ТипСущности1С = "Справочник" Тогда
			ТекущиеМетаданные = Метаданные.Справочники;
		ИначеЕсли ТипСущности1С = "Документ" Тогда
			ТекущиеМетаданные = Метаданные.Документы;
		КонецЕсли;
		
		Результат = ТекущиеМетаданные[Сущность1С].Реквизиты[Реквизит1С[0].Значение].Тип;
		
	КонецЕсли;
	
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьТипВыбранногоСвойства()
	
	Результат = Новый ОписаниеТипов;
	
	Если ЗначениеЗаполнено(СвойствоОбъекта) Тогда
		
		Результат = СвойствоОбъекта.ТипЗначения;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


&НаСервере
Функция СохранениеНастроек()
	
	СтруктураИнформацииОСтадиях = Новый Структура;
	СтруктураИнформацииОСтадиях.Вставить("ИсточникСтадийВ1С"	, ИсточникСтадийВ1С);
	СтруктураИнформацииОСтадиях.Вставить("СвойствоОбъекта"		, СвойствоОбъекта);
	СтруктураИнформацииОСтадиях.Вставить("Алгоритм"				, Алгоритм);
	СтруктураИнформацииОСтадиях.Вставить("Реквизит1С"			, Реквизит1С);
	
	СтруктураИнформацииОСтадиях.Вставить("СоответствияСтадийНаправлений", СоответствияСтадийНаправлений.Выгрузить());
	
	Результат = Новый Структура;
	Результат.Вставить("ВременныйАдресНастроекСтадий"	, ПоместитьВоВременноеХранилище(СтруктураИнформацииОСтадиях, Новый УникальныйИдентификатор));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

