
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("НастройкаПодключения") Тогда
		НастройкаПодключения 	= Параметры.НастройкаПодключения;
		Портал 					= НастройкаПодключения.Портал; 
	КонецЕсли;
	
	ПараметрПортал = Список.Параметры.Элементы.Найти("Портал");
	ПараметрПортал.Использование 	= Истина;
	ПараметрПортал.Значение 		= Портал;
	
КонецПроцедуры



&НаКлиенте
Процедура ЗагрузитьСБитрикс24(Команда)
	
	Если НЕ Б24_К_СлужебныйВызовСервера.БазовыйМодульДоступен(НастройкаПодключения) Тогда
		Б24_К_СлужебныйКлиент.ОповеститьОбНедоступностиФункционалаБазовогоМодуляПоТарифномуПлану();
		Возврат;	
	КонецЕсли;
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение2 = "Загрузка пользователей с портала.";
	
	ЗагрузитьСБитрикс24Сервер(СтруктураНастроек);
	
	Элементы.Список.Обновить();
		
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСБитрикс24Сервер(СтруктураНастроек)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Портал", Портал);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ПользователиБитрикс24.Портал КАК Портал,
	|	Б24_К_ПользователиБитрикс24.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрСведений.Б24_К_ПользователиБитрикс24 КАК Б24_К_ПользователиБитрикс24
	|ГДЕ
	|	Б24_К_ПользователиБитрикс24.Портал = &Портал";
	Запрос.Выполнить();
	
	СтруктураНастроек.Логирование.Измерение2 = "Получение сотрудников";    
	
	ДанныеПользователей = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/user.get.json", "ID");	
	Для каждого ТекЭлемент Из ДанныеПользователей Цикл

		Если ТекЭлемент.Получить("ACTIVE") <> Истина Тогда
			Продолжить;
		КонецЕсли;
		
		Идентификатор = ТекЭлемент.Получить("ID");

		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ_Данные.Портал КАК Портал,
		|	ВТ_Данные.Идентификатор КАК Идентификатор
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|ГДЕ
		|	ВТ_Данные.Идентификатор = &Идентификатор";
		
		ВыполненныйЗапрос = Запрос.Выполнить();
		
		Если ВыполненныйЗапрос.Пустой() Тогда
			
			НоваяЗапись = РегистрыСведений.Б24_К_ПользователиБитрикс24.СоздатьМенеджерЗаписи();
			НоваяЗапись.Портал 			= Портал;
			НоваяЗапись.Идентификатор 	= Идентификатор;
			НоваяЗапись.Наименование 	= Строка(ТекЭлемент.Получить("NAME")) + " " + Строка(ТекЭлемент.Получить("LAST_NAME"));
			
			НоваяЗапись.Записать();
			
		Иначе      
			
			НоваяЗапись = РегистрыСведений.Б24_К_ПользователиБитрикс24.СоздатьМенеджерЗаписи();
			НоваяЗапись.Портал 			= Портал;
			НоваяЗапись.Идентификатор 	= Идентификатор;
			НоваяЗапись.Прочитать();
			
			НоваяЗапись.Наименование 	= Строка(ТекЭлемент.Получить("NAME")) + " " + Строка(ТекЭлемент.Получить("LAST_NAME"));
			
			НоваяЗапись.Записать();

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры


&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть(ДобавитьВыделенныеИдентификаторыВМассив());
	
КонецПроцедуры

&НаСервере
Функция ДобавитьВыделенныеИдентификаторыВМассив()
	
	Результат = Новый СписокЗначений;
	
	Для Каждого ТекСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
		
		ТекущаяЗапись = РегистрыСведений.Б24_К_ПользователиБитрикс24.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(ТекущаяЗапись, ТекСтрока);
		ТекущаяЗапись.Прочитать();
		
		Результат.Добавить(ТекущаяЗапись.Идентификатор, ТекущаяЗапись.Наименование);	
		
	КонецЦикла;
		
	Возврат Результат;
		
КонецФункции



&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(ДобавитьВыделенныеИдентификаторыВМассив());
	
КонецПроцедуры

