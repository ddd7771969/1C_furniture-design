
Функция ПолучитьСтруктуруДанных(Состояние = Неопределено, Исполнитель = Неопределено, Статус = Неопределено, ТипЗадачи = Неопределено) Экспорт
 
	Возврат Новый Структура("Состояние, Исполнитель, Статус, ТипЗадачи", Состояние, Исполнитель, Статус, ТипЗадачи); 
 
КонецФункции // ПолучитьСтруктуруДанных()

Процедура УстановитьСостояниеИсполнительЗадачи(Задача, стДанные, Комментарий = "") Экспорт

	Если стДанные.Состояние = Неопределено И стДанные.Исполнитель = Неопределено И стДанные.Статус = Неопределено И стДанные.ТипЗадачи = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		 "ВЫБРАТЬ
		 |	ТаймингЗадачСрезПоследних.Исполнитель КАК Исполнитель,
		 |	ТаймингЗадачСрезПоследних.Состояние КАК Состояние,
		 |	ТаймингЗадачСрезПоследних.Статус КАК Статус,
		 |	ТаймингЗадачСрезПоследних.ТипЗадачи КАК ТипЗадачи
		 |ИЗ
		 |	РегистрСведений.ТаймингЗадач.СрезПоследних(, Задача = &Задача) КАК ТаймингЗадачСрезПоследних";
	 
	 Запрос.УстановитьПараметр("Задача", Задача);
	 
	 Выборка = Запрос.Выполнить().Выбрать();
	 
	 текСостояние 	= Неопределено;
	 текИсполнитель = Неопределено;
	 текСтатус 		= Неопределено;
	 текТипЗадачи 	= Неопределено;
	 
	 Пока Выборка.Следующий() Цикл
		 
		 текСостояние 	= Выборка.Состояние;
		 текИсполнитель = Выборка.Исполнитель;
		 текСтатус 		= Выборка.Статус;
		 текТипЗадачи 	= Выборка.ТипЗадачи;
		 
	 КонецЦикла;
	 
	 Если текСостояние = стДанные.Состояние И текИсполнитель = стДанные.Исполнитель И текСтатус = стДанные.Статус И текТипЗадачи = стДанные.ТипЗадачи Тогда
		 Возврат;
	 КонецЕсли;
	 
	 текИсполнитель = ?(стДанные.Исполнитель 	= Неопределено, текИсполнитель, стДанные.Исполнитель); 
	 текСостояние 	= ?(стДанные.Состояние 		= Неопределено, текСостояние, 	стДанные.Состояние); 
	 текСтатус 		= ?(стДанные.Статус 		= Неопределено, текСтатус, 		стДанные.Статус); 
	 текТипЗадачи 	= ?(стДанные.ТипЗадачи 		= Неопределено, текТипЗадачи, 	стДанные.ТипЗадачи); 
	 
	 
	 УИД = Новый УникальныйИдентификатор();
	 
	 МенеджерЗаписи = РегистрыСведений.ТаймингЗадач.СоздатьМенеджерЗаписи();
	 МенеджерЗаписи.УИД 		= УИД;
	 МенеджерЗаписи.Период 		= ТекущаяДата();
	 МенеджерЗаписи.Задача 		= Задача;
	 МенеджерЗаписи.Исполнитель	= текИсполнитель;
	 МенеджерЗаписи.Состояние	= текСостояние;
	 МенеджерЗаписи.ТипЗадачи	= текТипЗадачи;
	 МенеджерЗаписи.Статус		= текСтатус;
	 МенеджерЗаписи.Автор		= ПараметрыСеанса.ТекущийПользователь;
	 
	 МенеджерЗаписи .Записать();
	 
	 Если НЕ ПустаяСтрока(Комментарий) Тогда
		 
		 МенеджерЗаписи = РегистрыСведений.ТаймингЗадачКомментарии.СоздатьМенеджерЗаписи();
		 МенеджерЗаписи.УИД 		= УИД;
		 МенеджерЗаписи.Комментарий	= Комментарий; 
		 
		 МенеджерЗаписи .Записать();
		 
	 КонецЕсли;
	 
 КонецПроцедуры
 
 Функция ПолучитьСостояниеЗадачи(Задача) Экспорт
	 
	 стДанные = Новый Структура("Представление, Исполнитель, Состояние", "");
	 
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	ТаймингЗадачСрезПоследних.Состояние КАК Состояние,
	 |	ТаймингЗадачСрезПоследних.Исполнитель КАК Исполнитель,
	 |	ТаймингЗадачСрезПоследних.Исполнитель.Наименование КАК ИсполнительНаименование
	 |ИЗ
	 |	РегистрСведений.ТаймингЗадач.СрезПоследних(, Задача = &Задача) КАК ТаймингЗадачСрезПоследних";
	 
	 Запрос.УстановитьПараметр("Задача", Задача);
	 
	 Выборка = Запрос.Выполнить().Выбрать();
	 
	 Пока Выборка.Следующий() Цикл
		 ЗаполнитьЗначенияСвойств(стДанные, Выборка);
		 ИсполнительНаименование = Выборка.ИсполнительНаименование;
	 КонецЦикла;
	 
	 Представление = "";
	 
	 Если ЗначениеЗаполнено(стДанные.Состояние) Тогда
		 Представление = Строка(стДанные.Состояние);
	 Иначе	
		 Представление = "Ожидание";
	 КонецЕсли;
	 
	 Если ЗначениеЗаполнено(стДанные.Исполнитель) Тогда
		 Представление = Представление + " " + ИсполнительНаименование;
	 КонецЕсли;
	 
	 стДанные.Представление = Представление;
	 
	 Возврат стДанные;
	 
 КонецФункции // ПолучитьСостояниеЗадачи(Задача)
 
 Функция ПолучитьИсториюЗадачи(Задача, ТипВозврата = "HTML") Экспорт
	 
	 тзДанные = ПолучитьДанныеИсториии(Задача);
	 
	 Если ТипВозврата = "HTML" Тогда
		 Возврат	ИсторияЗадачиHTML(Задача, тзДанные);
	 ИначеЕсли ТипВозврата = "строка" Тогда
		 Возврат	ИсторияЗадачиСтрокой(тзДанные);
	 КонецЕсли;
	 
	 Возврат тзДанные;
	 
 КонецФункции // ПолучитьИсториюЗадачи(Задача) 
 
 Функция ИсторияЗадачиHTML(Задача, тзДанные)
	 
	 стрВозврата = омHTML.ПолучитьЗаголовокHTML();
	 
	 стрВозврата = стрВозврата + омHTML.ПолучитьHeadHTML("История " + Задача, 1);
	 
	 стрВозврата = стрВозврата + "<table>";
	 стрВозврата = стрВозврата + "<tr><td>Дата</td><td>Исполнитель</td><td>Состояние</td><td>Автор</td><td>Комментарий</td></tr>";
	 
	 ШаблонСтроки = "<tr><td>%1</td><td>%2</td><td>%3</td><td>%4</td><td>%5</td></tr>";
	 
	 Для каждого СтрокаТаблицы Из тзДанные Цикл
		 стрВозврата = стрВозврата + СтрШаблон(ШаблонСтроки, СтрокаТаблицы.Период, СтрокаТаблицы.Исполнитель, СтрокаТаблицы.Состояние, СтрокаТаблицы.Автор, СтрокаТаблицы.Комментарий);	
	 КонецЦикла;
	 
	 стрВозврата = стрВозврата + "</table>";
	 
	 стрВозврата = стрВозврата + омHTML.ПолучитьХвостHTML();
	 
	 Возврат стрВозврата;
	 
 КонецФункции // ИсторияЗадачиHTML(тзДанные)
 
 Функция ИсторияЗадачиСтрокой(тзДанные)
	 
	 
	 
 КонецФункции // ИсторияЗадачиHTML(тзДанные)
 
 Функция ПолучитьДанныеИсториии(Задача)
	 
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	ТаймингЗадач.Период КАК Период,
	 |	ТаймингЗадач.Состояние КАК Состояние,
	 |	ТаймингЗадач.Автор КАК Автор,
	 |	ЕСТЬNULL(ТаймингЗадачКомментарии.Комментарий, """") КАК Комментарий,
	 |	ТаймингЗадач.Исполнитель КАК Исполнитель
	 |ИЗ
	 |	РегистрСведений.ТаймингЗадач КАК ТаймингЗадач
	 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймингЗадачКомментарии КАК ТаймингЗадачКомментарии
	 |		ПО ТаймингЗадач.УИД = ТаймингЗадачКомментарии.УИД
	 |ГДЕ
	 |	ТаймингЗадач.Задача = &Задача
	 |
	 |УПОРЯДОЧИТЬ ПО
	 |	Период УБЫВ";
	 
	 Запрос.УстановитьПараметр("Задача", Задача);
	 
	 Возврат Запрос.Выполнить().Выгрузить();
	 
	 
 КонецФункции // ПолучитьДанныеИсториии(Задача)
