
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ДополнительныеРеквизитыИСведенияИспользуются() Тогда
		ОбщегоНазначения.СообщитьПользователю("Для корректной работы требуется включить функционал дополнительных реквизитов и сведений");
		Возврат;
	КонецЕсли;
	
	НастройкаПодключения 	= Параметры.НастройкаПодключения;
	
	НастройкаСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкаСинхронизации.НастройкаПодключения = НастройкаПодключения;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, НастройкаСинхронизации); 
	
	СохраненныеНастройки = НастройкаСинхронизации.Настройки.Получить();
	Если СохраненныеНастройки = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю("Некорректные сохраненные настройки");
		Возврат;
	КонецЕсли;
	
	Портал = НастройкаПодключения.Портал;
	
	Если ВерсияСчетов = "" Тогда
		Если СинхронизацияСчетов Тогда ВерсияСчетов = "v.1" Иначе ВерсияСчетов = "v.2" КонецЕсли;  
	КонецЕсли;
	
	Если ВерсияТоваров = "" Тогда
		Если СинхронизацияТоваров Тогда ВерсияТоваров = "v.1" Иначе ВерсияТоваров = "v.2" КонецЕсли;  
	КонецЕсли;
	
УстановитьПривилегированныйРежим(Истина);	 //Для возможности всем пользователям читать расписание		
		ИдентификаторРегламентногоЗадания 	= НастройкаСинхронизации.ИдентификаторРегламентногоЗадания; 
		
		НайденноеРегламентноеЗадание = Неопределено;
		Если ЗначениеЗаполнено(ИдентификаторРегламентногоЗадания) Тогда			
			НайденноеРегламентноеЗадание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗадания);
		КонецЕсли;			
	
		Если НайденноеРегламентноеЗадание 	= Неопределено Тогда
			ИдентификаторРегламентногоЗадания	= Неопределено;
			РасписаниеРегламентногоЗадания 	= Новый РасписаниеРегламентногоЗадания;
		Иначе
			РасписаниеРегламентногоЗадания 	= НайденноеРегламентноеЗадание.Расписание;
		КонецЕсли;		
УстановитьПривилегированныйРежим(Ложь);

	//Элементы.ГруппаСделки.Доступность 		= ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыКлиентов");
	//Элементы.ГруппаЗаказы.Доступность 		= ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыКлиентов");

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//Если НЕ Б24_К_СлужебныйВызовСервера.МодульСинхронизацииДоступен(НастройкаПодключения) Тогда
	//	Б24_К_СлужебныйКлиент.ОповеститьОбНедоступностиФункционалаСинхронизацииПоТарифномуПлану();
	//	Закрыть();	
	//	Возврат;
	//КонецЕсли;
	
	Заголовок = Строка(НастройкаПодключения);
	
	ОбновитьОтображениеЭлементовФормы();
	
	УстановитьНадписьРасписанияОбмена(); 
	
КонецПроцедуры


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НужноЗакрытьОкно = Истина;
		
		СохранитьВыгрузитьНастройки(Истина);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		НужноЗакрытьОкно = Истина;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СохранитьВыгрузитьНастройки(ЗакрытьПоОкончании = Ложь)
	
	НастройкаСохранена = СохранитьНастройкуСервер();
	
	Если НастройкаСохранена И ВыгружатьНастройкиНаПортал Тогда
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НастройкаПодключения, "ПометкаУдаления") = Ложь Тогда
			ВыгрузкаНастройки(ЗакрытьПоОкончании);
		Иначе
			Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
	КонецЕсли;
	
	Оповестить("СозданаИзмененаНастройка");
	
	Возврат НастройкаСохранена
	
КонецФункции  

&НаСервере
Функция СохранитьНастройкуСервер()
	
	Если Модифицированность Тогда
		
УстановитьПривилегированныйРежим(Истина); //Для возможности всем пользователям с правами устанавливать расписания		
		Если СпособСинхронизацииДанных = "ПоРасписанию"  И НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда

			Наименование = Строка(НастройкаПодключения);
			
			ИменаИспользуемыхРеглЗаданий 	= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьИменаИспользуемыхРеглЗаданий();
			СтруктураРеглЗаданий 			= Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьИспользуемыеРеглЗадания();

			ПараметрыЗапуска = Новый Массив();
			ПараметрыЗапуска.Добавить(СтруктураРеглЗаданий.СинхронизацияДанных);
			ПараметрыЗапуска.Добавить(НастройкаПодключения);

			Задание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗадания);
			Если Задание <> Неопределено Тогда

				ПараметрыИзмененияЗадания = Новый Структура();
				ПараметрыИзмененияЗадания.Вставить("Использование", Истина);
				ПараметрыИзмененияЗадания.Вставить("Параметры", ПараметрыЗапуска);
				ПараметрыИзмененияЗадания.Вставить("Расписание", РасписаниеРегламентногоЗадания);
				ПараметрыИзмененияЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
				ПараметрыИзмененияЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);

				РегламентныеЗаданияСервер.ИзменитьЗадание(ИдентификаторРегламентногоЗадания, ПараметрыИзмененияЗадания);

			Иначе

				ПараметрыЗадания = Новый Структура();
				ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ЗапускДополнительныхОбработок);
				ПараметрыЗадания.Вставить("Наименование", ИменаИспользуемыхРеглЗаданий.СинхронизацияДанных
					+ Наименование);
				ПараметрыЗадания.Вставить("Ключ", ИменаИспользуемыхРеглЗаданий.СинхронизацияДанных
					+ Наименование);
				ПараметрыЗадания.Вставить("Использование", Истина);
				ПараметрыЗадания.Вставить("ЭксклюзивноеВыполнение", Ложь);
				ПараметрыЗадания.Вставить("Параметры", ПараметрыЗапуска);
				ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
				ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);
				ПараметрыЗадания.Вставить("Расписание", РасписаниеРегламентногоЗадания);

				Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
				Если ОбщегоНазначения.РазделениеВключено() Тогда
					//@skip-warning
					ИдентификаторРегламентногоЗадания = Задание.Идентификатор.УникальныйИдентификатор();
				Иначе
					ИдентификаторРегламентногоЗадания = Задание.УникальныйИдентификатор;
				КонецЕсли;

			КонецЕсли;

			Если Задание = Неопределено Тогда
				ИдентификаторРегламентногоЗадания = Неопределено;
			КонецЕсли;

		Иначе
			РегламентныеЗаданияСервер.УдалитьЗадание(ИдентификаторРегламентногоЗадания);
			ИдентификаторРегламентногоЗадания = Неопределено;
		КонецЕсли;		
	
УстановитьПривилегированныйРежим(Ложь);	
		
		НоваяЗаписьНастройки = РегистрыСведений.Б24_КС_НастройкиСинхронизации.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(НоваяЗаписьНастройки, ЭтаФорма);
	
		лНастройки = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкиСинхронизацииПоУмолчанию(); 
		Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
		Пока Выборка.Следующий() Цикл
			Если Выборка.Настройки.Получить() <> Неопределено Тогда
				лНастройки = Выборка.Настройки.Получить();	
			КонецЕсли;
		КонецЦикла;
		
		Если СинхронизацияТоваров И ВерсияТоваров = "v.2" И лНастройки.НастройкиСинхронизацииТоваров.Свойство("ИдентификаторКаталогаТоваров") 
			И (НЕ ЗначениеЗаполнено(лНастройки.НастройкиСинхронизацииТоваров.ИдентификаторКаталогаТоваров)
			ИЛИ НЕ ЗначениеЗаполнено(лНастройки.НастройкиСинхронизацииТоваров.ИдентификаторКаталогаПредложений)) Тогда
			
			СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Истина);  
			Если СтруктураНастроек <> Неопределено Тогда

		    	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
				СтруктураНастроек.Логирование.Измерение2 = "Получение списка каталогов на портале.";
	
				СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/catalog.catalog.list", ""); 
				Если СтруктураОтвета <> Неопределено Тогда  
					
					спкКаталогов = Новый СписокЗначений;
					
					result = СтруктураОтвета.Получить("result"); 
					Если result <> Неопределено Тогда
						Каталоги = result.Получить("catalogs"); 
						Если Каталоги <> Неопределено Тогда
							Для каждого ТекЭлемент Из Каталоги Цикл     
								спкКаталогов.Добавить(Формат(ТекЭлемент.Получить("id"),"ЧГ=0"), ТекЭлемент.Получить("name"));
							КонецЦикла;  
						КонецЕсли;  
					КонецЕсли;	 
					
					Для каждого ТекЭлемент Из спкКаталогов Цикл
						Если СтрНайти(ТекЭлемент.Представление, "(предложения)") = 0 Тогда
							лНастройки.НастройкиСинхронизацииТоваров.ИдентификаторКаталогаТоваров = ТекЭлемент.Значение;
						КонецЕсли;
					КонецЦикла;  
					
					Для каждого ТекЭлемент Из спкКаталогов Цикл
						Если СтрНайти(ТекЭлемент.Представление, "(предложения)") > 0 Тогда
							лНастройки.НастройкиСинхронизацииТоваров.ИдентификаторКаталогаПредложений = ТекЭлемент.Значение;   
						КонецЕсли;
					КонецЦикла;
					
				КонецЕсли;
	
			КонецЕсли;
		КонецЕсли;
		
		НоваяЗаписьНастройки.ВерсияСчетов 	= ВерсияСчетов;
		НоваяЗаписьНастройки.ВерсияТоваров 	= ВерсияТоваров;
		НоваяЗаписьНастройки.Настройки = Новый ХранилищеЗначения(лНастройки);
		
		Попытка 
			
			НоваяЗаписьНастройки.Записать();
			
			ОбновитьПовторноИспользуемыеЗначения();

			Модифицированность = Ложь;
			                                                           
		Исключение
			Возврат Ложь;	
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции  

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	СохранитьВыгрузитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	СохранитьВыгрузитьНастройки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНастройку(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОбУдалении", ЭтотОбъект, Параметры);
	ПоказатьВопрос(Оповещение, "Настройки синхронизации будут удалены. Продолжить?", РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОбУдалении(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

	УдалитьНастройкуСервер(НастройкаПодключения);
	
	Модифицированность = Ложь;
	
	Оповестить("СозданаИзмененаНастройка");
	
	Закрыть();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьНастройкуСервер(НастройкаПодключения)
	
	ЗаписьНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.СоздатьМенеджерЗаписи();
	ЗаписьНастройкиСинхронизации.НастройкаПодключения = НастройкаПодключения;
	ЗаписьНастройкиСинхронизации.Прочитать();
	ЗаписьНастройкиСинхронизации.Удалить();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры


&НаКлиенте
Процедура НастроитьСинхронизациюКонтрагентов(Команда)
	
	СохранитьВыгрузитьНастройки();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыФормы.Вставить("ТипЗапуска"			, "Все");
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.НастройкаСинхронизацииКонтрагентов", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюИнформацииОТоварах(Команда)
	
	Если НЕ СохранитьВыгрузитьНастройки() Тогда
		Возврат;
	КонецЕсли;       
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыФормы.Вставить("ВерсияТоваров"			, ВерсияТоваров);           
	
	Если ВерсияТоваров = "v.2" Тогда
		ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.НастройкаСинхронизацииИнформацииОНоменклатуре2", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	Иначе
		ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.НастройкаСинхронизацииИнформацииОНоменклатуре", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюСделок(Команда)
	
	Если НЕ СохранитьВыгрузитьНастройки() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.НастройкаСинхронизацииСделок", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюСчетов(Команда)
	
	Если НЕ СохранитьВыгрузитьНастройки() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыФормы.Вставить("ВерсияСчетов"			, ВерсияСчетов);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.НастройкаСинхронизацииСчетов", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюЗаказов(Команда)
		
	Если НЕ СохранитьВыгрузитьНастройки() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.НастройкаСинхронизацииЗаказов", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНастройкиОбмена(Команда)
	
	ОчиститьНастройкиОбменаНаСервере();
	
	СохранитьВыгрузитьНастройки();

	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНастройкиОбменаНаСервере()
	
	НастройкаСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, НастройкаСинхронизации); 
	
	Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
	Пока Выборка.Следующий() Цикл
		Выборка.ПолучитьМенеджерЗаписи().Удалить();
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура НастроитьРасписаниеОбмена(Команда)
		
	Если РасписаниеРегламентногоЗадания = НеОпределено Тогда
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьИзменятьРасписание", ЭтотОбъект);
	
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИзменятьРасписание(РасписаниеЗадания, ДополнительныеПараметры) Экспорт
	
	Если РасписаниеЗадания = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РасписаниеРегламентногоЗадания = РасписаниеЗадания;
	
	УстановитьНадписьРасписанияОбмена();
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СпособСинхронизацииДанныхПриИзменении(Элемент)
	
	ВыгружатьНастройкиНаПортал = Истина;
	Если НЕ СохранитьВыгрузитьНастройки() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось сохранить настройку");
		Возврат;
	КонецЕсли;
	
	ОбновитьОтображениеЭлементовФормы();

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииФлажкаВыгрузки(Элемент)
	
	ВыгружатьНастройкиНаПортал = Истина;
	
	ОбновитьОтображениеЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВерсияСчетаПриИзменении(Элемент)
	
	Если ВерсияТоваров = "v.2" И ВерсияСчетов = "v.1" Тогда
		ВерсияСчетов = "v.2";
		ПоказатьПредупреждение(, "Счета v.1 доступны только с товарами v.1");
	КонецЕсли;
	
	ВыгружатьНастройкиНаПортал = Истина; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВерсияТоваровПриИзменении(Элемент)

//	Если ВерсияТоваров = "v.2" И ВерсияСчетов = "v.1" Тогда
//		ВерсияТоваров = "v.1";
//		ПоказатьПредупреждение(, "Счета v.1 доступны только с товарами v.1");
//	КонецЕсли;
	
	ВыгружатьНастройкиНаПортал = Истина;          
	
КонецПроцедуры   

#КонецОбласти


#Область ВыгрузкаНастроек

&НаКлиенте
Процедура ВыгрузкаНастройки(ЗакрытьПоОкончании)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ТекстСообщения             = "Выполняется выгрузка настроек..";
	ПараметрыОжидания.ВыводитьОкноОжидания       = Ложь;
	
	НачатьВыгрузкуНастройки(НастройкаПодключения);
	
	Если ЗакрытьПоОкончании Тогда
		Закрыть();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НачатьВыгрузкуНастройки(НастройкаПодключения)
	
	УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка регистрации изменений в Битрикс24";
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыВыгрузки.Вставить("Наименование"			, Строка(НастройкаПодключения));
	                                 
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.Б24_КС_НастройкиСинхронизации.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти


#Область Прочие

&НаКлиенте
Процедура ОбновитьОтображениеЭлементовФормы()
	
	Если СпособСинхронизацииДанных = "ПоРасписанию" Тогда
		Элементы.НастроитьРасписаниеОбмена.Видимость = НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса();   
		Элементы.ДекорацияРасписаниеФреш.Видимость	 = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса(); 	
	Иначе
		Элементы.НастроитьРасписаниеОбмена.Видимость = Ложь;   
		Элементы.ДекорацияРасписаниеФреш.Видимость	 = Ложь; 	
	КонецЕсли;
	
	Элементы.НастроитьВыгрузкуИнформацииОТоварах.Доступность 	= СинхронизацияТоваров;
	Элементы.НастроитьСинхронизациюКонтрагентов.Доступность 	= СинхронизацияКлиентов;
	Элементы.НастроитьСинхронизациюСчетов.Доступность 			= СинхронизацияСчетов;
	Элементы.ВерсияСчета.Доступность 							= СинхронизацияСчетов;
	Элементы.НастроитьСинхронизациюСделок.Доступность 			= СинхронизацияСделок;
	Элементы.НастроитьСинхронизациюЗаказов.Доступность 			= СинхронизацияЗаказов;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНадписьРасписанияОбмена()
	
	Если РасписаниеРегламентногоЗадания = НеОпределено Тогда
		ТекстЗаголовка = НСтр("ru='Настроить расписание обмена'");
	Иначе
		ТекстЗаголовка = РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Элементы.НастроитьРасписаниеОбмена.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкиНДССтавкаНДСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка) 

	СтандартнаяОбработка = Ложь;
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВыбораСтавкиНДС", ЭтаФорма, ПараметрыОО);
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("БезОтбораПоДате", Истина);
	ОткрытьФорму("Справочник.СтавкиНДС.ФормаВыбора",ПараметрыВыбора, ЭтаФорма,,,,ОО);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСтавкиНДС(Результат, пПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		Элементы.СтавкиНДС.ТекущиеДанные.СтавкаНДС = Результат;     
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
