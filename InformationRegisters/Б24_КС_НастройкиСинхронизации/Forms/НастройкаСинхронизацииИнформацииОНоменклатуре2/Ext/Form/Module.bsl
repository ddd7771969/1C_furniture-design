
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПодключения = Параметры.НастройкаПодключения;	
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииТоваров") Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиСинхронизацииТоваров = НастройкиОбмена.НастройкиСинхронизацииТоваров;	
	НастройкиСинхронизацииТоваров.Свойство("ИсточникИерархии"				, ИсточникИерархии);
	НастройкиСинхронизацииТоваров.Свойство("ВыгружатьПредложения"			, ВыгружатьПредложения);
	НастройкиСинхронизацииТоваров.Свойство("ВыгружатьКартинкиИФайлы"		, ВыгружатьКартинкиИФайлы);  
	НастройкиСинхронизацииТоваров.Свойство("ЗагружатьКартинкиИФайлы"		, ЗагружатьКартинкиИФайлы);
	НастройкиСинхронизацииТоваров.Свойство("ВыгружатьШтрихкоды"				, ВыгружатьШтрихкоды);  
	НастройкиСинхронизацииТоваров.Свойство("ПакетнаяРегистрацияИзменений"	, ПакетнаяРегистрацияИзменений); 
	
	НастройкиСинхронизацииТоваров.Свойство("ВыгружатьОстаткиПоСкладам"			, ВыгружатьОстаткиПоСкладам);
	НастройкиСинхронизацииТоваров.Свойство("ВыгружатьОстаткиСУчетомРезерва"		, ВыгружатьОстаткиСУчетомРезерва);
	НастройкиСинхронизацииТоваров.Свойство("ВыгружатьПланируемуюДатуПоступления", ВыгружатьПланируемуюДатуПоступления);
	
	Если НастройкиСинхронизацииТоваров.Свойство("Склады") Тогда
		Склады = НастройкиСинхронизацииТоваров.Склады;
	КонецЕсли;
	
	Если НастройкиСинхронизацииТоваров.Свойство("Прайсы") Тогда
		Прайсы = НастройкиСинхронизацииТоваров.Прайсы;
	КонецЕсли;  
	
	НастройкиСинхронизацииТоваров.Свойство("ИдентификаторКаталогаТоваров"		, ИдентификаторКаталогаТоваров);
	НастройкиСинхронизацииТоваров.Свойство("ИдентификаторКаталогаПредложений"	, ИдентификаторКаталогаПредложений);
	НастройкиСинхронизацииТоваров.Свойство("ДеревоГрупп"						, ДеревоГрупп);

	Если НЕ ЗначениеЗаполнено(ИсточникИерархии) Тогда
		Если ЗначениеЗаполнено(ДеревоГрупп) Тогда
			ИсточникИерархии = "Произвольная иерархия";	
		Иначе
			ИсточникИерархии = "Иерархия номенклатуры";	
		КонецЕсли;
	КонецЕсли;
	
	ВключенСкладскойУчетБ24 = СкладскойУчетБитрикс24Включен(НастройкаПодключения);	 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗагрузкаСпискаКаталогов();
	
	ПроверитьЗаполнитьКаталогиПоУмолчанию();
	
	ОбновитьОтображениеЭлементов();
	
	Если ВключенСкладскойУчетБ24 И Склады.Количество() > 0 Тогда     
		ПоказатьПредупреждение(,"При включенном складском учете Битрикс24 нельзя выгружать остатки");
		Склады.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ДеревоГрупп" Тогда
		ДеревоГрупп = Параметр;
		Модифицированность = Истина;
		СохранитьВыгрузитьНастройки();
	КонецЕсли;

КонецПроцедуры
	

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по товарам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НужноЗакрытьОкно = Истина;
		
		СохранитьВыгрузитьНастройки(Истина);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		НужноЗакрытьОкно = Истина;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	СохранитьВыгрузитьНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьДеревоГрупп(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДеревоГрупп"	, ДеревоГрупп); 
	ПараметрыФормы.Вставить("Наименование"	, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьРеквизитОбъекта(НастройкаПодключения, "Наименование"));
	
	ОткрытьФорму("Справочник.Б24_КС_ПользовательскиеГруппыТоваров.Форма.НастройкаДереваГрупп", ПараметрыФормы, ЭтаФорма);
КонецПроцедуры


&НаКлиенте
Процедура ВыгрузитьВидыНоменклатуры(Команда)
	
	ВыгрузитьВидыНоменклатурыНаСервере(НастройкаПодключения);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыгрузитьВидыНоменклатурыНаСервере(НастройкаПодключения)
	
	СтруктураНастроек =  Б24_КС_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение2 = "Полная выгрузка свойства ""Категории номенклатуры""";
	
	НаборЗаписей = РегистрыСведений.Б24_КС_ТаблицаИзменений.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ТипДанных.Установить(СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара2);
	НаборЗаписей.Записать();       
	
	НаборЗаписей = РегистрыСведений.Б24_КС_ТаблицаИзменений.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ТипДанных.Установить(СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара2);
	НаборЗаписей.Записать();             
	
	СвойствоВидНоменклатуры = Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.СоздатьПолучитьСвойствоВидаНоменклатурыБитрикс24(НастройкаПодключения, Истина);
	
	//Б24_КС_РегистрацияИзмененийВызовСервера.ПроверитьИДобавитьОбъектВТаблицуИзменений(СтруктураНастроек.НастройкаПодключения, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара2, СвойствоВидНоменклатуры);
	
	Выборка = Справочники.ЗначенияСвойствОбъектов.Выбрать(,СвойствоВидНоменклатуры);
	Пока Выборка.Следующий() Цикл
		//Б24_КС_РегистрацияИзмененийВызовСервера.ПроверитьИДобавитьОбъектВТаблицуИзменений(СтруктураНастроек.НастройкаПодключения, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара2, Выборка.Ссылка);
	КонецЦикла;
	
	СтруктураНастроек.ВремяЗапускаВМиллисекундах = ТекущаяУниверсальнаяДатаВМиллисекундах();	
	
	Б24_КС_ОбщегоНазначенияВызовСервера.СформироватьИОтправить(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара2);
	Б24_КС_ОбщегоНазначенияВызовСервера.СформироватьИОтправить(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара2);

КонецПроцедуры


&НаКлиенте
Процедура НастроитьСинхронизациюТоваров(Команда)
	
	ВидыСущностей 			= Новый Массив;
	ВидыСущностей.Добавить("Товар2");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры    

&НаКлиенте
Процедура НастроитьСинхронизациюПредложений(Команда)
	
	ВидыСущностей 			= Новый Массив;
	ВидыСущностей.Добавить("Предложение");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОбновитьОтображениеЭлементов()  

	ОбновитьОтображениеДанных();
	
	Элементы.ГруппаДеревоГрупп.Видимость = ИсточникИерархии = "Произвольная иерархия";
	
	Элементы.НастроитьСинхронизациюПредложений.Доступность = ВыгружатьПредложения;

	Элементы.Склады.Доступность 		= НЕ ВключенСкладскойУчетБ24; 
	Элементы.ИнфоПоОстаткам.Видимость 	= ВключенСкладскойУчетБ24;   
	
КонецПроцедуры


&НаКлиенте
Процедура ИсточникИерархииПриИзменении(Элемент)
	ОбновитьОтображениеЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьПредложенияПриИзменении(Элемент)
	
	ВыгружатьНастройкиНаПортал = Истина;
	
	ОбновитьОтображениеЭлементов();  
	
КонецПроцедуры

&НаКлиенте
Процедура ТипЦеныПриИзменении(Элемент)
	ПрайсыИзменены = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	СкладыИзменены = Истина;
КонецПроцедуры

#КонецОбласти


#Область Прочие

&НаКлиенте
Функция СохранитьВыгрузитьНастройки(ЗакрытьПоОкончании = Ложь)
	
	НастройкаСохранена = СохранитьНастройкуСервер();
	
	Если НастройкаСохранена И ВыгружатьНастройкиНаПортал Тогда
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НастройкаПодключения, "ПометкаУдаления") = Ложь Тогда
			
			СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
			
			Если СтруктураНастроек = Неопределено Тогда
				ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
				Возврат Ложь;
			КонецЕсли;
			
			ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании);
		Иначе
			Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкаСохранена
	
КонецФункции  

&НаСервере
Функция СохранитьНастройкуСервер()
	
	Если Модифицированность Тогда
			
		СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
		НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
		
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииТоваров") Тогда
			СтруктураНастроек = Новый Структура;	
		Иначе
			СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииТоваров;	
		КонецЕсли;
		
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИсточникИерархии"		, ИсточникИерархии);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьПредложения"	, ВыгружатьПредложения);
			
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьКартинкиИФайлы"		, ВыгружатьКартинкиИФайлы);   
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ЗагружатьКартинкиИФайлы"		, ЗагружатьКартинкиИФайлы);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьШтрихкоды"			, ВыгружатьШтрихкоды);     
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ПакетнаяРегистрацияИзменений", ПакетнаяРегистрацияИзменений);     
			
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьОстаткиПоСкладам"			, ВыгружатьОстаткиПоСкладам);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьОстаткиСУчетомРезерва"		, ВыгружатьОстаткиСУчетомРезерва);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьПланируемуюДатуПоступления"	, ВыгружатьПланируемуюДатуПоступления);
			
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Склады"					, Склады);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Прайсы"					, Прайсы);   
			
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИдентификаторКаталогаТоваров"	, ИдентификаторКаталогаТоваров);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИдентификаторКаталогаПредложений", ИдентификаторКаталогаПредложений);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ДеревоГрупп"				, ДеревоГрупп);
			
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииТоваров") Тогда
			НастройкиОбмена.Вставить("НастройкиСинхронизацииТоваров", СтруктураНастроек); 	
		Иначе
			НастройкиОбмена.НастройкиСинхронизацииТоваров = СтруктураНастроек;
		КонецЕсли;
		
		Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
		Пока Выборка.Следующий() Цикл
			ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
			ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(НастройкиОбмена); 
			ТекСохраненныеНастройки.Записать();	
		КонецЦикла;
		
		Если ПрайсыИзменены Тогда  
			ТипыДанныхДляОбменаСПорталом = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();	
			Для каждого ТекЗначение Из Прайсы Цикл 
				РегистрыСведений.Б24_КС_ТаблицаИзменений.ДобавитьОбъектВТаблицуИзменений(НастройкаПодключения, ТипыДанныхДляОбменаСПорталом.Прайс, ТекЗначение.Значение);  
			КонецЦикла;
		КонецЕсли;
		
		ОбновитьПовторноИспользуемыеЗначения();
		
		Модифицированность = Ложь;
		
	КонецЕсли;
	
	Возврат Истина
		
КонецФункции  

&НаКлиенте
Процедура ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании)
	
	ДлительнаяОперацияЗавершение = Новый ОписаниеОповещения("ПослеВыгрузкиНастроек", ЭтаФорма, ЗакрытьПоОкончании);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ТекстСообщения             = "Выполняется выгрузка настроек..";
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания       = Истина;
	ПараметрыОжидания.ВыводитьСообщения          = Истина;
	
	ДлительнаяОперация = НачатьВыгрузкуНастройки(СтруктураНастроек);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ДлительнаяОперацияЗавершение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыгрузкиНастроек(Результат, ПараметрыВыгрузки) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность 			= Ложь;
		ВыгружатьНастройкиНаПортал 	= Ложь;
		
		Если ПараметрыВыгрузки = Истина Тогда
			Закрыть();	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьВыгрузкуНастройки(СтруктураНастроек)
	
	УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка регистрации изменений в Битрикс24";
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыВыгрузки.Вставить("Наименование"			, Строка(НастройкаПодключения));
	                                 
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.Б24_КС_НастройкиСинхронизации.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ЗагрузкаСпискаКаталогов()
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка каталогов на портале.";
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/catalog.catalog.list", ""); 
	Если СтруктураОтвета <> Неопределено Тогда
		
		result = СтруктураОтвета.Получить("result"); 
		Если result <> Неопределено Тогда
			
			Каталоги = result.Получить("catalogs"); 
			Если Каталоги <> Неопределено Тогда

				Элементы.ИдентификаторКаталогаТоваров.СписокВыбора.Очистить();
				Элементы.ИдентификаторКаталогаПредложений.СписокВыбора.Очистить(); 
				
				Для каждого ТекЭлемент Из Каталоги Цикл     
					Элементы.ИдентификаторКаталогаТоваров.СписокВыбора.Добавить(Формат(ТекЭлемент.Получить("id"),"ЧГ=0"), ТекЭлемент.Получить("name"));
					Элементы.ИдентификаторКаталогаПредложений.СписокВыбора.Добавить(Формат(ТекЭлемент.Получить("id"),"ЧГ=0"), ТекЭлемент.Получить("name"));
				КонецЦикла;  
				
			КонецЕсли;
			
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнитьКаталогиПоУмолчанию() 
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторКаталогаТоваров) Тогда 
		Для каждого ТекЭлемент Из Элементы.ИдентификаторКаталогаТоваров.СписокВыбора Цикл
			Если СтрНайти(ТекЭлемент.Представление, "(предложения)") = 0 Тогда
				ИдентификаторКаталогаТоваров = ТекЭлемент.Значение; 
				Модифицированность = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторКаталогаПредложений) Тогда
		Для каждого ТекЭлемент Из Элементы.ИдентификаторКаталогаПредложений.СписокВыбора Цикл
			Если СтрНайти(ТекЭлемент.Представление, "(предложения)") > 0 Тогда
				ИдентификаторКаталогаПредложений = ТекЭлемент.Значение;    
				Модифицированность = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПослеВводаТонкихНастроек(Результат, пПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Свойство("ВыгружатьНастройкиНаПортал") Тогда
			ВыгружатьНастройкиНаПортал = Результат.ВыгружатьНастройкиНаПортал;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СкладскойУчетБитрикс24Включен(НастройкаПодключения)
	
	Результат = Ложь;
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Истина);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	ТелоHTTPЗапроса = "";	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/catalog.document.mode.status", ТелоHTTPЗапроса); 
	Если СтруктураОтвета <> Неопределено Тогда
		result = СтруктураОтвета.Получить("result");
		Результат = result = "Y"	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

#КонецОбласти

