
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновлениеЭлементовФормы();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПодключения 	= Параметры.НастройкаПодключения;	
	ТипИсточника 			= Параметры.ТипИсточника;
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	НастройкиСинхронизацииЗаказов = НастройкиОбмена.НастройкиСинхронизацииЗаказов;
	
	Если НастройкиСинхронизацииЗаказов.Свойство("ИнформацияОДоставках") Тогда
		
		ИнформацияОДоставках = НастройкиСинхронизацииЗаказов.ИнформацияОДоставках;
		
		ИсточникСлужбДоставкиЗаказов 	= ИнформацияОДоставках.ИсточникСлужбДоставкиЗаказов;
		ИсточникСлужбДоставкиОтгрузок 	= ИнформацияОДоставках.ИсточникСлужбДоставкиОтгрузок;
		
		СвойствоЗаказа 		= ИнформацияОДоставках.СвойствоЗаказа;
		СвойствоОтгрузки 	= ИнформацияОДоставках.СвойствоОтгрузки;
		
		СоответствияСЗначениямиСвойствЗаказа.Загрузить(ИнформацияОДоставках.СоответствияСЗначениямиСвойствЗаказа);
		СоответствияСпособовДоставкиЗаказа.Загрузить(ИнформацияОДоставках.СоответствияСпособовДоставкиЗаказа);   
		СоответствияСЗначениямиСвойствОтгрузки.Загрузить(ИнформацияОДоставках.СоответствияСЗначениямиСвойствОтгрузки);  
		СоответствияСпособовДоставкиОтгрузки.Загрузить(ИнформацияОДоставках.СоответствияСпособовДоставкиОтгрузки);  
		
	Иначе
		ИсточникСлужбДоставкиЗаказов 	= "СпособыДоставкиЗаказов";
		ИсточникСлужбДоставкиОтгрузок 	= "СпособыДоставкиОтгрузок";
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
		
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по доставкам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда		
		Если (ТипИсточника = "Заказ" И ИсточникСлужбДоставкиЗаказов = "СвойствоЗаказов" И НЕ ЗначениеЗаполнено(СвойствоЗаказа))
		ИЛИ (ТипИсточника = "Отгрузка" И ИсточникСлужбДоставкиОтгрузок = "СвойствоОтгрузок" И НЕ ЗначениеЗаполнено(СвойствоОтгрузки))	Тогда
			ПоказатьПредупреждение(,"Не указано свойство заказов или отгрузок. Запись настроек невозможна");
		Иначе		
			НужноЗакрытьОкно = Истина;
			СохранениеНастроек();
        		Закрыть();
       		 КонецЕсли;
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		НужноЗакрытьОкно = Истина;
		Закрыть();
    КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	Если (ТипИсточника = "Заказ" И ИсточникСлужбДоставкиЗаказов = "СвойствоЗаказов" И НЕ ЗначениеЗаполнено(СвойствоЗаказа))
	ИЛИ (ТипИсточника = "Отгрузка" И ИсточникСлужбДоставкиОтгрузок = "СвойствоОтгрузок" И НЕ ЗначениеЗаполнено(СвойствоОтгрузки))	Тогда
		ПоказатьПредупреждение(,"Не указано свойство счета. Запись настроек невозможна");
	Иначе
		СохранениеНастроек();
		Закрыть();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ИсточникСлужбДоставкиЗаказаПриИзменении(Элемент)
	ОбновлениеЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ИсточникСлужбДоставкиОтгрузокПриИзменении(Элемент)
	ОбновлениеЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСлужбыДоставки(Команда)
		
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка статусов заказов.";
	
	ТелоHTTPЗапроса = "";	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/sale.deliveryservices.getactivelist", ТелоHTTPЗапроса); 
	
	Если СтруктураОтвета <> Неопределено Тогда
		
		result = СтруктураОтвета.Получить("result");
		
		Если result <> Неопределено Тогда
			
			deliveryServices =  result.Получить("deliveryServices");
			
			Если deliveryServices <> Неопределено Тогда
				
				мТаблицы = Новый Массив;
				мТаблицы.Добавить(СоответствияСЗначениямиСвойствЗаказа);
				мТаблицы.Добавить(СоответствияСЗначениямиСвойствОтгрузки);
				мТаблицы.Добавить(СоответствияСпособовДоставкиЗаказа);
				мТаблицы.Добавить(СоответствияСпособовДоставкиОтгрузки);
				
				Для Каждого delivery Из deliveryServices Цикл 
					
					НазваниеСлужбы 		= delivery.Получить("name");
					ИдСлужбы 			= Формат(delivery.Получить("id"),"ЧГ=0");
					
					Для Каждого ТекТаблица Из мТаблицы Цикл
						
						НайденныеСтроки = ТекТаблица.НайтиСтроки(Новый Структура("ИдСлужбы", ИдСлужбы));
						
						Если НайденныеСтроки.количество() = 0 Тогда
							НоваяЗапись = ТекТаблица.Добавить();
							НоваяЗапись.ИдСлужбы			= ИдСлужбы;
							НоваяЗапись.НазваниеСлужбы 		= НазваниеСлужбы;
						Иначе
							Для Каждого ТекСтрокаТаблицы Из ТекТаблица Цикл
								Если ТекСтрокаТаблицы.ИдСлужбы 		= ИдСлужбы Тогда
									ТекСтрокаТаблицы.НазваниеСлужбы = НазваниеСлужбы; 
									Прервать;
								КонецЕсли;
							КонецЦикла;
						КонецЕсли; 						
					КонецЦикла;
					
				КонецЦикла;

			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область Прочее

&НаСервере
Процедура СохранениеНастроек()
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииЗаказов") Тогда
		СтруктураНастроек = Новый Структура;	
	Иначе
		СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииЗаказов;	
	КонецЕсли;
	
	СтруктураИнформацииОСтатусах = Новый Структура;
	СтруктураИнформацииОСтатусах.Вставить("ИсточникСлужбДоставкиЗаказов"	, ИсточникСлужбДоставкиЗаказов);
	СтруктураИнформацииОСтатусах.Вставить("ИсточникСлужбДоставкиОтгрузок"	, ИсточникСлужбДоставкиОтгрузок);
	
	СтруктураИнформацииОСтатусах.Вставить("СвойствоЗаказа"		, СвойствоЗаказа);
	СтруктураИнформацииОСтатусах.Вставить("СвойствоОтгрузки"	, СвойствоОтгрузки);		

	СтруктураИнформацииОСтатусах.Вставить("СоответствияСЗначениямиСвойствЗаказа"	, СоответствияСЗначениямиСвойствЗаказа.Выгрузить());
	СтруктураИнформацииОСтатусах.Вставить("СоответствияСЗначениямиСвойствОтгрузки"	, СоответствияСЗначениямиСвойствОтгрузки.Выгрузить());
	СтруктураИнформацииОСтатусах.Вставить("СоответствияСпособовДоставкиЗаказа"		, СоответствияСпособовДоставкиЗаказа.Выгрузить());
	СтруктураИнформацииОСтатусах.Вставить("СоответствияСпособовДоставкиОтгрузки"	, СоответствияСпособовДоставкиОтгрузки.Выгрузить());
	
	РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИнформацияОДоставках"	, СтруктураИнформацииОСтатусах);
			
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииЗаказов") Тогда
		НастройкиОбмена.Вставить("НастройкиСинхронизацииЗаказов", СтруктураНастроек); 	
	Иначе
		НастройкиОбмена.НастройкиСинхронизацииЗаказов = СтруктураНастроек;
	КонецЕсли;
	
	Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
	Пока Выборка.Следующий() Цикл
		ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
		ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(НастройкиОбмена); 
		ТекСохраненныеНастройки.Записать();	
	КонецЦикла;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеЭлементовФормы()

	Если ТипИсточника = "Заказ" Тогда
		
		Элементы.СтраницыТипыИсточников.ТекущаяСтраница = Элементы.Заказы;		
	
		Если ИсточникСлужбДоставкиЗаказов = "СвойствоЗаказов" Тогда			
			Элементы.СтраницыЗаказ.ТекущаяСтраница = Элементы.СтраницаСвойствоЗаказа;			
		ИначеЕсли ИсточникСлужбДоставкиЗаказов = "СпособыДоставкиЗаказов" Тогда 
			Элементы.СтраницыЗаказ.ТекущаяСтраница = Элементы.СтраницаСпособыДоставкиЗаказа;
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = "Отгрузка" Тогда
		
		Элементы.СтраницыТипыИсточников.ТекущаяСтраница = Элементы.Отгрузки;		

		Если ИсточникСлужбДоставкиОтгрузок = "СвойствоОтгрузок" Тогда			
			Элементы.СтраницыОтгрузка.ТекущаяСтраница = Элементы.СтраницаСвойствоОтгрузки;		
		ИначеЕсли ИсточникСлужбДоставкиОтгрузок = "СпособыДоставкиОтгрузок" Тогда 
			Элементы.СтраницыОтгрузка.ТекущаяСтраница = Элементы.СтраницаСпособыДоставкиОтгрузки;
		КонецЕсли;
			
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти
