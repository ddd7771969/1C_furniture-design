
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПодключения 	= Параметры.НастройкаПодключения;	
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииКлиентов") Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиСинхронизацииКлиентов = НастройкиОбмена.НастройкиСинхронизацииКлиентов;
	
	НастройкиСинхронизацииКлиентов.Свойство("ВидМеткиДляСквознойАналитики"		, ВидМеткиДляСквознойАналитики);
	НастройкиСинхронизацииКлиентов.Свойство("ВыгружатьМеткуСквознойАналитики"	, ВыгружатьМеткуСквознойАналитики);
	НастройкиСинхронизацииКлиентов.Свойство("СвойствоДляСквознойАналитики"		, СвойствоДляСквознойАналитики);
	НастройкиСинхронизацииКлиентов.Свойство("ИсточникДляСквознойАналитики"		, ИсточникДляСквознойАналитики);
	
	НастройкиСинхронизацииКлиентов.Свойство("ВыгружатьВзаиморасчетыКлиентов"	, ВыгружатьВзаиморасчетыКлиентов);
	
	НастройкиСинхронизацииКлиентов.Свойство("ВыгружатьФайлыКлиентов", ВыгружатьФайлыКлиентов);
	
	НастройкиСинхронизацииКлиентов.Свойство("СвойствоФайловБ24Компаний", СвойствоФайловБ24Компаний);
	Если ЗначениеЗаполнено(СвойствоФайловБ24Компаний) Тогда
		мДанные = СтрРазделить(СвойствоФайловБ24Компаний, ";");    
		Элементы.СвойствоФайловКомпаний.СписокВыбора.Добавить(мДанные[0], мДанные[1]);
		СвойствоФайловБ24Компаний = мДанные[0];
	КонецЕсли;
	
	НастройкиСинхронизацииКлиентов.Свойство("СвойствоФайловБ24Контактов", СвойствоФайловБ24Контактов);
	Если ЗначениеЗаполнено(СвойствоФайловБ24Контактов) Тогда
		мДанные = СтрРазделить(СвойствоФайловБ24Контактов, ";");    
		Элементы.СвойствоФайловКонтактов.СписокВыбора.Добавить(мДанные[0], мДанные[1]);
		СвойствоФайловБ24Контактов = мДанные[0];
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ФиксироватьПервичныйИнтерес") Тогда
		Элементы.ВидМеткиДляСквознойАналитики.СписокВыбора.Добавить("Канал", "Канал");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по контрагентам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НужноЗакрытьОкно = Истина;
		
		СохранитьВыгрузитьНастройки(Истина);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		НужноЗакрытьОкно = Истина;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВыгружатьМеткуСквознойАналитикиПриИзменении(Элемент)
	
	Если ВидМеткиДляСквознойАналитики = "" Тогда ВидМеткиДляСквознойАналитики = "По умолчанию" КонецЕсли;
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидМеткиДляСквознойАналитикиПриИзменении(Элемент)
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры


&НаКлиенте
Процедура ВыгружатьФайлыКлиентовПриИзменении(Элемент)
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры 


&НаКлиенте
Процедура СвойствоДокументовКонтактовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	
	КлючиПользовательскиеПоляСущностиПортала = ПолучитьКлючиПользовательскиеПоляСущностиПортала("Контакт");
	Для Каждого ТекСтрока Из КлючиПользовательскиеПоляСущностиПортала.ПользовательскиеПоля Цикл
		Если ТекСтрока.Множественное И ТекСтрока.ТипПоля = "file" Тогда
			Элемент.СписокВыбора.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.Описание); 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры      

&НаКлиенте
Процедура СвойствоДокументовКомпанийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();

	КлючиПользовательскиеПоляСущностиПортала = ПолучитьКлючиПользовательскиеПоляСущностиПортала("Компания");
	Для Каждого ТекСтрока Из КлючиПользовательскиеПоляСущностиПортала.ПользовательскиеПоля Цикл
		Если ТекСтрока.Множественное И ТекСтрока.ТипПоля = "file" Тогда
			Элемент.СписокВыбора.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.Описание); 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	СохранитьВыгрузитьНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюБанковскихСчетов(Команда)
	
	ВидыСущностей 			= Новый Массив;
	ВидыСущностей.Добавить("Банковский счет");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюКомпаний(Команда)
	
	ВидыСущностей 			= Новый Массив;
	ВидыСущностей.Добавить("Компания");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюКонтактов(Команда)
	
	ВидыСущностей 			= Новый Массив;
	ВидыСущностей.Добавить("Контакт");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюРеквизитов(Команда)
	
	ВидыСущностей 			= Новый Массив;
	ВидыСущностей.Добавить("Реквизит");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти


#Область Прочие

&НаКлиенте
Функция СохранитьВыгрузитьНастройки(ЗакрытьПоОкончании = Ложь)
	
	НастройкаСохранена = СохранитьНастройкуСервер();
	
	Если НастройкаСохранена И ВыгружатьНастройкиНаПортал Тогда
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НастройкаПодключения, "ПометкаУдаления") = Ложь Тогда
			
			СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
			
			Если СтруктураНастроек = Неопределено Тогда
				ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
				Возврат Ложь;
			КонецЕсли;
			
			ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании);
		Иначе
			Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкаСохранена
	
КонецФункции  

&НаСервере
Функция СохранитьНастройкуСервер()
	
	Если Модифицированность Тогда
			
		СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
		НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
		
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииКлиентов") Тогда
			СтруктураНастроек = Новый Структура;	
		Иначе
			СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииКлиентов;	
		КонецЕсли;
				
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВидМеткиДляСквознойАналитики"	, ВидМеткиДляСквознойАналитики);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьМеткуСквознойАналитики"	, ВыгружатьМеткуСквознойАналитики);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СвойствоДляСквознойАналитики"	, СвойствоДляСквознойАналитики);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИсточникДляСквознойАналитики"	, ИсточникДляСквознойАналитики);
			
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьВзаиморасчетыКлиентов"	, ВыгружатьВзаиморасчетыКлиентов);
			
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьФайлыКлиентов"		, ВыгружатьФайлыКлиентов);
			
			НайденноеЗначение = Элементы.СвойствоФайловКомпаний.СписокВыбора.НайтиПоЗначению(СвойствоФайловБ24Компаний);
			Если НайденноеЗначение <> Неопределено Тогда
				РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СвойствоФайловБ24Компаний", НайденноеЗначение.Значение + ";" + НайденноеЗначение.Представление);
			Иначе
				РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СвойствоФайловБ24Компаний", "");	
			КонецЕсли;
			
			НайденноеЗначение = Элементы.СвойствоФайловКонтактов.СписокВыбора.НайтиПоЗначению(СвойствоФайловБ24Контактов);
			Если НайденноеЗначение <> Неопределено Тогда
				РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СвойствоФайловБ24Контактов", НайденноеЗначение.Значение + ";" + НайденноеЗначение.Представление);
			Иначе
				РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СвойствоФайловБ24Контактов", "");	
			КонецЕсли;
			
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииКлиентов") Тогда
			НастройкиОбмена.Вставить("НастройкиСинхронизацииКлиентов", СтруктураНастроек); 	
		Иначе
			НастройкиОбмена.НастройкиСинхронизацииКлиентов = СтруктураНастроек;
		КонецЕсли;
		
		Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
		Пока Выборка.Следующий() Цикл
			ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
			ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(НастройкиОбмена); 
			ТекСохраненныеНастройки.Записать();	
		КонецЦикла;
		
		ОбновитьПовторноИспользуемыеЗначения();
		
		Модифицированность = Ложь;
		
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции  

&НаКлиенте
Процедура ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании)
	
	ДлительнаяОперацияЗавершение = Новый ОписаниеОповещения("ПослеВыгрузкиНастроек", ЭтаФорма, ЗакрытьПоОкончании);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ТекстСообщения             = "Выполняется выгрузка настроек..";
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания       = Истина;
	ПараметрыОжидания.ВыводитьСообщения          = Истина;
	
	ДлительнаяОперация = НачатьВыгрузкуНастройки(СтруктураНастроек);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ДлительнаяОперацияЗавершение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыгрузкиНастроек(Результат, ПараметрыВыгрузки) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность 			= Ложь;
		ВыгружатьНастройкиНаПортал 	= Ложь;
		
		Если ПараметрыВыгрузки = Истина Тогда
			Закрыть();	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьВыгрузкуНастройки(СтруктураНастроек)
	
	УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка регистрации изменений в Битрикс24";
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыВыгрузки.Вставить("Наименование"			, Строка(НастройкаПодключения));
	                                 
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.Б24_КС_НастройкиСинхронизации.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
	
КонецФункции


&НаКлиенте
Процедура ОбновитьОтображениеЭлементов()
	
	Элементы.ВидМеткиДляСквознойАналитики.Видимость = ВыгружатьМеткуСквознойАналитики;
	Если ВыгружатьМеткуСквознойАналитики Тогда
		
		Если ВидМеткиДляСквознойАналитики = "По умолчанию" Тогда
			Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
			Элементы.СвойствоСквознойАналитики.Видимость 	= Ложь;
		ИначеЕсли ВидМеткиДляСквознойАналитики = "Канал" Тогда
			Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
			Элементы.СвойствоСквознойАналитики.Видимость 	= Ложь;
		ИначеЕсли ВидМеткиДляСквознойАналитики = "Свойство" Тогда
			Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
			Элементы.СвойствоСквознойАналитики.Видимость 	= Истина;
		КонецЕсли;	
		
	Иначе
		Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
		Элементы.СвойствоСквознойАналитики.Видимость 	= Ложь;
	КонецЕсли;	
	
	Элементы.ГруппаФайлПодробно.Видимость = ВыгружатьФайлыКлиентов;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПослеВводаТонкихНастроек(Результат, пПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Свойство("ВыгружатьНастройкиНаПортал") Тогда
			ВыгружатьНастройкиНаПортал = Результат.ВыгружатьНастройкиНаПортал;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Функция ПолучитьКлючиПользовательскиеПоляСущностиПортала(ВидСущности)
	
	Возврат РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьКлючиПользовательскиеПоляСущностиПортала(НастройкаПодключения, ВидСущности);

КонецФункции  

#КонецОбласти
