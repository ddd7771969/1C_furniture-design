
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПодключения 	= Параметры.НастройкаПодключения;	
	ВерсияСчетов			= Параметры.ВерсияСчетов;
	
	Элементы.Подразделение.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьПодразделения");
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииСчетов") Тогда
		Возврат;                         
	КонецЕсли;
	
	НастройкиСинхронизацииСчетов = НастройкиОбмена.НастройкиСинхронизацииСчетов;
	
	НастройкиСинхронизацииСчетов.Свойство("ДатаНачалаЗагрузкиСчетов"	, ДатаНачалаЗагрузкиСчетов);
	НастройкиСинхронизацииСчетов.Свойство("ДатаНачалаВыгрузкиСчетов"	, ДатаНачалаВыгрузкиСчетов);
	НастройкиСинхронизацииСчетов.Свойство("Подразделение"				, Подразделение);
	
	НастройкиСинхронизацииСчетов.Свойство("ИсточникДатыДокумента"		, ИсточникДатыДокумента);
	НастройкиСинхронизацииСчетов.Свойство("ИсточникНомераДокумента"		, ИсточникНомераДокумента);
	
	НастройкиСинхронизацииСчетов.Свойство("ВидМеткиДляСквознойАналитики"		, ВидМеткиДляСквознойАналитики);
	НастройкиСинхронизацииСчетов.Свойство("ВыгружатьМеткуСквознойАналитики"		, ВыгружатьМеткуСквознойАналитики);
	НастройкиСинхронизацииСчетов.Свойство("СвойствоДляСквознойАналитики"		, СвойствоДляСквознойАналитики);
	НастройкиСинхронизацииСчетов.Свойство("ИсточникДляСквознойАналитики"		, ИсточникДляСквознойАналитики);
	
	НастройкиСинхронизацииСчетов.Свойство("ВыгружатьФайлыСчетов", ВыгружатьФайлыСчетов);
	
	НастройкиСинхронизацииСчетов.Свойство("СвойствоФайловБ24Счетов", СвойствоФайловБ24Счетов);
	Если ЗначениеЗаполнено(СвойствоФайловБ24Счетов) Тогда
		мДанные = СтрРазделить(СвойствоФайловБ24Счетов, ";");    
		Элементы.СвойствоФайловБ24Счетов.СписокВыбора.Добавить(мДанные[0], мДанные[1]);
		СвойствоФайловБ24Счетов = мДанные[0];
	КонецЕсли;
	
	ПечатныеФормыСчетов.Загрузить(НастройкиСинхронизацииСчетов.ПечатныеФормыСчетов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
		
	Если НЕ ВерсияСчетов = "v.2" Тогда
			
		ЕстьДляКомпаний = Ложь;
		ЕстьДляКонтактов = Ложь;		
		
		Для каждого ТекСтрока Из ПечатныеФормыСчетов Цикл
			Если ТекСтрока.ТипКлиента = "Компания" И ТекСтрока.Используется = Истина Тогда
				ЕстьДляКомпаний = Истина;
			КонецЕсли;		
			Если ТекСтрока.ТипКлиента = "Контакт" И ТекСтрока.Используется = Истина Тогда
				ЕстьДляКонтактов = Истина;
			КонецЕсли;					
		КонецЦикла;
		
		Если НЕ ЕстьДляКомпаний ИЛИ НЕ ЕстьДляКонтактов Тогда	
			ПоказатьОповещениеПользователя("Важно",,"Не указаны используемые печатные формы для компаний и контактов",, СтатусОповещенияПользователя.Важное);			
		КонецЕсли;	
		
	КонецЕсли;	
	
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по счетам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НужноЗакрытьОкно = Истина;
		
		СохранитьВыгрузитьНастройки(Истина);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		НужноЗакрытьОкно = Истина;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПечатныеФормыСчетовТипКлиентаПриИзменении(Элемент)
		
	ТекущиеДанные = Элементы.ПечатныеФормыСчетов.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если ТекущиеДанные.Используется	Тогда
			
			УжеЕстьИспользуемый = Ложь;
			
			Для Каждого ТекСтрока Из ПечатныеФормыСчетов Цикл
				
				Если ТекСтрока.ТипКлиента = ТекущиеДанные.ТипКлиента И ТекСтрока.Организация = ТекущиеДанные.Организация  И ТекСтрока.ИдПечатнойФормы <> ТекущиеДанные.ИдПечатнойФормы И  ТекСтрока.Используется = ТекущиеДанные.Используется Тогда
					УжеЕстьИспользуемый = Истина;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если УжеЕстьИспользуемый Тогда
				ТекущиеДанные.Используется = Ложь;	
				ПоказатьОповещениеПользователя(,,"Уже есть используемая печатная форма для этого типа клиента");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПечатныеФормыСчетовОрганизацияПриИзменении(Элемент)
			
	ТекущиеДанные = Элементы.ПечатныеФормыСчетов.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если ТекущиеДанные.Используется	Тогда
			
			УжеЕстьИспользуемый = Ложь;
			
			Для Каждого ТекСтрока Из ПечатныеФормыСчетов Цикл
				
				Если ТекСтрока.ТипКлиента = ТекущиеДанные.ТипКлиента И ТекСтрока.Организация = ТекущиеДанные.Организация  И ТекСтрока.ИдПечатнойФормы <> ТекущиеДанные.ИдПечатнойФормы И  ТекСтрока.Используется = ТекущиеДанные.Используется Тогда
					УжеЕстьИспользуемый = Истина;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если УжеЕстьИспользуемый Тогда
				ТекущиеДанные.Организация = Неопределено;	
				ПоказатьОповещениеПользователя(,,"Уже есть используемая печатная форма для этого типа клиента");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;


КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьМеткуСквознойАналитикиПриИзменении(Элемент)
	
	Если ВидМеткиДляСквознойАналитики = "" Тогда ВидМеткиДляСквознойАналитики = "По умолчанию" КонецЕсли;
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидМеткиДляСквознойАналитикиПриИзменении(Элемент)
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьФайлыСчетовПриИзменении(Элемент)
		
	ОбновитьОтображениеЭлементов();

КонецПроцедуры     

&НаКлиенте
Процедура СвойствоФайловБ24СчетовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	Элемент.СписокВыбора.Очистить();
	
	КлючиПользовательскиеПоляСущностиПортала = ПолучитьКлючиПользовательскиеПоляСущностиПортала("Счет2");
	Для Каждого ТекСтрока Из КлючиПользовательскиеПоляСущностиПортала.ПользовательскиеПоля Цикл
		Если ТекСтрока.Множественное И ТекСтрока.ТипПоля = "file" Тогда
			Элемент.СписокВыбора.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.Описание); 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры 

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьСопоставлениеСтатусов(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыФормы.Вставить("ВерсияСчетов"			, ВерсияСчетов);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаСопоставленияСтатусовСчетов", ПараметрыФормы, ЭтаФорма, ,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	СохранитьВыгрузитьНастройки(Истина);

КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюКонтрагентов(Команда)
			
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыФормы.Вставить("ТипЗапуска"			, "Загрузка");
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.НастройкаСинхронизацииКонтрагентов", ПараметрыФормы, ЭтаФорма, ,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПечатныеФормыСчетаСПортала(Команда)
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка печатных форм счета для клиентов и компаний.";
	
	ТелоHTTPЗапроса = "";
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.persontype.list", ТелоHTTPЗапроса); 
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОтвета.Получить("result") = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ИдТипаКомпании = "";
	
	Для каждого ТекЭлемент Из СтруктураОтвета.Получить("result") Цикл 
		
		НаименованиеТипа = ТекЭлемент.Получить("NAME");
		
		Если НаименованиеТипа = "CRM_COMPANY" Тогда
			ИдТипаКомпании = Формат(ТекЭлемент.Получить("ID"),"ЧГ=0"); 	
		КонецЕсли;
		
	КонецЦикла;
		
	ТелоHTTPЗапроса = "&filter[ACTIVE]=Y";	
	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.paysystem.list", ТелоHTTPЗапроса); 
	
	Если СтруктураОтвета <> Неопределено Тогда
		
		Если СтруктураОтвета.Получить("result") <> Неопределено Тогда
		
			Для каждого ТекЭлемент Из СтруктураОтвета.Получить("result") Цикл
				
				Если  СтрНайти(ТекЭлемент.Получить("HANDLER"),"bill") = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НайденныеСтроки = ПечатныеФормыСчетов.НайтиСтроки(Новый Структура("ИдПечатнойФормы", ТекЭлемент.Получить("ID")));
				
				Если НайденныеСтроки.Количество() = 0 Тогда
					НоваяСтрока = ПечатныеФормыСчетов.Добавить();
					НоваяСтрока.НаименованиеПечатнойФормы 	= ТекЭлемент.Получить("NAME");
					НоваяСтрока.ИдПечатнойФормы 			= Формат(ТекЭлемент.Получить("ID"), "ЧГ=0");
					НоваяСтрока.ТипКлиента					= ?(Формат(ТекЭлемент.Получить("PERSON_TYPE_ID"),"ЧГ=0") = ИдТипаКомпании, "Компания", "Контакт");
				КонецЕсли;
				
			КонецЦикла;
		
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюСчетов(Команда)
	
	мРежимыЗаписи = Новый Массив;
	мРежимыЗаписи.Добавить("Только записывать");
	мРежимыЗаписи.Добавить("Проводить");
	
	ВидыСущностей = Новый Массив;

	Если ВерсияСчетов = "v.1" Тогда
		мРежимыЗаписи.Добавить("Проводить оплаченные");
		ВидыСущностей.Добавить("Счет");
	Иначе
		ВидыСущностей.Добавить("Счет2");
	КонецЕсли;
	
	ВидыСущностей 			= Новый Массив;
	ВидыСущностей.Добавить("Счет");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	ПередаваемыеПараметры.Вставить("РежимыЗаписи"			, мРежимыЗаписи);
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти


#Область Прочие

&НаКлиенте
Функция СохранитьВыгрузитьНастройки(ЗакрытьПоОкончании = Ложь)
	
	НастройкаСохранена = СохранитьНастройкуСервер();
	
	Если НастройкаСохранена И ВыгружатьНастройкиНаПортал Тогда
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НастройкаПодключения, "ПометкаУдаления") = Ложь Тогда
			
			СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
			
			Если СтруктураНастроек = Неопределено Тогда
				ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
				Возврат Ложь;
			КонецЕсли;
			
			ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании);
		Иначе
			Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкаСохранена
	
КонецФункции  

&НаСервере
Функция СохранитьНастройкуСервер()
	
	Если Модифицированность Тогда
			
		СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
		НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
		
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииСчетов") Тогда
			СтруктураНастроек = Новый Структура;	
		Иначе
			СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииСчетов;	
		КонецЕсли;
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ДатаНачалаЗагрузкиСчетов", ДатаНачалаЗагрузкиСчетов);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ДатаНачалаВыгрузкиСчетов", ДатаНачалаВыгрузкиСчетов);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Подразделение"			, Подразделение);
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИсточникДатыДокумента"	, ИсточникДатыДокумента);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИсточникНомераДокумента"	, ИсточникНомераДокумента);
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ПечатныеФормыСчетов"		, ПечатныеФормыСчетов.Выгрузить());
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВидМеткиДляСквознойАналитики"	, ВидМеткиДляСквознойАналитики);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьМеткуСквознойАналитики"	, ВыгружатьМеткуСквознойАналитики);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СвойствоДляСквознойАналитики"	, СвойствоДляСквознойАналитики);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИсточникДляСквознойАналитики"	, ИсточникДляСквознойАналитики);
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьФайлыСчетов", ВыгружатьФайлыСчетов);
		
		НайденноеЗначение = Элементы.СвойствоФайловБ24Счетов.СписокВыбора.НайтиПоЗначению(СвойствоФайловБ24Счетов);
		Если НайденноеЗначение <> Неопределено Тогда
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СвойствоФайловБ24Счетов", НайденноеЗначение.Значение + ";" + НайденноеЗначение.Представление);
		Иначе
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СвойствоФайловБ24Счетов", "");	
		КонецЕсли;
		
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииСчетов") Тогда
			НастройкиОбмена.Вставить("НастройкиСинхронизацииСчетов", СтруктураНастроек); 	
		Иначе
			НастройкиОбмена.НастройкиСинхронизацииСчетов = СтруктураНастроек;
		КонецЕсли;
		
		Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
		Пока Выборка.Следующий() Цикл
			ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
			ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(НастройкиОбмена); 
			ТекСохраненныеНастройки.Записать();	
		КонецЦикла;
		
		ОбновитьПовторноИспользуемыеЗначения();
		
		Модифицированность = Ложь;
		
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции  

&НаКлиенте
Процедура ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании)
	
	ДлительнаяОперацияЗавершение = Новый ОписаниеОповещения("ПослеВыгрузкиНастроек", ЭтаФорма, ЗакрытьПоОкончании);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ТекстСообщения             = "Выполняется выгрузка настроек..";
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания       = Истина;
	ПараметрыОжидания.ВыводитьСообщения          = Истина;
	
	ДлительнаяОперация = НачатьВыгрузкуНастройки(СтруктураНастроек);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ДлительнаяОперацияЗавершение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыгрузкиНастроек(Результат, ПараметрыВыгрузки) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность 			= Ложь;
		ВыгружатьНастройкиНаПортал 	= Ложь;
		
		Если ПараметрыВыгрузки = Истина Тогда
			Закрыть();	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьВыгрузкуНастройки(СтруктураНастроек)
	
	УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка регистрации изменений в Битрикс24";
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыВыгрузки.Вставить("Наименование"			, Строка(НастройкаПодключения));
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.Б24_КС_НастройкиСинхронизации.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
	
КонецФункции


&НаКлиенте
Процедура ПослеВводаТонкихНастроек(Результат, пПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Свойство("ВыгружатьНастройкиНаПортал") Тогда
			ВыгружатьНастройкиНаПортал = Результат.ВыгружатьНастройкиНаПортал;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры



&НаКлиенте
Процедура ОбновитьОтображениеЭлементов()
	
	Если ВерсияСчетов = "v.2" Тогда
		Элементы.УстановкаИспользуемыхПечатныхФормСчетов.Видимость = Ложь;
		Элементы.ГруппаСквознаяАналитика.Видимость = Истина;	  
		Элементы.ГруппаФайлы.Видимость = Истина;	  
	Иначе
		Элементы.УстановкаИспользуемыхПечатныхФормСчетов.Видимость = Истина;	  
		Элементы.ГруппаСквознаяАналитика.Видимость = Ложь;	  
		Элементы.ГруппаФайлы.Видимость = Ложь;	  
	КонецЕсли;
	
	Элементы.ВидМеткиДляСквознойАналитики.Видимость = ВыгружатьМеткуСквознойАналитики;   
	
	Если ВыгружатьМеткуСквознойАналитики Тогда
		
		Если ВидМеткиДляСквознойАналитики = "По умолчанию" Тогда
			Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
			Элементы.СвойствоСквознойАналитики.Видимость 	= Ложь;
		ИначеЕсли ВидМеткиДляСквознойАналитики = "Источник привлечения" Тогда
			Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
			Элементы.СвойствоСквознойАналитики.Видимость 	= Ложь;
		ИначеЕсли ВидМеткиДляСквознойАналитики = "Свойство" Тогда
			Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
			Элементы.СвойствоСквознойАналитики.Видимость 	= Истина;
		КонецЕсли;	
		
	Иначе
		Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
		Элементы.СвойствоСквознойАналитики.Видимость 	= Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКлючиПользовательскиеПоляСущностиПортала(ВидСущности)
	
	Возврат РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьКлючиПользовательскиеПоляСущностиПортала(НастройкаПодключения, ВидСущности);
	
КонецФункции

#КонецОбласти
