
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПодключения = Параметры.НастройкаПодключения;	
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииТоваров") Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиСинхронизацииТоваров = НастройкиОбмена.НастройкиСинхронизацииТоваров;	
	
	НастройкиСинхронизацииТоваров.Свойство("ВыгружатьКартинкиИФайлы"	, ВыгружатьКартинкиИФайлы);
	НастройкиСинхронизацииТоваров.Свойство("ВыгружатьШтрихкоды"			, ВыгружатьШтрихкоды);
	НастройкиСинхронизацииТоваров.Свойство("ВыгружатьПланируемуюДатуПоступления", ВыгружатьПланируемуюДатуПоступления);
	
	НастройкиСинхронизацииТоваров.Свойство("Склад"		, Склад);
	НастройкиСинхронизацииТоваров.Свойство("ТипЦены"	, Прайс);
	
	НастройкиСинхронизацииТоваров.Свойство("ЗагружатьКартинкиИФайлы", ЗагружатьКартинкиИФайлы);
	
	НастройкиСинхронизацииТоваров.Свойство("ИдентификаторКаталога"	, ИдентификаторКаталога);
	НастройкиСинхронизацииТоваров.Свойство("ДеревоГрупп"			, ДеревоГрупп);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ДеревоГрупп" Тогда
		ДеревоГрупп = Параметр;
		Модифицированность = Истина;
		СохранитьВыгрузитьНастройки();
	КонецЕсли;

КонецПроцедуры
	

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по товарам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НужноЗакрытьОкно = Истина;
		
		СохранитьВыгрузитьНастройки(Истина);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		НужноЗакрытьОкно = Истина;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	СохранитьВыгрузитьНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьДеревоГрупп(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДеревоГрупп"	, ДеревоГрупп); 
	ПараметрыФормы.Вставить("Наименование"	, Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьРеквизитОбъекта(НастройкаПодключения, "Наименование"));
	
	ОткрытьФорму("Справочник.Б24_КС_ПользовательскиеГруппыТоваров.Форма.НастройкаДереваГрупп", ПараметрыФормы, ЭтаФорма);
КонецПроцедуры


&НаКлиенте
Процедура ВыгрузитьВидыНоменклатуры(Команда)
	
	ВыгрузитьВидыНоменклатурыНаСервере(НастройкаПодключения);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыгрузитьВидыНоменклатурыНаСервере(НастройкаПодключения)
	
	СтруктураНастроек =  Б24_КС_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение2 = "Полная выгрузка свойства ""Категории номенклатуры""";
	
	НаборЗаписей = РегистрыСведений.Б24_КС_ТаблицаИзменений.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ТипДанных.Установить(СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара);
	НаборЗаписей.Записать();       
	
	НаборЗаписей = РегистрыСведений.Б24_КС_ТаблицаИзменений.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ТипДанных.Установить(СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара);
	НаборЗаписей.Записать();             
	
	СвойствоВидНоменклатуры = Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.СоздатьПолучитьСвойствоВидаНоменклатурыБитрикс24(НастройкаПодключения, Истина);
	
	Б24_КС_РегистрацияИзмененийВызовСервера.ПроверитьИДобавитьОбъектВТаблицуИзменений(СтруктураНастроек.НастройкаПодключения, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара, СвойствоВидНоменклатуры);
	Б24_КС_РегистрацияИзмененийВызовСервера.ПроверитьИДобавитьОбъектВТаблицуИзменений(СтруктураНастроек.НастройкаПодключения, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара, СвойствоВидНоменклатуры);
	
	СтруктураНастроек.ВремяЗапускаВМиллисекундах = ТекущаяУниверсальнаяДатаВМиллисекундах();	
	
	Б24_КС_ОбщегоНазначенияВызовСервера.СформироватьИОтправить(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.СвойствоТовара);
	Б24_КС_ОбщегоНазначенияВызовСервера.СформироватьИОтправить(СтруктураНастроек, СтруктураНастроек.ТипыОбъектовОбмена.ЗначениеСвойстваТовара);

КонецПроцедуры


&НаКлиенте
Процедура НастроитьСинхронизациюТоваров(Команда)
	
	ВидыСущностей 			= Новый Массив;
	ВидыСущностей.Добавить("Товар");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОбновитьОтображениеЭлементов()
	
КонецПроцедуры

#КонецОбласти


#Область Прочие

&НаКлиенте
Функция СохранитьВыгрузитьНастройки(ЗакрытьПоОкончании = Ложь)
	
	НастройкаСохранена = СохранитьНастройкуСервер();
	
	Если НастройкаСохранена И ВыгружатьНастройкиНаПортал Тогда
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НастройкаПодключения, "ПометкаУдаления") = Ложь Тогда
			
			СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
			
			Если СтруктураНастроек = Неопределено Тогда
				ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
				Возврат Ложь;
			КонецЕсли;
			
			ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании);
		Иначе
			Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкаСохранена
	
КонецФункции  

&НаСервере
Функция СохранитьНастройкуСервер()
	
	Если Модифицированность Тогда
			
		СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
		НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
		
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииТоваров") Тогда
			СтруктураНастроек = Новый Структура;	
		Иначе
			СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииТоваров;	
		КонецЕсли;
		
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьКартинкиИФайлы"	, ВыгружатьКартинкиИФайлы);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьШтрихкоды"		, ВыгружатьШтрихкоды);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Склад"					, Склад);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ТипЦены"					, Прайс);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ЗагружатьКартинкиИФайлы"	, ЗагружатьКартинкиИФайлы);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИдентификаторКаталога"	, ИдентификаторКаталога);
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ДеревоГрупп"				, ДеревоГрупп);
			
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьПланируемуюДатуПоступления", ВыгружатьПланируемуюДатуПоступления);
			
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииТоваров") Тогда
			НастройкиОбмена.Вставить("НастройкиСинхронизацииТоваров", СтруктураНастроек); 	
		Иначе
			НастройкиОбмена.НастройкиСинхронизацииТоваров = СтруктураНастроек;
		КонецЕсли;
		
		Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
		Пока Выборка.Следующий() Цикл
			ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
			ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(НастройкиОбмена); 
			ТекСохраненныеНастройки.Записать();	
		КонецЦикла;
		
		ОбновитьПовторноИспользуемыеЗначения();
		
		Модифицированность = Ложь;
		
	КонецЕсли;
	
	Возврат Истина
		
КонецФункции  

&НаКлиенте
Процедура ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании)
	
	ДлительнаяОперацияЗавершение = Новый ОписаниеОповещения("ПослеВыгрузкиНастроек", ЭтаФорма, ЗакрытьПоОкончании);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ТекстСообщения             = "Выполняется выгрузка настроек..";
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания       = Истина;
	ПараметрыОжидания.ВыводитьСообщения          = Истина;
	
	ДлительнаяОперация = НачатьВыгрузкуНастройки(СтруктураНастроек);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ДлительнаяОперацияЗавершение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыгрузкиНастроек(Результат, ПараметрыВыгрузки) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность 			= Ложь;
		ВыгружатьНастройкиНаПортал 	= Ложь;
		
		Если ПараметрыВыгрузки = Истина Тогда
			Закрыть();	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьВыгрузкуНастройки(СтруктураНастроек)
	
	УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка регистрации изменений в Битрикс24";
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыВыгрузки.Вставить("Наименование"			, Строка(НастройкаПодключения));
	                                 
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.Б24_КС_НастройкиСинхронизации.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
	
КонецФункции


&НаКлиенте
Процедура ПослеВводаТонкихНастроек(Результат, пПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Свойство("ВыгружатьНастройкиНаПортал") Тогда
			ВыгружатьНастройкиНаПортал = Результат.ВыгружатьНастройкиНаПортал;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
