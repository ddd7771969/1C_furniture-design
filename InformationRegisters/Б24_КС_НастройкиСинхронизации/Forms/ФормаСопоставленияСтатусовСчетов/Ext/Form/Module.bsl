
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПодключения = Параметры.НастройкаПодключения;	
	ВерсияСчетов		 = Параметры.ВерсияСчетов;
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	НастройкиСинхронизацииСчетов = НастройкиОбмена.НастройкиСинхронизацииСчетов;
	
	Если НастройкиСинхронизацииСчетов.Свойство("ИнформацияОСтатусах") Тогда
		
		ИсточникСтатусов1С 	= НастройкиСинхронизацииСчетов.ИнформацияОСтатусах.ИсточникСтатусов1С;
		СвойствоСчета 		= НастройкиСинхронизацииСчетов.ИнформацияОСтатусах.СвойствоСчета;
		
		НастройкиСинхронизацииСчетов.ИнформацияОСтатусах.Свойство("СтатусПоУмолчанию", СтатусПоУмолчанию);
		
		СоответствияЗначенийСвойств.Загрузить(НастройкиСинхронизацииСчетов.ИнформацияОСтатусах.СоответствияЗначенийСвойств);
		
		ПереводитьВСтатусКогдаОплачен 			= "";
		ПереводитьВСтатусКогдаОтгружен 			= "";
		ПереводитьВСтатусКогдаОплаченОтгружен 	= "";
		
		НастройкиСинхронизацииСчетов.ИнформацияОСтатусах.Свойство("ПереводитьВСтатусКогдаОплачен", ПереводитьВСтатусКогдаОплачен);
		НастройкиСинхронизацииСчетов.ИнформацияОСтатусах.Свойство("ПереводитьВСтатусКогдаОтгружен", ПереводитьВСтатусКогдаОтгружен);
		НастройкиСинхронизацииСчетов.ИнформацияОСтатусах.Свойство("ПереводитьВСтатусКогдаОплаченОтгружен", ПереводитьВСтатусКогдаОплаченОтгружен);
		
	Иначе
		ИсточникСтатусов1С = "СвойствоСчета";
	КонецЕсли;

	Для Каждого ТекСтрока Из СоответствияЗначенийСвойств Цикл
		
		Элементы.ПереводитьВСтатусКогдаОплачен.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
		Элементы.ПереводитьВСтатусКогдаОтгружен.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
		Элементы.ПереводитьВСтатусКогдаОплаченОтгружен.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьСтатусыПоУмолчанию();	
	
	ОбновлениеЭлементовФормы();  
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
		
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по товарам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		НужноЗакрытьОкно = Истина;
		СохранениеНастроек();
        Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		НужноЗакрытьОкно = Истина;
		Закрыть();
    КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СохранениеНастроек()
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииСчетов") Тогда
		СтруктураНастроек = Новый Структура;	
	Иначе
		СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииСчетов;	
	КонецЕсли;
	
	СтруктураИнформацииОСтатусах = Новый Структура;
	СтруктураИнформацииОСтатусах.Вставить("ПереводитьВСтатусКогдаОплачен"			, ПереводитьВСтатусКогдаОплачен);
	СтруктураИнформацииОСтатусах.Вставить("ПереводитьВСтатусКогдаОтгружен"			, ПереводитьВСтатусКогдаОтгружен);
	СтруктураИнформацииОСтатусах.Вставить("ПереводитьВСтатусКогдаОплаченОтгружен"	, ПереводитьВСтатусКогдаОплаченОтгружен);
	
	СтруктураИнформацииОСтатусах.Вставить("ИсточникСтатусов1С"				, ИсточникСтатусов1С);
	СтруктураИнформацииОСтатусах.Вставить("СвойствоСчета"					, СвойствоСчета);
	СтруктураИнформацииОСтатусах.Вставить("СтатусПоУмолчанию"				, СтатусПоУмолчанию);  
	
	СтруктураИнформацииОСтатусах.Вставить("СоответствияЗначенийСвойств"		, СоответствияЗначенийСвойств.Выгрузить());
	
	РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИнформацияОСтатусах"	, СтруктураИнформацииОСтатусах);
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииСчетов") Тогда
		НастройкиОбмена.Вставить("НастройкиСинхронизацииСчетов", СтруктураНастроек); 	
	Иначе
		НастройкиОбмена.НастройкиСинхронизацииСчетов = СтруктураНастроек;
	КонецЕсли;
	
	Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
	Пока Выборка.Следующий() Цикл
		ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
		ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(НастройкиОбмена); 
		ТекСохраненныеНастройки.Записать();	
	КонецЦикла;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИсточникСтатусов1СПриИзменении(Элемент)
	ОбновлениеЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеЭлементовФормы()
	
	Если ИсточникСтатусов1С = "СвойствоСчета" Тогда
		Элементы.СтраницыИсточникиСтатусов.ТекущаяСтраница = Элементы.СтраницаСвойство;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьСтатусыСПортала(Команда)
	
	ЗагрузитьСтатусыСПорталаСервер();
	
	ЗаполнитьСтатусыПоУмолчанию();	
	
КонецПроцедуры     


&НаСервере
Процедура ЗагрузитьСтатусыСПорталаСервер()
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Истина);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка статусов счетов.";
	
	ДобавленыПринудительныеСтатусы = Ложь;
	Элементы.ПереводитьВСтатусКогдаОплачен.СписокВыбора.Очистить();
	Элементы.ПереводитьВСтатусКогдаОтгружен.СписокВыбора.Очистить();
	Элементы.ПереводитьВСтатусКогдаОплаченОтгружен.СписокВыбора.Очистить();
	
	мТаблицы = Новый Массив;
	мТаблицы.Добавить(СоответствияЗначенийСвойств);
	
	Если ВерсияСчетов = "v.2" Тогда
		
		Направления.Очистить();
		
		ТелоHTTPЗапроса = "&entityTypeId=31";
		НаправленияСБ24 = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.category.list", ТелоHTTPЗапроса); 
		Если НаправленияСБ24 = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		result = НаправленияСБ24.Получить("result");
		Если result <> Неопределено Тогда   
			categories = result.Получить("categories");
			Если categories <> Неопределено Тогда
				
				Для каждого ТекНаправление Из categories Цикл
					
					ИдНаправления			= Формат(ТекНаправление.Получить("id"),"ЧГ=0");
					НаименованиеНаправления = ТекНаправление.Получить("name");
					
					НоваяСтрока = Направления.Добавить();
					НоваяСтрока.ИдНаправления 	= ИдНаправления; 
					НоваяСтрока.Направление 	= НаименованиеНаправления; 
					
				КонецЦикла;
			
			КонецЕсли;
		КонецЕсли;      
		
		Для Каждого ТекТаблица Из мТаблицы Цикл 
				
			тзнАрхив = ТекТаблица.Выгрузить();
			ТекТаблица.Очистить();
		
			Для каждого ТекНаправление Из Направления Цикл
				
				ДопИдНаправления 	= "SMART_INVOICE_STAGE_" + ТекНаправление.ИдНаправления;
				ФильтрПоКатегории 	= "filter[CATEGORY_ID]=" + ТекНаправление.ИдНаправления;
				
				ДанныеНаправления = Б24_К_RestApiВызовСервера.ПолучитьМассивСтатусовCRM(СтруктураНастроек, ФильтрПоКатегории); 
				Для Каждого ТекСтатус Из ДанныеНаправления Цикл
		
					ВладелецСтатуса = Формат(ТекСтатус.Получить("ENTITY_ID"),"ЧГ=0");
					НазваниеСтатуса	= ТекСтатус.Получить("NAME");
					ИдСтатуса = Формат(ТекСтатус.Получить("STATUS_ID"),"ЧГ=0");
					
					Если ВладелецСтатуса = ДопИдНаправления Тогда
				
						ПолноеНазваниеСтатуса = ТекНаправление.Направление + ": " + НазваниеСтатуса;
						
						Если НЕ ДобавленыПринудительныеСтатусы Тогда 
							Элементы.ПереводитьВСтатусКогдаОплачен.СписокВыбора.Добавить(ИдСтатуса, ПолноеНазваниеСтатуса); 
							Элементы.ПереводитьВСтатусКогдаОтгружен.СписокВыбора.Добавить(ИдСтатуса, ПолноеНазваниеСтатуса); 
							Элементы.ПереводитьВСтатусКогдаОплаченОтгружен.СписокВыбора.Добавить(ИдСтатуса, ПолноеНазваниеСтатуса); 
						КонецЕсли;	
						
						НоваяЗапись = ТекТаблица.Добавить();
						НоваяЗапись.ИдНаправления 	= ТекНаправление.ИдНаправления;
						НоваяЗапись.ИдСтатуса		= ИдСтатуса;
						НоваяЗапись.НазваниеСтатуса = ПолноеНазваниеСтатуса;
						
						НайденныеСтроки = тзнАрхив.НайтиСтроки(Новый Структура("ИдНаправления, ИдСтатуса", ТекНаправление.ИдНаправления, ИдСтатуса));
						Если НайденныеСтроки.Количество() > 0 Тогда
							НоваяЗапись.НеЗагружать = НайденныеСтроки[0].НеЗагружать;
							НоваяЗапись.Статус 		= НайденныеСтроки[0].Статус;
						КонецЕсли;   
						
					КонецЕсли;                                 
					
				КонецЦикла;
			
			КонецЦикла;
		
			ДобавленыПринудительныеСтатусы = Истина;
			
		КонецЦикла;   
		
	Иначе
		
		ТелоHTTPЗапроса = "";	
		ДанныеСтатусов = Б24_К_RestApiВызовСервера.ПолучитьМассивСтатусовCRM(СтруктураНастроек, ТелоHTTPЗапроса);
	
		Для Каждого ТекТаблица Из мТаблицы Цикл 
					
			тзнАрхив = ТекТаблица.Выгрузить();
			ТекТаблица.Очистить();
			
			Для каждого ТекЭлемент Из ДанныеСтатусов Цикл
				
				Если ТекЭлемент.Получить("ENTITY_ID") <> "INVOICE_STATUS" Тогда    // ломали метод, поэтому проверка, на всякий
					Продолжить;
				КонецЕсли;
				
				ИдСтатуса 			= Формат(ТекЭлемент.Получить("STATUS_ID"), "ЧГ=0");
				НаименованиеСтатуса = ТекЭлемент.Получить("NAME"); 
				
				Если НЕ ДобавленыПринудительныеСтатусы  Тогда
					Элементы.ПереводитьВСтатусКогдаОплачен.СписокВыбора.Добавить(ИдСтатуса, НаименованиеСтатуса); 
					Элементы.ПереводитьВСтатусКогдаОтгружен.СписокВыбора.Добавить(ИдСтатуса, НаименованиеСтатуса); 
					Элементы.ПереводитьВСтатусКогдаОплаченОтгружен.СписокВыбора.Добавить(ИдСтатуса, НаименованиеСтатуса); 
				КонецЕсли;
				
				НоваяСтрока = ТекТаблица.Добавить();
				НоваяСтрока.НазваниеСтатуса = НаименованиеСтатуса;
				НоваяСтрока.ИдСтатуса 		= ИдСтатуса;
				
				НайденныеСтроки = тзнАрхив.НайтиСтроки(Новый Структура("ИдСтатуса", ИдСтатуса));
				Если НайденныеСтроки.Количество() > 0 Тогда
					НоваяСтрока.НеЗагружать 	= НайденныеСтроки[0].НеЗагружать;
					НоваяСтрока.Статус 		= НайденныеСтроки[0].Статус;
					НоваяСтрока.ИдНаправления = НайденныеСтроки[0].ИдНаправления;
				КонецЕсли;                                 
				
			КонецЦикла;
			
			ДобавленыПринудительныеСтатусы = Истина;
			
		КонецЦикла;

	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюОСтатусахСмартПроцесса(СтруктураОтвета, ИдНаправления)
	
	мТаблицы = Новый Массив;
	мТаблицы.Добавить(СоответствияЗначенийСвойств);
	
	Для Каждого ТекСтатус Из СтруктураОтвета.Получить("result") Цикл
		
		ВладелецСтатуса = Формат(ТекСтатус.Получить("ENTITY_ID"),"ЧГ=0");
		НазваниеСтатуса	= ТекСтатус.Получить("NAME");
		ИдСтатуса = Формат(ТекСтатус.Получить("STATUS_ID"),"ЧГ=0");
		
		Для каждого ТекНаправление Из Направления Цикл
			
			ДопИдНаправления = "SMART_INVOICE_STAGE_" + ТекНаправление.ИдНаправления;
			
			Если ВладелецСтатуса = ДопИдНаправления Тогда
				
				Для Каждого ТекТаблица Из мТаблицы Цикл
					
					ПолноеНазваниеСтатуса = ТекНаправление.Направление + ": " + НазваниеСтатуса;
					
					НайденныеСтроки = ТекТаблица.НайтиСтроки(Новый Структура("ИдНаправления, ИдСтатуса", ТекНаправление.ИдНаправления, ИдСтатуса));
					
					Элементы.ПереводитьВСтатусКогдаОплачен.СписокВыбора.Добавить(ИдСтатуса, ПолноеНазваниеСтатуса); 
					Элементы.ПереводитьВСтатусКогдаОтгружен.СписокВыбора.Добавить(ИдСтатуса, ПолноеНазваниеСтатуса); 
					Элементы.ПереводитьВСтатусКогдаОплаченОтгружен.СписокВыбора.Добавить(ИдСтатуса, ПолноеНазваниеСтатуса); 
					
					Если НайденныеСтроки.количество() = 0 Тогда
						НоваяЗапись = ТекТаблица.Добавить();
						НоваяЗапись.ИдНаправления 	= ТекНаправление.ИдНаправления;
						
						НоваяЗапись.ИдСтатуса		= ИдСтатуса;
						НоваяЗапись.НазваниеСтатуса = ПолноеНазваниеСтатуса;
					Иначе
						Для Каждого ТекСтрокаТаблицы Из ТекТаблица Цикл
							Если ТекСтрокаТаблицы.ИдНаправления = ТекНаправление.ИдНаправления И ТекСтрокаТаблицы.ИдСтатуса = ИдСтатуса Тогда
								ТекСтрокаТаблицы.НазваниеСтатуса = ПолноеНазваниеСтатуса; 
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;   
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	Если ИсточникСтатусов1С = "СвойствоСчета" И НЕ ЗначениеЗаполнено(СвойствоСчета) Тогда
		ПоказатьПредупреждение(,"Не указано свойство счета. Запись настроек невозможна");
	Иначе
		СохранениеНастроек();
		Закрыть();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область Прочее

&НаКлиенте
Процедура ЗаполнитьСтатусыПоУмолчанию()
	
	Элементы.СтатусПоУмолчанию.СписокВыбора.Очистить();
	
	Для каждого ТекСтатус Из СоответствияЗначенийСвойств Цикл
		Элементы.СтатусПоУмолчанию.СписокВыбора.Добавить(ТекСтатус.ИдСтатуса, ТекСтатус.НазваниеСтатуса);
	КонецЦикла;
	
	ОбновитьОтображениеДанных(Элементы.СтатусПоУмолчанию);
	
КонецПроцедуры

#КонецОбласти

