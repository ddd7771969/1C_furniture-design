
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПравилаПоиска") Тогда
		Если ЗначениеЗаполнено(Параметры.ПравилаПоиска) Тогда
			лДеревоГрупп = ЗначениеИзСтрокиВнутр(Параметры.ПравилаПоиска);
			ЗначениеВРеквизитФормы(лДеревоГрупп, "ДеревоОтбора");
		Иначе
			НоваяСтрока = ДеревоОтбора.ПолучитьЭлементы().Добавить();
			НоваяСтрока.Использование 	= Истина;
			НоваяСтрока.ТипЗаписи 		= 0;
			НоваяСтрока.ЛевоеЗначение 	= "Правило сопоставления";
		КонецЕсли;	
	КонецЕсли;
	
	СписокАлгоритмов.Добавить("Не указано");
	Если Параметры.Свойство("ВидСущности") Тогда
		
		Если Параметры.ВидСущности = "Компания" Тогда
			СписокАлгоритмов.Добавить("Наименование компании");
			СписокАлгоритмов.Добавить("Телефон");
			СписокАлгоритмов.Добавить("Емейл");
			СписокАлгоритмов.Добавить("ИНН");
			СписокАлгоритмов.Добавить("ИНН+КПП");
			СписокАлгоритмов.Добавить("КПП");
		ИначеЕсли Параметры.ВидСущности = "Контакт" Тогда
			СписокАлгоритмов.Добавить("Фамилия");
			СписокАлгоритмов.Добавить("Имя");
			СписокАлгоритмов.Добавить("Отчество");
			СписокАлгоритмов.Добавить("Телефон");
			СписокАлгоритмов.Добавить("Емейл");
			СписокАлгоритмов.Добавить("ИНН");
			СписокАлгоритмов.Добавить("ИНН+КПП");
			СписокАлгоритмов.Добавить("КПП");
		ИначеЕсли Параметры.ВидСущности = "Реквизит" Тогда
			СписокАлгоритмов.Добавить("Наименование компании");
			СписокАлгоритмов.Добавить("Фамилия");
			СписокАлгоритмов.Добавить("Имя");
			СписокАлгоритмов.Добавить("Отчество");
			СписокАлгоритмов.Добавить("ИНН");
			СписокАлгоритмов.Добавить("ИНН+КПП");
			СписокАлгоритмов.Добавить("КПП");
		ИначеЕсли Параметры.ВидСущности = "Банковский счет" Тогда
			СписокАлгоритмов.Добавить("Номер счета");
			СписокАлгоритмов.Добавить("БИК");
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьЭлементыФормы();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьНовыйЭлемент(Команда)
	
	ТекущиеДанныеДерева = Элементы.ДеревоОтбора.ТекущиеДанные;
	Если ТекущиеДанныеДерева <> Неопределено Тогда
		
		Если ТекущиеДанныеДерева.ТипЗаписи = 9 Тогда
			
			лРодитель = ТекущиеДанныеДерева.ПолучитьРодителя();
			Если лРодитель <> Неопределено Тогда
				НоваяСтрока =  лРодитель.ПолучитьЭлементы().Добавить();
				НоваяСтрока.ТипЗаписи 		= 9;
				НоваяСтрока.Использование 	= Истина;
				НоваяСтрока.ЛевоеЗначение 	= "Не указано";
			КонецЕсли;
			
		Иначе
			НоваяСтрока = ТекущиеДанныеДерева.ПолучитьЭлементы().Добавить();
			НоваяСтрока.ТипЗаписи 		= 9;
			НоваяСтрока.Использование 	= Истина;
			НоваяСтрока.ЛевоеЗначение 	= "Не указано";
		КонецЕсли;		
	
	Иначе
		
		НоваяСтрока = ДеревоОтбора.ПолучитьЭлементы().Добавить();
		НоваяСтрока.ТипЗаписи 		= 9;
		НоваяСтрока.Использование 	= Истина;
		НоваяСтрока.ЛевоеЗначение 	= "Не указано";
		
	КонецЕсли;
	
	РазвернутьВсеДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура СгруппироватьУсловия(Команда)
	
	ТекущиеДанныеДерева = Элементы.ДеревоОтбора.ТекущиеДанные;
	Если ТекущиеДанныеДерева <> Неопределено Тогда
		
		Если ТекущиеДанныеДерева.ТипЗаписи = 9 Тогда
			
			лРодитель = ТекущиеДанныеДерева.ПолучитьРодителя();
			Если лРодитель <> Неопределено Тогда
				НоваяСтрока =  лРодитель.ПолучитьЭлементы().Добавить();
				НоваяСтрока.ТипЗаписи 		= 1;
				НоваяСтрока.Использование 	= Истина;
				НоваяСтрока.ЛевоеЗначение 	= "Группа И";
			КонецЕсли;
			
		Иначе
			НоваяСтрока = ТекущиеДанныеДерева.ПолучитьЭлементы().Добавить();
			НоваяСтрока.ТипЗаписи 		= 1;
			НоваяСтрока.Использование 	= Истина;
			НоваяСтрока.ЛевоеЗначение 	= "Группа И";
		КонецЕсли;		
	
	Иначе
		
		НоваяСтрока = ДеревоОтбора.ПолучитьЭлементы().Добавить();
		НоваяСтрока.ТипЗаписи 		= 1;
		НоваяСтрока.Использование 	= Истина;
		НоваяСтрока.ЛевоеЗначение 	= "Группа И";
		
	КонецЕсли;
	
	РазвернутьВсеДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	Закрыть(ПрименитьНаСервере());
	
КонецПроцедуры


&НаСервере
Функция ПрименитьНаСервере()
	
	лЭлементыДерева = ДеревоОтбора.ПолучитьЭлементы();
	Если лЭлементыДерева.Количество() > 0 Тогда
		Если лЭлементыДерева[0].ПолучитьЭлементы().Количество() > 0 Тогда	
			Возврат ЗначениеВСтрокуВнутр(ДанныеФормыВЗначение(ДеревоОтбора, Тип("ДеревоЗначений")));
		КонецЕсли;
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДеревоОтбораПриАктивизацииСтроки(Элемент)
	ОбновитьЭлементыФормы();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОтбораПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если Строка = 0 Тогда СтандартнаяОбработка = Ложь КонецЕсли;
	
	НайденнаяСтрока = ДеревоОтбора.НайтиПоИдентификатору(Строка);
	Если НайденнаяСтрока <> Неопределено Тогда
		Если НайденнаяСтрока.ТипЗаписи = 9 Тогда СтандартнаяОбработка = Ложь КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область Прочие

&НаКлиенте
Процедура ОбновитьЭлементыФормы()
	
	ТекущиеДанныеДерева = Элементы.ДеревоОтбора.ТекущиеДанные;
	
	Если ТекущиеДанныеДерева <> Неопределено Тогда
		
		Если ТекущиеДанныеДерева.ТипЗаписи = 0 Тогда
			ПереключениеНаКорень();
		ИначеЕсли ТекущиеДанныеДерева.ТипЗаписи = 1 Тогда
			ПереключениеНаГруппу();
		Иначе
			ПереключениеНаЗапись();
		КонецЕсли;
		
	Иначе
		ПереключениеНаКорень();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключениеНаКорень()
	
	Элементы.ДеревоОтбораЛевоеЗначение.Заголовок 		= " ";
	Элементы.ДеревоОтбораЛевоеЗначение.ТолькоПросмотр 	= Истина;
	
	Элементы.ДеревоОтбораЛевоеЗначение.КнопкаВыпадающегоСписка = Ложь;
	Элементы.ДеревоОтбораЛевоеЗначение.СписокВыбора.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключениеНаГруппу()
	
	Элементы.ДеревоОтбораЛевоеЗначение.Заголовок 		= "Тип группы";
	Элементы.ДеревоОтбораЛевоеЗначение.ТолькоПросмотр 	= Ложь;
	
	Элементы.ДеревоОтбораЛевоеЗначение.КнопкаВыпадающегоСписка = Истина;
	Элементы.ДеревоОтбораЛевоеЗначение.СписокВыбора.Очистить();
	Элементы.ДеревоОтбораЛевоеЗначение.СписокВыбора.Добавить("Группа И");
	Элементы.ДеревоОтбораЛевоеЗначение.СписокВыбора.Добавить("Группа ИЛИ");
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключениеНаЗапись()
	
	Элементы.ДеревоОтбораЛевоеЗначение.Заголовок 	= "Поле";
	
	Элементы.ДеревоОтбораЛевоеЗначение.ТолькоПросмотр 	= Ложь;
	
	Элементы.ДеревоОтбораЛевоеЗначение.КнопкаВыпадающегоСписка = Истина;
	Элементы.ДеревоОтбораЛевоеЗначение.СписокВыбора.ЗагрузитьЗначения(СписокАлгоритмов.ВыгрузитьЗначения());
	
КонецПроцедуры


&НаКлиенте
Процедура РазвернутьВсеДерево() 

    Для каждого ЭлементКоллекции Из ДеревоОтбора.ПолучитьЭлементы() Цикл
        Элементы.ДеревоОтбора.Развернуть(ЭлементКоллекции.ПолучитьИдентификатор(), Истина);
    КонецЦикла;
    
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОтбораПередУдалением(Элемент, Отказ)
	
	ТекущиеДанныеДерева = Элементы.ДеревоОтбора.ТекущиеДанные;
	Если ТекущиеДанныеДерева <> Неопределено Тогда
		Если ТекущиеДанныеДерева.ЛевоеЗначение = "Правило сопоставления" Тогда
			Отказ = Истина;	
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

