
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПодключения = Параметры.НастройкаПодключения;	
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	НастройкиСинхронизацииСделок = НастройкиОбмена.НастройкиСинхронизацииСделок;
	
	Если НастройкиСинхронизацииСделок.Свойство("ИнформацияОСтатусах") Тогда
		
		ИсточникСтатусов1С 	= НастройкиСинхронизацииСделок.ИнформацияОСтатусах.ИсточникСтатусов1С;
		СвойствоЗаказов 	= НастройкиСинхронизацииСделок.ИнформацияОСтатусах.СвойствоЗаказов;
		
		НастройкиСинхронизацииСделок.ИнформацияОСтатусах.Свойство("СтатусПоУмолчанию", СтатусПоУмолчанию);
		
		Если НастройкиСинхронизацииСделок.ИнформацияОСтатусах.Свойство("СтатусыНаправленийЗначенийСвойств") Тогда
			СтатусыНаправленийЗначенийСвойств.Загрузить(НастройкиСинхронизацииСделок.ИнформацияОСтатусах.СтатусыНаправленийЗначенийСвойств);	
		КонецЕсли;
		
		Если НастройкиСинхронизацииСделок.ИнформацияОСтатусах.Свойство("СтатусыНаправленийСостояний") Тогда
			СтатусыНаправленийСостояний.Загрузить(НастройкиСинхронизацииСделок.ИнформацияОСтатусах.СтатусыНаправленийСостояний);	
		КонецЕсли;
		
		Если НастройкиСинхронизацииСделок.ИнформацияОСтатусах.Свойство("СтатусыНаправленийСтатусов") Тогда
			СтатусыНаправленийСтатусов.Загрузить(НастройкиСинхронизацииСделок.ИнформацияОСтатусах.СтатусыНаправленийСтатусов);	
		КонецЕсли;
		
		Если НастройкиСинхронизацииСделок.ИнформацияОСтатусах.Свойство("НаправленияСделок") Тогда
			НаправленияСделок.Загрузить(НастройкиСинхронизацииСделок.ИнформацияОСтатусах.НаправленияСделок);	
		КонецЕсли;
		
		Если НастройкиСинхронизацииСделок.ИнформацияОСтатусах.Свойство("ПринудительныеСтатусы") Тогда
			ПринудительныеСтатусы.Загрузить(НастройкиСинхронизацииСделок.ИнформацияОСтатусах.ПринудительныеСтатусы);	
		КонецЕсли;
		
	Иначе
		ИсточникСтатусов1С = "СтатусыЗаказов";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ЗаполнитьСтатусыПоУмолчанию();

	ОбновлениеЭлементовФормы();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
		
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по статусам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		НужноЗакрытьОкно = Истина;
		СохранениеНастроек();
        Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		НужноЗакрытьОкно = Истина;
		Закрыть();
    КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СохранениеНастроек()
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииСделок") Тогда
		СтруктураНастроек = Новый Структура;	
	Иначе
		СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииСделок;	
	КонецЕсли;
	
	СтруктураИнформацииОСтатусах = Новый Структура;
	СтруктураИнформацииОСтатусах.Вставить("ИсточникСтатусов1С"			, ИсточникСтатусов1С);
	СтруктураИнформацииОСтатусах.Вставить("СвойствоЗаказов"				, СвойствоЗаказов);
	СтруктураИнформацииОСтатусах.Вставить("СтатусПоУмолчанию"			, СтатусПоУмолчанию);

	СтруктураИнформацииОСтатусах.Вставить("СтатусыНаправленийЗначенийСвойств"	, СтатусыНаправленийЗначенийСвойств.Выгрузить());
	СтруктураИнформацииОСтатусах.Вставить("СтатусыНаправленийСостояний"			, СтатусыНаправленийСостояний.Выгрузить());
	СтруктураИнформацииОСтатусах.Вставить("СтатусыНаправленийСтатусов"			, СтатусыНаправленийСтатусов.Выгрузить());
	СтруктураИнформацииОСтатусах.Вставить("ПринудительныеСтатусы"				, ПринудительныеСтатусы.Выгрузить());
	СтруктураИнформацииОСтатусах.Вставить("НаправленияСделок"					, НаправленияСделок.Выгрузить());
	
	РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИнформацияОСтатусах"	, СтруктураИнформацииОСтатусах);
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииСделок") Тогда
		НастройкиОбмена.Вставить("НастройкиСинхронизацииСделок", СтруктураНастроек); 	
	Иначе
		НастройкиОбмена.НастройкиСинхронизацииСделок = СтруктураНастроек;
	КонецЕсли;
	
	Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
	Пока Выборка.Следующий() Цикл
		ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
		ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(НастройкиОбмена); 
		ТекСохраненныеНастройки.Записать();	
	КонецЦикла;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИсточникСтатусов1СПриИзменении(Элемент)
	ОбновлениеЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеЭлементовФормы()
	
	Если ИсточникСтатусов1С = "СвойствоЗаказов" Тогда
		Элементы.СтраницыИсточникиСтатусов.ТекущаяСтраница = Элементы.СтраницаСвойство;
	ИначеЕсли ИсточникСтатусов1С = "СостоянияЗаказов" Тогда
		Элементы.СтраницыИсточникиСтатусов.ТекущаяСтраница = Элементы.СтраницаСостояния;
	ИначеЕсли ИсточникСтатусов1С = "СтатусыЗаказов" Тогда
		Элементы.СтраницыИсточникиСтатусов.ТекущаяСтраница = Элементы.СтраницаСтатусы;
	КонецЕсли;
	
	ОбновитьИнформациюОПринудительныхСтатусах();
	
КонецПроцедуры

&НаКлиенте
Процедура НаправленияСделокПриАктивизацииСтроки(Элемент)
	
	СоответствияСостояний.Очистить();
	СоответствияЗначенийСвойств.Очистить();
	СоответствияСтатусов.Очистить();
	
	Если Элементы.НаправленияСделок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдНаправления = Элементы.НаправленияСделок.ТекущиеДанные.ИдНаправления;
	
	Для каждого ТекСтрока Из СтатусыНаправленийЗначенийСвойств Цикл
		Если ТекСтрока.ИдНаправления = ИдНаправления Тогда
			НовСтрока = СоответствияЗначенийСвойств.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, ТекСтрока); 
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекСтрока Из СтатусыНаправленийСостояний Цикл
		Если ТекСтрока.ИдНаправления = ИдНаправления Тогда
			НовСтрока = СоответствияСостояний.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, ТекСтрока); 
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекСтрока Из СтатусыНаправленийСтатусов Цикл
		Если ТекСтрока.ИдНаправления = ИдНаправления Тогда
			НовСтрока = СоответствияСтатусов.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, ТекСтрока); 
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьИнформациюОПринудительныхСтатусах();
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияСостоянийПриИзменении(Элемент)
	
	Если Элементы.НаправленияСделок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдНаправления = Элементы.НаправленияСделок.ТекущиеДанные.ИдНаправления;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекЭлемент Из СтатусыНаправленийСостояний Цикл
		
		Если ТекЭлемент.ИдНаправления = ИдНаправления И Элемент.ТекущиеДанные.ИдСтатуса = ТекЭлемент.ИдСтатуса Тогда
			ТекЭлемент.Статус		 	= Элемент.ТекущиеДанные.Статус;	
			ТекЭлемент.НеЗагружать 		= Элемент.ТекущиеДанные.НеЗагружать;	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияЗначенийСвойствПриИзменении(Элемент)
		
	Если Элементы.НаправленияСделок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдНаправления = Элементы.НаправленияСделок.ТекущиеДанные.ИдНаправления;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекЭлемент Из СтатусыНаправленийЗначенийСвойств Цикл
		
		Если ТекЭлемент.ИдНаправления = ИдНаправления И Элемент.ТекущиеДанные.ИдСтатуса = ТекЭлемент.ИдСтатуса Тогда
			ТекЭлемент.Статус = Элемент.ТекущиеДанные.Статус;	
			ТекЭлемент.НеЗагружать = Элемент.ТекущиеДанные.НеЗагружать;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СоответствияСтатусовПриИзменении(Элемент)
		
	Если Элементы.НаправленияСделок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдНаправления = Элементы.НаправленияСделок.ТекущиеДанные.ИдНаправления;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекЭлемент Из СтатусыНаправленийСтатусов Цикл
		
		Если ТекЭлемент.ИдНаправления = ИдНаправления И Элемент.ТекущиеДанные.ИдСтатуса = ТекЭлемент.ИдСтатуса Тогда
			ТекЭлемент.Статус = Элемент.ТекущиеДанные.Статус;	
			ТекЭлемент.НеЗагружать = Элемент.ТекущиеДанные.НеЗагружать;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СоответствияСостоянийПередУдалением(Элемент, Отказ)
	
		
	Если Элементы.НаправленияСделок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдНаправления = Элементы.НаправленияСделок.ТекущиеДанные.ИдНаправления;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьСтатусНаправления(ИдНаправления, Элемент.ТекущиеДанные.ИдСтатуса);
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияЗначенийСвойствПередУдалением(Элемент, Отказ)
		
	Если Элементы.НаправленияСделок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдНаправления = Элементы.НаправленияСделок.ТекущиеДанные.ИдНаправления;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьСтатусНаправления(ИдНаправления, Элемент.ТекущиеДанные.ИдСтатуса);
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияСтатусовПередУдалением(Элемент, Отказ)
		
	Если Элементы.НаправленияСделок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдНаправления = Элементы.НаправленияСделок.ТекущиеДанные.ИдНаправления;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьСтатусНаправления(ИдНаправления, Элемент.ТекущиеДанные.ИдСтатуса);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереводитьВСтатусКогдаОплаченПриИзменении(Элемент)
	
	Если Элементы.НаправленияСделок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдНаправления = Элементы.НаправленияСделок.ТекущиеДанные.ИдНаправления;
	
	ЕстьОплата 		= Ложь;
	
	Для Каждого ТекПринудительныйСтатус Из ПринудительныеСтатусы Цикл
		Если ТекПринудительныйСтатус.ИдНаправления = ИдНаправления Тогда
			Если ТекПринудительныйСтатус.НазначениеСтатуса = "Оплата" Тогда
				ТекПринудительныйСтатус.ИдСтатуса = ПереводитьВСтатусКогдаОплачен;
				ЕстьОплата = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЕстьОплата Тогда
		НоваяЗапись = ПринудительныеСтатусы.Добавить();
		НоваяЗапись.ИдНаправления = ИдНаправления;
		НоваяЗапись.НазначениеСтатуса = "Оплата";
		НоваяЗапись.ИдСтатуса = ПереводитьВСтатусКогдаОплачен;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереводитьВСтатусКогдаОплаченОтгруженПриИзменении(Элемент)
	
	Если Элементы.НаправленияСделок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдНаправления = Элементы.НаправленияСделок.ТекущиеДанные.ИдНаправления;
	
	ЕстьОплата 		= Ложь;
	
	Для Каждого ТекПринудительныйСтатус Из ПринудительныеСтатусы Цикл
		Если ТекПринудительныйСтатус.ИдНаправления = ИдНаправления Тогда
			Если ТекПринудительныйСтатус.НазначениеСтатуса = "ОплатаОтгрузка" Тогда
				ТекПринудительныйСтатус.ИдСтатуса = ПереводитьВСтатусКогдаОплаченОтгружен;
				ЕстьОплата = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЕстьОплата Тогда
		НоваяЗапись = ПринудительныеСтатусы.Добавить();
		НоваяЗапись.ИдНаправления = ИдНаправления;
		НоваяЗапись.НазначениеСтатуса = "ОплатаОтгрузка";
		НоваяЗапись.ИдСтатуса = ПереводитьВСтатусКогдаОплаченОтгружен;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереводитьВСтатусКогдаОтгруженПриИзменении(Элемент)
	
	Если Элементы.НаправленияСделок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдНаправления = Элементы.НаправленияСделок.ТекущиеДанные.ИдНаправления;
	
	ЕстьОплата 		= Ложь;
	
	Для Каждого ТекПринудительныйСтатус Из ПринудительныеСтатусы Цикл
		Если ТекПринудительныйСтатус.ИдНаправления = ИдНаправления Тогда
			Если ТекПринудительныйСтатус.НазначениеСтатуса = "Отгрузка" Тогда
				ТекПринудительныйСтатус.ИдСтатуса = ПереводитьВСтатусКогдаОтгружен;
				ЕстьОплата = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЕстьОплата Тогда
		НоваяЗапись = ПринудительныеСтатусы.Добавить();
		НоваяЗапись.ИдНаправления = ИдНаправления;
		НоваяЗапись.НазначениеСтатуса = "Отгрузка";
		НоваяЗапись.ИдСтатуса = ПереводитьВСтатусКогдаОтгружен;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьСтатусыСПортала(Команда)
	
	ЗагрузитьСтатусыСПорталаСервер();

	ОбновитьСтатусыНаправлений();
	
	ЗаполнитьСтатусыПоУмолчанию();
	
	НаправленияСделокПриАктивизацииСтроки("");
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСтатусыСПорталаСервер()
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения, Истина);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка статусов сделок.";
	
	ДобавленыПринудительныеСтатусы = Ложь;
	Элементы.ПереводитьВСтатусКогдаОплачен.СписокВыбора.Очистить(); 
	Элементы.ПереводитьВСтатусКогдаОтгружен.СписокВыбора.Очистить(); 
	Элементы.ПереводитьВСтатусКогдаОплаченОтгружен.СписокВыбора.Очистить(); 
	
	мТаблицы = Новый Массив;
	мТаблицы.Добавить(СтатусыНаправленийЗначенийСвойств);
	мТаблицы.Добавить(СтатусыНаправленийСостояний);
	мТаблицы.Добавить(СтатусыНаправленийСтатусов);       
	
	тзнАрхив = НаправленияСделок.Выгрузить();
	НаправленияСделок.Очистить();   
	
	НоваяСтрока = НаправленияСделок.Добавить();
	НоваяСтрока.ИдНаправления 	= "";
	НоваяСтрока.Направление 	= "Общее";
	
	ТелоHTTPЗапроса = "&entityTypeId=2";
	НаправленияСделокСБ24 = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/crm.category.list", ТелоHTTPЗапроса); 
	Если НаправленияСделокСБ24 = Неопределено Тогда
		Возврат;
	КонецЕсли;

	result = НаправленияСделокСБ24.Получить("result"); 
	Если result <> Неопределено Тогда
		categories = result.Получить("categories"); 
		Если categories <> Неопределено Тогда  
			Для каждого ТекНаправление Из categories Цикл
				
				ИдНаправления			= Формат(ТекНаправление.Получить("id"),"ЧГ=0");
				НаименованиеНаправления = ТекНаправление.Получить("name");
				
				Если НаправленияСделок.НайтиСтроки(Новый Структура("ИдНаправления", ИдНаправления)).Количество() = 0 Тогда
					НоваяСтрока = НаправленияСделок.Добавить();
					НоваяСтрока.ИдНаправления 	= ИдНаправления;
					НоваяСтрока.Направление 	= НаименованиеНаправления;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;  
	КонецЕсли;
	
	ОбновитьСвойствоНаправленияСделки(СтруктураНастроек);
			
	Для Каждого ТекТаблица Из мТаблицы Цикл 
				
		тзнАрхив = ТекТаблица.Выгрузить();
		ТекТаблица.Очистить();
		
		Для каждого ТекНаправление Из НаправленияСделок Цикл
			
			ДопИдНаправления 	= "DEAL_STAGE" + ?(ЗначениеЗаполнено(ТекНаправление.ИдНаправления), "_" + ТекНаправление.ИдНаправления, "");
		   	ФильтрПоКатегории 	= "filter[CATEGORY_ID]=" + ТекНаправление.ИдНаправления;
				
			ДанныеНаправления = Б24_К_RestApiВызовСервера.ПолучитьМассивСтатусовCRM(СтруктураНастроек, ФильтрПоКатегории); 
			Для Каждого ТекСтатус Из ДанныеНаправления Цикл
				
				ВладелецСтатуса = Формат(ТекСтатус.Получить("ENTITY_ID"),"ЧГ=0");
				НазваниеСтатуса	= ТекСтатус.Получить("NAME");
				ИдСтатуса = Формат(ТекСтатус.Получить("STATUS_ID"),"ЧГ=0");
				
				Если ВладелецСтатуса = ДопИдНаправления Тогда
					
					Если НЕ ДобавленыПринудительныеСтатусы Тогда 
						Элементы.ПереводитьВСтатусКогдаОплачен.СписокВыбора.Добавить(ИдСтатуса, НазваниеСтатуса); 
						Элементы.ПереводитьВСтатусКогдаОтгружен.СписокВыбора.Добавить(ИдСтатуса, НазваниеСтатуса); 
						Элементы.ПереводитьВСтатусКогдаОплаченОтгружен.СписокВыбора.Добавить(ИдСтатуса, НазваниеСтатуса); 
					КонецЕсли;	
					
					НоваяЗапись = ТекТаблица.Добавить();
					НоваяЗапись.ИдНаправления 		= ТекНаправление.ИдНаправления; 
					НоваяЗапись.ДопИдНаправления	= ДопИдНаправления;
					НоваяЗапись.НазваниеНаправления	= ТекНаправление.Направление;
					
					НоваяЗапись.ИдСтатуса		= ИдСтатуса;        
					НоваяЗапись.НазваниеСтатуса = НазваниеСтатуса;
					
				КонецЕсли;                                 
				
			КонецЦикла;
			
		КонецЦикла;                   
		
		ДобавленыПринудительныеСтатусы = Истина;
		
	КонецЦикла;      
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюОПринудительныхСтатусах()
	
	Если  Элементы.НаправленияСделок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдНаправления = Элементы.НаправленияСделок.ТекущиеДанные.ИдНаправления;
	
	Если ИсточникСтатусов1С = "СостоянияЗаказов" Тогда
		тзнСтатусов = СтатусыНаправленийСостояний;	
	ИначеЕсли ИсточникСтатусов1С = "СвойствоЗаказов" Тогда
		тзнСтатусов = СтатусыНаправленийЗначенийСвойств; 
	ИначеЕсли ИсточникСтатусов1С = "СвойствоЗаказов" Тогда
		тзнСтатусов = СтатусыНаправленийЗначенийСвойств; 
	ИначеЕсли ИсточникСтатусов1С = "СтатусыЗаказов" Тогда
		тзнСтатусов = СтатусыНаправленийСтатусов; 	
	КонецЕсли;
	
	Элементы.ПереводитьВСтатусКогдаОплачен.СписокВыбора.Очистить();
	Элементы.ПереводитьВСтатусКогдаОтгружен.СписокВыбора.Очистить();
	Элементы.ПереводитьВСтатусКогдаОплаченОтгружен.СписокВыбора.Очистить();
	
	Для Каждого ТекСтрока Из тзнСтатусов Цикл
		
		Если ТекСтрока.ИдНаправления = ИдНаправления Тогда 
			Элементы.ПереводитьВСтатусКогдаОплачен.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
			Элементы.ПереводитьВСтатусКогдаОтгружен.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
			Элементы.ПереводитьВСтатусКогдаОплаченОтгружен.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
			
		КонецЕсли;	
	КонецЦикла;

	ЕстьОплата 			= Ложь;
	ЕстьОтгрузка		= Ложь;
	ЕстьОплатаОтгрузка	= Ложь;
	
	Для Каждого ТекПринудительныйСтатус Из ПринудительныеСтатусы Цикл
		
		Если ТекПринудительныйСтатус.ИдНаправления = ИдНаправления Тогда
			
			Если ТекПринудительныйСтатус.НазначениеСтатуса = "Оплата" Тогда
				ПереводитьВСтатусКогдаОплачен = ТекПринудительныйСтатус.ИдСтатуса;
				ЕстьОплата = Истина;
			ИначеЕсли ТекПринудительныйСтатус.НазначениеСтатуса = "Отгрузка" Тогда
				ПереводитьВСтатусКогдаОтгружен = ТекПринудительныйСтатус.ИдСтатуса;	
				ЕстьОтгрузка = Истина;	
			ИначеЕсли ТекПринудительныйСтатус.НазначениеСтатуса = "ОплатаОтгрузка" Тогда
				ПереводитьВСтатусКогдаОплаченОтгружен = ТекПринудительныйСтатус.ИдСтатуса;	
				ЕстьОплатаОтгрузка = Истина;	
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьОплата = Ложь Тогда
		ПереводитьВСтатусКогдаОплачен = "";	
	КонецЕсли;
	
	Если ЕстьОтгрузка = Ложь Тогда
		ПереводитьВСтатусКогдаОтгружен = "";	
	КонецЕсли;
	
	Если ЕстьОплатаОтгрузка = Ложь Тогда
		ПереводитьВСтатусКогдаОплаченОтгружен = "";	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	Если ИсточникСтатусов1С = "СвойствоЗаказов" И НЕ ЗначениеЗаполнено(СвойствоЗаказов) Тогда
		ПоказатьПредупреждение(,"Не указано свойство Проекты. Запись настроек невозможна");
	Иначе
		СохранениеНастроек();
		Закрыть();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область Прочее

&НаСервере
Процедура ОбновитьСтатусыНаправлений()
	
	мТаблицы = Новый Массив;
	мТаблицы.Добавить(СтатусыНаправленийЗначенийСвойств);
	мТаблицы.Добавить(СтатусыНаправленийСостояний);
	
	Для Каждого ТекТаблица Из мТаблицы Цикл
		
		тзнВрем = ТекТаблица.Выгрузить();
		ТекТаблица.Очистить();
		
		Для Каждого ТекСтрокаТзнВрем Из тзнВрем Цикл
			
			Для каждого ТекНаправление Из НаправленияСделок Цикл
				
				Если ТекСтрокаТзнВрем.ИдНаправления = ТекНаправление.ИдНаправления Тогда
					
					НовСтрока = ТекТаблица.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтрока, ТекСтрокаТзнВрем); 
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтатусНаправления(ИдНаправления, ИдСтатуса)
	
	мТаблицы = Новый Массив;
	мТаблицы.Добавить(СтатусыНаправленийЗначенийСвойств);
	мТаблицы.Добавить(СтатусыНаправленийСостояний);
	мТаблицы.Добавить(СтатусыНаправленийСтатусов);
	
	Для Каждого ТекТаблица Из мТаблицы Цикл
		
		тзнВрем = ТекТаблица.Выгрузить();
		тзнВрем.Очистить();
		
		Для Каждого ТекСтрока Из ТекТаблица Цикл
			
			Если ТекСтрока.ИдНаправления = ИдНаправления И  ТекСтрока.ИдСтатуса = ИдСтатуса Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = тзнВрем.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока); 	
			
		КонецЦикла;
		
		ТекТаблица.Загрузить(тзнВрем);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВидЗаказаПоУмолчанию()
	
	Возврат Неопределено;	
	
КонецФункции

&НаСервере
Процедура ОбновитьСвойствоНаправленияСделки(СтруктураНастроек)
	
	СвойствоНаправлениеСделки = Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.СоздатьПолучитьСвойствоНаправлениеСделкиБитрикс24(СтруктураНастроек.НастройкаПодключения);
	
	НаименованияНаправлений = Новый Массив;
	Для каждого ТекНаправление Из НаправленияСделок Цикл
		НаименованияНаправлений.Добавить(ТекНаправление.Направление);	
	КонецЦикла;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗначенияСвойствОбъектов.Наименование КАК Наименование,
	|	ЗначенияСвойствОбъектов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|ГДЕ
	|	ЗначенияСвойствОбъектов.Владелец = &Владелец
	|	И НЕ ЗначенияСвойствОбъектов.Наименование В (&НаименованияНаправлений)";
	Запрос.УстановитьПараметр("Владелец", СвойствоНаправлениеСделки);
	Запрос.УстановитьПараметр("НаименованияНаправлений", НаименованияНаправлений);
	ВыполненныйЗапрос = Запрос.Выполнить();

	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекЗначение = Выборка.Ссылка.ПолучитьОбъект();
			ТекЗначение.ПометкаУдаления = Истина;
			ТекЗначение.Записать();
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого ТекНаправление Из НаправленияСделок Цикл
		
		ЗначениеСсылка = Справочники.ЗначенияСвойствОбъектов.НайтиПоНаименованию(ТекНаправление.Направление,,,СвойствоНаправлениеСделки);

		Если ЗначениеСсылка.Пустая() Тогда
			НовоеЗначение = Справочники.ЗначенияСвойствОбъектов.СоздатьЭлемент();
			НовоеЗначение.Владелец = СвойствоНаправлениеСделки;
			НовоеЗначение.Наименование = ТекНаправление.Направление;
			НовоеЗначение.Записать();
			ЗначениеСсылка = НовоеЗначение.Ссылка;
		КонецЕсли;
			
		ТипыОбъектовОбмена = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();
		
		РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ЗаписатьИдБ24Объекта(СтруктураНастроек, ТипыОбъектовОбмена.ЗначениеСвойстваСделки, ЗначениеСсылка, "NS_"+ТекНаправление.ИдНаправления);  
			
	КонецЦикла;
	
КонецПроцедуры
 
&НаКлиенте
Процедура ЗаполнитьСтатусыПоУмолчанию()
	
	Элементы.СтатусПоУмолчанию.СписокВыбора.Очистить();
	
	Для каждого ТекНаправление Из НаправленияСделок Цикл
		Для каждого ТекСтатус Из СтатусыНаправленийЗначенийСвойств Цикл
			
			Если ТекНаправление.ИдНаправления = ТекСтатус.ИдНаправления Тогда    
				КлючСтатусаНаправления = ТекНаправление.ИдНаправления + "#" + ТекСтатус.ИдСтатуса; 
				Элементы.СтатусПоУмолчанию.СписокВыбора.Добавить(КлючСтатусаНаправления, ТекНаправление.Направление + ": " + ТекСтатус.НазваниеСтатуса);
			КонецЕсли;
				
		КонецЦикла;
	КонецЦикла;
	
	ОбновитьОтображениеДанных(Элементы.СтатусПоУмолчанию);
	
КонецПроцедуры

#КонецОбласти
