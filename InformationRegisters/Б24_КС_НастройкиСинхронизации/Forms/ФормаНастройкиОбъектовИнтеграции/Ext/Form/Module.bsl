
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//ПараметрыЭкрана = ПолучитьИнформациюЭкрановКлиента();
	//Ширина = ПараметрыЭкрана[0].Ширина;
	//Высота = ПараметрыЭкрана[0].Высота;	
	     
	Если Параметры.Свойство("НастройкаПодключения") Тогда
		НастройкаПодключения = Параметры.НастройкаПодключения;	
	Иначе
		НастройкаПодключения = Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНастройкуПодключенияПоУмолчанию();
	КонецЕсли;
	
	Портал = НастройкаПодключения.Портал;
	
	ПоддерживаетсяКастомизированнаяИдентификация = Ложь;
	
	Если Параметры.Свойство("ВидыСущностей") Тогда
		ВидыСущностей.ЗагрузитьЗначения(Параметры.ВидыСущностей);
	Иначе
		ВидыСущностей.Добавить("Компания");
		ВидыСущностей.Добавить("Контакт");
		ВидыСущностей.Добавить("Реквизит");
		ВидыСущностей.Добавить("Банковский счет");
		ВидыСущностей.Добавить("Товар");
		ВидыСущностей.Добавить("Товар2");
		ВидыСущностей.Добавить("Предложение");
		ВидыСущностей.Добавить("Счет");
		ВидыСущностей.Добавить("Счет2");
		ВидыСущностей.Добавить("Проект");
		ВидыСущностей.Добавить("Заказ");
		ВидыСущностей.Добавить("Оплата");
		ВидыСущностей.Добавить("Отгрузка");
	КонецЕсли;
	
	Если Параметры.Свойство("РежимыЗаписи") Тогда
		
		Элементы.ТаблицаСущностейРежимЗаписи.Видимость = Истина;
		Элементы.ТаблицаСущностейРежимЗаписи.СписокВыбора.ЗагрузитьЗначения(Параметры.РежимыЗаписи);
		
	КонецЕсли;
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);

	Настройки = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	мСущности 							= Настройки.Сущности;
	мВсеСоответствияВыгрузки 			= Настройки.СоответствияПолейСущностейДляВыгрузкиНаПортал;
	мВсеСоответствияЗагрузки 			= Настройки.СоответствияПолейСущностейДляЗагрузкиВ1С;
	
	мВсеСоответствияВыгрузкиТабЧасть 	= Настройки.СоответствияПолейСущностейТабЧастейДляВыгрузкиНаПортал;
	мВсеСоответствияЗагрузкиТабЧасть 	= Настройки.СоответствияПолейСущностейТабЧастейДляЗагрузкиВ1С;
	
	мВсеОтборыПоВыгрузкаДанныхНаПортал 	= Настройки.ОтборыДляВыгрузкиНаПортал;
	мВсеОтборыПоЗагрузкеДанныхВ1С 		= Настройки.ОтборыДляЗагрузкиДанныхВ1С;
	
	Для каждого ТекЭлемент Из мСущности Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			
			НоваяЗапись = ТаблицаСущностей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
			
			Если ТекЭлемент.ПравилаПоискаПриВыгрузкеНаПортал <> ""  Тогда
				НоваяЗапись.НастроитьПоискПриВыгрузкеНаПортал = "Настроено";	
			Иначе
				НоваяЗапись.НастроитьПоискПриВыгрузкеНаПортал = "Нет";	
			КонецЕсли;
			
			Если ТекЭлемент.ПравилаПоискаПриЗагрузкеВ1С <> "" Тогда
				НоваяЗапись.НастроитьПоискПриЗагрузкеВ1С = "Настроено";	
			Иначе
				НоваяЗапись.НастроитьПоискПриЗагрузкеВ1С = "Нет";	
			КонецЕсли;
			
			Если ТекЭлемент.ВидСущности = "Компания" ИЛИ ТекЭлемент.ВидСущности = "Контакт" 
				ИЛИ ТекЭлемент.ВидСущности = "Реквизит" ИЛИ ТекЭлемент.ВидСущности = "Банковский счет" Тогда
				ПоддерживаетсяКастомизированнаяИдентификация = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ТекЭлемент Из мВсеСоответствияВыгрузки Цикл
		НоваяЗапись = ВсеСоответствияВыгрузки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
	КонецЦикла;
	
	Для каждого ТекЭлемент Из мВсеСоответствияЗагрузки Цикл
		НоваяЗапись = ВсеСоответствияЗагрузки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
	КонецЦикла;
	
	
	Для каждого ТекЭлемент Из мВсеСоответствияВыгрузкиТабЧасть Цикл
		НоваяЗапись = ВсеСоответствияВыгрузкиТабЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
	КонецЦикла;
	
	Для каждого ТекЭлемент Из мВсеСоответствияЗагрузкиТабЧасть Цикл
		НоваяЗапись = ВсеСоответствияЗагрузкиТабЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
	КонецЦикла;
	
	
	Для каждого ТекЭлемент Из мВсеОтборыПоВыгрузкаДанныхНаПортал Цикл
		НоваяЗапись = ВсеОтборыПоВыгрузкаДанныхНаПортал.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
	КонецЦикла;
	
	Для каждого ТекЭлемент Из мВсеОтборыПоЗагрузкеДанныхВ1С Цикл
		НоваяЗапись = ВсеОтборыПоЗагрузкеДанныхВ1С.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
	КонецЦикла;
	
	Элементы.ТаблицаСущностейНастроитьПоискПриВыгрузкеНаПортал.Видимость 	= ПоддерживаетсяКастомизированнаяИдентификация;
	Элементы.ТаблицаСущностейНастроитьПоискПриЗагрузкеВ1С.Видимость 		= ПоддерживаетсяКастомизированнаяИдентификация;
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	Состояние("Загрузка данных", 0);
		
		ЗаполнитьПользовательскиеПоляИКлючиПортала(ВидыСущностей);
		
	Состояние("Загрузка данных", 50);
		
		ЗаполнитьСмартПроцессыПортала();
		
	Состояние("Загрузка данных", 70);
		
		ЗаполнитьРеквизитыОбъектов1С();
		
	Состояние("Загрузка данных", 80);
		
		ЗаполнитьРеквизитыТЧОбъектов1С();
		
	Состояние("Загрузка данных", 90);
		
		ПризнакСворачиванияПравойКолонки = Истина;
		
		ОбновитьОтображениеЭлементовФормы();
		
	Состояние("Загрузка данных", 100);
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьПользовательскиеПоляИКлючиПортала(ВидыСущностей)
	
	Для каждого ТекЭлемент Из ВидыСущностей Цикл
		
		ПользовательскиеПоляИКлючи = ПолучитьКлючиПользовательскиеПоляСущностиПортала(ТекЭлемент.Значение);
		Для каждого ТекСтрока Из ПользовательскиеПоляИКлючи.Ключи Цикл
			НоваяЗапись = КлючиJSON.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока);
		КонецЦикла;
		
		Для каждого ТекСтрока Из ПользовательскиеПоляИКлючи.ПользовательскиеПоля Цикл
			НоваяЗапись = ПользовательскиеПоляПортала.Добавить();	
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока);
		КонецЦикла;
		
		Для каждого ТекСтрока Из ПользовательскиеПоляИКлючи.ЗначенияПользовательскихПолей Цикл
			НоваяЗапись = ЗначенияПользовательскихПолейПортала.Добавить();	
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСмартПроцессыПортала()
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПортала = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/crm.type.list", "id", "", "types");
	
	Для каждого ТекЭлемент Из ДанныеПортала Цикл
		НоваяСтрока = СмартПроцессы.Добавить();
		НоваяСтрока.Ид 					= Формат(ТекЭлемент.Получить("id"), "ЧГ=0");
		НоваяСтрока.Наименование 		= ТекЭлемент.Получить("title"); 
		НоваяСтрока.ТипВладельца 		= Формат(ТекЭлемент.Получить("entityTypeId"), "ЧГ=0"); 
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыОбъектов1С()
	
	РеквизитыОбъектов.Очистить();
	
	мМетаданныеОбъектов = Новый Массив;
	Для каждого ТекЭлемент Из ТаблицаСущностей Цикл
		
		Если ТекЭлемент.ВидОбъекта = "Справочник" Тогда
			МетаданныеОбъекта = Метаданные.Справочники[ТекЭлемент.ИмяОбъекта];
		ИначеЕсли ТекЭлемент.ВидОбъекта = "Документ" Тогда
			МетаданныеОбъекта = Метаданные.Документы[ТекЭлемент.ИмяОбъекта];
		КонецЕсли;
		
		Если мМетаданныеОбъектов.Найти(МетаданныеОбъекта) = Неопределено Тогда
			мМетаданныеОбъектов.Добавить(МетаданныеОбъекта);	
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ТекМетаданные Из мМетаданныеОбъектов Цикл	
		
		Для каждого ТекЭлемент Из ТекМетаданные.СтандартныеРеквизиты Цикл
			
			Если ТекЭлемент.Имя = "Родитель" ИЛИ ТекЭлемент.Имя = "Наименование" ИЛИ ТекЭлемент.Имя = "ПометкаУдаления" 
				ИЛИ ТекЭлемент.Имя = "Код" ИЛИ ТекЭлемент.Имя = "Дата" ИЛИ ТекЭлемент.Имя = "Номер" ИЛИ ТекЭлемент.Имя = "Владелец" Тогда
				
				НоваяЗапись = РеквизитыОбъектов.Добавить();
				НоваяЗапись.ИмяОбъекта 		= ТекМетаданные.Имя; 
				НоваяЗапись.Реквизит1С 		= ТекЭлемент.Имя; 
				НоваяЗапись.Наименование 	= ?(ЗначениеЗаполнено(ТекЭлемент.Синоним), ТекЭлемент.Синоним, ТекЭлемент.Имя); 
				
				Если ТекЭлемент.Имя = "ПометкаУдаления" Тогда
					НоваяЗапись.Наименование = "Пометка удаления"; 
				ИначеЕсли ТекЭлемент.Имя = "Родитель" Тогда
					НоваяЗапись.Наименование = "Группа"; 
				КонецЕсли;
				
			КонецЕсли;
				
		КонецЦикла;
		
		Для каждого ТекЭлемент Из ТекМетаданные.Реквизиты Цикл
			НоваяЗапись = РеквизитыОбъектов.Добавить();
			НоваяЗапись.ИмяОбъекта 		= ТекМетаданные.Имя; 
			НоваяЗапись.Реквизит1С 		= ТекЭлемент.Имя; 
			НоваяЗапись.Наименование 	= ?(ЗначениеЗаполнено(ТекЭлемент.Синоним), ТекЭлемент.Синоним, ТекЭлемент.Имя); 
		КонецЦикла;

	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыТЧОбъектов1С()
	
	мМетаданныеОбъектов = Новый Массив;
	Для каждого ТекЭлемент Из ТаблицаСущностей Цикл
		
		Если ТекЭлемент.ВидОбъекта = "Справочник" Тогда
			МетаданныеОбъекта = Метаданные.Справочники[ТекЭлемент.ИмяОбъекта];
		ИначеЕсли ТекЭлемент.ВидОбъекта = "Документ" Тогда
			МетаданныеОбъекта = Метаданные.Документы[ТекЭлемент.ИмяОбъекта];
		КонецЕсли;
		
		Если мМетаданныеОбъектов.Найти(МетаданныеОбъекта) = Неопределено Тогда
			мМетаданныеОбъектов.Добавить(МетаданныеОбъекта);	
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ТекМетаданные Из мМетаданныеОбъектов Цикл	
		
		Для каждого ТекТабЧасть Из ТекМетаданные.ТабличныеЧасти Цикл
		
			Для каждого ТекЭлемент Из ТекМетаданные.ТабличныеЧасти[ТекТабЧасть.Имя].Реквизиты Цикл
				НоваяЗапись = РеквизитыОбъектовТЧ.Добавить();
				НоваяЗапись.ИмяТабЧасти 	= ТекТабЧасть.Имя; 
				НоваяЗапись.ИмяОбъекта 		= ТекМетаданные.Имя; 
				НоваяЗапись.Реквизит1С 		= ТекЭлемент.Имя; 
				НоваяЗапись.Наименование 	= ?(ЗначениеЗаполнено(ТекЭлемент.Синоним), ТекЭлемент.Синоним, ТекЭлемент.Имя); 
			КонецЦикла;
			
		КонецЦикла;

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


#Область КомандыФормы

&НаКлиенте
Процедура КомандаСдвинутьРаздвинуть(Команда)
	
	ПризнакСворачиванияПравойКолонки = НЕ ПризнакСворачиванияПравойКолонки;
	
	ОбновитьОтображениеЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура КомандаПрименить(Команда)
	
	СохранитьНастройки();
	
	Результат = Новый Структура;
	Результат.Вставить("ВыгружатьНастройкиНаПортал", ВыгружатьНастройкиНаПортал);
	
	Закрыть(Результат);
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаЗаполнитьПоУмолчанию(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОПерезаполненииПоУмолчанию", ЭтотОбъект, Параметры);
	ПоказатьВопрос(Оповещение, "Вы точно желаете перезаполнить по умолчанию?", РежимДиалогаВопрос.ДаНет, 0);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОПерезаполненииПоУмолчанию(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
			
	КомандаЗаполнитьПоУмолчаниюНаСервере();

	ЗаполнитьРеквизитыОбъектов1С();
	
	ЗаполнитьРеквизитыТЧОбъектов1С();
	
КонецПроцедуры
	
&НаСервере
Процедура КомандаЗаполнитьПоУмолчаниюНаСервере()
	
	мДанные = Новый Массив;
	мДанные.Добавить(Новый Структура("ТзнДанных, ТзнФормы", ТаблицаСущностей.Выгрузить()					, ТаблицаСущностей));
	мДанные.Добавить(Новый Структура("ТзнДанных, ТзнФормы", ВсеСоответствияВыгрузки.Выгрузить()				, ВсеСоответствияВыгрузки));
	мДанные.Добавить(Новый Структура("ТзнДанных, ТзнФормы", ВсеСоответствияЗагрузки.Выгрузить()				, ВсеСоответствияЗагрузки));
	мДанные.Добавить(Новый Структура("ТзнДанных, ТзнФормы", ВсеСоответствияВыгрузкиТабЧасть.Выгрузить()		, ВсеСоответствияВыгрузкиТабЧасть));
	мДанные.Добавить(Новый Структура("ТзнДанных, ТзнФормы", ВсеСоответствияЗагрузкиТабЧасть.Выгрузить()		, ВсеСоответствияЗагрузкиТабЧасть));
	мДанные.Добавить(Новый Структура("ТзнДанных, ТзнФормы", ВсеОтборыПоВыгрузкаДанныхНаПортал.Выгрузить()	, ВсеОтборыПоВыгрузкаДанныхНаПортал));
	мДанные.Добавить(Новый Структура("ТзнДанных, ТзнФормы", ВсеОтборыПоЗагрузкеДанныхВ1С.Выгрузить()		, ВсеОтборыПоЗагрузкеДанныхВ1С));
	
	ТаблицаСущностей.Очистить();
	
	ТаблицаСоответствийВыгрузки.Очистить();
	ТаблицаСоответствийЗагрузки.Очистить();
	ТаблицаСоответствийВыгрузкиТабЧасть.Очистить();
	ТаблицаСоответствийЗагрузкиТабЧасть.Очистить();
	
	ВсеСоответствияВыгрузки.Очистить();
	ВсеСоответствияЗагрузки.Очистить();
	ВсеСоответствияВыгрузкиТабЧасть.Очистить();
	ВсеСоответствияЗагрузкиТабЧасть.Очистить();
	
	ТаблицаОтборовПоЗагрузкеДанныхВ1С.Очистить();
	ВсеОтборыПоВыгрузкаДанныхНаПортал.Очистить();
	ВсеОтборыПоЗагрузкеДанныхВ1С.Очистить();
	КомпоновщикНастроекКомпоновкиДанных = Неопределено;
	
	Для Каждого ТекЭлемент Из мДанные Цикл
		
		Для каждого ТекСтрока Из ТекЭлемент.ТзнДанных Цикл
			Если ВидыСущностей.НайтиПоЗначению(ТекСтрока.ВидСущности) = Неопределено Тогда
				НоваяСтрока = ТекЭлемент.ТзнФормы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			КонецЕсли;	
		КонецЦикла;
	
	КонецЦикла;
	
	СтруктураДанныхКонструктораИнтеграцийПоУмолчанию = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСтруктуруДанныхКонструктораИнтеграцийПоУмолчанию();
	
	Для каждого ТекЭлемент Из СтруктураДанныхКонструктораИнтеграцийПоУмолчанию.Сущности Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяЗапись = ТаблицаСущностей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекЭлемент Из СтруктураДанныхКонструктораИнтеграцийПоУмолчанию.ВсеСоответствияВыгрузки Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяЗапись = ВсеСоответствияВыгрузки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекЭлемент Из СтруктураДанныхКонструктораИнтеграцийПоУмолчанию.ВсеСоответствияЗагрузки Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяЗапись = ВсеСоответствияЗагрузки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекЭлемент Из СтруктураДанныхКонструктораИнтеграцийПоУмолчанию.ВсеСоответствияВыгрузкиТабЧасть Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяЗапись = ВсеСоответствияВыгрузкиТабЧасть.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекЭлемент Из СтруктураДанныхКонструктораИнтеграцийПоУмолчанию.ВсеСоответствияЗагрузкиТабЧасть Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяЗапись = ВсеСоответствияЗагрузкиТабЧасть.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекЭлемент Из СтруктураДанныхКонструктораИнтеграцийПоУмолчанию.ВсеОтборыПоВыгрузкаДанныхНаПортал Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяЗапись = ВсеОтборыПоВыгрузкаДанныхНаПортал.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекЭлемент Из СтруктураДанныхКонструктораИнтеграцийПоУмолчанию.ВсеОтборыПоЗагрузкеДанныхВ1С Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяЗапись = ВсеОтборыПоЗагрузкеДанныхВ1С.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекСтрока Из ТаблицаСущностей Цикл
		
		Если ТекСтрока.ПравилаПоискаПриВыгрузкеНаПортал <> ""  Тогда
			ТекСтрока.НастроитьПоискПриВыгрузкеНаПортал = "Настроено";	
		Иначе
			ТекСтрока.НастроитьПоискПриВыгрузкеНаПортал = "Нет";	
		КонецЕсли;
		
		Если ТекСтрока.ПравилаПоискаПриЗагрузкеВ1С <> "" Тогда
			ТекСтрока.НастроитьПоискПриЗагрузкеВ1С = "Настроено";	
		Иначе
			ТекСтрока.НастроитьПоискПриЗагрузкеВ1С = "Нет";	
		КонецЕсли;
		
	КонецЦикла;
	
	ВыгружатьНастройкиНаПортал = Истина;
	
КонецПроцедуры

#КонецОбласти


#Область ОбщиеСобытияЭлементовФормы

&НаКлиенте
Процедура СущностиПриАктивизацииСтроки(Элемент)
	
	Состояние("Инициализация",0);
	
	ТекущаяСущность = Элементы.ТаблицаСущностей.ТекущиеДанные;
	Если ТекущаяСущность = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.СтраницаВыгрузкаНаПортал.Видимость 			= ТекущаяСущность.ВыгружатьНаПортал; 
	Элементы.СтраницаЗагрузкаВ1С.Видимость 					= ТекущаяСущность.ЗагружатьВ1С; 
	Элементы.СтраницаОтборыВыгрузкиДанныхНаПортал.Видимость = ТекущаяСущность.ВыгружатьНаПортал; 
	Элементы.СтраницаОтборыЗагрузкиДанныхВ1С.Видимость 		= ТекущаяСущность.ЗагружатьВ1С; 
	
	
	Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1СКлючЗапроса.СписокВыбора.Очистить();
	Для каждого ТекПоле Из ПользовательскиеПоляПортала Цикл
		Если ТекущаяСущность.ВидСущности = ТекПоле.ВидСущности Тогда
			Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1СКлючЗапроса.СписокВыбора.Добавить(ТекПоле.КлючЗапроса, "Пользовательское поле '" + ТекПоле.Описание + "'");
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекКлюч Из КлючиJSON Цикл
		Если ТекущаяСущность.ВидСущности = ТекКлюч.ВидСущности Тогда
			Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1СКлючЗапроса.СписокВыбора.Добавить(ТекКлюч.КлючЗапроса);
		КонецЕсли;
	КонецЦикла;
	
	Состояние("Инициализация",10);
	
#Область СохранениеДанныхТаблиц	

	Если ТаблицаСоответствийЗагрузки.количество() > 0 Тогда
		НайденныеСтроки = ВсеСоответствияЗагрузки.НайтиСтроки(Новый Структура("ВидСущности, ИмяОбъекта", ПредВидСущности, ПредИмяОбъекта));
		Для каждого ТекСтрока Из НайденныеСтроки Цикл
			ВсеСоответствияЗагрузки.Удалить(ВсеСоответствияЗагрузки.Индекс(ТекСтрока));
		КонецЦикла;                                       
		
		Для каждого ТекСтрока Из ТаблицаСоответствийЗагрузки Цикл
			НоваяЗапись = ВсеСоответствияЗагрузки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
		КонецЦикла;
	КонецЕсли;

	Если ТаблицаСоответствийВыгрузки.количество() > 0 Тогда
		НайденныеСтроки = ВсеСоответствияВыгрузки.НайтиСтроки(Новый Структура("ВидСущности, ИмяОбъекта", ПредВидСущности, ПредИмяОбъекта));
		Для каждого ТекСтрока Из НайденныеСтроки Цикл
			ВсеСоответствияВыгрузки.Удалить(ВсеСоответствияВыгрузки.Индекс(ТекСтрока));
		КонецЦикла;                                       
		
		Для каждого ТекСтрока Из ТаблицаСоответствийВыгрузки Цикл
			НоваяЗапись = ВсеСоответствияВыгрузки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
		КонецЦикла;
	КонецЕсли;

	Если ТаблицаСоответствийВыгрузкиТабЧасть.количество() > 0 Тогда
		НайденныеСтроки = ВсеСоответствияВыгрузкиТабЧасть.НайтиСтроки(Новый Структура("ВидСущности, ИмяОбъекта", ПредВидСущности, ПредИмяОбъекта));
		Для каждого ТекСтрока Из НайденныеСтроки Цикл
			ВсеСоответствияВыгрузкиТабЧасть.Удалить(ВсеСоответствияВыгрузкиТабЧасть.Индекс(ТекСтрока));
		КонецЦикла;                                       

		Для каждого ТекСтрока Из ТаблицаСоответствийВыгрузкиТабЧасть Цикл
			НоваяЗапись = ВсеСоответствияВыгрузкиТабЧасть.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
		КонецЦикла;
	КонецЕсли;

	Если ТаблицаСоответствийЗагрузкиТабЧасть.количество() > 0 Тогда
		НайденныеСтроки = ВсеСоответствияЗагрузкиТабЧасть.НайтиСтроки(Новый Структура("ВидСущности, ИмяОбъекта", ПредВидСущности, ПредИмяОбъекта));
		Для каждого ТекСтрока Из НайденныеСтроки Цикл
			ВсеСоответствияЗагрузкиТабЧасть.Удалить(ВсеСоответствияЗагрузкиТабЧасть.Индекс(ТекСтрока));
		КонецЦикла;                                       

		Для каждого ТекСтрока Из ТаблицаСоответствийЗагрузкиТабЧасть Цикл
			НоваяЗапись = ВсеСоответствияЗагрузкиТабЧасть.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
		КонецЦикла;
	КонецЕсли;

	ПредВидСущности = ТекущаяСущность.ВидСущности;
	ПредИмяОбъекта	= ТекущаяСущность.ИмяОбъекта;

#КонецОбласти
	
	Состояние("Инициализация",20);
	
#Область ЗаполнениеПоВыгрузке	    
	
	ТаблицаСоответствийВыгрузки.Очистить();
	
	НайденныеСтроки = ВсеСоответствияВыгрузки.НайтиСтроки(Новый Структура("ВидСущности, ВидОбъекта, ИмяОбъекта", ТекущаяСущность.ВидСущности, ТекущаяСущность.ВидОбъекта, ТекущаяСущность.ИмяОбъекта));
	Для каждого ТекСтрока Из НайденныеСтроки Цикл
		
		Если Лев(ТекСтрока.КлючЗапроса, 7) = "UF_CRM_" ИЛИ Лев(ТекСтрока.КлючЗапроса, 8) = "property" ИЛИ Лев(ТекСтрока.КлючЗапроса, 9) = "PROPERTY_" ИЛИ Лев(ТекСтрока.КлючЗапроса, 20) = "ufCrm_SMART_INVOICE_" Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = ТаблицаСоответствийВыгрузки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
		
	КонецЦикла;
	
	ПользовательскиеПоля = ПользовательскиеПоляПортала.НайтиСтроки(Новый Структура("ВидСущности", ТекущаяСущность.ВидСущности));
	Для каждого ТекЭлемент Из ПользовательскиеПоля Цикл
		
		НоваяЗапись = ТаблицаСоответствийВыгрузки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущаяСущность); 
		
		Для каждого СтрокаДанных Из ВсеСоответствияВыгрузки Цикл
			
			ЕстьСтрока = Ложь;
			Если СтрокаДанных.ВидСущности = ТекущаяСущность.ВидСущности И СтрокаДанных.ВидОбъекта = ТекущаяСущность.ВидОбъекта
				И СтрокаДанных.ИмяОбъекта = ТекущаяСущность.ИмяОбъекта И СтрокаДанных.КлючЗапроса = ТекЭлемент.КлючЗапроса Тогда
		 			
				Если НЕ ЗначениеЗаполнено(СтрокаДанных.Значение) И НЕ СтрокаДанных.ИсточникДанных = "Не изменять" Тогда
					
					НайденноеСвойство = ПолучитьСвойство1СПоИдентификатору(ТекущаяСущность.ВидСущности, ТекущаяСущность.ИмяОбъекта, ТекЭлемент.Идентификатор);
					СтрокаДанных.Значение = НайденноеСвойство;
					
					Если ЗначениеЗаполнено(НайденноеСвойство) Тогда
						ЭтоДополнительноеСведение = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НайденноеСвойство, "ЭтоДополнительноеСведение");	
						СтрокаДанных.ИсточникДанных = ?(ЭтоДополнительноеСведение, "Из дополнительного сведения", "Из дополнительного реквизита");
					Иначе
						СтрокаДанных.ИсточникДанных = "Из дополнительного реквизита"; 
					КонецЕсли;
					
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаДанных); 
				
				ЕстьСтрока = Истина; 
				
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ ЕстьСтрока Тогда
			
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекЭлемент); 
			
			НайденноеСвойство = ПолучитьСвойство1СПоИдентификатору(ТекущаяСущность.ВидСущности, ТекущаяСущность.ИмяОбъекта, ТекЭлемент.Идентификатор);
			НоваяЗапись.Значение = НайденноеСвойство;
			
			Если ЗначениеЗаполнено(НайденноеСвойство) Тогда
				ЭтоДополнительноеСведение = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НайденноеСвойство, "ЭтоДополнительноеСведение");	
				НоваяЗапись.ИсточникДанных = ?(ЭтоДополнительноеСведение, "Из дополнительного сведения", "Из дополнительного реквизита");
			Иначе
				НоваяЗапись.ИсточникДанных = "Из дополнительного реквизита"; 
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НоваяЗапись.Описание) Тогда
			мНаименованияСлужебныхСвойств = Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаименованияСлужебныхСвойствБ24();
			Если мНаименованияСлужебныхСвойств.Найти(НоваяЗапись.Описание) <> Неопределено Тогда
				НоваяЗапись.Служебное = Истина;	
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ТекСмартПроцесс Из СмартПроцессы Цикл
		Для каждого ТекКлючJSON Из КлючиJSON Цикл
			
			Если ТекКлючJSON.КлючЗапроса = "PARENT_ID_" + ТекСмартПроцесс.ТипВладельца ИЛИ ТекКлючJSON.КлючЗапроса = "parentId" + ТекСмартПроцесс.ТипВладельца Тогда 
				
				НайденныеСтроки = ТаблицаСоответствийВыгрузки.НайтиСтроки(Новый Структура("КлючЗапроса", ТекКлючJSON.КлючЗапроса));
				Если НайденныеСтроки.Количество() = 0 Тогда
					НоваяЗапись = ТаблицаСоответствийВыгрузки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущаяСущность); 
					НоваяЗапись.КлючЗапроса 	= ТекКлючJSON.КлючЗапроса;
					НоваяЗапись.Описание 		= ТекСмартПроцесс.Наименование; 
					НоваяЗапись.Идентификатор 	= ТекСмартПроцесс.ТипВладельца;
					НоваяЗапись.Значение 		= "";
				Иначе
					Для каждого СтрокаДанных Из ВсеСоответствияВыгрузки Цикл
						
						Если СтрокаДанных.ВидСущности = ТекущаяСущность.ВидСущности И СтрокаДанных.ВидОбъекта = ТекущаяСущность.ВидОбъекта
							И СтрокаДанных.ИмяОбъекта = ТекущаяСущность.ИмяОбъекта И СтрокаДанных.КлючЗапроса = НайденныеСтроки[0].КлючЗапроса Тогда
							
							ЗаполнитьЗначенияСвойств(НайденныеСтроки[0], СтрокаДанных); 
							Прервать;
							
						КонецЕсли;
						
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаСоответствийВыгрузкиТабЧасть.Очистить();
	
	НайденныеСтроки = ВсеСоответствияВыгрузкиТабЧасть.НайтиСтроки(Новый Структура("ВидСущности, ВидОбъекта, ИмяОбъекта", ТекущаяСущность.ВидСущности, ТекущаяСущность.ВидОбъекта, ТекущаяСущность.ИмяОбъекта));
	Для каждого ТекСтрока Из НайденныеСтроки Цикл
		НоваяЗапись = ТаблицаСоответствийВыгрузкиТабЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
	КонецЦикла;
	
#КонецОбласти	

	Состояние("Инициализация",30);
	
#Область ЗаполнениеПоЗагрузке

	ТаблицаСоответствийЗагрузки.Очистить();
	   
	НайденныеРеквизитыСущности = РеквизитыОбъектов.НайтиСтроки(Новый Структура("ИмяОбъекта", ТекущаяСущность.ИмяОбъекта));
	Для каждого ТекСтрока Из НайденныеРеквизитыСущности Цикл
		
		НоваяЗапись = ТаблицаСоответствийЗагрузки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущаяСущность); 
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
		
		Для каждого СтрокаДанных Из ВсеСоответствияЗагрузки Цикл
			
			ЕстьСтрока = Ложь;
			Если СтрокаДанных.ВидСущности = ТекущаяСущность.ВидСущности И СтрокаДанных.ИмяОбъекта = ТекСтрока.ИмяОбъекта И СтрокаДанных.Реквизит1С = ТекСтрока.Реквизит1С Тогда
				ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаДанных); 
				НоваяЗапись.Наименование = ТекСтрока.Наименование;
				
				ЕстьСтрока = Истина;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ ЕстьСтрока Тогда
			НоваяЗапись.Приоритет = 100;
		КонецЕсли;
		
	КонецЦикла;
	
	мПоля = ПолучитьМассивДопРеквизитовИСведенийОбъекта(ТекущаяСущность.ВидОбъекта, ТекущаяСущность.ИмяОбъекта, ТекущаяСущность.ВидСущности);
	Для каждого ТекСтрока Из мПоля Цикл
	
		НоваяЗапись = ТаблицаСоответствийЗагрузки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущаяСущность); 
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
	
		Для каждого СтрокаДанных Из ВсеСоответствияЗагрузки Цикл     			
	
			Если СтрокаДанных.ВидСущности = ТекущаяСущность.ВидСущности И СтрокаДанных.ИмяОбъекта = ТекущаяСущность.ИмяОбъекта И СтрокаДанных.Реквизит1С = ТекСтрока.Реквизит1С Тогда
				
				ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаДанных);
				Если НЕ ЗначениеЗаполнено(НоваяЗапись.Значение) И НоваяЗапись.ИсточникДанных = "Из пользовательского поля" Тогда
					НоваяЗапись.Значение = ТекСтрока.Значение;	
				КонецЕсли;
				НоваяЗапись.Наименование 		= ТекСтрока.Наименование;
				НоваяЗапись.СсылкаНаСвойство 	= ТекСтрока.СсылкаНаСвойство;
				
				Если ЗначениеЗаполнено(НоваяЗапись.СсылкаНаСвойство) Тогда
					мНаименованияСлужебныхСвойств = Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаименованияСлужебныхСвойствБ24();
					Если мНаименованияСлужебныхСвойств.Найти(ЗначениеРеквизитаОбъекта(НоваяЗапись.СсылкаНаСвойство, "Заголовок")) <> Неопределено Тогда
						НоваяЗапись.Служебное = Истина;	
					КонецЕсли;
				КонецЕсли;
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

	ТаблицаСоответствийЗагрузки.Сортировать("Приоритет, Наименование");
	
	ТаблицаСоответствийЗагрузкиТабЧасть.Очистить();
	
	НайденныеСтроки = ВсеСоответствияЗагрузкиТабЧасть.НайтиСтроки(Новый Структура("ВидСущности, ВидОбъекта, ИмяОбъекта", ТекущаяСущность.ВидСущности, ТекущаяСущность.ВидОбъекта, ТекущаяСущность.ИмяОбъекта));
	Для каждого ТекСтрока Из НайденныеСтроки Цикл
		
		НоваяЗапись = ТаблицаСоответствийЗагрузкиТабЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
		
		НайденныеСтроки = РеквизитыОбъектовТЧ.НайтиСтроки(Новый Структура("ИмяОбъекта, ИмяТабЧасти, Реквизит1С", ТекСтрока.ИмяОбъекта, ТекСтрока.ИмяТабЧасти, ТекСтрока.Реквизит1С));
		Если НайденныеСтроки.Количество() > 0 Тогда
			НоваяЗапись.Наименование = НайденныеСтроки[0].Наименование;	
		Иначе	
			НоваяЗапись.Наименование = НоваяЗапись.Реквизит1С;	
		КонецЕсли;
		
		
	КонецЦикла;
	НайденныеРеквизитыСущности = РеквизитыОбъектовТЧ.НайтиСтроки(Новый Структура("ИмяОбъекта", ТекущаяСущность.ИмяОбъекта));
	Для каждого ТекСтрока Из НайденныеРеквизитыСущности Цикл
		
		НайденныеСтроки = ТаблицаСоответствийЗагрузкиТабЧасть.НайтиСтроки(Новый Структура("ИмяОбъекта, Реквизит1С", ТекСтрока.ИмяОбъекта, ТекСтрока.Реквизит1С));
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяЗапись = ТаблицаСоответствийЗагрузкиТабЧасть.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущаяСущность); 
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
		КонецЕсли;
		
	КонецЦикла;

#КонецОбласти
	
	Состояние("Инициализация",90);
	
#Область ЗаполнениеОтборов

	ЗаполнитьНастройкуОтборовДляВыгрузкиНаПортал(ТекущаяСущность.ВидСущности, ТекущаяСущность.ВидОбъекта, ТекущаяСущность.ИмяОбъекта);		

	ТаблицаОтборовПоЗагрузкеДанныхВ1С.Очистить();
	Для каждого ТекСтрока Из ВсеОтборыПоЗагрузкеДанныхВ1С Цикл
		
		Если ТекСтрока.ВидСущности = ТекущаяСущность.ВидСущности И ТекущаяСущность.ВидОбъекта = ТекущаяСущность.ВидОбъекта 
			И ТекСтрока.ИмяОбъекта = ТекущаяСущность.ИмяОбъекта Тогда
			
			НоваяЗапись = ТаблицаОтборовПоЗагрузкеДанныхВ1С.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
			
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.СтраницыОтборы.Видимость = НЕ (ТекущаяСущность.ВидСущности = "Предложение" И ТекущаяСущность.ИмяОбъекта = "Номенклатура");

#КонецОбласти
	
	Состояние("Инициализация",100);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСущностейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаСущностей.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если Элемент.ТекущийЭлемент.Имя = "ТаблицаСущностейНастроитьПоискПриЗагрузкеВ1С" Тогда
			
			Оповещение = Новый ОписаниеОповещения("ПослеНастройкиИдентификации", ЭтотОбъект, "ПравилаПоискаПриЗагрузкеВ1С");
		
			лПараметры = Новый Структура;
			лПараметры.Вставить("ПравилаПоиска"	, ТекущиеДанные.ПравилаПоискаПриЗагрузкеВ1С);
			лПараметры.Вставить("ВидСущности"	, ТекущиеДанные.ВидСущности);
			
			ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиИдентификации", лПараметры,,,,,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
			
		ИначеЕсли Элемент.ТекущийЭлемент.Имя = "ТаблицаСущностейНастроитьПоискПриВыгрузкеНаПортал" Тогда	
			
			Оповещение = Новый ОписаниеОповещения("ПослеНастройкиИдентификации", ЭтотОбъект, "ПравилаПоискаПриВыгрузкеНаПортал");
		
			лПараметры = Новый Структура;
			лПараметры.Вставить("ПравилаПоиска"	, ТекущиеДанные.ПравилаПоискаПриВыгрузкеНаПортал);
			лПараметры.Вставить("ВидСущности"	, ТекущиеДанные.ВидСущности);
			
			ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиИдентификации", лПараметры,,,,,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеНастройкиИдентификации(Результат, ИмяПоля) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ТекущиеДанные = Элементы.ТаблицаСущностей.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			
			Если ИмяПоля = "ПравилаПоискаПриВыгрузкеНаПортал" Тогда
				
				ТекущиеДанные.ПравилаПоискаПриВыгрузкеНаПортал = Результат; 				
				Если Результат <> "" Тогда
					ТекущиеДанные.НастроитьПоискПриВыгрузкеНаПортал = "Настроено";	
				Иначе
					ТекущиеДанные.НастроитьПоискПриВыгрузкеНаПортал = "Нет";	
				КонецЕсли;
				
			ИначеЕсли ИмяПоля = "ПравилаПоискаПриЗагрузкеВ1С" Тогда 
				
				ТекущиеДанные.ПравилаПоискаПриЗагрузкеВ1С = Результат;
				Если Результат <> "" Тогда
					ТекущиеДанные.НастроитьПоискПриЗагрузкеВ1С = "Настроено";	
				Иначе
					ТекущиеДанные.НастроитьПоискПриЗагрузкеВ1С = "Нет";	
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура СущностиВыгружатьНаПорталПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСущностей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ТекущиеДанные.ВыгружатьНаПортал Тогда
		ТекущиеДанные.ОбновлятьНаПортале = Ложь;	
	КонецЕсли;
	
	Элементы.СтраницаВыгрузкаНаПортал.Видимость 			= ТекущиеДанные.ВыгружатьНаПортал; 
	Элементы.СтраницаОтборыВыгрузкиДанныхНаПортал.Видимость = ТекущиеДанные.ВыгружатьНаПортал; 
	
	ВыгружатьНастройкиНаПортал = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СущностиЗагружатьВ1СПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСущностей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ВидСущности = "Предложение" И ТекущиеДанные.ИмяОбъекта = "Номенклатура" Тогда
		ТекущиеДанные.ЗагружатьВ1С = Ложь;
	Иначе
	
		Если НЕ ТекущиеДанные.ЗагружатьВ1С Тогда
			ТекущиеДанные.ОбновлятьВ1С 	= Ложь;	
			ТекущиеДанные.РежимЗаписи 	= Ложь;	
		КонецЕсли;
		
		Элементы.СтраницаЗагрузкаВ1С.Видимость 				= ТекущиеДанные.ЗагружатьВ1С; 
		Элементы.СтраницаОтборыЗагрузкиДанныхВ1С.Видимость 	= ТекущиеДанные.ЗагружатьВ1С; 
		
		ВыгружатьНастройкиНаПортал = Истина;   
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СущностиОбновлятьНаПорталеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСущностей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ОбновлятьНаПортале = ?(ТекущиеДанные.ВыгружатьНаПортал, ТекущиеДанные.ОбновлятьНаПортале, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СущностиОбновлятьВ1СПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСущностей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ВидСущности = "Предложение" И ТекущиеДанные.ИмяОбъекта = "Номенклатура" Тогда
		ТекущиеДанные.ОбновлятьВ1С = Ложь;
	Иначе
		ТекущиеДанные.ОбновлятьВ1С = ?(ТекущиеДанные.ЗагружатьВ1С, ТекущиеДанные.ОбновлятьВ1С, Ложь); 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ВыгрузкаДанных

&НаКлиенте
Процедура ТаблицаСоответствийВыгрузкиПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийВыгрузки.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		мИсточникДанных = Новый Массив;  
		мИсточникДанных.Добавить("Не изменять");
		мИсточникДанных.Добавить("Предопределенный алгоритм");
		мИсточникДанных.Добавить("Фиксированное значение");
		мИсточникДанных.Добавить("Реквизит объекта");  
		
		Если НЕ (ТекущиеДанные.ИмяОбъекта = "Номенклатура" И ТекущиеДанные.ВидСущности = "Предложение") Тогда 
			мИсточникДанных.Добавить("Из дополнительного реквизита");
			мИсточникДанных.Добавить("Из дополнительного сведения");
		КонецЕсли;
		
		Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
			мИсточникДанных.Добавить("<Свой алгоритм>");
		КонецЕсли;
		
		мИсточникДанных.Добавить("");
		
		Элементы.ТаблицаСоответствийВыгрузкиЗначение.ТолькоПросмотр			= Ложь;
		Элементы.ТаблицаСоответствийВыгрузкиИсточникДанных.ТолькоПросмотр 	= Ложь;
		
		Если Лев(ТекущиеДанные.КлючЗапроса, 7) = "UF_CRM_" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 8) = "property" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 9) = "PROPERTY_" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 20) = "ufCrm_SMART_INVOICE_" Тогда
			НайденныеСтроки = ПользовательскиеПоляПортала.НайтиСтроки(Новый Структура("ВидСущности, КлючЗапроса", ТекущиеДанные.ВидСущности, ТекущиеДанные.КлючЗапроса));	
			Если НайденныеСтроки.Количество() > 0 Тогда
				Если НайденныеСтроки[0].ТипПоля = "enumeration" ИЛИ НайденныеСтроки[0].ТипПоля = "L_" Тогда
					
					мИсточникДанных = Новый Массив;  
					мИсточникДанных.Добавить("Не изменять");
					мИсточникДанных.Добавить("Фиксированное значение");
					мИсточникДанных.Добавить("Из дополнительного реквизита");
					мИсточникДанных.Добавить("Из дополнительного сведения");
					мИсточникДанных.Добавить("Реквизит объекта");
					Элементы.ТаблицаСоответствийВыгрузкиЗначение.ТолькоПросмотр		= Истина;
					
				КонецЕсли;
			КонецЕсли;

			
			Если ТекущиеДанные.Служебное Тогда
				Элементы.ТаблицаСоответствийВыгрузкиЗначение.ТолькоПросмотр			= Истина;
				Элементы.ТаблицаСоответствийВыгрузкиИсточникДанных.ТолькоПросмотр 	= Истина;
				Возврат;
			КонецЕсли;
	
		КонецЕсли;
		
		Элементы.ТаблицаСоответствийВыгрузкиИсточникДанных.СписокВыбора.ЗагрузитьЗначения(мИсточникДанных);	
		
		лЭлемент = Элементы.ТаблицаСоответствийВыгрузки.ПодчиненныеЭлементы.ТаблицаСоответствийВыгрузкиЗначение;
		
		Если ТекущиеДанные.ИсточникДанных = "Предопределенный алгоритм" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.ЗагрузитьЗначения(ПолучитьМассивПредопределенныхАлгоритмовВыгрузки(ТекущиеДанные.ВидСущности));
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Фиксированное значение" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.СписокВыбора.Очистить();
			
			Если Лев(ТекущиеДанные.КлючЗапроса, 7) = "UF_CRM_" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 8) = "property" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 9) = "PROPERTY_" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 20) = "ufCrm_SMART_INVOICE_" Тогда
				НайденныеСтроки = ПользовательскиеПоляПортала.НайтиСтроки(Новый Структура("ВидСущности, КлючЗапроса", ТекущиеДанные.ВидСущности, ТекущиеДанные.КлючЗапроса));	
				Если НайденныеСтроки.Количество() > 0 Тогда
					Если НайденныеСтроки[0].ТипПоля = "enumeration" ИЛИ НайденныеСтроки[0].ТипПоля = "L_"Тогда
						
						лЭлемент.ТолькоПросмотр				= Ложь;
						лЭлемент.РедактированиеТекста 		= Ложь;
						лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
						лЭлемент.КнопкаВыбора 				= Истина;
						
						Для каждого ТекСтрока Из ЗначенияПользовательскихПолейПортала Цикл
							Если ТекСтрока.ВидСущности = ТекущиеДанные.ВидСущности И ТекСтрока.Идентификатор = НайденныеСтроки[0].Идентификатор Тогда
								лЭлемент.СписокВыбора.Добавить(ТекСтрока.ИдентификаторЗначения, ТекСтрока.ПредставлениеЗначения);
							КонецЕсли;
						КонецЦикла;
						
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "<Свой алгоритм>" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Из дополнительного реквизита" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Истина;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения");
			
			Элементы.ТаблицаСоответствийВыгрузкиЗначение.ПараметрыВыбора = ПолучитьПараметрыВыбораДляСвойств(ТекущиеДанные.ВидСущности, ТекущиеДанные.ИсточникДанных);
				
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Из дополнительного сведения" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Истина;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения");
			
			Элементы.ТаблицаСоответствийВыгрузкиЗначение.ПараметрыВыбора = ПолучитьПараметрыВыбораДляСвойств(ТекущиеДанные.ВидСущности, ТекущиеДанные.ИсточникДанных);
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Реквизит объекта" Тогда
			
 			лЭлемент.ВыбиратьТип 		  		= Истина;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
			Для каждого ТекРеквизит Из РеквизитыОбъектов Цикл
				Если ТекРеквизит.ИмяОбъекта = ТекущиеДанные.ИмяОбъекта Тогда
					лЭлемент.СписокВыбора.Добавить(ТекРеквизит.Реквизит1С, ТекРеквизит.Наименование);
				КонецЕсли;	
			КонецЦикла;    
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Не изменять" Тогда      
			
			лЭлемент.СписокВыбора.Очистить();
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Истина;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "" Тогда
			
			лЭлемент.СписокВыбора.Очистить();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивПредопределенныхАлгоритмовВыгрузки(ВидСущности)
	
	Возврат РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьМассивПредопределенныхАлгоритмовВыгрузки(ВидСущности);
	
КонецФункции

&НаСервере
Функция ПолучитьМассивПредопределенныхАлгоритмовВыгрузкиТЧ(ВидСущности)
	
	Возврат РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьМассивПредопределенныхАлгоритмовВыгрузкиТЧ(ВидСущности);
	
КонецФункции

&НаСервере
Функция ПолучитьПараметрыВыбораДляСвойств(ВидСущности, ИсточникДанных)
	
	МассивПараметров = Новый Массив;
	
	Если ВидСущности = "Компания" Тогда
		//МассивЗначений = Новый Массив;
		//МассивЗначений.Добавить(Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Партнеры"));
		//МассивЗначений.Добавить(Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Партнеры_Общие"));
		//МассивЗначений.Добавить(Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Организации"));		
		//МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.НаборСвойств", Новый ФиксированныйМассив(МассивЗначений)));	
	ИначеЕсли ВидСущности = "Контакт" Тогда
		//МассивЗначений = Новый Массив;
		//МассивЗначений.Добавить(Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Партнеры"));
		//МассивЗначений.Добавить(Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Партнеры_Общие"));
		//МассивЗначений.Добавить(Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_КонтактныеЛицаПартнеров"));		
		//МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.НаборСвойств", Новый ФиксированныйМассив(МассивЗначений)));	
	ИначеЕсли ВидСущности = "Товар" Тогда    //для оптимизации не фильтруем
		
	ИначеЕсли ВидСущности = "Проект" Тогда
		МассивЗначений = Новый Массив;
		МассивЗначений.Добавить(Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Документ_ЗаказКлиента"));
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.НаборСвойств", Новый ФиксированныйМассив(МассивЗначений)));	
	ИначеЕсли ВидСущности = "Счет" ИЛИ ВидСущности = "Счет2" Тогда
		МассивЗначений = Новый Массив;
		МассивЗначений.Добавить(Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Документ_СчетНаОплатуКлиенту"));
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.НаборСвойств", Новый ФиксированныйМассив(МассивЗначений)));	
		
	ИначеЕсли ВидСущности = "Заказ" Тогда
		МассивЗначений = Новый Массив;
		МассивЗначений.Добавить(Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Документ_ЗаказКлиента"));
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.НаборСвойств", Новый ФиксированныйМассив(МассивЗначений)));	
	ИначеЕсли ВидСущности = "Отгрузка" Тогда
		МассивЗначений = Новый Массив;
		МассивЗначений.Добавить(Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Документ_РеализацияТоваровУслуг"));
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.НаборСвойств", Новый ФиксированныйМассив(МассивЗначений)));	
	КонецЕсли;
 
	Если ИсточникДанных = "Из дополнительного реквизита" Тогда
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ЭтоДополнительноеСведение", Ложь));	
	ИначеЕсли ИсточникДанных = "Из дополнительного сведения" Тогда
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ЭтоДополнительноеСведение", Истина));	
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(МассивПараметров);
	
КонецФункции


&НаКлиенте
Процедура ТаблицаСоответствийВыгрузкиИсточникДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийВыгрузки.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.ИсточникДанных <> ВыбранноеЗначение Тогда
			ТекущиеДанные.Значение = "";	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаСоответствийВыгрузкиЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЭлементРодитель = Элемент.Родитель;
	
	Если ЭлементРодитель.ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные = ЭлементРодитель.ТекущиеДанные;
	
	Если ТекущиеДанные.ИсточникДанных = "<Свой алгоритм>" Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОО = Новый Структура;
		ПараметрыОО.Вставить("Элемент", ЭлементРодитель);
		ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаЗаполненияВыгрузки", ЭтаФорма, ПараметрыОО);
		
		ПередаваемыеПараметры = Новый Структура;
		ПередаваемыеПараметры.Вставить("Тип"		, "Функция");
		ПередаваемыеПараметры.Вставить("Назначение"	, "Выгрузка");
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Значение) Тогда
			ПередаваемыеПараметры.Вставить("Алгоритм", ТекущиеДанные.Значение);
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаАлгоритмаЗаполненияВыгрузки(Алгоритм, пПараметры) Экспорт
	
	Если НЕ Алгоритм = Неопределено Тогда
		пПараметры.Элемент.ТекущиеДанные.Значение = Алгоритм;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура КомпоновщикНастроекКомпоновкиДанныхПользовательскиеНастройкиПриИзменении(Элемент)
	
	ТекущаяСущность = Элементы.ТаблицаСущностей.ТекущиеДанные;
	Если ТекущаяСущность = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаСуществует = Ложь;
	Для каждого ТекСтрокаДанных Из ВсеОтборыПоВыгрузкаДанныхНаПортал Цикл
		
		Если ТекСтрокаДанных.ВидСущности = ТекущаяСущность.ВидСущности 
			И ТекСтрокаДанных.ВидОбъекта = ТекущаяСущность.ВидОбъекта И ТекСтрокаДанных.ИмяОбъекта = ТекущаяСущность.ИмяОбъекта Тогда
			
			ТекСтрокаДанных.Значение = ПолучитьНастройкиКомпоновщикаНастроекКомпоновкиДанных();
			СтрокаСуществует = Истина;
			
		КонецЕсли;
		
	КонецЦикла;

	Если НЕ СтрокаСуществует Тогда
		НоваяЗапись = ВсеОтборыПоВыгрузкаДанныхНаПортал.Добавить();
		НоваяЗапись.ВидСущности = ТекущаяСущность.ВидСущности;
		НоваяЗапись.ВидОбъекта 	= ТекущаяСущность.ВидОбъекта;
		НоваяЗапись.ИмяОбъекта 	= ТекущаяСущность.ИмяОбъекта;
		НоваяЗапись.Значение 	= ПолучитьНастройкиКомпоновщикаНастроекКомпоновкиДанных();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНастройкиКомпоновщикаНастроекКомпоновкиДанных()
	
	Возврат КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки();
	
КонецФункции

&НаСервере
Процедура ЗаполнитьНастройкуОтборовДляВыгрузкиНаПортал(ВидСущности, ВидОбъекта, ИмяОбъекта)
	
	НайденныеСтроки = ВсеОтборыПоВыгрузкаДанныхНаПортал.НайтиСтроки(Новый Структура("ВидСущности, ВидОбъекта, ИмяОбъекта", ВидСущности, ВидОбъекта, ИмяОбъекта));
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		Схема = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСхемуКомпоновки(ВидСущности, ИмяОбъекта);
		
		Если Схема = Неопределено Тогда
			КомпоновщикНастроекКомпоновкиДанных = Неопределено;
			Возврат;
		КонецЕсли;
		
		АдресСхемы = ПоместитьВоВременноеХранилище(Схема, УникальныйИдентификатор);
		КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы)); 
		КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(НайденныеСтроки[0].Значение);                      
		КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьПользовательскиеНастройки(КомпоновщикНастроекКомпоновкиДанных.ПользовательскиеНастройки);
		КомпоновщикНастроекКомпоновкиДанных.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
		
	Иначе
		КомпоновщикНастроекКомпоновкиДанных = Неопределено;
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура АлгоритмПриВыгрузкеДанныхНаПорталНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаСущностей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаВыгрузкиЭлемента", ЭтаФорма, ПараметрыОО);
	
	
	ПередаваемыеПараметры = Новый Структура;
	ПередаваемыеПараметры.Вставить("Тип"		, "Процедура");
	ПередаваемыеПараметры.Вставить("Назначение"	, "Выгрузка");
		
	Если ЗначениеЗаполнено(ТекущиеДанные.АлгоритмПриВыгрузкеДанныхНаПортал) Тогда
		ПередаваемыеПараметры.Вставить("Алгоритм", ТекущиеДанные.АлгоритмПриВыгрузкеДанныхНаПортал);
	КонецЕсли;
	
	спкКлючей = ПолучитьСпкПользовательскихПолейПортала(ТекущиеДанные.ВидСущности);
		
	мДанные = ПолучитьМассивКлючейJSON(ТекущиеДанные.ВидСущности);
	Для каждого ТекЭлемент Из мДанные Цикл
		спкКлючей.Добавить(ТекЭлемент, ТекЭлемент);
	КонецЦикла;
		
	ПередаваемыеПараметры.Вставить("КлючиПортала", спкКлючей);
		
	ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаАлгоритмаВыгрузкиЭлемента(Алгоритм, пПараметры) Экспорт
	
	Если Алгоритм <> Неопределено Тогда
		
		ТекущиеДанные = Элементы.ТаблицаСущностей.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущиеДанные.АлгоритмПриВыгрузкеДанныхНаПортал = Алгоритм;	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаСоответствийВыгрузкиТабЧастьПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийВыгрузкиТабЧасть.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		мИсточникДанных = Новый Массив;   
		мИсточникДанных.Добавить("Не изменять");
		мИсточникДанных.Добавить("Предопределенный алгоритм");
		мИсточникДанных.Добавить("Фиксированное значение");
		Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
			мИсточникДанных.Добавить("<Свой алгоритм>");
		КонецЕсли;
		мИсточникДанных.Добавить("");

		Элементы.ТаблицаСоответствийВыгрузкиТабЧастьИсточникДанных.СписокВыбора.ЗагрузитьЗначения(мИсточникДанных);	
		
		лЭлемент = Элементы.ТаблицаСоответствийВыгрузкиТабЧасть.ПодчиненныеЭлементы.ТаблицаСоответствийВыгрузкиТабЧастьЗначение;
		
		Если ТекущиеДанные.ИсточникДанных = "Предопределенный алгоритм" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.ЗагрузитьЗначения(ПолучитьМассивПредопределенныхАлгоритмовВыгрузкиТЧ(ТекущиеДанные.ВидСущности));
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Фиксированное значение" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "<Свой алгоритм>" Тогда
			
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Не изменять" Тогда      
			
			лЭлемент.СписокВыбора.Очистить();
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Истина;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "" Тогда
			
			лЭлемент.СписокВыбора.Очистить();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийВыгрузкиТабЧастьИсточникДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийВыгрузкиТабЧасть.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.ИсточникДанных <> ВыбранноеЗначение Тогда
			ТекущиеДанные.Значение = "";	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийВыгрузкиТабЧастьЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЭлементРодитель = Элемент.Родитель;
	
	Если ЭлементРодитель.ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные = ЭлементРодитель.ТекущиеДанные;
	
	Если ТекущиеДанные.ИсточникДанных = "<Свой алгоритм>" Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОО = Новый Структура;
		ПараметрыОО.Вставить("Элемент", ЭлементРодитель);
		ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаЗаполненияВыгрузки", ЭтаФорма, ПараметрыОО);
		
		ПередаваемыеПараметры = Новый Структура;
		ПередаваемыеПараметры.Вставить("Тип"		, "Функция");
		ПередаваемыеПараметры.Вставить("Назначение"	, "Выгрузка");
		ПередаваемыеПараметры.Вставить("ЭтоСтрокаТЧ", Истина);
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Значение) Тогда
			ПередаваемыеПараметры.Вставить("Алгоритм", ТекущиеДанные.Значение);
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Функция ПолучитьСвойство1СПоИдентификатору(ВидСущности, ИмяОбъекта, Идентификатор)
	
	ТипДанных = ПолучитьТипСвойстваПоВидуСущности(ВидСущности);
	
	МенеджерВременныхТаблиц = ПолучитьМенеджерВременныхТаблицСвойств(ИмяОбъекта, ВидСущности);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Портал"			, Портал);
	Запрос.УстановитьПараметр("ТипДанных"		, ТипДанных);
	Запрос.УстановитьПараметр("Идентификатор"	, Идентификатор);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_К_ИдентификаторыОбъектов.Объект КАК Объект
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрСведений.Б24_К_ИдентификаторыОбъектов КАК Б24_К_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_К_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_К_ИдентификаторыОбъектов.ТипДанных = &ТипДанных
	|	И Б24_К_ИдентификаторыОбъектов.Идентификатор = &Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Данные.Объект КАК Объект
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Свойства КАК ВТ_Свойства
	|		ПО ВТ_Данные.Объект = ВТ_Свойства.Ссылка";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		
		Выборка.Следующий();
		
		Результат = Выборка.Объект;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


#Область ЗагрузкаДанных

&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийЗагрузки.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Элементы.ТаблицаСоответствийЗагрузкиЗначение.ТолькоПросмотр			= Ложь;
		Элементы.ТаблицаСоответствийЗагрузкиИсточникДанных.ТолькоПросмотр 	= Ложь;
		
		Если ТекущиеДанные.Служебное Тогда
			Элементы.ТаблицаСоответствийЗагрузкиЗначение.ТолькоПросмотр			= Истина;
			Элементы.ТаблицаСоответствийЗагрузкиИсточникДанных.ТолькоПросмотр 	= Истина;
			Возврат;
		КонецЕсли;
		
		мИсточникДанных = Новый Массив;
		мИсточникДанных.Добавить("Не изменять");
		мИсточникДанных.Добавить("Предопределенный алгоритм");
		мИсточникДанных.Добавить("Фиксированное значение");
		мИсточникДанных.Добавить("Из пользовательского поля");
		мИсточникДанных.Добавить("Ключ JSON");
		
		Если СмартПроцессы.Количество() > 0 Тогда
			мИсточникДанных.Добавить("Из смарт-процесса");
		КонецЕсли;
		
		Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
			мИсточникДанных.Добавить("<Свой алгоритм>");
		КонецЕсли;
		
		Если ТекущиеДанные.ВидСущности = "Отгрузка" ИЛИ ТекущиеДанные.ВидСущности = "Отгрузка" Тогда
			мИсточникДанных.Добавить("Из основания");
		КонецЕсли;
		
		мИсточникДанных.Добавить("");
		
		Элементы.ТаблицаСоответствийЗагрузкиИсточникДанных.СписокВыбора.ЗагрузитьЗначения(мИсточникДанных);	
		
		лЭлемент = Элементы.ТаблицаСоответствийЗагрузки.ПодчиненныеЭлементы.ТаблицаСоответствийЗагрузкиЗначение;
		
		лЭлемент.СписокВыбора.Очистить();

		Элементы.ТаблицаСоответствийЗагрузкиЗначение.ПараметрыВыбора = ПолучитьПараметрыВыбораЗначенийСвойств(Неопределено);

		Если ТекущиеДанные.ИсточникДанных = "Предопределенный алгоритм" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.ЗагрузитьЗначения(ПолучитьМассивПредопределенныхАлгоритмовЗагрузки(ТекущиеДанные.ВидСущности, ТекущиеДанные.ИмяОбъекта));
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Фиксированное значение" Тогда
			
			
			Если ТекущиеДанные.ТипРеквизита = "Свойство" ИЛИ ТекущиеДанные.ТипРеквизита = "Сведение" Тогда
				
				ОписаниеСвойства = ПолучитьОписаниеСвойстваДляУказанияЗначения(ТекущиеДанные.ВидСущности, ТекущиеДанные.ИмяОбъекта, ТекущиеДанные.Реквизит1С);

				Если ЗначениеЗаполнено(ОписаниеСвойства.Свойство) Тогда
					
					лЭлемент.ОграничениеТипа 			= ОписаниеСвойства.ТипЗначения;
					лЭлемент.ВыбиратьТип 				= Истина;
					лЭлемент.КнопкаВыбора 				= Истина;
					лЭлемент.РедактированиеТекста 		= Истина;
					лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
					
					Если ОписаниеСвойства.ДополнительныеЗначенияИспользуются Тогда
						Элементы.ТаблицаСоответствийЗагрузкиЗначение.ПараметрыВыбора = ПолучитьПараметрыВыбораЗначенийСвойств(ОписаниеСвойства.Свойство);
					КонецЕсли;
							
				Иначе
					лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
					лЭлемент.ВыбиратьТип 		  		= Ложь;
					лЭлемент.РедактированиеТекста 		= Ложь;
					лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
					лЭлемент.КнопкаВыбора 				= Ложь;
					лЭлемент.ТолькоПросмотр		  		= Истина;
				КонецЕсли;
				
			Иначе
				
				лЭлемент.ОграничениеТипа 			= ПолучитьОграничениеТипаРеквизита1С(ТекущиеДанные.ВидОбъекта, ТекущиеДанные.ИмяОбъекта, ТекущиеДанные.Реквизит1С, ТекущиеДанные.ТипРеквизита);
				лЭлемент.ВыбиратьТип 				= Истина;
				лЭлемент.РедактированиеТекста 		= Истина;
				лЭлемент.КнопкаВыпадающегоСписка	= Истина;
				лЭлемент.КнопкаВыбора 				= Истина;
				лЭлемент.ТолькоПросмотр 			= Ложь;    
				
				Если ТекущиеДанные.Реквизит1С = "Родитель" Тогда
					лЭлемент.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;	
				КонецЕсли;
				
			КонецЕсли;

		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Ключ JSON" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.ЗагрузитьЗначения(ПолучитьМассивКлючейJSON(ТекущиеДанные.ВидСущности));

		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Из смарт-процесса" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			
			Для каждого ТекСтрока Из СмартПроцессы Цикл
				лЭлемент.СписокВыбора.Добавить(ТекСтрока.ТипВладельца, ТекСтрока.Наименование);
			КонецЦикла;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "<Свой алгоритм>" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Из пользовательского поля" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			СпкПоля = ПолучитьСпкПользовательскихПолейПортала(ТекущиеДанные.ВидСущности);
			Для каждого ТекЭлемент Из СпкПоля Цикл 
				лЭлемент.СписокВыбора.Добавить(ТекЭлемент.Значение, ТекЭлемент.Представление);
			КонецЦикла;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Из основания" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Истина;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Не изменять" Тогда      
			
			лЭлемент.СписокВыбора.Очистить();
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Истина;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "" Тогда
			
			лЭлемент.СписокВыбора.Очистить();
			
		КонецЕсли;
	
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивПредопределенныхАлгоритмовЗагрузки(ВидСущности, ИмяОбъекта)
	
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьМассивПредопределенныхАлгоритмовЗагрузки(ВидСущности, ИмяОбъекта));
	
КонецФункции

&НаСервере
Функция ПолучитьМассивПредопределенныхАлгоритмовЗагрузкиТЧ(ВидСущности, ИмяОбъекта)
	
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьМассивПредопределенныхАлгоритмовЗагрузкиТЧ(ВидСущности, ИмяОбъекта));
	
КонецФункции

&НаСервере
Функция ПолучитьОграничениеТипаРеквизита1С(ВидОбъекта, ИмяОбъекта, Реквизит1С, ТипРеквизита, ТабличнаяЧасть = "")
	
	Результат = Новый ОписаниеТипов("Строка");
	
	Попытка 
		Если ВидОбъекта = "Справочник" Тогда
			МетаданныеОбъекта = Метаданные.Справочники[ИмяОбъекта];
		ИначеЕсли ВидОбъекта = "Документ" Тогда
			МетаданныеОбъекта = Метаданные.Документы[ИмяОбъекта];
		КонецЕсли;
		
		Если ТипРеквизита = "Свойство" Тогда
			
		ИначеЕсли ТипРеквизита = "Сведение" Тогда
			
		Иначе
			
			Для каждого ТекРеквизит Из МетаданныеОбъекта.СтандартныеРеквизиты Цикл
				Если ТекРеквизит.Имя = Реквизит1С Тогда
					Результат = ТекРеквизит.Тип;
				КонецЕсли;
			КонецЦикла;
			
			Если ЗначениеЗаполнено(ТабличнаяЧасть) Тогда
				Для каждого ТекРеквизит Из МетаданныеОбъекта.ТабличныеЧасти[ТабличнаяЧасть].Реквизиты Цикл
					Если ТекРеквизит.Имя = Реквизит1С Тогда
						Результат = ТекРеквизит.Тип;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Для каждого ТекРеквизит Из МетаданныеОбъекта.Реквизиты Цикл
					Если ТекРеквизит.Имя = Реквизит1С Тогда
						Результат = ТекРеквизит.Тип;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	Исключение
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПолучитьСпкПользовательскихПолейПортала(ВидСущности)
	
	Результат = Новый СписокЗначений;
	
	Для каждого ТекСтрока Из ПользовательскиеПоляПортала Цикл
		
		Если ТекСтрока.ВидСущности = ВидСущности Тогда
			Результат.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.Описание);	
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьОписаниеСвойстваДляУказанияЗначения(ВидСущности, ИмяОбъекта, НаименованиеСвойства)
	
	Результат = Новый Структура;
	Результат.Вставить("Свойство"							, ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка());
	Результат.Вставить("ТипЗначения"						, Новый ОписаниеТипов);
	Результат.Вставить("ЭтоДополнительноеСведение"			, Ложь);
	Результат.Вставить("ДополнительныеЗначенияИспользуются"	, Ложь);
	
	МенеджерВременныхТаблиц = ПолучитьМенеджерВременныхТаблицСвойств(ИмяОбъекта, ВидСущности);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Наименование", НаименованиеСвойства);
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_Свойства.Ссылка КАК Ссылка,
	|	ДополнительныеРеквизитыИСведения.ТипЗначения КАК ТипЗначения,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	ДополнительныеРеквизитыИСведения.ДополнительныеЗначенияИспользуются КАК ДополнительныеЗначенияИспользуются
	|ИЗ
	|	ВТ_Свойства КАК ВТ_Свойства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО ВТ_Свойства.Ссылка = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	ДополнительныеРеквизитыИСведения.Наименование = &Наименование";
	
	ВыполненныйЗапрос = Запрос.Выполнить();
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Выборка.Следующий();
		
		Результат.Свойство 								= Выборка.Ссылка; 
		Результат.ТипЗначения 							= Выборка.ТипЗначения; 
		Результат.ЭтоДополнительноеСведение 			= Выборка.ЭтоДополнительноеСведение; 
		Результат.ДополнительныеЗначенияИспользуются 	= Выборка.ДополнительныеЗначенияИспользуются; 
	                    
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаСервере
Функция ПолучитьПараметрыВыбораЗначенийСвойств(Свойство)
	
	МассивПараметров 	= Новый Массив;
	
	Если ЗначениеЗаполнено(Свойство) Тогда
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Владелец", Свойство));	
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(МассивПараметров);
	
КонецФункции

&НаКлиенте
Функция ПолучитьМассивКлючейJSON(ВидСущности)
	
	Результат = Новый Массив;
	
	НайденныеСтроки = КлючиJSON.НайтиСтроки(Новый Структура("ВидСущности", ВидСущности));
	Для каждого ТекСтрока Из НайденныеСтроки Цикл
		Результат.Добавить(ТекСтрока.КлючЗапроса);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции


&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиИсточникДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийЗагрузки.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.ИсточникДанных <> ВыбранноеЗначение Тогда
			ТекущиеДанные.Значение = "";	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ЭлементРодитель = Элемент.Родитель;
	
	Если ЭлементРодитель.ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные = ЭлементРодитель.ТекущиеДанные;
	
	Если ТекущиеДанные.ИсточникДанных = "<Свой алгоритм>" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОО = Новый Структура;
		ПараметрыОО.Вставить("Элемент", ЭлементРодитель);
		ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаЗаполненияЗагрузки", ЭтаФорма, ПараметрыОО);
			
			
		ПередаваемыеПараметры = Новый Структура;
		ПередаваемыеПараметры.Вставить("Тип"		, "Функция");
		ПередаваемыеПараметры.Вставить("Назначение"	, "Загрузка");
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Значение) Тогда
			ПередаваемыеПараметры.Вставить("Алгоритм", ТекущиеДанные.Значение);
		КонецЕсли;
		
		спкКлючей = ПолучитьСпкПользовательскихПолейПортала(ТекущиеДанные.ВидСущности);
			
		мДанные = ПолучитьМассивКлючейJSON(ТекущиеДанные.ВидСущности);
		Для каждого ТекЭлемент Из мДанные Цикл
			спкКлючей.Добавить(ТекЭлемент, ТекЭлемент);
		КонецЦикла;
			
		ПередаваемыеПараметры.Вставить("КлючиПортала", спкКлючей);
			
		ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаАлгоритмаЗаполненияЗагрузки(Алгоритм, пПараметры) Экспорт
	
	Если НЕ Алгоритм = Неопределено Тогда
		пПараметры.Элемент.ТекущиеДанные.Значение = Алгоритм;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаОтборовПоЗагрузкеДанныхВ1СПриИзменении(Элемент)
	
	ТекущаяСущность = Элементы.ТаблицаСущностей.ТекущиеДанные;
	Если ТекущаяСущность = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Элемент.ТекущиеДанные, ТекущаяСущность);
	КонецЕсли;
	
	НайденныеСтроки = ВсеОтборыПоЗагрузкеДанныхВ1С.НайтиСтроки(Новый Структура("ВидСущности, ВидОбъекта, ИмяОбъекта", ТекущаяСущность.ВидСущности, ТекущаяСущность.ВидОбъекта, ТекущаяСущность.ИмяОбъекта));
	Для каждого ТекСтрока Из НайденныеСтроки Цикл
		ВсеОтборыПоЗагрузкеДанныхВ1С.Удалить(ВсеОтборыПоЗагрузкеДанныхВ1С.Индекс(ТекСтрока));
	КонецЦикла;                                       
	
	Для каждого ТекСтрока Из ТаблицаОтборовПоЗагрузкеДанныхВ1С Цикл
		НоваяЗапись = ВсеОтборыПоЗагрузкеДанныхВ1С.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока); 
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Функция ПолучитьМассивДопРеквизитовИСведенийОбъекта(ВидОбъекта, ИмяОбъекта, ВидСущности)
	
	Результат = Новый Массив;
	
	ТипДанных = ПолучитьТипСвойстваПоВидуСущности(ВидСущности);
	
	МенеджерВременныхТаблиц = ПолучитьМенеджерВременныхТаблицСвойств(ИмяОбъекта, ВидСущности);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_Свойства.Ссылка КАК Ссылка,
	|	ВТ_Свойства.Заголовок КАК Заголовок,
	|	ВТ_Свойства.Наименование КАК Наименование,
	|	ВТ_Свойства.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение
	|ИЗ
	|	ВТ_Свойства КАК ВТ_Свойства";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураСоответствийЗагрузки = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСтруктуруСоответствийЗагрузки();	
		СтруктураСоответствийЗагрузки.ВидСущности 		= ВидСущности;
		СтруктураСоответствийЗагрузки.ВидОбъекта 		= ВидОбъекта;
		СтруктураСоответствийЗагрузки.ИмяОбъекта 		= ИмяОбъекта;
		СтруктураСоответствийЗагрузки.Реквизит1С 		= Выборка.Наименование;
		СтруктураСоответствийЗагрузки.ИсточникДанных 	= "Из пользовательского поля";
		СтруктураСоответствийЗагрузки.СсылкаНаСвойство 	= Выборка.Ссылка;
		
		Если Выборка.ЭтоДополнительноеСведение Тогда
			СтруктураСоответствийЗагрузки.Наименование 	= "Доп. свойство: '" + Выборка.Заголовок + "'";
			
			СтруктураСоответствийЗагрузки.ТипРеквизита = "Свойство";
			СтруктураСоответствийЗагрузки.Приоритет = 2;
		Иначе
			СтруктураСоответствийЗагрузки.Наименование 		= "Доп. реквизит: '" + Выборка.Заголовок + "'";
			СтруктураСоответствийЗагрузки.ТипРеквизита = "Сведение";
			СтруктураСоответствийЗагрузки.Приоритет = 3;
		КонецЕсли;
		
		Если ВидСущности = "Товар" Тогда
			ИдСвойства = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьИдОбъекта(Портал, ТипДанных, Выборка.Ссылка);
			Если ЗначениеЗаполнено(ИдСвойства) Тогда
	    		СтруктураСоответствийЗагрузки.Значение = "PROPERTY_" + ИдСвойства;
			КонецЕсли;       
		ИначеЕсли ВидСущности = "Товар2" Тогда
			ИдСвойства = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьИдОбъекта(Портал, ТипДанных, Выборка.Ссылка);
			Если ЗначениеЗаполнено(ИдСвойства) Тогда
	    		СтруктураСоответствийЗагрузки.Значение = "property" + ИдСвойства;
			КонецЕсли;
			
		Иначе
			СтруктураСоответствийЗагрузки.Значение = РегистрыСведений.Б24_К_ИдентификаторыОбъектов.ПолучитьДопИдОбъекта(Портал, Выборка.Ссылка, ТипДанных);
		КонецЕсли;
		
		Результат.Добавить(СтруктураСоответствийЗагрузки); 
		
	КонецЦикла;
	
	Возврат Результат;	
	
КонецФункции



&НаКлиенте
Процедура ТаблицаОтборовПоЗагрузкеДанныхВ1СПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1С.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	лЭлемент = Элемент.ТекущийЭлемент;
	
	Если лЭлемент.Имя = "ТаблицаОтборовПоЗагрузкеДанныхВ1СЗначение" Тогда
		
		лЭлемент.ВыбиратьТип 		  		= Ложь;
		лЭлемент.РедактированиеТекста 		= Истина;
		лЭлемент.КнопкаВыбора 				= Ложь;
		лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
		лЭлемент.ТолькоПросмотр		  		= Ложь;
		лЭлемент.ТолькоПросмотр		  		= Ложь;
		
		Если ТипЗнч(ТекущиеДанные.Значение) = Тип("СписокЗначений") Тогда
			лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("СписокЗначений");
		КонецЕсли;
		
		
		Если ТекущиеДанные.ВидСравнения = "В списке" ИЛИ ТекущиеДанные.ВидСравнения = "Не в списке" Тогда
				
			лЭлемент.КнопкаВыбора 	= Истина;
			
			Если ТипЗнч(ТекущиеДанные.Значение) <> Тип("СписокЗначений") Тогда
				
				лЭлемент.ОграничениеТипа	= Новый ОписаниеТипов("СписокЗначений");
				         
				спкЗаглушка = Новый СписокЗначений;
				спкЗаглушка.ТипЗначения = ПолучитьОписаниеТиповДляПользовательскогоПоля(ТекущиеДанные.ВидСущности, ТекущиеДанные.ТипПоля);
				ТекущиеДанные.Значение 	= спкЗаглушка;
				
			КонецЕсли;
			
		Иначе	
			
			Если Лев(ТекущиеДанные.КлючЗапроса, 7) = "UF_CRM_" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 8) = "property" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 9) = "PROPERTY_" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 20) = "ufCrm_SMART_INVOICE_" Тогда

				лЭлемент.ОграничениеТипа = ПолучитьОписаниеТиповДляПользовательскогоПоля(ТекущиеДанные.ВидСущности, ТекущиеДанные.ТипПоля);
				
				Если ТекущиеДанные.ТипПоля = "boolean" Тогда 
					лЭлемент.КнопкаВыбора 	= Истина;
				ИначеЕсли ТекущиеДанные.ТипПоля = "employee" ИЛИ ТекущиеДанные.ТипПоля = "S_employee" Тогда  			
					лЭлемент.КнопкаВыбора 	= Истина;
				ИначеЕсли ТекущиеДанные.ТипПоля = "enumeration" ИЛИ ТекущиеДанные.ТипПоля = "L_" Тогда  
					лЭлемент.КнопкаВыбора 	= Истина;
				КонецЕсли;
				
			Иначе
				
				лЭлемент.ОграничениеТипа = Новый ОписаниеТипов("Строка");
	
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОтборовПоЗагрузкеДанныхВ1СКлючЗапросаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1С.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Лев(ТекущиеДанные.КлючЗапроса, 7) = "UF_CRM_" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 8) = "property" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 9) = "PROPERTY_" ИЛИ Лев(ТекущиеДанные.КлючЗапроса, 20) = "ufCrm_SMART_INVOICE_" Тогда
		
		НайденныеСтроки = ПользовательскиеПоляПортала.НайтиСтроки(Новый Структура("ВидСущности, КлючЗапроса", ТекущиеДанные.ВидСущности, ТекущиеДанные.КлючЗапроса));	
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТекущиеДанные.Идентификатор = НайденныеСтроки[0].Идентификатор;	
			ТекущиеДанные.ТипПоля 		= НайденныеСтроки[0].ТипПоля;	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаОтборовПоЗагрузкеДанныхВ1СЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1С.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.ТипЗначения = ПолучитьОписаниеТиповДляПользовательскогоПоля(ТекущиеДанные.ВидСущности, ТекущиеДанные.ТипПоля);
	
	Если ТекущиеДанные.ТипПоля = "employee" ИЛИ ТекущиеДанные.ТипПоля = "S_employee" Тогда  			
		
		СтандартнаяОбработка = Ложь;
		
		СписокВыбора.ТипЗначения = Новый ОписаниеТипов("Строка");
		
		СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
		Если СтруктураНастроек = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
		СтруктураНастроек.Логирование.Измерение2 = "Получение полей данных";
		
		мДанные = Б24_К_RestApiВызовСервера.ПолучитьМассивДанныхСПорталаЧерезСписок(СтруктураНастроек, "/rest/user.get.json", "ID");
		Для каждого ТекЭлемент Из мДанные Цикл 
			СписокВыбора.Добавить(ТекЭлемент.Получить("ID"), Строка(ТекЭлемент.Получить("NAME")) + " " + Строка(ТекЭлемент.Получить("LAST_NAME")));
		КонецЦикла;
		
	ИначеЕсли ТекущиеДанные.ТипПоля = "enumeration" ИЛИ ТекущиеДанные.ТипПоля = "L_" Тогда  
		
		СтандартнаяОбработка = Ложь;
		
		СписокВыбора.ТипЗначения = Новый ОписаниеТипов("Строка");
		
		НайденныеСтрок = ЗначенияПользовательскихПолейПортала.НайтиСтроки(Новый Структура("ВидСущности, Идентификатор", ТекущиеДанные.ВидСущности, ТекущиеДанные.Идентификатор));
		Для каждого ТекЭлемент Из НайденныеСтрок Цикл 
			СписокВыбора.Добавить(ТекЭлемент.ИдентификаторЗначения, ТекЭлемент.ПредставлениеЗначения);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТекущиеДанные.ВидСравнения = "В списке" ИЛИ ТекущиеДанные.ВидСравнения = "Не в списке" Тогда
		
		Если ТипЗнч(ТекущиеДанные.Значение) = Тип("СписокЗначений") Тогда
			Для каждого ТекЭлемент Из ТекущиеДанные.Значение Цикл
				Для каждого ТекВсеЭлементы Из СписокВыбора Цикл
					
					Если ТекЭлемент.Значение = ТекВсеЭлементы.Значение Тогда
						ТекВсеЭлементы.Пометка = Истина;
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		Оп = Новый ОписаниеОповещения("ВыполнитьПослеВыбораЗначенийОтбораЗагрузкиДанныхВ1С", ЭтотОбъект, Параметры);
		СписокВыбора.ПоказатьОтметкуЭлементов(Оп, "Выберите значения");
	ИначеЕсли СтандартнаяОбработка = Ложь Тогда
		Оп = Новый ОписаниеОповещения("ВыполнитьПослеВыбораЗначенияОтбораЗагрузкиДанныхВ1С", ЭтотОбъект, Параметры);
		
		ЗначениеПоУмолчанию = Неопределено;
		Если ТипЗнч(ТекущиеДанные.Значение) = Тип("СписокЗначений") Тогда
			Если ТекущиеДанные.Значение.Количество() > 0 Тогда
				ЗначениеПоУмолчанию = СписокВыбора.НайтиПоЗначению(ТекущиеДанные.Значение[0].Значение);	
			КонецЕсли;
		КонецЕсли;
		ПоказатьВыборИзСписка(Оп, СписокВыбора,Элемент, ЗначениеПоУмолчанию);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеВыбораЗначенийОтбораЗагрузкиДанныхВ1С(ВыбранныеЗначения, пПараметры) Экспорт
	
	Если ВыбранныеЗначения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1С.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый СписокЗначений;
	
	Для каждого ТекЭлемент Из ВыбранныеЗначения Цикл
		
		Если ТекЭлемент.Пометка Тогда
			Результат.Добавить(ТекЭлемент.Значение, ТекЭлемент.Представление);	
		КонецЕсли;
		
	КонецЦикла;
	
	ТекущиеДанные.Значение = Результат;
	
	ТаблицаОтборовПоЗагрузкеДанныхВ1СПриИзменении(Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1С);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеВыбораЗначенияОтбораЗагрузкиДанныхВ1С(ВыбранноеЗначение, пПараметры) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1С.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый СписокЗначений;
	Результат.Добавить(ВыбранноеЗначение.Значение, ВыбранноеЗначение.Представление);
	
	ТекущиеДанные.Значение = Результат;
	
	ТаблицаОтборовПоЗагрузкеДанныхВ1СПриИзменении(Элементы.ТаблицаОтборовПоЗагрузкеДанныхВ1С);
	
КонецПроцедуры


&НаКлиенте
Процедура АлгоритмПриЗагрузкеДанныхВ1СНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаСущностей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаЗагрузкиЭлемента", ЭтаФорма, ПараметрыОО);
	
	
	ПередаваемыеПараметры = Новый Структура;
	ПередаваемыеПараметры.Вставить("Тип"		, "Процедура");
	ПередаваемыеПараметры.Вставить("Назначение"	, "Загрузка");
	
	Если ЗначениеЗаполнено(ТекущиеДанные.АлгоритмПриЗагрузкеДанныхВ1С) Тогда
		ПередаваемыеПараметры.Вставить("Алгоритм", ТекущиеДанные.АлгоритмПриЗагрузкеДанныхВ1С);
	КонецЕсли;
	
	спкКлючей = ПолучитьСпкПользовательскихПолейПортала(ТекущиеДанные.ВидСущности);
		
	мДанные = ПолучитьМассивКлючейJSON(ТекущиеДанные.ВидСущности);
	Для каждого ТекЭлемент Из мДанные Цикл
		спкКлючей.Добавить(ТекЭлемент, ТекЭлемент);
	КонецЦикла;
		
	ПередаваемыеПараметры.Вставить("КлючиПортала", спкКлючей);
		
	ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаАлгоритмаЗагрузкиЭлемента(Алгоритм, пПараметры) Экспорт
	
	Если Алгоритм <> Неопределено Тогда
		
		ТекущиеДанные = Элементы.ТаблицаСущностей.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущиеДанные.АлгоритмПриЗагрузкеДанныхВ1С = Алгоритм;	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры



&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиТабЧастьПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийЗагрузкиТабЧасть.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		мИсточникДанных = Новый Массив;        
		мИсточникДанных.Добавить("Не изменять");
		мИсточникДанных.Добавить("Предопределенный алгоритм");
		мИсточникДанных.Добавить("Фиксированное значение");
		Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
			мИсточникДанных.Добавить("<Свой алгоритм>");
		КонецЕсли;
		мИсточникДанных.Добавить("");
		
		Элементы.ТаблицаСоответствийЗагрузкиТабЧастьИсточникДанных.СписокВыбора.ЗагрузитьЗначения(мИсточникДанных);	
		
		лЭлемент = Элементы.ТаблицаСоответствийЗагрузкиТабЧасть.ПодчиненныеЭлементы.ТаблицаСоответствийЗагрузкиТабЧастьЗначение;
		
		лЭлемент.СписокВыбора.Очистить();

		Элементы.ТаблицаСоответствийЗагрузкиТабЧастьЗначение.ПараметрыВыбора = ПолучитьПараметрыВыбораЗначенийСвойств(Неопределено);

		Если ТекущиеДанные.ИсточникДанных = "Предопределенный алгоритм" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Ложь;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.ЗагрузитьЗначения(ПолучитьМассивПредопределенныхАлгоритмовЗагрузкиТЧ(ТекущиеДанные.ВидСущности, ТекущиеДанные.ИмяОбъекта));
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Фиксированное значение" Тогда
			
			лЭлемент.ОграничениеТипа 			= ПолучитьОграничениеТипаРеквизита1С(ТекущиеДанные.ВидОбъекта, ТекущиеДанные.ИмяОбъекта, ТекущиеДанные.Реквизит1С, ТекущиеДанные.ТипРеквизита, ТекущиеДанные.ИмяТабЧасти);
			лЭлемент.ВыбиратьТип 				= Истина;
			лЭлемент.РедактированиеТекста 		= Истина;
			лЭлемент.КнопкаВыпадающегоСписка	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр 			= Ложь;

		ИначеЕсли ТекущиеДанные.ИсточникДанных = "<Свой алгоритм>" Тогда
			
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Ложь;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Ложь;
			
			лЭлемент.СписокВыбора.Очистить();
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "Не изменять" Тогда      
			
			лЭлемент.СписокВыбора.Очистить();
			лЭлемент.ОграничениеТипа 			= Новый ОписаниеТипов("Строка");
			лЭлемент.ВыбиратьТип 		  		= Ложь;
			лЭлемент.РедактированиеТекста 		= Ложь;
			лЭлемент.КнопкаВыпадающегоСписка 	= Истина;
			лЭлемент.КнопкаВыбора 				= Истина;
			лЭлемент.ТолькоПросмотр		  		= Истина;
			
		ИначеЕсли ТекущиеДанные.ИсточникДанных = "" Тогда
			
			лЭлемент.СписокВыбора.Очистить();
			
		КонецЕсли;
	
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиТабЧастьИсточникДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствийЗагрузкиТабЧасть.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.ИсточникДанных <> ВыбранноеЗначение Тогда
			ТекущиеДанные.Значение = "";	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийЗагрузкиТабЧастьЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЭлементРодитель = Элемент.Родитель;
	
	Если ЭлементРодитель.ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные = ЭлементРодитель.ТекущиеДанные;
	
	Если ТекущиеДанные.ИсточникДанных = "<Свой алгоритм>" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОО = Новый Структура;
		ПараметрыОО.Вставить("Элемент", ЭлементРодитель);
		ОО = Новый ОписаниеОповещения("ПослеВводаАлгоритмаЗаполненияЗагрузки", ЭтаФорма, ПараметрыОО);
			
			
		ПередаваемыеПараметры = Новый Структура;
		ПередаваемыеПараметры.Вставить("Тип"		, "Функция");
		ПередаваемыеПараметры.Вставить("Назначение"	, "Загрузка");
		ПередаваемыеПараметры.Вставить("ЭтоСтрокаТЧ", Истина);
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Значение) Тогда
			ПередаваемыеПараметры.Вставить("Алгоритм", ТекущиеДанные.Значение);
		КонецЕсли;
		
		спкКлючей = ПолучитьСпкПользовательскихПолейПортала(ТекущиеДанные.ВидСущности);
			
		мДанные = ПолучитьМассивКлючейJSON(ТекущиеДанные.ВидСущности);
		Для каждого ТекЭлемент Из мДанные Цикл
			спкКлючей.Добавить(ТекЭлемент, ТекЭлемент);
		КонецЦикла;
			
		ПередаваемыеПараметры.Вставить("КлючиПортала", спкКлючей);
			
		ОткрытьФорму("ОбщаяФорма.Б24_К_УстановкиАлгоритмов", ПередаваемыеПараметры,,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьОписаниеТиповДляПользовательскогоПоля(ВидСущности, ТипПоля)
	
	ОписаниеТипов	= Новый ОписаниеТипов("Строка");   //если не свойство или свойство неопределенного типа
	
	спкЗначений		= Новый СписокЗначений;
	МассивТипов 	= Новый Массив;
	
	Если ВидСущности = "Товар" ИЛИ ВидСущности = "Товар2" Тогда
		
		
		Если ТипПоля = "S_" ИЛИ ТипПоля = "S_HTML" Тогда  
			
			ОписаниеТипов = Новый ОписаниеТипов("Строка");
			
		ИначеЕсли ТипПоля = "N_" Тогда  
			
			КЧ = Новый КвалификаторыЧисла(12,3);
			МассивТипов.Добавить(Тип("Число"));
			ОписаниеТипов  = Новый ОписаниеТипов(МассивТипов, , КЧ);
			
		ИначеЕсли ТипПоля = "S_DateTime" Тогда 
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя);
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КД);
			
		ИначеЕсли ТипПоля = "S_Date" Тогда  
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КД);
			
		ИначеЕсли ТипПоля = "S_employee" Тогда  			
			ОписаниеТипов	= Новый ОписаниеТипов("СписокЗначений");
		ИначеЕсли ТипПоля = "L_" Тогда  
			ОписаниеТипов	= Новый ОписаниеТипов("СписокЗначений");
		КонецЕсли;
		
	Иначе
		
	
		Если ТипПоля = "boolean" Тогда  	
			
			ОписаниеТипов = Новый ОписаниеТипов("Булево");
			
		ИначеЕсли ТипПоля = "string" ИЛИ ТипПоля = "url" Тогда  
			
			ОписаниеТипов = Новый ОписаниеТипов("Строка");
			
		ИначеЕсли ТипПоля = "integer" Тогда  	
			
			КЧ = Новый КвалификаторыЧисла(12,0);
			МассивТипов.Добавить(Тип("Число"));
			ОписаниеТипов  = Новый ОписаниеТипов(МассивТипов, , КЧ);
			
		ИначеЕсли ТипПоля = "double" Тогда  
			
			КЧ = Новый КвалификаторыЧисла(12,3);
			МассивТипов.Добавить(Тип("Число"));
			ОписаниеТипов  = Новый ОписаниеТипов(МассивТипов, , КЧ);
			
		ИначеЕсли ТипПоля = "datetime" Тогда 
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя);
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КД);
			
		ИначеЕсли ТипПоля = "date" Тогда  
			
			КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТипов = Новый ОписаниеТипов(МассивТипов, , КД);
			
		ИначеЕсли ТипПоля = "employee" ИЛИ ТипПоля = "S_employee" Тогда  			
			ОписаниеТипов	= Новый ОписаниеТипов("СписокЗначений");
		ИначеЕсли ТипПоля = "enumeration" ИЛИ ТипПоля = "L_" Тогда  
			ОписаниеТипов	= Новый ОписаниеТипов("СписокЗначений");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОписаниеТипов;
	
КонецФункции

#КонецОбласти


#Область Прочие

&НаСервере
Функция ПолучитьИдентификаторКаталогаТоваров(НастройкаПодключения)
	
	НастройкиСинхронизации = Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНастройкиСинхронизации(НастройкаПодключения);	
	Возврат НастройкиСинхронизации.НастройкиСинхронизацииТоваров.ИдентификаторКаталогаТоваров;

КонецФункции  

&НаСервере
Функция ПолучитьИдентификаторКаталогаПредложений(НастройкаПодключения)
	
	НастройкиСинхронизации = Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНастройкиСинхронизации(НастройкаПодключения);	
	Возврат НастройкиСинхронизации.НастройкиСинхронизацииТоваров.ИдентификаторКаталогаПредложений;

КонецФункции  

&НаСервере
Функция ПолучитьКлючиПользовательскиеПоляСущностиПортала(ВидСущности)

	ИдентификаторКаталогаТоваров = "";
	ИдентификаторКаталогаПредложений = "";
	Если ВидСущности = "Товар2" Тогда
		ИдентификаторКаталогаТоваров = ПолучитьИдентификаторКаталогаТоваров(НастройкаПодключения);
	ИначеЕсли ВидСущности = "Предложение" Тогда
		ИдентификаторКаталогаПредложений = ПолучитьИдентификаторКаталогаПредложений(НастройкаПодключения);  
	КонецЕсли;
	
	Результат = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьКлючиПользовательскиеПоляСущностиПортала(НастройкаПодключения, ВидСущности, ИдентификаторКаталогаТоваров, ИдентификаторКаталогаПредложений);
	
	Возврат Результат;	
	
КонецФункции

&НаКлиенте
Процедура ОбновитьОтображениеЭлементовФормы()
	
	Элементы.ГруппаНастройкаДанных.Видимость = НЕ ПризнакСворачиванияПравойКолонки;
	
	Если ПризнакСворачиванияПравойКолонки Тогда                               
		Элементы.ГруппаЛево.Ширина = 0;
		Элементы.СдвинутьРаздвинуть.Картинка = БиблиотекаКартинок.ПереместитьВлево;
	Иначе
		Элементы.ГруппаЛево.Ширина = 52;
		Элементы.СдвинутьРаздвинуть.Картинка = БиблиотекаКартинок.ПереместитьВправо;
	КонецЕсли;
	
	НайденныеСтроки = ТаблицаСущностей.НайтиСтроки(Новый Структура("ВидОбъекта", "Документ"));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Элементы.ГруппаЗагрузкаТабЧасть.Видимость = Истина;
		Элементы.ГруппаВыгрузкаТабЧасть.Видимость = Истина;
	Иначе
		Элементы.ГруппаЗагрузкаТабЧасть.Видимость = Ложь;
		Элементы.ГруппаВыгрузкаТабЧасть.Видимость = Ложь;
	КонецЕсли;
	
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда
		Элементы.ГруппаАлгоритмВыгрузкиНаПортал.Видимость 	= Ложь;	
		Элементы.ГруппаАлгоритмЗагрузкиВ1С.Видимость 		= Ложь;	
	Иначе
		Элементы.ГруппаАлгоритмВыгрузкиНаПортал.Видимость 	= Истина;	
		Элементы.ГруппаАлгоритмЗагрузкиВ1С.Видимость 		= Истина;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки()
	
	СущностиПриАктивизацииСтроки(Неопределено);
	
	СохранитьНастройкиНаСервере();
	 
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);

	Настройки = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	мСущности 							= Настройки.Сущности;
	мВсеСоответствияВыгрузки 			= Настройки.СоответствияПолейСущностейДляВыгрузкиНаПортал;
	мВсеСоответствияЗагрузки 			= Настройки.СоответствияПолейСущностейДляЗагрузкиВ1С;
	
	мВсеСоответствияВыгрузкиТабЧасть 	= Настройки.СоответствияПолейСущностейТабЧастейДляВыгрузкиНаПортал;
	мВсеСоответствияЗагрузкиТабЧасть 	= Настройки.СоответствияПолейСущностейТабЧастейДляЗагрузкиВ1С;
	
	мВсеОтборыПоВыгрузкаДанныхНаПортал 	= Настройки.ОтборыДляВыгрузкиНаПортал;
	мВсеОтборыПоЗагрузкеДанныхВ1С 		= Настройки.ОтборыДляЗагрузкиДанныхВ1С;
	
	тзнТаблицаСущностей = ТаблицаСущностей.Выгрузить();
	Для каждого ТекЭлемент Из тзнТаблицаСущностей Цикл
		
		лЕстьСтрока = Ложь;
		Для каждого ТекСтрока Из мСущности Цикл
			
			Если ТекЭлемент.ВидСущности = ТекСтрока.ВидСущности И ТекЭлемент.ВидОбъекта = ТекСтрока.ВидОбъекта И ТекЭлемент.ИмяОбъекта = ТекСтрока.ИмяОбъекта Тогда
				ЗаполнитьЗначенияСвойств(ТекСтрока, ТекЭлемент);
				лЕстьСтрока = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ лЕстьСтрока Тогда
			
			ВремСтруктура = Новый Структура;
			Для каждого ТекКолонка Из тзнТаблицаСущностей.Колонки Цикл 
				ВремСтруктура.Вставить(ТекКолонка.Имя, ТекЭлемент[ТекКолонка.Имя]);	
			КонецЦикла;
			
			мСущности.Добавить(ВремСтруктура);
			
		КонецЕсли;
		
	КонецЦикла;
	
	
	ВТ_Данные = Новый Массив;
	Для каждого ТекСтрока Из мВсеСоответствияВыгрузки Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекСтрока.ВидСущности) = Неопределено Тогда
			ВТ_Данные.Добавить(ТекСтрока);	
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекЭлемент Из ВсеСоответствияВыгрузки Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяСтруктураДанных = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСтруктуруСоответствийВыгрузки();
			ЗаполнитьЗначенияСвойств(НоваяСтруктураДанных, ТекЭлемент); 
			ВТ_Данные.Добавить(НоваяСтруктураДанных);	
		КонецЕсли;
	КонецЦикла;
	мВсеСоответствияВыгрузки = ВТ_Данные; 
	
	
	ВТ_Данные = Новый Массив;
	Для каждого ТекСтрока Из мВсеСоответствияЗагрузки Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекСтрока.ВидСущности) = Неопределено Тогда
			ВТ_Данные.Добавить(ТекСтрока);	
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекЭлемент Из ВсеСоответствияЗагрузки Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяСтруктураДанных = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСтруктуруСоответствийЗагрузки();
			ЗаполнитьЗначенияСвойств(НоваяСтруктураДанных, ТекЭлемент); 
			ВТ_Данные.Добавить(НоваяСтруктураДанных);	
		КонецЕсли;
	КонецЦикла;
	мВсеСоответствияЗагрузки = ВТ_Данные; 
	
	
	ВТ_Данные = Новый Массив;
	Для каждого ТекСтрока Из мВсеСоответствияВыгрузкиТабЧасть Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекСтрока.ВидСущности) = Неопределено Тогда
			ВТ_Данные.Добавить(ТекСтрока);	
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекЭлемент Из ВсеСоответствияВыгрузкиТабЧасть Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяСтруктураДанных = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСтруктуруСоответствийТабЧастейДляВыгрузки();
			ЗаполнитьЗначенияСвойств(НоваяСтруктураДанных, ТекЭлемент); 
			ВТ_Данные.Добавить(НоваяСтруктураДанных);	
		КонецЕсли;
	КонецЦикла;
	мВсеСоответствияВыгрузкиТабЧасть = ВТ_Данные; 
	
	
	ВТ_Данные = Новый Массив;
	Для каждого ТекСтрока Из мВсеСоответствияЗагрузкиТабЧасть Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекСтрока.ВидСущности) = Неопределено Тогда
			ВТ_Данные.Добавить(ТекСтрока);	
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекЭлемент Из ВсеСоответствияЗагрузкиТабЧасть Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяСтруктураДанных = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСтруктуруСоответствийТабЧастейДляЗагрузки();
			ЗаполнитьЗначенияСвойств(НоваяСтруктураДанных, ТекЭлемент); 
			ВТ_Данные.Добавить(НоваяСтруктураДанных);	
		КонецЕсли;
	КонецЦикла;
	мВсеСоответствияЗагрузкиТабЧасть = ВТ_Данные; 
	

	ВТ_Данные = Новый Массив;
	Для каждого ТекСтрока Из мВсеОтборыПоВыгрузкаДанныхНаПортал Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекСтрока.ВидСущности) = Неопределено Тогда
			ВТ_Данные.Добавить(ТекСтрока);	
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекСтрока Из ВсеОтборыПоВыгрузкаДанныхНаПортал Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекСтрока.ВидСущности) <> Неопределено Тогда
			
			НоваяСтруктураДанных = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСтруктуруСоответствийОтборовПоВыгрузкеДанныхНаПортал();
			ЗаполнитьЗначенияСвойств(НоваяСтруктураДанных, ТекСтрока); 
			
			Для каждого ТекОтбор Из мВсеОтборыПоВыгрузкаДанныхНаПортал Цикл
				Если ТекСтрока.ВидСущности = ТекОтбор.ВидСущности И ТекСтрока.ИмяОбъекта = ТекОтбор.ИмяОбъекта Тогда
					
					НоваяСтруктураДанных.Схема = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСхемуКомпоновки(ТекСтрока.ВидСущности, ТекСтрока.ИмяОбъекта);
					
				КонецЕсли;
			КонецЦикла;
			
			ВТ_Данные.Добавить(НоваяСтруктураДанных);	
			
		КонецЕсли;
	КонецЦикла;
	мВсеОтборыПоВыгрузкаДанныхНаПортал = ВТ_Данные; 
	

	ВТ_Данные = Новый Массив;
	Для каждого ТекСтрока Из мВсеОтборыПоЗагрузкеДанныхВ1С Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекСтрока.ВидСущности) = Неопределено Тогда
			ВТ_Данные.Добавить(ТекСтрока);	
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекЭлемент Из ВсеОтборыПоЗагрузкеДанныхВ1С Цикл
		Если ВидыСущностей.НайтиПоЗначению(ТекЭлемент.ВидСущности) <> Неопределено Тогда
			НоваяСтруктураДанных = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьСтруктуруСоответствийОтборовПоЗагрузкеДанныхВ1С();
			ЗаполнитьЗначенияСвойств(НоваяСтруктураДанных, ТекЭлемент); 
			ВТ_Данные.Добавить(НоваяСтруктураДанных);	
		КонецЕсли;
	КонецЦикла;
	мВсеОтборыПоЗагрузкеДанныхВ1С = ВТ_Данные; 
	
	
	Настройки.Сущности 										= мСущности;
	Настройки.СоответствияПолейСущностейДляВыгрузкиНаПортал = мВсеСоответствияВыгрузки;
	Настройки.СоответствияПолейСущностейДляЗагрузкиВ1С 		= мВсеСоответствияЗагрузки;
	Настройки.ОтборыДляВыгрузкиНаПортал 					= мВсеОтборыПоВыгрузкаДанныхНаПортал;
	Настройки.ОтборыДляЗагрузкиДанныхВ1С 					= мВсеОтборыПоЗагрузкеДанныхВ1С;
	
	Настройки.СоответствияПолейСущностейТабЧастейДляВыгрузкиНаПортал 	= мВсеСоответствияВыгрузкиТабЧасть;
	Настройки.СоответствияПолейСущностейТабЧастейДляЗагрузкиВ1С			= мВсеСоответствияЗагрузкиТабЧасть;
	
	Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
	Пока Выборка.Следующий() Цикл
		ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
		ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(Настройки); 
		ТекСохраненныеНастройки.Записать();	
	КонецЦикла;
		
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаСервере
Функция ЗначениеРеквизитаОбъекта(Объект1С, ИмяРеквизита)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект1С, ИмяРеквизита);
	
КонецФункции


&НаСервере
Функция ПолучитьМенеджерВременныхТаблицСвойств(ИмяОбъекта, ВидСущности)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; 
	
	КлючКомментарияВключения 		= "Ключ Битрикс24 не указан";
	КлючКомментарияИсключения 		= "Ключ Битрикс24 не указан";
	УсловиеЗапроса 			= "";
	НаборСвойств 			= Неопределено;
	Если ВидСущности = "Компания" И ИмяОбъекта = "Организации"  Тогда
		КлючКомментарияВключения 	= "Свойство компании (Битрикс24)";
		КлючКомментарияИсключения 	= "Свойство контакта (Битрикс24)";
		НаборСвойств 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Организации");	
	ИначеЕсли ВидСущности = "Компания" И ИмяОбъекта = "Партнеры"  Тогда
		КлючКомментарияВключения 	= "Свойство компании (Битрикс24)";
		КлючКомментарияИсключения 	= "Свойство контакта (Битрикс24)";
		НаборСвойств 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Партнеры");	
	ИначеЕсли ВидСущности = "Контакт" И ИмяОбъекта = "Партнеры" Тогда
		КлючКомментарияВключения 	= "Свойство контакта (Битрикс24)";
		КлючКомментарияИсключения 	= "Свойство компании (Битрикс24)";
		НаборСвойств 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Партнеры");	
	ИначеЕсли ВидСущности = "Контакт" И ИмяОбъекта = "КонтактныеЛицаПартнеров" Тогда
		КлючКомментарияВключения 	= "Свойство контакта (Битрикс24)";
		КлючКомментарияИсключения 	= "Свойство компании (Битрикс24)";
		НаборСвойств 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_КонтактныеЛицаПартнеров");	
	ИначеЕсли ВидСущности = "Реквизит" И ИмяОбъекта = "Организации" Тогда
		//НаборСвойств 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Организации");	
	ИначеЕсли ВидСущности = "Реквизит" И ИмяОбъекта = "Контрагенты" Тогда
		//НаборСвойств 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Контрагенты");	
	ИначеЕсли ВидСущности = "Счет" ИЛИ ВидСущности = "Счет2" Тогда
		НаборСвойств 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Документ_СчетНаОплатуКлиенту");	
	ИначеЕсли ВидСущности = "Проект" Тогда
		КлючКомментарияВключения 	= "Свойство сделки (Битрикс24)";
		КлючКомментарияИсключения 	= "Свойство заказа (Битрикс24)";
		НаборСвойств 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Документ_ЗаказКлиента");	
	ИначеЕсли ВидСущности = "Товар" ИЛИ ВидСущности = "Товар2" Тогда
		НаборСвойств 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_Номенклатура");	
	ИначеЕсли ВидСущности = "Предложение" И ИмяОбъекта = "ХарактеристикиНоменклатуры"  Тогда
		НаборСвойств 	= Б24_КС_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьНаборСвойств("Справочник_ХарактеристикиНоменклатуры");	
	КонецЕсли;
	
	//нужна переработка свойств под менеджер, чтобы из одного места собирались свойства. Свойства нужно через набор + определение для компаний/контактовв
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НаборСвойств"				, НаборСвойств);
	Запрос.УстановитьПараметр("КлючКомментарияВключения"	, КлючКомментарияВключения);
	Запрос.УстановитьПараметр("КлючКомментарияИсключения"	, КлючКомментарияИсключения);
	Запрос.Текст = "ВЫБРАТЬ
	|	ДополнительныеРеквизиты.Ссылка КАК Набор,
	|	ДополнительныеРеквизиты.Свойство КАК Свойство
	|ПОМЕСТИТЬ ВТ_ВсеНаборы
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДополнительныеСведения.Ссылка,
	|	ДополнительныеСведения.Свойство
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВсеНаборы.Набор КАК НаборСвойств,
	|	ВТ_ВсеНаборы.Свойство КАК Свойство
	|ПОМЕСТИТЬ ВТ_СвойстваНаборы
	|ИЗ
	|	ВТ_ВсеНаборы КАК ВТ_ВсеНаборы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВсеНаборы.Набор,
	|	ВТ_ВсеНаборы.Свойство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка,
	|	ДополнительныеРеквизитыИСведения.Заголовок КАК Заголовок,
	|	ДополнительныеРеквизитыИСведения.Наименование КАК Наименование,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	ВЫРАЗИТЬ(ДополнительныеРеквизитыИСведения.Комментарий КАК СТРОКА(100)) КАК Комментарий
	|ПОМЕСТИТЬ ВТ_СвойстваПром
	|ИЗ
	|	ВТ_СвойстваНаборы КАК ВТ_СвойстваНаборы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО ВТ_СвойстваНаборы.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	ВТ_СвойстваНаборы.НаборСвойств В ИЕРАРХИИ(&НаборСвойств)
	|	И ДополнительныеРеквизитыИСведения.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СвойстваПром.Ссылка КАК Ссылка,
	|	ВТ_СвойстваПром.Заголовок КАК Заголовок,
	|	ВТ_СвойстваПром.Наименование КАК Наименование,
	|	ВТ_СвойстваПром.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	ВТ_СвойстваПром.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ВТ_Свойства
	|ИЗ
	|	ВТ_СвойстваПром КАК ВТ_СвойстваПром
	|ГДЕ
	|	(ВТ_СвойстваПром.Комментарий ПОДОБНО &КлючКомментарияВключения
	|			ИЛИ ВТ_СвойстваПром.Комментарий = """"
	|			ИЛИ (ВТ_СвойстваПром.Комментарий <> """"
	|				И НЕ ВТ_СвойстваПром.Комментарий ПОДОБНО &КлючКомментарияИсключения))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СвойстваПром.Комментарий,
	|	ВТ_СвойстваПром.Ссылка,
	|	ВТ_СвойстваПром.Заголовок,
	|	ВТ_СвойстваПром.ЭтоДополнительноеСведение,
	|	ВТ_СвойстваПром.Наименование";
	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;	
	
КонецФункции

&НаСервере
Функция ПолучитьТипСвойстваПоВидуСущности(ВидСущности)
	
	ТипыДанныхДляОбменаСПорталом = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьТипыДанныхДляОбменаСПорталом();
	
	ТипДанных = Неопределено;
	Если ВидСущности = "Компания" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.СвойствоКомпании;	
	ИначеЕсли ВидСущности = "Контакт" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.СвойствоКонтакта;	
	ИначеЕсли ВидСущности = "Реквизит" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.СвойствоРеквизита;	
	ИначеЕсли ВидСущности = "Счет" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.СвойствоСчета;	
	ИначеЕсли ВидСущности = "Счет2" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.СвойствоСчета2;	
	ИначеЕсли ВидСущности = "Проект" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.СвойствоСделки;	
	ИначеЕсли ВидСущности = "Товар" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.СвойствоТовара;
	ИначеЕсли ВидСущности = "Товар2" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.СвойствоТовара2;
	ИначеЕсли ВидСущности = "Предложение" Тогда
		ТипДанных = ТипыДанныхДляОбменаСПорталом.СвойствоПредложения;	
	КонецЕсли;
	
	Возврат ТипДанных;	
	
КонецФункции

#КонецОбласти
