
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПодключения = Параметры.НастройкаПодключения;	
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииСделок") Тогда
		Возврат;                         
	КонецЕсли;

	НастройкиСинхронизацииСделок = НастройкиОбмена.НастройкиСинхронизацииСделок;
	
	НастройкиСинхронизацииСделок.Свойство("Подразделение"	, Подразделение);
	НастройкиСинхронизацииСделок.Свойство("Организация"		, Организация);
	НастройкиСинхронизацииСделок.Свойство("Соглашение"		, Соглашение);
	НастройкиСинхронизацииСделок.Свойство("НоменклатураДоставка", НоменклатураДоставка);  
	
	НастройкиСинхронизацииСделок.Свойство("ДатаНачалаЗагрузкиСделок"	, ДатаНачалаЗагрузкиСделок);
	НастройкиСинхронизацииСделок.Свойство("ДатаНачалаВыгрузкиСделок"	, ДатаНачалаВыгрузкиСделок);
	НастройкиСинхронизацииСделок.Свойство("ИсточникДатыДокумента"		, ИсточникДатыДокумента);
	НастройкиСинхронизацииСделок.Свойство("ИсточникНомераДокумента"		, ИсточникНомераДокумента);
	
	НастройкиСинхронизацииСделок.Свойство("ВидМеткиДляСквознойАналитики"		, ВидМеткиДляСквознойАналитики);
	НастройкиСинхронизацииСделок.Свойство("ВыгружатьМеткуСквознойАналитики"		, ВыгружатьМеткуСквознойАналитики);
	НастройкиСинхронизацииСделок.Свойство("СвойствоДляСквознойАналитики"		, СвойствоДляСквознойАналитики);
	НастройкиСинхронизацииСделок.Свойство("ИсточникДляСквознойАналитики"		, ИсточникДляСквознойАналитики);
	
	НастройкиСинхронизацииСделок.Свойство("ВыгружатьФайлыСделок", ВыгружатьФайлыСделок);
	
	НастройкиСинхронизацииСделок.Свойство("СвойствоФайловБ24Сделок", СвойствоФайловБ24Сделок);
	Если ЗначениеЗаполнено(СвойствоФайловБ24Сделок) Тогда
		мДанные = СтрРазделить(СвойствоФайловБ24Сделок, ";");    
		Элементы.СвойствоФайловБ24Сделок.СписокВыбора.Добавить(мДанные[0], мДанные[1]);
		СвойствоФайловБ24Сделок = мДанные[0];
	КонецЕсли;
	
	лИсточникиПривлечения = Неопределено;
	Если НастройкиСинхронизацииСделок.Свойство("ИсточникиПривлечения", лИсточникиПривлечения) Тогда
		ИсточникиПривлечения.Загрузить(лИсточникиПривлечения);	
	КонецЕсли;
	
	Элементы.Подразделение.Видимость 	= ПолучитьФункциональнуюОпцию("ИспользоватьПодразделения");
	
КонецПроцедуры


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по сделкам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НужноЗакрытьОкно = Истина;
		
		СохранитьВыгрузитьНастройки(Истина);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		НужноЗакрытьОкно = Истина;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВыгружатьМеткуСквознойАналитикиПриИзменении(Элемент)
	
	Если ВидМеткиДляСквознойАналитики = "" Тогда ВидМеткиДляСквознойАналитики = "По умолчанию" КонецЕсли;
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидМеткиДляСквознойАналитикиПриИзменении(Элемент)
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьФайлыСделокПриИзменении(Элемент)
		
	ОбновитьОтображениеЭлементов();

КонецПроцедуры

&НаКлиенте
Процедура СвойствоФайловБ24СделокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	Элемент.СписокВыбора.Очистить();
	
	КлючиПользовательскиеПоляСущностиПортала = ПолучитьКлючиПользовательскиеПоляСущностиПортала("Проект");
	Для Каждого ТекСтрока Из КлючиПользовательскиеПоляСущностиПортала.ПользовательскиеПоля Цикл
		Если ТекСтрока.Множественное И ТекСтрока.ТипПоля = "file" Тогда
			Элемент.СписокВыбора.Добавить(ТекСтрока.КлючЗапроса, ТекСтрока.Описание); 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры   

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьСопоставлениеСтатусов(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаСопоставленияСтатусовСделок", ПараметрыФормы, ЭтаФорма, ,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	СохранитьВыгрузитьНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюКонтрагентов(Команда)
			
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыФормы.Вставить("ТипЗапуска"			, "Загрузка");
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.НастройкаСинхронизацииКонтрагентов", ПараметрыФормы, ЭтаФорма, ,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюСделок(Команда)
	
	мРежимыЗаписи = Новый Массив;
	мРежимыЗаписи.Добавить("Только записывать");
	мРежимыЗаписи.Добавить("Проводить");
	мРежимыЗаписи.Добавить("Проводить закрытые");
	
	ВидыСущностей = Новый Массив;
	ВидыСущностей.Добавить("Проект");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	ПередаваемыеПараметры.Вставить("РежимыЗаписи"			, мРежимыЗаписи);
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти


#Область Прочие

&НаКлиенте
Процедура ОбновитьОтображениеЭлементов()
	
	Элементы.ВидМеткиДляСквознойАналитики.Видимость = ВыгружатьМеткуСквознойАналитики;
	Если ВыгружатьМеткуСквознойАналитики Тогда
		
		Если ВидМеткиДляСквознойАналитики = "По умолчанию" Тогда
			Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
			Элементы.СвойствоСквознойАналитики.Видимость 	= Ложь;
		ИначеЕсли ВидМеткиДляСквознойАналитики = "Источник привлечения" Тогда
			Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
			Элементы.СвойствоСквознойАналитики.Видимость 	= Ложь;
		ИначеЕсли ВидМеткиДляСквознойАналитики = "Свойство" Тогда
			Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
			Элементы.СвойствоСквознойАналитики.Видимость 	= Истина;
		КонецЕсли;	
		
	Иначе
		Элементы.ИсточникДляСквознойАналитики.Видимость = Ложь;
		Элементы.СвойствоСквознойАналитики.Видимость 	= Ложь;
	КонецЕсли;	
	
	Элементы.ГруппаФайлПодробно.Видимость = ВыгружатьФайлыСделок;
	
КонецПроцедуры

&НаКлиенте
Функция СохранитьВыгрузитьНастройки(ЗакрытьПоОкончании = Ложь)
	
	НастройкаСохранена = СохранитьНастройкуСервер();
	
	Если НастройкаСохранена И ВыгружатьНастройкиНаПортал Тогда
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НастройкаПодключения, "ПометкаУдаления") = Ложь Тогда
			
			СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
			
			Если СтруктураНастроек = Неопределено Тогда
				ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
				Возврат Ложь;
			КонецЕсли;
			
			ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании);
		Иначе
			Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкаСохранена
	
КонецФункции  

&НаСервере
Функция СохранитьНастройкуСервер()
	
	Если Модифицированность Тогда
			
		СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
		НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
		
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииСделок") Тогда
			СтруктураНастроек = Новый Структура;	
		Иначе
			СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииСделок;	
		КонецЕсли;
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Соглашение"		, Соглашение);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Подразделение"	, Подразделение);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Организация"		, Организация);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "НоменклатураДоставка", НоменклатураДоставка);
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ДатаНачалаЗагрузкиСделок", ДатаНачалаЗагрузкиСделок);	
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ДатаНачалаВыгрузкиСделок", ДатаНачалаВыгрузкиСделок);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИсточникДатыДокумента"	, ИсточникДатыДокумента);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИсточникНомераДокумента", ИсточникНомераДокумента);
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВидМеткиДляСквознойАналитики"	, ВидМеткиДляСквознойАналитики);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьМеткуСквознойАналитики"	, ВыгружатьМеткуСквознойАналитики);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СвойствоДляСквознойАналитики"	, СвойствоДляСквознойАналитики);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИсточникДляСквознойАналитики"	, ИсточникДляСквознойАналитики);
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВыгружатьФайлыСделок", ВыгружатьФайлыСделок);
		
		НайденноеЗначение = Элементы.СвойствоФайловБ24Сделок.СписокВыбора.НайтиПоЗначению(СвойствоФайловБ24Сделок);
		Если НайденноеЗначение <> Неопределено Тогда
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СвойствоФайловБ24Сделок", НайденноеЗначение.Значение + ";" + НайденноеЗначение.Представление);
		Иначе
			РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СвойствоФайловБ24Сделок", "");	
		КонецЕсли;
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИсточникиПривлечения", ИсточникиПривлечения.Выгрузить());
		
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииСделок") Тогда
			НастройкиОбмена.Вставить("НастройкиСинхронизацииСделок", СтруктураНастроек); 	
		Иначе
			НастройкиОбмена.НастройкиСинхронизацииСделок = СтруктураНастроек;
		КонецЕсли;
		
		Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
		Пока Выборка.Следующий() Цикл
			ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
			ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(НастройкиОбмена); 
			ТекСохраненныеНастройки.Записать();	
		КонецЦикла;
		
		ОбновитьПовторноИспользуемыеЗначения();
		
		Модифицированность = Ложь;
		
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции  

&НаКлиенте
Процедура ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании)
	
	ДлительнаяОперацияЗавершение = Новый ОписаниеОповещения("ПослеВыгрузкиНастроек", ЭтаФорма, ЗакрытьПоОкончании);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ТекстСообщения             = "Выполняется выгрузка настроек..";
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания       = Истина;
	ПараметрыОжидания.ВыводитьСообщения          = Истина;
	
	ДлительнаяОперация = НачатьВыгрузкуНастройки(СтруктураНастроек);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ДлительнаяОперацияЗавершение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыгрузкиНастроек(Результат, ПараметрыВыгрузки) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность 			= Ложь;
		ВыгружатьНастройкиНаПортал 	= Ложь;
		
		Если ПараметрыВыгрузки = Истина Тогда
			Закрыть();	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьВыгрузкуНастройки(СтруктураНастроек)
	
	УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка регистрации изменений в Битрикс24";
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыВыгрузки.Вставить("Наименование"			, Строка(НастройкаПодключения));
	                                 
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.Б24_КС_НастройкиСинхронизации.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
	
КонецФункции


&НаКлиенте
Процедура ПослеВводаТонкихНастроек(Результат, пПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Свойство("ВыгружатьНастройкиНаПортал") Тогда
			ВыгружатьНастройкиНаПортал = Результат.ВыгружатьНастройкиНаПортал;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКлючиПользовательскиеПоляСущностиПортала(ВидСущности)
	
	Возврат РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьКлючиПользовательскиеПоляСущностиПортала(НастройкаПодключения, ВидСущности);
	
КонецФункции

&НаСервере
Процедура ЗагрузитьИсточникиПривлеченияНаСервере()
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьИсточникиПривлечения(Команда)
	
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	Если СтруктураНастроек = Неопределено Тогда
		ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
		Возврат;
	КонецЕсли;
		
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Загрузка источников привлечения";
	
	Фильтр = "";
	ДанныеСправочников = Б24_К_RestApiВызовСервера.ПолучитьМассивСтатусовCRM(СтруктураНастроек, Фильтр); 
	Для каждого ТекЭлемент Из ДанныеСправочников Цикл
			
		Если НЕ ТекЭлемент.Получить("ENTITY_ID") = "SOURCE" Тогда
			Продолжить;
		КонецЕсли;
		                 
		ИдИсточникаПривлечения = Формат(ТекЭлемент.Получить("STATUS_ID"), "ЧГ=0");
		
		НайденныеСтроки = ИсточникиПривлечения.НайтиСтроки(Новый Структура("ИдИсточникаПривлечения", ИдИсточникаПривлечения));
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока = ИсточникиПривлечения.Добавить();
			НоваяСтрока.НаименованиеИсточникаПривлечения = Строка(ТекЭлемент.Получить("NAME"));
			НоваяСтрока.ИдИсточникаПривлечения =  ИдИсточникаПривлечения;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

