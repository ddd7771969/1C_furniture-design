
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПодключения = Параметры.НастройкаПодключения;	
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	НастройкиСинхронизацииЗаказов = НастройкиОбмена.НастройкиСинхронизацииЗаказов;
	
	Если НастройкиСинхронизацииЗаказов.Свойство("ИнформацияОПлатежныхСистемах") Тогда
		
		ИнформацияОПлатежныхСистемах 	= НастройкиСинхронизацииЗаказов.ИнформацияОПлатежныхСистемах;
		СоответствияПлатежныхСистем 	= ИнформацияОПлатежныхСистемах.СоответствияПлатежныхСистем;
		
		Для каждого ТекСтрока Из СоответствияПлатежныхСистем Цикл
			
			Если ЗначениеЗаполнено(ТекСтрока.Касса) Тогда
				
				НовСтрока = Кассы.Добавить();
				НовСтрока.Касса 			= ТекСтрока.Касса;
				НовСтрока.ПлатежнаяСистема 	= ТекСтрока.ПлатежнаяСистема;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекСтрока.Терминал) ИЛИ ЗначениеЗаполнено(ТекСтрока.ВидПлатежнойКарты) Тогда
				
				НовСтрока = Терминалы.Добавить();
				НовСтрока.Терминал 			= ТекСтрока.Терминал;
				НовСтрока.ПлатежнаяСистема 	= ТекСтрока.ПлатежнаяСистема;
				НовСтрока.ВидПлатежнойКарты = ТекСтрока.ВидПлатежнойКарты;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекСтрока.РасчетныйСчет) Тогда
				
				НовСтрока = РасчетныеСчета.Добавить();
				НовСтрока.РасчетныйСчет 	= ТекСтрока.РасчетныйСчет;
				НовСтрока.ПлатежнаяСистема 	= ТекСтрока.ПлатежнаяСистема;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
		
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по платежным системам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда		
		НужноЗакрытьОкно = Истина;
		СохранениеНастроек();
    	Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		НужноЗакрытьОкно = Истина;
		Закрыть();
    КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьПлатежныеСистемы(Команда)
		
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка статусов заказов.";
	
	ТелоHTTPЗапроса = "";	  
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/sale.paysystem.list", ТелоHTTPЗапроса); 
	
	Если СтруктураОтвета <> Неопределено Тогда
		
		Если СтруктураОтвета.Получить("result") <> Неопределено Тогда
			
			ПлатежныеСистемы.Очистить();
			
			Для каждого ТекЭлемент Из СтруктураОтвета.Получить("result") Цикл 
				
				ИдЭлемента 		= Формат(ТекЭлемент.Получить("ID"), "ЧГ=0");
				Активно 		= ТекЭлемент.Получить("ACTIVE");
				Наименование 	= ТекЭлемент.Получить("NAME");
				ТипПС 			= ТекЭлемент.Получить("IS_CASH");          //"Y" - наличные, "A" -эквайринг, "N" -безнал
				ТипВладельца 	= ТекЭлемент.Получить("ENTITY_REGISTRY_TYPE");
				
				Если Активно <> "Y" Тогда
					Продолжить;
				КонецЕсли;
				
				Если ТипВладельца <> "ORDER" Тогда
					Продолжить;
				КонецЕсли;
				
				Если Элементы.ГруппаОбъектов1С.ТекущаяСтраница = Элементы.ГруппаКасс И ТипПС <> "Y" Тогда
					Продолжить;
				ИначеЕсли Элементы.ГруппаОбъектов1С.ТекущаяСтраница = Элементы.ГруппаЭкТерминалов И ТипПС <> "A" Тогда
					Продолжить;
				ИначеЕсли Элементы.ГруппаОбъектов1С.ТекущаяСтраница = Элементы.ГруппаРС И ТипПС <> "N" Тогда
					Продолжить;
				КонецЕсли;		
				
				Используется = Ложь;	
			
				Для каждого ТекКасса Из Кассы Цикл
					Если ТекКасса.ПлатежнаяСистема.НайтиПоЗначению(ИдЭлемента) <> Неопределено Тогда
						Используется = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Для каждого ТекТерминал Из Терминалы Цикл
					Если ТекТерминал.ПлатежнаяСистема.НайтиПоЗначению(ИдЭлемента) <> Неопределено Тогда
						Используется = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Для каждого ТекСчет Из РасчетныеСчета Цикл
					Если ТекСчет.ПлатежнаяСистема.НайтиПоЗначению(ИдЭлемента) <> Неопределено Тогда
						Используется = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			
				Если Не Используется Тогда
					ПлатежныеСистемы.Добавить(ИдЭлемента, Наименование); 	
				КонецЕсли;
					
				
			КонецЦикла;

		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	СохранениеНастроек();
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура СохранениеНастроек()
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииЗаказов") Тогда
		СтруктураНастроек = Новый Структура;	
	Иначе
		СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииЗаказов;	
	КонецЕсли;
	

	СоответствиеПлатежныхСистем = Новый ТаблицаЗначений;
	СоответствиеПлатежныхСистем.Колонки.Добавить("Касса");
	СоответствиеПлатежныхСистем.Колонки.Добавить("Терминал");
	СоответствиеПлатежныхСистем.Колонки.Добавить("РасчетныйСчет");
	СоответствиеПлатежныхСистем.Колонки.Добавить("ВидПлатежнойКарты");
	СоответствиеПлатежныхСистем.Колонки.Добавить("ПлатежнаяСистема");

	Для каждого ТекСтрока Из Кассы Цикл
		НоваяСтрока = СоответствиеПлатежныхСистем.Добавить();
		НоваяСтрока.Касса 				= ТекСтрока.Касса;
		НоваяСтрока.ПлатежнаяСистема 	= ТекСтрока.ПлатежнаяСистема;
	КонецЦикла;
	
	Для каждого ТекСтрока Из Терминалы Цикл
		НоваяСтрока = СоответствиеПлатежныхСистем.Добавить();
		НоваяСтрока.Терминал 			= ТекСтрока.Терминал;
		НоваяСтрока.ПлатежнаяСистема 	= ТекСтрока.ПлатежнаяСистема;
		НоваяСтрока.ВидПлатежнойКарты 	= ТекСтрока.ВидПлатежнойКарты;
	КонецЦикла;
	
	Для каждого ТекСтрока Из РасчетныеСчета Цикл
		НоваяСтрока = СоответствиеПлатежныхСистем.Добавить();
		НоваяСтрока.РасчетныйСчет 		= ТекСтрока.РасчетныйСчет;
		НоваяСтрока.ПлатежнаяСистема 	= ТекСтрока.ПлатежнаяСистема;
	КонецЦикла;
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
		
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииЗаказов") Тогда
		СтруктураНастроек = Новый Структура;	
	Иначе
		СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииЗаказов;	
	КонецЕсли;
	
	СтруктураИнформацииОСтатусах = Новый Структура;
	СтруктураИнформацииОСтатусах.Вставить("СоответствияПлатежныхСистем"	, СоответствиеПлатежныхСистем);
	
	РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИнформацияОПлатежныхСистемах", СтруктураИнформацииОСтатусах);
			
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииЗаказов") Тогда
		НастройкиОбмена.Вставить("НастройкиСинхронизацииЗаказов", СтруктураНастроек); 	
	Иначе
		НастройкиОбмена.НастройкиСинхронизацииЗаказов = СтруктураНастроек;
	КонецЕсли;
	
	Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
	Пока Выборка.Следующий() Цикл
		ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
		ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(НастройкиОбмена); 
		ТекСохраненныеНастройки.Записать();	
	КонецЦикла;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ДобавитьПС(Команда)
	
	ПлатежнаяСистема = Элементы.ПлатежныеСистемы.ТекущиеДанные;
	
	Если ПлатежнаяСистема = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ГруппаОбъектов1С.ТекущаяСтраница = Элементы.ГруппаКасс Тогда
		
		Если Элементы.Кассы.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;	
		
		НайденнаяСтрока = Кассы.НайтиПоИдентификатору(Элементы.Кассы.ТекущаяСтрока);
		
		Если НайденнаяСтрока <> Неопределено Тогда
			
			НайденнаяСтрока.ПлатежнаяСистема.Добавить(ПлатежнаяСистема.Значение, ПлатежнаяСистема.Представление);	
			
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаОбъектов1С.ТекущаяСтраница = Элементы.ГруппаЭкТерминалов Тогда	
		
		Если Элементы.Терминалы.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		НайденнаяСтрока = Терминалы.НайтиПоИдентификатору(Элементы.Терминалы.ТекущаяСтрока);
		
		Если НайденнаяСтрока <> Неопределено Тогда
			
			НайденнаяСтрока.ПлатежнаяСистема.Добавить(ПлатежнаяСистема.Значение, ПлатежнаяСистема.Представление);	
			
		КонецЕсли;
		
		
	ИначеЕсли Элементы.ГруппаОбъектов1С.ТекущаяСтраница = Элементы.ГруппаРС Тогда	
		
		Если Элементы.РасчетныеСчета.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		НайденнаяСтрока = РасчетныеСчета.НайтиПоИдентификатору(Элементы.РасчетныеСчета.ТекущаяСтрока);
		
		Если НайденнаяСтрока <> Неопределено Тогда
			
			НайденнаяСтрока.ПлатежнаяСистема.Добавить(ПлатежнаяСистема.Значение, ПлатежнаяСистема.Представление);	
			
		КонецЕсли;
		
	КонецЕсли;
	
	НайденноеЗначение = ПлатежныеСистемы.НайтиПоИдентификатору(Элементы.ПлатежныеСистемы.ТекущаяСтрока);
	
	Если НайденноеЗначение <> Неопределено Тогда
		
		ПлатежныеСистемы.Удалить(НайденноеЗначение);
			
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УбратьПС(Команда)
	
	Если Элементы.ГруппаОбъектов1С.ТекущаяСтраница = Элементы.ГруппаКасс Тогда
		
		Если Элементы.Кассы.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;	
		
		Для каждого Элемент Из Элементы.Кассы.ТекущиеДанные.ПлатежнаяСистема Цикл
			ПлатежныеСистемы.Добавить(Элемент.Значение, Элемент.Представление);
		КонецЦикла;
		
		Кассы.НайтиПоИдентификатору(Элементы.Кассы.ТекущаяСтрока).ПлатежнаяСистема.Очистить();
		
	ИначеЕсли Элементы.ГруппаОбъектов1С.ТекущаяСтраница = Элементы.ГруппаЭкТерминалов Тогда	
		
		Если Элементы.Терминалы.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;	
		
		Для каждого Элемент Из Элементы.Терминалы.ТекущиеДанные.ПлатежнаяСистема Цикл
			ПлатежныеСистемы.Добавить(Элемент.Значение, Элемент.Представление);
		КонецЦикла;
		
		Терминалы.НайтиПоИдентификатору(Элементы.Терминалы.ТекущаяСтрока).ПлатежнаяСистема.Очистить();
		
	ИначеЕсли Элементы.ГруппаОбъектов1С.ТекущаяСтраница = Элементы.ГруппаРС Тогда	
		
		Если Элементы.РасчетныеСчета.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;	
		
		Для каждого Элемент Из Элементы.РасчетныеСчета.ТекущиеДанные.ПлатежнаяСистема Цикл
			ПлатежныеСистемы.Добавить(Элемент.Значение, Элемент.Представление);
		КонецЦикла;
		
		РасчетныеСчета.НайтиПоИдентификатору(Элементы.Терминалы.ТекущаяСтрока).ПлатежнаяСистема.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивВидовПлатежныхКарт(пТерминал)
	
	Результат = Новый Массив;
	
	Для Каждого ТекСтрока Из пТерминал.ВидыПлатежныхКарт Цикл
		
		Результат.Добавить(ТекСтрока.ВидПлатежнойКарты);	
		
	КонецЦикла;
	
	Возврат Результат;	
	
КонецФункции

&НаКлиенте
Процедура ГруппаОбъектов1СПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ПлатежныеСистемы.Очистить();
КонецПроцедуры

#КонецОбласти

