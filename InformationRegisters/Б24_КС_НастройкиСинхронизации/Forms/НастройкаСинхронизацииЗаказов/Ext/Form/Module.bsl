
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Подразделение.Видимость 	= ПолучитьФункциональнуюОпцию("ИспользоватьПодразделения");
	Элементы.Склад.Видимость 			= ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
	
	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПодключения = Параметры.НастройкаПодключения;	
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииЗаказов") Тогда
		Возврат;                         
	КонецЕсли;

	НастройкиСинхронизацииЗаказов = НастройкиОбмена.НастройкиСинхронизацииЗаказов;
	
	НастройкиСинхронизацииЗаказов.Свойство("Подразделение"	, Подразделение);
	НастройкиСинхронизацииЗаказов.Свойство("Соглашение"		, Соглашение);
	НастройкиСинхронизацииЗаказов.Свойство("Организация"	, Организация);
	НастройкиСинхронизацииЗаказов.Свойство("ВидЗаказа"		, ВидЗаказа);	
	НастройкиСинхронизацииЗаказов.Свойство("Ответственный"	, Ответственный);	
				
	НастройкиСинхронизацииЗаказов.Свойство("ДатаНачалаЗагрузкиЗаказов"	, ДатаНачалаЗагрузкиЗаказов);
	НастройкиСинхронизацииЗаказов.Свойство("ДатаНачалаВыгрузкиЗаказов"	, ДатаНачалаВыгрузкиЗаказов);
	НастройкиСинхронизацииЗаказов.Свойство("ИсточникДатыДокумента"		, ИсточникДатыДокумента);
	НастройкиСинхронизацииЗаказов.Свойство("ИсточникНомераДокумента"	, ИсточникНомераДокумента);
	НастройкиСинхронизацииЗаказов.Свойство("КогдаОтменен"				, КогдаОтменен);		
	НастройкиСинхронизацииЗаказов.Свойство("Склад"						, Склад);	
	НастройкиСинхронизацииЗаказов.Свойство("НоменклатураДоставка"		, НоменклатураДоставка);	
	
	НастройкиСинхронизацииЗаказов.Свойство("СтатьяДДС"				, СтатьяДДС);	
	НастройкиСинхронизацииЗаказов.Свойство("СтавкаНДСРасшифровки"	, СтавкаНДСРасшифровки);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОтображениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по заказам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НужноЗакрытьОкно = Истина;
		
		СохранитьВыгрузитьНастройки(Истина);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		НужноЗакрытьОкно = Истина;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	СохранитьВыгрузитьНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСоответствияСтатусовЗаказов(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыФормы.Вставить("ТипИсточника"			, "Заказ");	
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаСопоставленияСтатусовЗаказовИОтгрузок", ПараметрыФормы, ЭтаФорма, ,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	

КонецПроцедуры

&НаКлиенте
Процедура НастроитьСоответствияСтатусовОтгрузок(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыФормы.Вставить("ТипИсточника"			, "Отгрузка");	
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаСопоставленияСтатусовЗаказовИОтгрузок", ПараметрыФормы, ЭтаФорма, ,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	

КонецПроцедуры

&НаКлиенте
Процедура НастроитьСоответствияСлужбДоставкиЗаказов(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыФормы.Вставить("ТипИсточника"			, "Заказ");	
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаСопоставленияСлужбДоставкиЗаказовИОтгрузок", ПараметрыФормы, ЭтаФорма, ,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСоответствияСлужбДоставкиОтгрузок(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыФормы.Вставить("ТипИсточника"			, "Отгрузка");	
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаСопоставленияСлужбДоставкиЗаказовИОтгрузок", ПараметрыФормы, ЭтаФорма, ,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	

КонецПроцедуры

&НаКлиенте
Процедура НастроитьСоответствияПлатежныхСистем(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаСопоставленияПлатежныхСистем", ПараметрыФормы, ЭтаФорма, ,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	

КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюКонтрагентов(Команда)
			
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыФормы.Вставить("ТипЗапуска"			, "Загрузка");
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.НастройкаСинхронизацииКонтрагентов", ПараметрыФормы, ЭтаФорма, ,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#КонецОбласти


#Область Прочие

&НаКлиенте
Процедура ОбновитьОтображениеЭлементов()
	//
	//Элементы.НастройкиЗагрузкиДокументов.Доступность = ЗагружатьЗаказы;	
	//Элементы.НастройкаВыгрузкиДокументов.Доступность = ВыгружатьЗаказы;		

	//Элементы.СтраницаЗаказы.Доступность 	= ЗагружатьЗаказы ИЛИ ВыгружатьЗаказы;
	//Элементы.СтраницаОплаты.Доступность 	= ЗагружатьЗаказы ИЛИ ВыгружатьЗаказы;
	//Элементы.СтраницаОтгрузки.Доступность 	= ЗагружатьЗаказы ИЛИ ВыгружатьЗаказы;

	//Элементы.ПроводитьЕслиОплачен.Доступность 	= ЗагружатьОплаты;
	//Элементы.СтатьяДДС.Доступность 				= ЗагружатьОплаты;
	//Элементы.СтавкаНДСРасшифровки.Доступность 	= ЗагружатьОплаты;
	//
	//Элементы.ПроводитьЕслиОтгружен.Доступность 	= ЗагружатьОтгрузки;
	//
	//Если Ложь Тогда
	//    ВыгружатьОфлайнЗаказы = Ложь;
	//	Элементы.ВыгружатьОфлайнЗаказы.Доступность = Ложь;	
	//КонецЕсли;

	//Элементы.ЗагружатьПользовательскиеПоляЗаказов.Доступность 	= ЗагружатьЗаказы;	
	//Элементы.ВыгружатьПользовательскиеПоляСделок.Доступность 	= ВыгружатьЗаказы;		
	//
	//Элементы.ГруппаОплатыЗагрузка.Доступность					= ЗагружатьЗаказы;		
	//Элементы.ГруппаОтгрузкиЗагрузка.Доступность 				= ЗагружатьЗаказы;		
	//
	//Элементы.ГруппаОплатыВыгрузка.Доступность	 				= ВыгружатьЗаказы;		
	//Элементы.ГруппаОтгрузкиВыгрузка.Доступность 				= ВыгружатьЗаказы;		
	//
КонецПроцедуры

&НаКлиенте
Функция СохранитьВыгрузитьНастройки(ЗакрытьПоОкончании = Ложь)
	
	НастройкаСохранена = СохранитьНастройкуСервер();
	
	Если НастройкаСохранена И ВыгружатьНастройкиНаПортал Тогда
		
		Если Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеРеквизитаОбъекта(НастройкаПодключения, "ПометкаУдаления") = Ложь Тогда
			
			СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
			
			Если СтруктураНастроек = Неопределено Тогда
				ПоказатьПредупреждение(, "Не удалось подключиться к Битрикс24");
				Возврат Ложь;
			КонецЕсли;
			
			ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании);
		Иначе
			Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если ЗакрытьПоОкончании Тогда Закрыть() КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкаСохранена
	
КонецФункции  

&НаСервере
Функция СохранитьНастройкуСервер()
	
	Если Модифицированность Тогда
			
		СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
		НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
		
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииЗаказов") Тогда
			СтруктураНастроек = Новый Структура;	
		Иначе
			СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииЗаказов;	
		КонецЕсли;
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Подразделение"			, Подразделение);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Соглашение"				, Соглашение);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Организация"				, Организация);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ВидЗаказа"				, ВидЗаказа);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Ответственный"			, Ответственный);
			
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ДатаНачалаЗагрузкиЗаказов", ДатаНачалаЗагрузкиЗаказов);	
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ДатаНачалаВыгрузкиЗаказов", ДатаНачалаВыгрузкиЗаказов);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИсточникДатыДокумента"	, ИсточникДатыДокумента);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИсточникНомераДокумента"	, ИсточникНомераДокумента);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "КогдаОтменен"			, КогдаОтменен);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "Склад"					, Склад);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "НоменклатураДоставка"	, НоменклатураДоставка);
		
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СтатьяДДС"				, СтатьяДДС);
		РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "СтавкаНДСРасшифровки"	, СтавкаНДСРасшифровки);
		
		
		Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииЗаказов") Тогда
			НастройкиОбмена.Вставить("НастройкиСинхронизацииЗаказов", СтруктураНастроек); 	
		Иначе
			НастройкиОбмена.НастройкиСинхронизацииЗаказов = СтруктураНастроек;
		КонецЕсли;
		
		Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
		Пока Выборка.Следующий() Цикл
			ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
			ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(НастройкиОбмена); 
			ТекСохраненныеНастройки.Записать();	
		КонецЦикла;
		
		ОбновитьПовторноИспользуемыеЗначения();
		
		Модифицированность = Ложь;
		
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции  

&НаКлиенте
Процедура ВыгрузкаНастройки(СтруктураНастроек, ЗакрытьПоОкончании)
	
	ДлительнаяОперацияЗавершение = Новый ОписаниеОповещения("ПослеВыгрузкиНастроек", ЭтаФорма, ЗакрытьПоОкончании);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ТекстСообщения             = "Выполняется выгрузка настроек..";
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания       = Истина;
	ПараметрыОжидания.ВыводитьСообщения          = Истина;
	
	ДлительнаяОперация = НачатьВыгрузкуНастройки(СтруктураНастроек);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ДлительнаяОперацияЗавершение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыгрузкиНастроек(Результат, ПараметрыВыгрузки) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность 			= Ложь;
		ВыгружатьНастройкиНаПортал 	= Ложь;
		
		Если ПараметрыВыгрузки = Истина Тогда
			Закрыть();	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьВыгрузкуНастройки(СтруктураНастроек)
	
	УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторЗадания);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Выгрузка регистрации изменений в Битрикс24";
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПараметрыВыгрузки.Вставить("Наименование"			, Строка(НастройкаПодключения));
	                                 
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.Б24_КС_НастройкиСинхронизации.ВыгрузитьНастройкиВФоне", ПараметрыВыгрузки, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура НастроитьСинхронизациюЗаказов(Команда)
	
	мРежимыЗаписи = Новый Массив;
	мРежимыЗаписи.Добавить("Только записывать");
	мРежимыЗаписи.Добавить("Проводить");
	мРежимыЗаписи.Добавить("Проводить закрытые");
	
	ВидыСущностей 			= Новый Массив;
	ВидыСущностей.Добавить("Заказ");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	ПередаваемыеПараметры.Вставить("РежимыЗаписи"			, мРежимыЗаписи);
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюОтгрузки(Команда)
	
	мРежимыЗаписи = Новый Массив;
	мРежимыЗаписи.Добавить("Только записывать");
	мРежимыЗаписи.Добавить("Проводить");
	мРежимыЗаписи.Добавить("Проводить отгруженные");
	
	ВидыСущностей 			= Новый Массив;
	ВидыСущностей.Добавить("Отгрузка");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	ПередаваемыеПараметры.Вставить("РежимыЗаписи"			, мРежимыЗаписи);
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСинхронизациюОплат(Команда)
	
	мРежимыЗаписи = Новый Массив;
	мРежимыЗаписи.Добавить("Только записывать");
	мРежимыЗаписи.Добавить("Проводить");
	мРежимыЗаписи.Добавить("Проводить оплаченные");
	
	ВидыСущностей 			= Новый Массив;
	ВидыСущностей.Добавить("Оплата");
	
	ПередаваемыеПараметры 	= Новый Структура;
	ПередаваемыеПараметры.Вставить("НастройкаПодключения"	, НастройкаПодключения);
	ПередаваемыеПараметры.Вставить("ВидыСущностей"			, ВидыСущностей);
	ПередаваемыеПараметры.Вставить("РежимыЗаписи"			, мРежимыЗаписи);
	
	ПараметрыОО = Новый Структура;
	ОО = Новый ОписаниеОповещения("ПослеВводаТонкихНастроек", ЭтаФорма, ПараметрыОО);
	
	ОткрытьФорму("РегистрСведений.Б24_КС_НастройкиСинхронизации.Форма.ФормаНастройкиОбъектовИнтеграции", ПередаваемыеПараметры,,,,,ОО , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры


&НаКлиенте
Процедура ПослеВводаТонкихНастроек(Результат, пПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Свойство("ВыгружатьНастройкиНаПортал") Тогда
			ВыгружатьНастройкиНаПортал = Результат.ВыгружатьНастройкиНаПортал;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти
