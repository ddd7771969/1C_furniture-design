
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновлениеЭлементовФормы();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если НЕ Параметры.Свойство("НастройкаПодключения") Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПодключения 	= Параметры.НастройкаПодключения;	
	ТипИсточника 			 = Параметры.ТипИсточника;
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	НастройкиСинхронизацииЗаказов = НастройкиОбмена.НастройкиСинхронизацииЗаказов;
	
	Если НастройкиСинхронизацииЗаказов.Свойство("ИнформацияОСтатусах") Тогда
		
		ИнформацияОСтатусах = НастройкиСинхронизацииЗаказов.ИнформацияОСтатусах;
		
		ИсточникСтатусовЗаказов 	= ИнформацияОСтатусах.ИсточникСтатусовЗаказов;
		ИсточникСтатусовОтгрузок 	= ИнформацияОСтатусах.ИсточникСтатусовОтгрузок;
		
		СвойствоЗаказа 		= ИнформацияОСтатусах.СвойствоЗаказа;
		СвойствоОтгрузки 	= ИнформацияОСтатусах.СвойствоОтгрузки;
		
		
		ПереводитьВСтатусКогдаОплаченЗаказ 			= "";
		ПереводитьВСтатусКогдаОтгруженЗаказ 		= "";
		ПереводитьВСтатусКогдаОплаченОтгруженЗаказ 	= "";
		
		ИнформацияОСтатусах.Свойство("ПереводитьВСтатусКогдаОплаченЗаказ"			, ПереводитьВСтатусКогдаОплаченЗаказ);
		ИнформацияОСтатусах.Свойство("ПереводитьВСтатусКогдаОтгруженЗаказ"			, ПереводитьВСтатусКогдаОтгруженЗаказ);
		ИнформацияОСтатусах.Свойство("ПереводитьВСтатусКогдаОплаченОтгруженЗаказ"	, ПереводитьВСтатусКогдаОплаченОтгруженЗаказ);
		
		
		ПереводитьВСтатусКогдаОплаченаОтгрузка 			= "";
		ПереводитьВСтатусКогдаОтгруженаОтгрузка 		= "";
		ПереводитьВСтатусКогдаОплаченОтгруженОтгрузка 	= "";
		
		ИнформацияОСтатусах.Свойство("ПереводитьВСтатусКогдаОплаченаОтгрузка"		, ПереводитьВСтатусКогдаОплаченаОтгрузка);
		ИнформацияОСтатусах.Свойство("ПереводитьВСтатусКогдаОтгруженаОтгрузка"		, ПереводитьВСтатусКогдаОтгруженаОтгрузка);
		ИнформацияОСтатусах.Свойство("ПереводитьВСтатусКогдаОплаченОтгруженОтгрузка", ПереводитьВСтатусКогдаОплаченОтгруженОтгрузка);
		
		СоответствияСЗначениямиСвойствЗаказа.Загрузить(ИнформацияОСтатусах.СоответствияСЗначениямиСвойствЗаказа);
		СоответствияССостояниямиЗаказа.Загрузить(ИнформацияОСтатусах.СоответствияССостояниямиЗаказа);
		СоответствияССтатусамиЗаказа.Загрузить(ИнформацияОСтатусах.СоответствияССтатусамиЗаказа);		
			
		СоответствияСЗначениямиСвойствОтгрузки.Загрузить(ИнформацияОСтатусах.СоответствияСЗначениямиСвойствОтгрузки);
		СоответствияССтатусамиОтгрузки.Загрузить(ИнформацияОСтатусах.СоответствияССтатусамиОтгрузки);				
		
	Иначе
		ИсточникСтатусовЗаказов 	= "СтатусыЗаказов";
		ИсточникСтатусовОтгрузок 	= "СвойствоОтгрузок";
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
		
	Если Модифицированность И НЕ НужноЗакрытьОкно Тогда
		Отказ = Истина; 
		
		Если ЗавершениеРаботы Тогда 
			ТекстПредупреждения = "Настройки по статусам не будут сохранены. Закрыть?"; 
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОЗакрытииОкна", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Настройки были изменены. Сохранить изменения перед закрытием?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		КонецЕсли;
	КонецЕсли;
	

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОЗакрытииОкна(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда		
		Если (ТипИсточника = "Заказ" И ИсточникСтатусовЗаказов = "СвойствоЗаказов" И НЕ ЗначениеЗаполнено(СвойствоЗаказа))
		ИЛИ   (ТипИсточника = "Отгрузка" И ИсточникСтатусовОтгрузок = "СвойствоОтгрузок" И НЕ ЗначениеЗаполнено(СвойствоОтгрузки))	Тогда
			ПоказатьПредупреждение(,"Не указано свойство заказов или доставок. Запись настроек невозможна");
		Иначе		
			НужноЗакрытьОкно = Истина;
			СохранениеНастроек();
        		Закрыть();
       		 КонецЕсли;
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		НужноЗакрытьОкно = Истина;
		Закрыть();
    КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	Если (ТипИсточника = "Заказ" И ИсточникСтатусовЗаказов = "СвойствоЗаказов" И НЕ ЗначениеЗаполнено(СвойствоЗаказа))
	ИЛИ (ТипИсточника = "Отгрузка" И ИсточникСтатусовОтгрузок = "СвойствоОтгрузок" И НЕ ЗначениеЗаполнено(СвойствоОтгрузки))	Тогда
		ПоказатьПредупреждение(,"Не указано свойство счета. Запись настроек невозможна");
	Иначе
		СохранениеНастроек();
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСтатусыЗаказа(Команда)
		
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	мТаблицы = Новый Массив;
	мТаблицы.Добавить(СоответствияСЗначениямиСвойствЗаказа);
	мТаблицы.Добавить(СоответствияССостояниямиЗаказа);
	мТаблицы.Добавить(СоответствияССтатусамиЗаказа);
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка статусов заказов.";
	
	ТелоHTTPЗапроса = "&filter[type]=O";	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/sale.status.list", ТелоHTTPЗапроса); 
	
	
	Если СтруктураОтвета <> Неопределено Тогда
		
		result = СтруктураОтвета.Получить("result"); 
		
		Если result <> Неопределено Тогда
			
			statuses = result.Получить("statuses"); 
			
			Если statuses <> Неопределено Тогда
				
				Для каждого ТекЭлемент Из statuses Цикл 
					
				    ИдСтатуса 	= Формат(ТекЭлемент.Получить("id"),"ЧГ=0");
					
					ТелоHTTPЗапроса = "&filter[statusId]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдСтатуса)+"&filter[lid]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЛокализациюБитрикс24());
					СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/sale.statusLang.list", ТелоHTTPЗапроса); 
					Если СтруктураОтвета = Неопределено Тогда
		            	Продолжить;
					КонецЕсли;
					
					result2 = СтруктураОтвета.Получить("result"); 
					Если result2 = Неопределено Тогда
		            	Продолжить;
					КонецЕсли;
					
					statusLangs = result2.Получить("statusLangs");
					Если statusLangs = Неопределено Тогда
		            	Продолжить;
					КонецЕсли;
						   
					Если statusLangs.Количество() = 0 Тогда
		            	Продолжить;
					КонецЕсли;
					   
					НазваниеСтатуса = statusLangs[0].Получить("name");
	   
					Для Каждого ТекТаблица Из мТаблицы Цикл
						
						НайденныеСтроки = ТекТаблица.НайтиСтроки(Новый Структура("ИдСтатуса", ИдСтатуса));
						
						Если НайденныеСтроки.количество() = 0 Тогда
							НоваяЗапись = ТекТаблица.Добавить();
							
							НоваяЗапись.ИдСтатуса			= ИдСтатуса;
							НоваяЗапись.НазваниеСтатуса 	= НазваниеСтатуса;
						Иначе
							
							Для Каждого ТекСтрокаТаблицы Из ТекТаблица Цикл
								Если ТекСтрокаТаблицы.ИдСтатуса = ИдСтатуса Тогда
									ТекСтрокаТаблицы.НазваниеСтатуса = НазваниеСтатуса; 
									Прервать;
								КонецЕсли;
							КонецЦикла;
							
						КонецЕсли; 						
						
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбновлениеЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСтатусыОтгрузок(Команда)
		
	СтруктураНастроек =  Б24_К_ОбщегоНазначенияВызовСервера.СформироватьСтруктуруНастроек(НастройкаПодключения);  
	
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	мТаблицы = Новый Массив;
	мТаблицы.Добавить(СоответствияСЗначениямиСвойствОтгрузки);
	мТаблицы.Добавить(СоответствияССтатусамиОтгрузки);
	
	СтруктураНастроек.Логирование.Измерение1 = "Служебные операции";
	СтруктураНастроек.Логирование.Измерение2 = "Получение списка статусов отгрузок.";
	
	ТелоHTTPЗапроса = "&filter[type]=D";	
	СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/sale.status.list", ТелоHTTPЗапроса); 
	
	
	Если СтруктураОтвета <> Неопределено Тогда
		
		result = СтруктураОтвета.Получить("result"); 
		
		Если result <> Неопределено Тогда
			
			statuses = result.Получить("statuses"); 
			
			Если statuses <> Неопределено Тогда
				
				Для каждого ТекЭлемент Из statuses Цикл 
					
				    ИдСтатуса 	= Формат(ТекЭлемент.Получить("id"),"ЧГ=0");
					
					ТелоHTTPЗапроса = "&filter[statusId]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(ИдСтатуса)+"&filter[lid]=" + Б24_К_RestApiВызовСервера.ЗакодироватьСтрокуСервер(Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЛокализациюБитрикс24());
					СтруктураОтвета = Б24_К_RestApiВызовСервера.ОтправкаДанныхНаПортал(СтруктураНастроек, "/rest/sale.statusLang.list", ТелоHTTPЗапроса); 
					Если СтруктураОтвета = Неопределено Тогда
		            	Продолжить;
					КонецЕсли;
					
					result2 = СтруктураОтвета.Получить("result"); 
					Если result2 = Неопределено Тогда
		            	Продолжить;
					КонецЕсли;
					
					statusLangs = result2.Получить("statusLangs");
					Если statusLangs = Неопределено Тогда
		            	Продолжить;
					КонецЕсли;
						   
					Если statusLangs.Количество() = 0 Тогда
		            	Продолжить;
					КонецЕсли;
					   
					НазваниеСтатуса = statusLangs[0].Получить("name");
	   
					Для Каждого ТекТаблица Из мТаблицы Цикл
						
						НайденныеСтроки = ТекТаблица.НайтиСтроки(Новый Структура("ИдСтатуса", ИдСтатуса));
						
						Если НайденныеСтроки.количество() = 0 Тогда
							НоваяЗапись = ТекТаблица.Добавить();
							
							НоваяЗапись.ИдСтатуса			= ИдСтатуса;
							НоваяЗапись.НазваниеСтатуса 	= НазваниеСтатуса;
						Иначе
							
							Для Каждого ТекСтрокаТаблицы Из ТекТаблица Цикл
								Если ТекСтрокаТаблицы.ИдСтатуса = ИдСтатуса Тогда
									ТекСтрокаТаблицы.НазваниеСтатуса = НазваниеСтатуса; 
									Прервать;
								КонецЕсли;
							КонецЦикла;
							
						КонецЕсли; 						
						
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбновлениеЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ИсточникСтатусовЗаказовПриИзменении(Элемент)
	ОбновлениеЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ИсточникСтатусовОтгрузокПриИзменении(Элемент)
	ОбновлениеЭлементовФормы();
КонецПроцедуры

#КонецОбласти


#Область Прочее

&НаСервере
Процедура СохранениеНастроек()
	
	СохраненныеНастройкиСинхронизации = РегистрыСведений.Б24_КС_НастройкиСинхронизации.ПолучитьНастройкуСинхронизацииПоНастройкеПодключения(НастройкаПодключения);
	НастройкиОбмена = СохраненныеНастройкиСинхронизации.Настройки.Получить();
	
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииЗаказов") Тогда
		СтруктураНастроек = Новый Структура;	
	Иначе
		СтруктураНастроек = НастройкиОбмена.НастройкиСинхронизацииЗаказов;	
	КонецЕсли;
	
	СтруктураИнформацииОСтатусах = Новый Структура;
	СтруктураИнформацииОСтатусах.Вставить("ИсточникСтатусовЗаказов"	, ИсточникСтатусовЗаказов);
	СтруктураИнформацииОСтатусах.Вставить("ИсточникСтатусовОтгрузок"	, ИсточникСтатусовОтгрузок);
	
	СтруктураИнформацииОСтатусах.Вставить("ПереводитьВСтатусКогдаОплаченЗаказ"			, ПереводитьВСтатусКогдаОплаченЗаказ);
	СтруктураИнформацииОСтатусах.Вставить("ПереводитьВСтатусКогдаОплаченаОтгрузка"		, ПереводитьВСтатусКогдаОплаченаОтгрузка);
	СтруктураИнформацииОСтатусах.Вставить("ПереводитьВСтатусКогдаОплаченОтгруженЗаказ"	, ПереводитьВСтатусКогдаОплаченОтгруженЗаказ);
	
	СтруктураИнформацииОСтатусах.Вставить("ПереводитьВСтатусКогдаОтгруженЗаказ"				, ПереводитьВСтатусКогдаОтгруженЗаказ);
	СтруктураИнформацииОСтатусах.Вставить("ПереводитьВСтатусКогдаОтгруженаОтгрузка"			, ПереводитьВСтатусКогдаОтгруженаОтгрузка);	
	СтруктураИнформацииОСтатусах.Вставить("ПереводитьВСтатусКогдаОплаченОтгруженОтгрузка"	, ПереводитьВСтатусКогдаОплаченОтгруженОтгрузка);	
	
	СтруктураИнформацииОСтатусах.Вставить("СвойствоЗаказа"		, СвойствоЗаказа);
	СтруктураИнформацииОСтатусах.Вставить("СвойствоОтгрузки"	, СвойствоОтгрузки);		

	СтруктураИнформацииОСтатусах.Вставить("СоответствияСЗначениямиСвойствЗаказа"	, СоответствияСЗначениямиСвойствЗаказа.Выгрузить());
	СтруктураИнформацииОСтатусах.Вставить("СоответствияСЗначениямиСвойствОтгрузки"	, СоответствияСЗначениямиСвойствОтгрузки.Выгрузить());
	СтруктураИнформацииОСтатусах.Вставить("СоответствияССостояниямиЗаказа"			, СоответствияССостояниямиЗаказа.Выгрузить());
	СтруктураИнформацииОСтатусах.Вставить("СоответствияССтатусамиЗаказа"			, СоответствияССтатусамиЗаказа.Выгрузить());	
	СтруктураИнформацииОСтатусах.Вставить("СоответствияССтатусамиОтгрузки"			, СоответствияССтатусамиОтгрузки.Выгрузить());
	
	РегистрыСведений.Б24_КС_НастройкиСинхронизации.ДобавитьНастройкуВСтруктуру(СтруктураНастроек, "ИнформацияОСтатусах"	, СтруктураИнформацииОСтатусах);
			
	Если НЕ НастройкиОбмена.Свойство("НастройкиСинхронизацииЗаказов") Тогда
		НастройкиОбмена.Вставить("НастройкиСинхронизацииЗаказов", СтруктураНастроек); 	
	Иначе
		НастройкиОбмена.НастройкиСинхронизацииЗаказов = СтруктураНастроек;
	КонецЕсли;
	
	Выборка = РегистрыСведений.Б24_КС_НастройкиСинхронизации.Выбрать(Новый Структура("НастройкаПодключения", НастройкаПодключения));
	Пока Выборка.Следующий() Цикл
		ТекСохраненныеНастройки = Выборка.ПолучитьМенеджерЗаписи();
		ТекСохраненныеНастройки.Настройки = Новый ХранилищеЗначения(НастройкиОбмена); 
		ТекСохраненныеНастройки.Записать();	
	КонецЦикла;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеЭлементовФормы()

	Если ТипИсточника = "Заказ" Тогда
		
		Элементы.СтраницыТипыИсточников.ТекущаяСтраница = Элементы.Заказы;		
	
		Если ИсточникСтатусовЗаказов = "СвойствоЗаказов" Тогда			
			Элементы.СтраницыЗаказ.ТекущаяСтраница = Элементы.СтраницаСвойствоЗаказа;			
		ИначеЕсли ИсточникСтатусовЗаказов = "СостоянияЗаказов" Тогда 
			Элементы.СтраницыЗаказ.ТекущаяСтраница = Элементы.СтраницаСостояниеЗаказа;
		Иначе
			ИсточникСтатусовЗаказов = "СтатусыЗаказов";	
			Элементы.СтраницыЗаказ.ТекущаяСтраница = Элементы.СтраницаСтатусЗаказа;
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = "Отгрузка" Тогда
		
		Элементы.СтраницыТипыИсточников.ТекущаяСтраница = Элементы.Отгрузки;		

		Если ИсточникСтатусовОтгрузок = "СвойствоОтгрузок" Тогда			
			Элементы.СтраницыОтгрузка.ТекущаяСтраница = Элементы.СтраницаСвойствоОтгрузки;			
		ИначеЕсли ИсточникСтатусовОтгрузок = "СостоянияОтгрузок" Тогда 
			Элементы.СтраницыОтгрузка.ТекущаяСтраница = Элементы.СтраницаСтатусОтгрузки;
		КонецЕсли;
			
	КонецЕсли;
	
	ОбновитьИнформациюОПринудительныхСтатусах();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюОПринудительныхСтатусах()
	
	Если ТипИсточника = "Заказ" Тогда
		
		Если ИсточникСтатусовЗаказов = "СвойствоЗаказов" Тогда
			тзнСтатусов = СоответствияСЗначениямиСвойствЗаказа;	
		ИначеЕсли ИсточникСтатусовЗаказов = "СостоянияЗаказов" Тогда
			тзнСтатусов = СоответствияССостояниямиЗаказа; 	
		ИначеЕсли ИсточникСтатусовЗаказов = "СтатусыЗаказов" Тогда
			тзнСтатусов = СоответствияССтатусамиЗаказа; 	
		КонецЕсли;
		
		Элементы.ПереводитьВСтатусКогдаОплаченЗаказ.СписокВыбора.Очистить();
		Элементы.ПереводитьВСтатусКогдаОтгруженЗаказ.СписокВыбора.Очистить();
		Элементы.ПереводитьВСтатусКогдаОплаченОтгруженЗаказ.СписокВыбора.Очистить();
		
		Для Каждого ТекСтрока Из тзнСтатусов Цикл
			Элементы.ПереводитьВСтатусКогдаОплаченЗаказ.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
			Элементы.ПереводитьВСтатусКогдаОтгруженЗаказ.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
			Элементы.ПереводитьВСтатусКогдаОплаченОтгруженЗаказ.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
		КонецЦикла;
		
	ИначеЕсли ТипИсточника = "Отгрузка" Тогда
		
		Если ИсточникСтатусовОтгрузок = "СвойствоОтгрузок" Тогда
			тзнСтатусов = СоответствияСЗначениямиСвойствОтгрузки;	
		ИначеЕсли ИсточникСтатусовОтгрузок = "СостоянияОтгрузок" Тогда
			тзнСтатусов = СоответствияССтатусамиОтгрузки; 	
		КонецЕсли;
		
		Элементы.ПереводитьВСтатусКогдаОплаченаОтгрузка.СписокВыбора.Очистить();
		Элементы.ПереводитьВСтатусКогдаОтгруженаОтгрузка.СписокВыбора.Очистить();
		Элементы.ПереводитьВСтатусКогдаОплаченОтгруженОтгрузка.СписокВыбора.Очистить();
		
		Для Каждого ТекСтрока Из тзнСтатусов Цикл
			Элементы.ПереводитьВСтатусКогдаОплаченаОтгрузка.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
			Элементы.ПереводитьВСтатусКогдаОтгруженаОтгрузка.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
			Элементы.ПереводитьВСтатусКогдаОплаченОтгруженОтгрузка.СписокВыбора.Добавить(ТекСтрока.ИдСтатуса, ТекСтрока.НазваниеСтатуса); 
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
