
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область РаботаСИдентификаторамиБитрикс24

Функция ПолучитьИдОбъекта(Портал, ТипВладельцаСмартПроцесса, ИдСмартПроцесса, Объект) Экспорт
	
	МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(Объект));
	ТипСущности1С = ?(Метаданные.Справочники.Содержит(МетаданныеОбъекта), "Справочник", "Документ");
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Портал"						, Портал);
	СтруктураОтбора.Вставить("ТипВладельцаСмартПроцесса"	, ТипВладельцаСмартПроцесса);
	СтруктураОтбора.Вставить("ИдСмартПроцесса"				, ИдСмартПроцесса);
	СтруктураОтбора.Вставить("ТипСущности1С"				, ТипСущности1С);
	СтруктураОтбора.Вставить("Сущность1С"					, МетаданныеОбъекта.Имя);
	СтруктураОтбора.Вставить("ИдентификаторОбъекта"			, XMLСтрока(Объект));
	
	Возврат РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.Получить(СтруктураОтбора).Идентификатор;
	
КонецФункции

Функция ПолучитьЗначениеПоИдБ24Объекта(Портал, ТипВладельцаСмартПроцесса, ИдСмартПроцесса, ТипСущности1С, Сущность1С, ИдБитрикс24) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Портал"			, Портал);
	Запрос.УстановитьПараметр("ТипВладельца"	, ТипВладельцаСмартПроцесса);
	Запрос.УстановитьПараметр("ИдСмартПроцесса"	, ИдСмартПроцесса);
	Запрос.УстановитьПараметр("ТипСущности1С"	, ТипСущности1С);
	Запрос.УстановитьПараметр("Сущность1С"		, Сущность1С);
	Запрос.УстановитьПараметр("Идентификатор"	, ИдБитрикс24);
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СМ_ИдентификаторыОбъектов.ИдентификаторОбъекта КАК ИдентификаторОбъекта
	|ИЗ
	|	РегистрСведений.Б24_СМ_ИдентификаторыОбъектов КАК Б24_СМ_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_СМ_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_СМ_ИдентификаторыОбъектов.ТипВладельцаСмартПроцесса = &ТипВладельца
	|	И Б24_СМ_ИдентификаторыОбъектов.ИдСмартПроцесса = &ИдСмартПроцесса
	|	И Б24_СМ_ИдентификаторыОбъектов.ТипСущности1С = &ТипСущности1С
	|	И Б24_СМ_ИдентификаторыОбъектов.Сущность1С = &Сущность1С
	|	И Б24_СМ_ИдентификаторыОбъектов.Идентификатор = &Идентификатор";
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Выборка.Следующий();
		
		Если ТипСущности1С = "Справочник" Тогда
			ТипОбъектаСтрокой = "СправочникСсылка."+Сущность1С;
		ИначеЕсли ТипСущности1С = "Документ" Тогда
			ТипОбъектаСтрокой = "ДокументСсылка."+Сущность1С;
		Иначе
			ТипОбъектаСтрокой = "Строка";	
		КонецЕсли;
		
		ТипОбъекта = Тип(ТипОбъектаСтрокой);
		
		Результат = XMLЗначение(ТипОбъекта, Выборка.ИдентификаторОбъекта);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСтруктуруДляЗаписиИдентификатора() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ТипВладельцаСмартПроцесса"	, "");
	Результат.Вставить("ИдСмартПроцесса"			, "");
	Результат.Вставить("ТипСущности1С"				, "");
	Результат.Вставить("Сущность1С"					, "");
	Результат.Вставить("Объект"						, "");
	Результат.Вставить("Идентификатор"				, "");
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаписатьИдБ24Объекта(СтруктураНастроек, СтруктураДляЗаписиИдентификатора) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтруктураДляЗаписиИдентификатора.Идентификатор) Тогда
		Возврат;
	КонецЕсли;
	
	Идентификатор = Формат(СтруктураДляЗаписиИдентификатора.Идентификатор, "ЧГ=0");	
	
	НоваяЗапись = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
	НоваяЗапись.Портал 						= СтруктураНастроек.Портал;
	НоваяЗапись.ТипВладельцаСмартПроцесса 	= СтруктураДляЗаписиИдентификатора.ТипВладельцаСмартПроцесса;
	НоваяЗапись.ИдСмартПроцесса 			= СтруктураДляЗаписиИдентификатора.ИдСмартПроцесса;
	НоваяЗапись.ТипСущности1С 				= СтруктураДляЗаписиИдентификатора.ТипСущности1С;
	НоваяЗапись.Сущность1С 					= СтруктураДляЗаписиИдентификатора.Сущность1С;
	НоваяЗапись.ИдентификаторОбъекта 		= XMLСтрока(СтруктураДляЗаписиИдентификатора.Объект);
	НоваяЗапись.Идентификатор 				= СтруктураДляЗаписиИдентификатора.Идентификатор;
	
	НоваяЗапись.Записать(Истина);
	
КонецПроцедуры

Процедура УдалитьИдБ24Объекта(СтруктураНастроек, СтруктураДляЗаписиИдентификатора) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Портал"						, СтруктураНастроек.Портал);
	Запрос.УстановитьПараметр("ТипВладельцаСмартПроцесса"	, СтруктураДляЗаписиИдентификатора.ТипВладельцаСмартПроцесса);
	Запрос.УстановитьПараметр("ИдСмартПроцесса"				, СтруктураДляЗаписиИдентификатора.ИдСмартПроцесса);
	Запрос.УстановитьПараметр("ТипСущности1С"				, СтруктураДляЗаписиИдентификатора.ТипСущности1С);
	Запрос.УстановитьПараметр("Сущность1С"					, СтруктураДляЗаписиИдентификатора.Сущность1С);
	Запрос.УстановитьПараметр("ИдентификаторОбъекта"		, XMLСтрока(СтруктураДляЗаписиИдентификатора.Объект));
	Запрос.Текст = "ВЫБРАТЬ
	|	Б24_СМ_ИдентификаторыОбъектов.Портал КАК Портал,
	|	Б24_СМ_ИдентификаторыОбъектов.ТипВладельцаСмартПроцесса КАК ТипВладельцаСмартПроцесса,
	|	Б24_СМ_ИдентификаторыОбъектов.ИдСмартПроцесса КАК ИдСмартПроцесса,
	|	Б24_СМ_ИдентификаторыОбъектов.ТипСущности1С КАК ТипСущности1С,
	|	Б24_СМ_ИдентификаторыОбъектов.Сущность1С КАК Сущность1С,
	|	Б24_СМ_ИдентификаторыОбъектов.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
	|	Б24_СМ_ИдентификаторыОбъектов.Идентификатор КАК Идентификатор
	|ИЗ
	|	РегистрСведений.Б24_СМ_ИдентификаторыОбъектов КАК Б24_СМ_ИдентификаторыОбъектов
	|ГДЕ
	|	Б24_СМ_ИдентификаторыОбъектов.Портал = &Портал
	|	И Б24_СМ_ИдентификаторыОбъектов.ТипВладельцаСмартПроцесса = &ТипВладельцаСмартПроцесса
	|	И Б24_СМ_ИдентификаторыОбъектов.ИдСмартПроцесса = &ИдСмартПроцесса
	|	И Б24_СМ_ИдентификаторыОбъектов.ТипСущности1С = &ТипСущности1С
	|	И Б24_СМ_ИдентификаторыОбъектов.Сущность1С = &Сущность1С
	|	И Б24_СМ_ИдентификаторыОбъектов.ИдентификаторОбъекта = &ИдентификаторОбъекта";
	ВыполненныйЗапрос = Запрос.Выполнить();
	
	Если НЕ ВыполненныйЗапрос.Пустой() Тогда
		
		Выборка = ВыполненныйЗапрос.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяЗапись = РегистрыСведений.Б24_СМ_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			НоваяЗапись.Прочитать();
			НоваяЗапись.Удалить();
			Б24_К_ОбщегоНазначенияВызовСервера.ДобавитьВЛог(СтруктураНастроек,  СтруктураНастроек.ТипыСообщений.Ошибка, "Удален id для объекта:" + Строка(СтруктураДляЗаписиИдентификатора.Объект));
			
			
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
