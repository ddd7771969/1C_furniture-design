
&НаСервере
Процедура СделкаПриИзмененииНаСервере()
	ПолеВывода.Очистить();
	
	Если НЕ ЗначениеЗаполнено(Проект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МИНИМУМ(ДатыКомплекта.НачалоРКД) КАК НачалоРКД,
		|	ИзделияЗаказчикаПомещения.Помещение КАК Помещение,
		|	ИзделияЗаказчикаПомещения.Помещение.НаименованиеБезКода КАК НаименованиеБезКода,
		|	ИзделияЗаказчикаПомещения.Помещение.НомерПомещения КАК НомерПомещения
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИзделияКомплект.ИзделияЭлемент КАК ИзделияКомплектИзделияЭлемент
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИзделияЗаказчика.ИзделияЭлемент КАК ИзделияЗаказчикаИзделияЭлемент
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИзделияЗаказчика.Помещения КАК ИзделияЗаказчикаПомещения
		|				ПО ИзделияЗаказчикаИзделияЭлемент.Ссылка = ИзделияЗаказчикаПомещения.Ссылка
		|			ПО ИзделияКомплектИзделияЭлемент.ИзделиеЭлемент = ИзделияЗаказчикаИзделияЭлемент.ИзделиеЭлемент
		|		ПО ДатыКомплекта.Комплект = ИзделияКомплектИзделияЭлемент.Ссылка
		|ГДЕ
		|	ДатыКомплекта.Проект = &Проект
		|
		|СГРУППИРОВАТЬ ПО
		|	ИзделияЗаказчикаПомещения.Помещение,
		|	ИзделияЗаказчикаПомещения.Помещение.НаименованиеБезКода,
		|	ИзделияЗаказчикаПомещения.Помещение.НомерПомещения
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерПомещения";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Макет = Отчеты.ОжидаемыеДатыГотовностиПомещений.ПолучитьМакет("МакетОжидаемая");
	ПолеВывода.Вывести(Макет.ПолучитьОбласть("Шапка"));
	
	обСтрока = Макет.ПолучитьОбласть("Строка");
	
	Пока Выборка.Следующий() Цикл
		обСтрока.Параметры.Помещение 	= Выборка.Помещение;
		обСтрока.Параметры.стрПомещение = Строка(Выборка.НомерПомещения) + " " + Выборка.НаименованиеБезКода;
		обСтрока.Параметры.Дата 		= Выборка.НачалоРКД; 
		ПолеВывода.Вывести(обСтрока);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СделкаПриИзменении(Элемент)
	СделкаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	СделкаПриИзмененииНаСервере();
КонецПроцедуры
