&НаСервере
Перем ПустаяДата, СекундВДате;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Период.ДатаНачала = НачалоДня(ТекущаяДата());
	Период.ДатаОкончания = КонецДня(Период.ДатаНачала + 24 * 3600 * 40);
	СформироватьОтчет();
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчет()
	
	СекундВДате = 24 * 3600;
	ПустаяДата = Дата(1,1,1);

	ТекущийОбъект = РеквизитФормыВЗначение("Отчет");
	Макет = ТекущийОбъект.ПолучитьМакет("Макет");

	ПолеВывода.Очистить();

	стДанные = ПолучитьТаблицыДанных();
	КрайниеДаты = Новый Структура("МинДата, МаксДата", ПустаяДата, ПустаяДата);
	СписокКонструкторов = ПолучитьСписокКонструкторов(стДанные.тзДатыКомплектов, КрайниеДаты);
	
	Если КрайниеДаты.МинДата < Период.ДатаНачала Тогда
		КрайниеДаты.МинДата = Период.ДатаНачала;
	КонецЕсли;
	
	Если КрайниеДаты.МаксДата > Период.ДатаОкончания Тогда
		КрайниеДаты.МаксДата = Период.ДатаОкончания;
	КонецЕсли;
	
	ВывестиШапку(стДанные.тзРабочииДни, КрайниеДаты, Макет);
	
	стКонструктор 				= Новый Структура("Конструктор", );
	стОтборДанныеКонструктор 	= Новый Структура("Дата, Комплект, КомплектПредставление", );
	
	ОписаниеБулево = Новый ОписаниеТипов("Булево");
	тзДанныеКонструктор = Новый ТаблицаЗначений;
	тзДанныеКонструктор.Колонки.Добавить("Дата", 					Новый ОписаниеТипов("Дата"));
	тзДанныеКонструктор.Колонки.Добавить("Комплект", 				Новый ОписаниеТипов("СправочникСсылка.ИзделияКомплект"));
	тзДанныеКонструктор.Колонки.Добавить("КомплектПредставление", 	Новый ОписаниеТипов("Строка"));
	тзДанныеКонструктор.Колонки.Добавить("ТЗ", 						ОписаниеБулево);
	тзДанныеКонструктор.Колонки.Добавить("ПКД", 					ОписаниеБулево);
	тзДанныеКонструктор.Колонки.Добавить("РКД", 					ОписаниеБулево);
	тзДанныеКонструктор.Индексы.Добавить("Дата, Комплект"); 
	
	стОбласти = Новый Структура("Конструктор, Дата, ИтогКонструктор, ДатаОшибка, Конструктор1, Дата1, ИтогКонструктор1, ДатаОшибка1"
								,Макет.ПолучитьОбласть("Строка|Конструктор")
								,Макет.ПолучитьОбласть("Строка|Дата")
								,Макет.ПолучитьОбласть("Строка|Итого")
								,Макет.ПолучитьОбласть("Строка|Выходной")
								,Макет.ПолучитьОбласть("Строка1|Конструктор")
								,Макет.ПолучитьОбласть("Строка1|Дата")
								,Макет.ПолучитьОбласть("Строка1|Итого")
								,Макет.ПолучитьОбласть("Строка1|Выходной")
								);
	
								
								
	Для каждого Конструктор Из СписокКонструкторов Цикл
	    стКонструктор.Конструктор = Конструктор.Значение;

		ВывестиКонструктора(стДанные, стКонструктор, стОтборДанныеКонструктор, тзДанныеКонструктор, стОбласти, КрайниеДаты);	
	
	КонецЦикла;

КонецПроцедуры // СформироватьОтчет()

&НаСервере
Процедура ВывестиКонструктора(стДанные, стКонструктор, стОтборДанныеКонструктор, тзДанныеКонструктор, стОбласти, КрайниеДаты)
	
	тзДанныеКонструктор.Очистить();
	НайденыеСтроки = стДанные.тзДатыКомплектов.НайтиСтроки(стКонструктор);
	
	//Колонки таблицы тзДатыКомплектов
	//Комплект, НачалоПКД, НачалоРКД, НачалоТЗ, НормаВремени_ПКД, НормаВремени_РКД, НормаВремени_ТЗ
	//Конструктор, КонструкторПредставление, КомплектПредставление
	
	Для каждого текДанные Из НайденыеСтроки Цикл
		стОтборДанныеКонструктор.Комплект 				= текДанные.Комплект; 
		стОтборДанныеКонструктор.КомплектПредставление 	= текДанные.КомплектПредставление; 
		
		Если НЕ текДанные.НачалоПКД = ПустаяДата Тогда
			стОтборДанныеКонструктор.Дата = текДанные.НачалоПКД;
			Заполнить_тзДанныеКонструктор(тзДанныеКонструктор, текДанные.НормаВремени_ПКД, "ПКД", стОтборДанныеКонструктор, стДанные);
		КонецЕсли;
		
		Если НЕ текДанные.НачалоРКД = ПустаяДата Тогда
			стОтборДанныеКонструктор.Дата = текДанные.НачалоРКД;
			Заполнить_тзДанныеКонструктор(тзДанныеКонструктор, текДанные.НормаВремени_РКД, "РКД", стОтборДанныеКонструктор, стДанные);
		КонецЕсли;
		
		Если НЕ текДанные.НачалоТЗ = ПустаяДата Тогда
			стОтборДанныеКонструктор.Дата = текДанные.НачалоТЗ;
			Заполнить_тзДанныеКонструктор(тзДанныеКонструктор, текДанные.НормаВремени_ТЗ, "ТЗ", стОтборДанныеКонструктор, стДанные);
		КонецЕсли;
		
	КонецЦикла; 
	
	стОбласти.Конструктор1.Параметры.Данные = стКонструктор.Конструктор;
	ПолеВывода.Вывести(стОбласти.Конструктор1);
	
	//Колонки тзДанныеКонструктор
	//Дата, Комплект, ТЗ, ПКД, РКД
	
	ВсегоЗадач = 0;
	стОтборДата = Новый Структура("Дата", );
	стКомплекты = Новый СписокЗначений;
    ВыводДанных(КрайниеДаты, тзДанныеКонструктор, стОтборДата, стОбласти, Истина, стКомплекты, ВсегоЗадач);
	стКомплекты.СортироватьПоПредставлению();	
	
	стОтборДата.Вставить("Комплект",);
	
	Для каждого текКомплект Из стКомплекты Цикл
		стОбласти.Конструктор.Параметры.Данные = текКомплект.Представление;
		ПолеВывода.Вывести(стОбласти.Конструктор);
		стОтборДата.Комплект = текКомплект.Значение;
		ВыводДанных(КрайниеДаты, тзДанныеКонструктор, стОтборДата, стОбласти, Ложь);
	КонецЦикла;
КонецПроцедуры // ВывестиКонструктора(стДанные, Конструктор) 

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ВыводДанных(КрайниеДаты, тзДанныеКонструктор, стОтборДата, стОбласти, ИспользоватьПервыеОбласти, стКомплекты = Неопределено, ВсегоЗадач = Неопределено)
	ТекДата = КрайниеДаты.МинДата;
	Пока ТекДата <= КрайниеДаты.МаксДата Цикл
		стОтборДата.Дата = ТекДата;
		
		КоличествоЗадач = 0;
		
		НайденыеСтроки = тзДанныеКонструктор.НайтиСтроки(стОтборДата); 
		
		
		Для каждого ДатаЗадача Из НайденыеСтроки Цикл
			Если ДатаЗадача.ПКД ИЛИ ДатаЗадача.РКД ИЛИ ДатаЗадача.ТЗ Тогда
				КоличествоЗадач = КоличествоЗадач + 1; 
				Если НЕ стКомплекты = Неопределено Тогда
					Если стКомплекты.НайтиПоЗначению(ДатаЗадача.Комплект) = Неопределено Тогда
						стКомплекты.Добавить(ДатаЗадача.Комплект, ДатаЗадача.КомплектПредставление);	
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ВсегоЗадач = Неопределено Тогда
			ВсегоЗадач = ВсегоЗадач + КоличествоЗадач;
		КонецЕсли;
		
		Если КоличествоЗадач > 1 Тогда
			Если ИспользоватьПервыеОбласти Тогда
				текОбласть = стОбласти.ДатаОшибка1;
			Иначе
				текОбласть = стОбласти.ДатаОшибка;
			КонецЕсли;
		Иначе
			Если ИспользоватьПервыеОбласти Тогда
				текОбласть = стОбласти.Дата1;
			Иначе
				текОбласть = стОбласти.Дата;
			КонецЕсли;
		КонецЕсли;
		
		текОбласть.Параметры.Данные = ?(КоличествоЗадач = 0, "", КоличествоЗадач);
		
		ПолеВывода.Присоединить(текОбласть);
		
		ТекДата 			= ТекДата + СекундВДате;
	КонецЦикла;
КонецПроцедуры // ВыводДанных()



&НаСервере
Процедура Заполнить_тзДанныеКонструктор(тзДанныеКонструктор, НормаВремени, ТипЭтапа, стОтборДанныеКонструктор, стДанные)
	
	НормаВремени = ?(НормаВремени = 0, 1, НормаВремени);
	
	Для х = 1 По НормаВремени Цикл
		НайденыеСтроки = тзДанныеКонструктор.НайтиСтроки(стОтборДанныеКонструктор);
		
		Если НайденыеСтроки.Количество() = 0 Тогда
			НС = тзДанныеКонструктор.Добавить();
			ЗаполнитьЗначенияСвойств(НС, стОтборДанныеКонструктор);
		Иначе
		    НС = НайденыеСтроки[0];
		КонецЕсли;
		
		НС[ТипЭтапа] = Истина;
		
		стОтборДанныеКонструктор.Дата = стОтборДанныеКонструктор.Дата + СекундВДате;
		
		Пока ЭтоВыходной(стДанные.тзРабочииДни, стОтборДанныеКонструктор.Дата) Цикл
			стОтборДанныеКонструктор.Дата = стОтборДанныеКонструктор.Дата + СекундВДате;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // Заполнить_тзДанныеКонструктор(тзДанныеКонструктор, НормаВремени, ТипЭтапа, стОтборДанныеКонструктор, стДанные) 


&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	СформироватьОтчет();
КонецПроцедуры

&НаСервере
Процедура ВывестиШапку(тзРабочииДни, КрайниеДаты, Макет)

	спМесяцы = Новый СписокЗначений;
	омМесяц.ЗаполнитьСписокМесяцами(спМесяцы);
	
	ПолеВывода.Вывести(Макет.ПолучитьОбласть("Заголовок|Конструктор"));	
	
	ТекДата = КрайниеДаты.МинДата;
	ТекущаяКолонка 		= 3;
	КолонкаНачалаМесяца = ТекущаяКолонка;
	ТекущийМесяц		= 0;
	
	ОбластьДата 	= Макет.ПолучитьОбласть("Заголовок|Дата");
	ОбластьВыходной = Макет.ПолучитьОбласть("Заголовок|Выходной");
	
	стПараметровОбласти = омТабличныйДокументКлиентСервер.ПолучитьСтруктуруПараметров();
	стПараметровОбласти.НомерСтроки 	= 2;
	стПараметровОбласти.Полужирный 		= Истина;

	ЕстьДатаБезМесяца = Ложь;	
	
	Пока ТекДата <= КрайниеДаты.МаксДата Цикл
		НомерМесяца = Месяц(ТекДата);
		ЕстьДатаБезМесяца = Истина;	
		Если НЕ НомерМесяца = ТекущийМесяц Тогда
			Если НЕ ТекущийМесяц = 0 Тогда
				стПараметровОбласти.НомерКолонки 	= КолонкаНачалаМесяца;	
				стПараметровОбласти.ДлинаОбласти 	= ТекущаяКолонка - КолонкаНачалаМесяца;
				стПараметровОбласти.Текст 			= спМесяцы.НайтиПоЗначению(ТекущийМесяц).Представление; 
				омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти);
				
				КолонкаНачалаМесяца = ТекущаяКолонка;
			КонецЕсли;
			ТекущийМесяц = НомерМесяца;
			ЕстьДатаБезМесяца = Ложь;	
		КонецЕсли;
		
		Если  ЭтоВыходной(тзРабочииДни, ТекДата) Тогда
			ТекущаяОбласть = ОбластьВыходной;
		Иначе	
			ТекущаяОбласть = ОбластьДата;
		КонецЕсли;
		
		ТекущаяОбласть.Параметры.Дата = День(ТекДата);
		
		ПолеВывода.Присоединить(ТекущаяОбласть);
		
	   ТекущаяКолонка 	= ТекущаяКолонка + 1;
	   ТекДата 			= ТекДата + СекундВДате;
   КонецЦикла; 
   
   Если ЕстьДатаБезМесяца Тогда
	   стПараметровОбласти.НомерКолонки 	= КолонкаНачалаМесяца;	
	   стПараметровОбласти.ДлинаОбласти 	= ТекущаяКолонка - КолонкаНачалаМесяца;
	   стПараметровОбласти.Текст 			= спМесяцы.НайтиПоЗначению(НомерМесяца).Представление; 
	   омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти);
   КонецЕсли;
КонецПроцедуры // ВыестиШапку()

&НаСервере
Функция ПолучитьТаблицыДанных()

	тзРабочииДни = омДаты.ПолучитьРабочиеДни(Период.ДатаНачала, Период.ДатаОкончания);
	
	тзДатыКомплектов = ПолучитьДатыКомплектов(); 
	тзДатыКомплектов.Индексы.Добавить("Конструктор");
	
	Возврат Новый Структура("тзРабочииДни, тзДатыКомплектов", тзРабочииДни, тзДатыКомплектов);
	
КонецФункции // ПолучитьТаблицыДанных()

&НаСервере
Функция ПолучитьДатыКомплектов()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплекта.Комплект КАК Комплект,
		|	ДатыКомплекта.НачалоПКД КАК НачалоПКД,
		|	ДатыКомплекта.НачалоРКД КАК НачалоРКД,
		|	ДатыКомплекта.НачалоТЗ КАК НачалоТЗ,
		|	ЕСТЬNULL(СрокиСделкиКомплекты.НормаВремени_ПКД, 1) КАК НормаВремени_ПКД,
		|	ЕСТЬNULL(СрокиСделкиКомплекты.НормаВремени_РКД, 1) КАК НормаВремени_РКД,
		|	ЕСТЬNULL(СрокиСделкиКомплекты.НормаВремени_ТЗ, 1) КАК НормаВремени_ТЗ
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗапускВРаботу.Комплекты КАК СрокиСделкиКомплекты
		|		ПО ДатыКомплекта.Комплект = СрокиСделкиКомплекты.Комплект
		|ГДЕ
		|	(ДатыКомплекта.НачалоПКД МЕЖДУ &ДатаН И &ДатаК
		|			ИЛИ ДатыКомплекта.НачалоРКД МЕЖДУ &ДатаН И &ДатаК
		|			ИЛИ ДатыКомплекта.НачалоТЗ МЕЖДУ &ДатаН И &ДатаК)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.Комплект КАК Комплект,
		|	вт.НачалоПКД КАК НачалоПКД,
		|	вт.НачалоРКД КАК НачалоРКД,
		|	вт.НачалоТЗ КАК НачалоТЗ,
		|	ВЫБОР
		|		КОГДА вт.НормаВремени_ПКД = 0
		|			ТОГДА 1
		|		ИНАЧЕ вт.НормаВремени_ПКД
		|	КОНЕЦ КАК НормаВремени_ПКД,
		|	ВЫБОР
		|		КОГДА вт.НормаВремени_РКД = 0
		|			ТОГДА 1
		|		ИНАЧЕ вт.НормаВремени_РКД
		|	КОНЕЦ КАК НормаВремени_РКД,
		|	ВЫБОР
		|		КОГДА вт.НормаВремени_ТЗ = 0
		|			ТОГДА 1
		|		ИНАЧЕ вт.НормаВремени_ТЗ
		|	КОНЕЦ КАК НормаВремени_ТЗ,
		|	вт.Комплект.Конструктор КАК Конструктор,
		|	вт.Комплект.Конструктор.Представление КАК КонструкторПредставление,
		|	вт.Комплект.Представление КАК КомплектПредставление
		|ИЗ
		|	вт КАК вт";
	
	Запрос.УстановитьПараметр("ДатаН", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаК", Период.ДатаОкончания);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции // ПолучитьДатыКомплектов()

&НаСервере
Функция ЭтоВыходной(тзРабочииДни, ТекДата)

	НайденоеЗначение = тзРабочииДни.Найти(ТекДата, "Дата");
	
	Если НайденоеЗначение = Неопределено Тогда
	
		Возврат Ложь;	
	
	КонецЕсли;
	
	Возврат НайденоеЗначение.РабочиеЧасы = 0;

КонецФункции // ЭтоВыхдной()

&НаСервере
Функция ПолучитьСписокКонструкторов(тзДатыКомплектов, КрайниеДаты)

	СписокКонструкторов = Новый СписокЗначений;
	
	Для каждого х Из тзДатыКомплектов Цикл
		
		Если НЕ ЗначениеЗаполнено(х.Конструктор) Тогда
		
			Продолжить;	
		
		КонецЕсли;
		
		Если СписокКонструкторов.НайтиПоЗначению(х.Конструктор) = Неопределено Тогда
			СписокКонструкторов.Добавить(х.Конструктор, х.КонструкторПредставление);
		КонецЕсли;
		
		Если х.НачалоПКД > ПустаяДата Тогда 
			Если КрайниеДаты.МинДата = ПустаяДата Тогда
				КрайниеДаты.МинДата = х.НачалоПКД;
			ИначеЕсли х.НачалоПКД < КрайниеДаты.МинДата Тогда
				КрайниеДаты.МинДата = х.НачалоПКД;
			КонецЕсли;
			
		    Если КрайниеДаты.МаксДата = ПустаяДата Тогда
				КрайниеДаты.МаксДата = х.НачалоПКД; 
			КонецЕсли;
			
		    Если КрайниеДаты.МаксДата < х.НачалоПКД Тогда
				КрайниеДаты.МаксДата = х.НачалоПКД; 
			КонецЕсли;
			
		КонецЕсли;
	
		Если х.НачалоРКД > ПустаяДата Тогда
			Если КрайниеДаты.МинДата = ПустаяДата Тогда
				КрайниеДаты.МинДата = х.НачалоРКД;
			ИначеЕсли х.НачалоРКД < КрайниеДаты.МинДата Тогда
				КрайниеДаты.МинДата = х.НачалоРКД;
			КонецЕсли;
			
		    Если КрайниеДаты.МаксДата = ПустаяДата Тогда
				КрайниеДаты.МаксДата = х.НачалоРКД; 
			КонецЕсли;
			
		    Если КрайниеДаты.МаксДата < х.НачалоРКД Тогда
				КрайниеДаты.МаксДата = х.НачалоРКД; 
			КонецЕсли;
			
		КонецЕсли;
	
		Если х.НачалоТЗ > ПустаяДата Тогда
			Если КрайниеДаты.МинДата = ПустаяДата Тогда
				КрайниеДаты.МинДата = х.НачалоТЗ;
			ИначеЕсли х.НачалоТЗ < КрайниеДаты.МинДата Тогда
				КрайниеДаты.МинДата = х.НачалоТЗ;
			КонецЕсли;
			
		    Если КрайниеДаты.МаксДата = ПустаяДата Тогда
				КрайниеДаты.МаксДата = х.НачалоТЗ; 
			КонецЕсли;
			
		    Если КрайниеДаты.МаксДата < х.НачалоПКД Тогда
				КрайниеДаты.МаксДата = х.НачалоТЗ; 
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;
	
    СписокКонструкторов.СортироватьПоПредставлению();
	
	Возврат СписокКонструкторов;
КонецФункции // ПолучитьСписокКонструкторов()

&НаКлиенте
Процедура Обновить(Команда)
	СформироватьОтчет();
КонецПроцедуры
