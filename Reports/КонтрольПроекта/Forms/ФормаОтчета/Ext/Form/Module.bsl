
#Область ПостроениеяДиаграммы

Процедура ПостроитьДиаграммуГанта() 
	
	стНаименованиеЭтапов = омДиаграммаГантаКлиентСервер.ПолучитНаименованиеЭтапов();
	
	спСделки = Новый СписокЗначений;
	ЗаполнитьЗадачиДоговора(спСделки);
	ИнтервалыСделок.Загрузить(омДиаграммаГанта.ПолучитьИнтервалДиаграммы(спСделки, тзДатыОкончания));
	
	
	тзЦвета = омДиаграммаГанта.ПолучитьТаблицуЦветов();
	
	Серия = Диаграмма.УстановитьСерию("Комплекты");

	ПустаяДата = Дата(1,1,1);
	
	стОтбор 				= Новый Структура("Комплект, 	Проект", );
	стОтборКомпозиция		= Новый Структура("Композиция, Проект", );
	стОтборПроект 			= Новый Структура("Проект", );
	мКомпозицииВыведенные 	= Новый Массив;
	мСтрок 					= Новый Массив(1);
	
	
	Для каждого ЭлементПроект Из спСделки Цикл
		
		СсылкаПроект 				= ЭлементПроект.Значение;
		стОтбор.Проект 				= СсылкаПроект;
		стОтборПроект.Проект 		= СсылкаПроект;
		стОтборКомпозиция.Проект 	= СсылкаПроект;
		
		ПроектТочка = ВывестиСделку(ИнтервалыСделок, ЭлементПроект, Серия, тзЦвета);
		
		Для каждого строкаКомплкет Из тзКомплекты.НайтиСтроки(стОтборПроект) Цикл
			стТочкаКомплект = ВывестиКомплект(строкаКомплкет, стОтбор, стОтборКомпозиция, Серия, тзЦвета, стНаименованиеЭтапов, мКомпозицииВыведенные, мСтрок);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ВывестиКомплект(СтрокаКомплкет, стОтбор, стОтборКомпозиция, Серия, тзЦвета, стНаименованиеЭтапов, мКомпозицииВыведенные, мСтрок)
	
	НаименованиеКомплект 	= СтрокаКомплкет.КомплектПредставление;
	СсылкаКомплект 			= СтрокаКомплкет.Комплект;
	
	Если ЗначениеЗаполнено(СтрокаКомплкет.Родитель) Тогда
		
		РодительДиаграммы 		= СтрокаКомплкет.Родитель;
		
		КомпозицияТочка 		= Диаграмма.УстановитьТочку(СтрокаКомплкет.Родитель, стОтбор.Проект);
		КомпозицияТочка.Текст 	= Строка(СтрокаКомплкет.Родитель);
		
		Если ЗначениеЗаполнено(СтрокаКомплкет.Композиция) И мКомпозицииВыведенные.Найти(СтрокаКомплкет.Композиция) = Неопределено Тогда
			
			стОтборКомпозиция.Композиция 	= СтрокаКомплкет.Композиция;
			НайденыеСтроки 					= ТЗЗадачи.НайтиСтроки(стОтборКомпозиция); 
			
			Если НайденыеСтроки.Количество() > 0 Тогда
				
				мСтрок[0] = НайденыеСтроки[0];
				мСтрок[0].Комплект = СтрокаКомплкет.Композиция; 
				
				ВывестиИнтервалыКомплекта(КомпозицияТочка, мСтрок, Серия, тзЦвета, стНаименованиеЭтапов.мЭтапыКонструктора);
				
			КонецЕсли;
			
			мКомпозицииВыведенные.Добавить(СтрокаКомплкет.Композиция);
			
		КонецЕсли; 
		
	Иначе
		РодительДиаграммы = стОтбор.Проект;
	КонецЕсли;
	
	стОтбор.Комплект 	= СсылкаКомплект;
	НайденыеСтроки 		= ТЗЗадачи.НайтиСтроки(стОтбор);	
	
	КомплектТочка 			= Диаграмма.УстановитьТочку(СсылкаКомплект, РодительДиаграммы);
	КомплектТочка.Текст 	= НаименованиеКомплект;
	
	Подписать = Ложь;
	Если НЕ НайденыеСтроки.Количество() = 0 Тогда
		Подписать = ВывестиИнтервалыКомплекта(КомплектТочка, НайденыеСтроки, Серия, тзЦвета, стНаименованиеЭтапов.мЭтапыКонструктора);
		ВывестиДанныеПроизводстваМонтажаКомплекта(КомплектТочка, НайденыеСтроки,стНаименованиеЭтапов.мЭтапыПроизводства, Серия, тзЦвета, "Производство"); 
		ВывестиДанныеПроизводстваМонтажаКомплекта(КомплектТочка, НайденыеСтроки,стНаименованиеЭтапов.мЭтапыМонтаж, Серия, тзЦвета, "Монтаж"); 
		ВывестиПодчиненыеКомплекты(СсылкаКомплект, НайденыеСтроки, Серия, Подписать, стНаименованиеЭтапов, тзЦвета);
	КонецЕсли;
	КомплектТочка.Картинка 	= ?(Подписать, БиблиотекаКартинок.ДобавитьВИзбранное, БиблиотекаКартинок.БыстрыйДоступДобавить);
	
	ЦветКрайнейТочки = омДиаграммаГанта.ПолучитьЦвет("КрайняяДата", тзЦвета);
	ВывестиКрайниеТочки(СсылкаКомплект, КомплектТочка, Серия, ЦветКрайнейТочки);
	
	Возврат Новый Структура("НайденыеСтроки,КомплектТочка", НайденыеСтроки, КомплектТочка);
КонецФункции // ВывестиСделку()


Процедура ВывестиКрайниеТочки(СсылкаКомплект,КомплектТочка,Серия,ЦветКрайнейТочки)
	стОтбор = Новый Структура("ИзделиеКомплект", СсылкаКомплект);
	НайденыеСтроки = тзДатыОкончания.НайтиСтроки(стОтбор);
	Если НайденыеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	ПочтиСуткиВСекундах = 24 * 3600; //Ширина крайней точки

	ДанныеДатыЭтап = Новый Структура("ДатаНачалаПлан,ДатаОкончанияПлан,Этап",НайденыеСтроки[0].ОкончаниеПроизводства,НайденыеСтроки[0].ОкончаниеПроизводства + ПочтиСуткиВСекундах,"Окончание производства");
	
	ДанныеДатыЭтап.ДатаНачалаПлан 		= НайденыеСтроки[0].ОкончаниеМонтаж;
	ДанныеДатыЭтап.ДатаОкончанияПлан 	= ДанныеДатыЭтап.ДатаНачалаПлан + ПочтиСуткиВСекундах;
	ДанныеДатыЭтап.Этап 				= "Окончание монтаж";
	ВывестиТочку(КомплектТочка, Серия, ДанныеДатыЭтап, Неопределено, Неопределено,Ложь,Ложь,ЦветКрайнейТочки);
	
	ДанныеДатыЭтап.ДатаНачалаПлан 		= НайденыеСтроки[0].ОкончаниеСЗапасом;
	ДанныеДатыЭтап.ДатаОкончанияПлан 	= ДанныеДатыЭтап.ДатаНачалаПлан + ПочтиСуткиВСекундах;
	ДанныеДатыЭтап.Этап 				= "Окончание с запасом";
	ВывестиТочку(КомплектТочка, Серия, ДанныеДатыЭтап, Неопределено, Неопределено,Ложь,Ложь,ЦветКрайнейТочки,Ложь,Ложь);
	
КонецПроцедуры // ВывестиКрайниеТочки(СтрокаКомплкет,Серия,тзЦвета)

Функция ВывестиПодчиненыеКомплекты(СсылкаКомплект, данныеИнтервала, Серия, Подписать, стНаименованиеЭтапов, тзЦвета)
	
	Возврат 0;
	
	ПустаяДата = Дата(1,1,1);
	
	Для каждого текИнтервал Из данныеИнтервала Цикл
		//Если НЕ ЗначениеЗаполнено(текИнтервал.ПодчиненыйКомплект) Тогда
		//	Продолжить;
		//КонецЕсли;
		
		Если ЗначениеЗаполнено(текИнтервал.ПроизводственныйКомплект) Тогда
			ПодчиненыйКомплектТочка 			= Диаграмма.УстановитьТочку(текИнтервал.ПроизводственныйКомплект,СсылкаКомплект);
		Иначе
			ПодчиненыйКомплектТочка 			= Диаграмма.УстановитьТочку(текИнтервал.ПодчиненыйКомплект,СсылкаКомплект);
		КонецЕсли;
		
		ПодчиненыйКомплектТочка.Текст 		= текИнтервал.ПодчиненыйКомплект;
		ПодчиненыйКомплектТочка.Картинка 	= ?(ТипЗнч(текИнтервал.ПодчиненыйКомплект) = Тип("СправочникСсылка.МонтажныеКомплекты")
												,БиблиотекаКартинок.МеткаЗеленая, БиблиотекаКартинок.МеткаКрасная); 
		ПодчиненыйКомплектТочка.Картинка 	= БиблиотекаКартинок.МеткаКрасная; 

		Если НЕ стНаименованиеЭтапов.мЭтапыПроизводства.Найти(текИнтервал.Этап) = Неопределено Тогда
			ТекущийЦвет			= омДиаграммаГанта.ПолучитьЦвет("Производство", тзЦвета);
		ИначеЕсли НЕ стНаименованиеЭтапов.мЭтапыМонтаж.Найти(текИнтервал.Этап) = Неопределено Тогда
			ТекущийЦвет			= омДиаграммаГанта.ПолучитьЦвет("Монтаж", тзЦвета);
		КонецЕсли;
		
		ВывестиТочку(ПодчиненыйКомплектТочка, Серия, текИнтервал, текИнтервал.ПодчиненыйКомплект,, Подписать,,ТекущийЦвет,,Истина);
		
	КонецЦикла; 
	
	
КонецФункции // ПолучитьДанныеПроизводстваКомплекта(НайденыеСтроки,стНаименованиеЭтапов.мЭтапыПроизводства)

Функция ВывестиДанныеПроизводстваМонтажаКомплекта(КомплектТочка, данныеИнтервала, мЭтапыПроизводства, Серия, тзЦвета, НаименованиеЭтапа)
	
	ПустаяДата = Дата(1,1,1);
	
	стДанныеКомплекта = Новый Структура("Проект,ДатаНачалаПлан,ДатаОкончанияПлан,Этап",Справочники.Проекты.ПустаяСсылка(),Дата(1,1,1),Дата(1,1,1),НаименованиеЭтапа);
	
	Для каждого текИнтервал Из данныеИнтервала Цикл
		
		Если мЭтапыПроизводства.Найти(текИнтервал.Этап) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если текИнтервал.ДатаОкончанияПлан > стДанныеКомплекта.ДатаОкончанияПлан  Тогда
			стДанныеКомплекта.ДатаОкончанияПлан = текИнтервал.ДатаОкончанияПлан;
		КонецЕсли;
		
		Если НЕ текИнтервал.ДатаНачалаПлан = ПустаяДата Тогда
			Если стДанныеКомплекта.ДатаНачалаПлан = ПустаяДата Тогда
				стДанныеКомплекта.ДатаНачалаПлан = текИнтервал.ДатаНачалаПлан;
			Иначе	
				стДанныеКомплекта.ДатаНачалаПлан = Мин(текИнтервал.ДатаНачалаПлан, стДанныеКомплекта.ДатаНачалаПлан);
			КонецЕсли;
		КонецЕсли;
		
		стДанныеКомплекта.Проект = текИнтервал.Проект; 
		
	КонецЦикла; 
	
	ВывестиТочку(КомплектТочка, Серия, стДанныеКомплекта, текИнтервал.Комплект, тзЦвета,, Истина,,Ложь,Ложь);
	
КонецФункции // ПолучитьДанныеПроизводстваКомплекта(КомплектТочка, данныеИнтервала, мЭтапыПроизводства, Серия, тзЦвета, НаименованиеЭтапа)

Функция ВывестиИнтервалыКомплекта(КомплектТочка, НайденыеСтроки, Серия, тзЦвета, мНаименованиеЭтапов)
	Подписать = Ложь;
	
	Для каждого данныеИнтервала Из НайденыеСтроки Цикл
		Если мНаименованиеЭтапов.Найти(данныеИнтервала.Этап) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СсылкаОбъект = ?(ЗначениеЗаполнено(данныеИнтервала.Комплект), данныеИнтервала.Комплект, данныеИнтервала.Композиция);
		
		ВывестиТочку(КомплектТочка, Серия, данныеИнтервала, СсылкаОбъект, тзЦвета, НЕ данныеИнтервала.Подписать);
		Подписать = Подписать ИЛИ данныеИнтервала.Подписать;
	КонецЦикла;
	
	Возврат Подписать; 
КонецФункции // ВывестиИнтервалыКоструктор(КомплектТочка, НайденыеСтроки, Серия, тзЦвета)

Функция ВывестиСделку(ИнтервалыСделок, ЭлементСделка, Серия, тзЦвета)
	
	СсылкаСделка = ЭлементСделка.Значение; 
	
	СделкаТочка 			= Диаграмма.Точки.Добавить();
	СделкаТочка.Текст 		= ЭлементСделка.Представление;
	СделкаТочка.Значение 	= СсылкаСделка;
	СделкаТочка.Картинка 	= БиблиотекаКартинок.СводнаяДиаграмма;
	
	стОтбор = Новый Структура("Проект", СсылкаСделка);
	НайденыеСтроки = ИнтервалыСделок.НайтиСтроки(стОтбор); 
	Если НайденыеСтроки.Количество() = 0 Тогда
    	Возврат Неопределено;
	КонецЕсли;
	
	НайденаяСтрока = НайденыеСтроки[0];
	Если НайденаяСтрока = Неопределено Тогда
		стДанные = Новый Структура("Проект,Этап,ДатаНачалаПлан,ДатаОкончанияПлан",Справочники.Проекты.ПустаяСсылка(),ЭлементСделка.Представление,Дата(1,1,1),Дата(1,1,1));
	Иначе
		стДанные = Новый Структура("Проект,Этап,ДатаНачалаПлан,ДатаОкончанияПлан",НайденаяСтрока.Проект, "Проект " + ЭлементСделка.Представление,НайденаяСтрока.ДатаНачала,НайденаяСтрока.ДатаОкончания);
	КонецЕсли;
	
	ВывестиТочку(СделкаТочка, Серия, стДанные, СсылкаСделка, тзЦвета, Ложь);
	
	Возврат СделкаТочка;
КонецФункции // ВывестиСделку()

Функция ВывестиТочку(Точка, Серия, ДанныеДатыЭтап, СсылкаЗначение, тзЦвета, Редактирование = Истина, ЕстьРасшифровка = Истина
		, ТекущийЦвет = Неопределено, ЭтоГруппа = Ложь, СократитьНаименование = Ложь)

	ПустаяДата = Дата(1, 1, 1);
	Если ДанныеДатыЭтап.ДатаНачалаПлан = ПустаяДата Тогда
	
		Возврат Неопределено;	
	
	КонецЕсли;
		
	Значение = Диаграмма.ПолучитьЗначение(Точка, Серия);
	Значение.Редактирование = Истина;//Редактирование;
	
	Интервал 				= Значение.Добавить();
	Интервал.Начало 		= ДанныеДатыЭтап.ДатаНачалаПлан;
	Интервал.Конец 			= ?(ДанныеДатыЭтап.ДатаОкончанияПлан - ДанныеДатыЭтап.ДатаНачалаПлан < 24 * 3600, ДанныеДатыЭтап.ДатаНачалаПлан + 24 * 3600, ДанныеДатыЭтап.ДатаОкончанияПлан);
	Интервал.Текст 			= ?(ДанныеДатыЭтап.Этап = "Производство", "Пр", ДанныеДатыЭтап.Этап);
	Если НЕ ТекущийЦвет = Неопределено Тогда
		Интервал.Цвет			= ТекущийЦвет;
	ИначеЕсли НЕ тзЦвета = Неопределено Тогда
		Интервал.Цвет			= омДиаграммаГанта.ПолучитьЦвет(ДанныеДатыЭтап.Этап, тзЦвета, ЭтоГруппа, СократитьНаименование);
	КонецЕсли;
	Если ЕстьРасшифровка Тогда
		Интервал.Расшифровка 	= Новый Структура("Проект,Этап,СсылкаКомплект", ДанныеДатыЭтап.Проект, ДанныеДатыЭтап.Этап, СсылкаЗначение);
	Иначе
		Интервал.Расшифровка 	= Неопределено;
	КонецЕсли;
	
	Если СократитьНаименование Тогда
		Интервал.Текст = Лев(Интервал.Текст, 1);
	КонецЕсли;
	
	УстановитьТекстЗначения(Значение.Текст, ДанныеДатыЭтап.ДатаНачалаПлан, ДанныеДатыЭтап.ДатаОкончанияПлан, ДанныеДатыЭтап.Этап); 
	
	Возврат Интервал;
КонецФункции // ВывестиТочку()

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекстЗначения(Текст, ДатаНачала, ДатаОкончания, Этап)
	Текст = Этап + " " + Формат(ДатаНачала, "ДФ=dd.MM.yy") + " - " + Формат(ДатаОкончания, "ДФ=dd.MM.yy");	
КонецПроцедуры

#КонецОбласти 


#Область РасчетДанныхДиаграммы

Процедура ЗаполнитьЗадачиДоговора(спСделки)   	

	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ДОБАВИТЬКДАТЕ(ПриложенияКДоговорамИзделиеЗаказчика.Ссылка.ДатаДоговора, НЕДЕЛЯ, ПриложенияКДоговорамИзделиеЗаказчика.Ссылка.СрокПроизводстваВНеделях)) КАК КонецДатыПроизводства,
	|	МАКСИМУМ(ДОБАВИТЬКДАТЕ(ПриложенияКДоговорамИзделиеЗаказчика.Ссылка.ДатаДоговора, НЕДЕЛЯ, ПриложенияКДоговорамИзделиеЗаказчика.Ссылка.СрокПроизводстваВНеделях + ПриложенияКДоговорамИзделиеЗаказчика.Ссылка.СрокМонтажаВНеделях)) КАК КонецДатыМонтажа,
	|	МАКСИМУМ(ДОБАВИТЬКДАТЕ(ПриложенияКДоговорамИзделиеЗаказчика.Ссылка.ДатаДоговора, НЕДЕЛЯ, ПриложенияКДоговорамИзделиеЗаказчика.Ссылка.СрокПроизводстваВНеделях + ПриложенияКДоговорамИзделиеЗаказчика.Ссылка.СрокМонтажаВНеделях + ПриложенияКДоговорамИзделиеЗаказчика.Ссылка.ВнутреннийЗапасВНеделях)) КАК КонецДатыЗапаса,
	|	ИзделияКомплектИзделияЭлемент.Ссылка КАК Комплект
	|ПОМЕСТИТЬ втКомплект
	|ИЗ
	|	Справочник.ПриложенияКДоговорам.ИзделияЗаказчика КАК ПриложенияКДоговорамИзделиеЗаказчика
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИзделияЗаказчика.ИзделияЭлемент КАК ИзделияЗаказчикаИзделияЭлемент
	|		ПО ПриложенияКДоговорамИзделиеЗаказчика.ИзделиеЗаказчика = ИзделияЗаказчикаИзделияЭлемент.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИзделияКомплект.ИзделияЭлемент КАК ИзделияКомплектИзделияЭлемент
	|		ПО (ИзделияЗаказчикаИзделияЭлемент.ИзделиеЭлемент = ИзделияКомплектИзделияЭлемент.ИзделиеЭлемент)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
	|				ТОГДА НЕ ПриложенияКДоговорамИзделиеЗаказчика.Ссылка.Проект.НеВыводитьНаДиаграмму
	|			ИНАЧЕ ПриложенияКДоговорамИзделиеЗаказчика.Ссылка.Проект = &Проект
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &Конструктор = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ИзделияКомплектИзделияЭлемент.Ссылка.Конструктор = &Конструктор
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзделияКомплектИзделияЭлемент.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыКомплекта.КрайПКД КАК КрайПКД,
	|	ДатыКомплекта.КрайРКД КАК КрайРКД,
	|	ДатыКомплекта.НачалоПКД КАК НачалоПКД,
	|	ДатыКомплекта.НачалоРКД КАК НачалоРКД,
	|	ДатыКомплекта.Комплект КАК Комплект,
	|	ДатыКомплекта.Комплект.Наименование КАК КомплектПредставление,
	|	СрокиСделкиКомплекты.Ссылка.Проект КАК Проект,
	|	СрокиСделкиКомплекты.Ссылка.Проект.Наименование КАК СделкаНаименование,
	|	ДатыКомплекта.ДнейПКД КАК ДнейПКД,
	|	ДатыКомплекта.ДнейРКД КАК ДнейРКД,
	|	ДатыКомплекта.ДнейТЗ КАК ДнейТЗ,
	|	ДатыКомплекта.Проект.Номер КАК СделкаНомер,
	|	ДатыКомплекта.Комплект.Родитель КАК Родитель,
	|	ДатыКомплекта.Комплект.Конструктор КАК Конструктор,
	|	ДатыКомплекта.Комплект.Родитель.Композиция КАК Композиция,
	|	ДатыКомплекта.КрайТЗ КАК КрайТЗ,
	|	ДатыКомплекта.НачалоТЗ КАК НачалоТЗ
	|ИЗ
	|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗапускВРаботу.Комплекты КАК СрокиСделкиКомплекты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКомплект КАК втКомплект
	|			ПО (втКомплект.Комплект = СрокиСделкиКомплекты.Комплект)
	|		ПО ДатыКомплекта.Комплект = СрокиСделкиКомплекты.Комплект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомпозиций
	|		ПО ДатыКомплекта.Комплект.Родитель.Композиция = ДатыКомпозиций.Комплект
	|			И (НЕ ДатыКомплекта.Комплект.Родитель.Композиция ЕСТЬ NULL)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
	|				ТОГДА НЕ ДатыКомплекта.Комплект.Проект.НеВыводитьНаДиаграмму
	|			ИНАЧЕ ДатыКомплекта.Проект = &Проект
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрокиСделкиПроизводствоКомплекты.Ссылка.Проект КАК Проект,
	|	ДатыКомплектаПроизводство.Комплект КАК Комплект,
	|	ДатыКомплектаПроизводство.НачалоПроизводства КАК НачалоПроизводства,
	|	ДатыКомплектаПроизводство.КрайСтолярка КАК КрайСтолярка,
	|	ДатыКомплектаПроизводство.НачалоСтолярка КАК НачалоСтолярка,
	|	ДатыКомплектаПроизводство.КрайМалярка КАК КрайМалярка,
	|	ДатыКомплектаПроизводство.НачалоМалярка КАК НачалоМалярка,
	|	ДатыКомплектаПроизводство.КрайПодрядчик КАК КрайПодрядчик,
	|	ДатыКомплектаПроизводство.НачалоПодрядчик КАК НачалоПодрядчик,
	|	ДатыКомплектаПроизводство.КрайПроизводства КАК КрайПроизводства,
	|	СрокиСделкиПроизводствоКомплекты.Ссылка.Проект.Наименование КАК СделкаНаименование,
	|	ДатыКомплектаПроизводство.Комплект.Наименование КАК КомплектПредставление,
	|	ДатыКомплектаПроизводство.Комплект КАК ИзделиеКомплект,
	|	СрокиСделкиПроизводствоКомплекты.НормаВремени_столярка КАК НормаВремени_столярка,
	|	СрокиСделкиПроизводствоКомплекты.НормаВремени_малярка КАК НормаВремени_малярка,
	|	СрокиСделкиПроизводствоКомплекты.НормаВремени_подрядчик КАК НормаВремени_подрядчик,
	|	СрокиСделкиПроизводствоКомплекты.Ссылка.Проект.Номер КАК СделкаНомер,
	|	ДатыКомплектаПроизводство.НормаВремениПроизводства КАК НормаВремениПроизводства,
	|	ДатыКомплектаПроизводство.Комплект.Родитель КАК Родитель,
	|	ДатыКомплектаПроизводство.Комплект.Конструктор КАК Конструктор
	|ИЗ
	|	РегистрСведений.ДатыКомплектаПроизводство КАК ДатыКомплектаПроизводство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СрокиПроектаПроизводство.Комплекты КАК СрокиСделкиПроизводствоКомплекты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКомплект КАК втКомплект
	|			ПО (втКомплект.Комплект = СрокиСделкиПроизводствоКомплекты.Комплект)
	|		ПО ДатыКомплектаПроизводство.Комплект = СрокиСделкиПроизводствоКомплекты.Комплект
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
	|				ТОГДА НЕ ДатыКомплектаПроизводство.Комплект.Проект.НеВыводитьНаДиаграмму
	|			ИНАЧЕ ДатыКомплектаПроизводство.Проект = &Проект
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрокиСделкиМонтажКомплекты.Ссылка.Проект КАК Проект,
	|	ДатыКомплектаМонтаж.Комплект КАК Комплект,
	|	ДатыКомплектаМонтаж.КрайМонтажа КАК КрайМонтажа,
	|	ДатыКомплектаМонтаж.НачалоМонтажа КАК НачалоМонтажа,
	|	ДатыКомплектаМонтаж.Комплект.Наименование КАК КомплектПредставление,
	|	СрокиСделкиМонтажКомплекты.Ссылка.Проект.Наименование КАК СделкаНаименование,
	|	СрокиСделкиМонтажКомплекты.Ссылка.Проект.Номер КАК СделкаНомер,
	|	ДатыКомплектаМонтаж.НормаВремениМонтажа КАК НормаВремениМонтажа,
	|	ДатыКомплектаМонтаж.КоличествоСотрудниковМонтаж КАК КоличествоСотрудниковМонтаж,
	|	ДатыКомплектаМонтаж.Комплект.Родитель КАК Родитель,
	|	ДатыКомплектаМонтаж.Комплект.Конструктор КАК Конструктор
	|ИЗ
	|	РегистрСведений.ДатыКомплектаМонтаж КАК ДатыКомплектаМонтаж
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СрокиПроектаМонтаж.Комплекты КАК СрокиСделкиМонтажКомплекты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКомплект КАК втКомплект
	|			ПО (втКомплект.Комплект = СрокиСделкиМонтажКомплекты.Комплект)
	|		ПО ДатыКомплектаМонтаж.Комплект = СрокиСделкиМонтажКомплекты.Комплект
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
	|				ТОГДА НЕ ДатыКомплектаМонтаж.Комплект.Проект.НеВыводитьНаДиаграмму
	|			ИНАЧЕ ДатыКомплектаМонтаж.Проект = &Проект
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втКомплект.КонецДатыПроизводства КАК КонецДатыПроизводства,
	|	втКомплект.КонецДатыМонтажа КАК КонецДатыМонтажа,
	|	втКомплект.КонецДатыЗапаса КАК КонецДатыЗапаса,
	|	втКомплект.Комплект КАК Комплект,
	|	втКомплект.Комплект.Родитель КАК КРодитель,
	|	втКомплект.Комплект.Конструктор КАК Конструктор
	|ИЗ
	|	втКомплект КАК втКомплект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОконачаниеДоговораСрезПоследних.ИзделияКомплект КАК ИзделиеКомплект,
	|	ОконачаниеДоговораСрезПоследних.ОкончаниеПроизводства КАК ОкончаниеПроизводства,
	|	ОконачаниеДоговораСрезПоследних.ОкончаниеМонтаж КАК ОкончаниеМонтаж,
	|	ОконачаниеДоговораСрезПоследних.ОкончаниеСЗапасом КАК ОкончаниеСЗапасом,
	|	ОконачаниеДоговораСрезПоследних.ПриложениеКДоговору КАК ПриложениеКДоговору,
	|	ОконачаниеДоговораСрезПоследних.ИзделияКомплект.Родитель КАК Родитель,
	|	ОконачаниеДоговораСрезПоследних.ИзделияКомплект.Конструктор КАК Конструктор,
	|	ОконачаниеДоговораСрезПоследних.ИзделияКомплект.Проект КАК Проект
	|ИЗ
	|	РегистрСведений.ОконачаниеДоговора.СрезПоследних КАК ОконачаниеДоговораСрезПоследних
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
	|				ТОГДА НЕ ОконачаниеДоговораСрезПоследних.ИзделияКомплект.Проект.НеВыводитьНаДиаграмму
	|			ИНАЧЕ ОконачаниеДоговораСрезПоследних.ИзделияКомплект.Проект = &Проект
	|		КОНЕЦ";
	
	
	Запрос.УстановитьПараметр("Проект", Проект);
	Запрос.УстановитьПараметр("Конструктор", Конструктор);
	ВыборкаПакет = Запрос.ВыполнитьПакет();
	
	ПустаяДата 				= Дата(1,1,1);
	КоличествоСекундВДне 	= 24 * 3600;
	ПустойКомплект 			= Справочники.ИзделияКомплект.ПустаяСсылка();
	
	тзКомплекты.Очистить();

	стОтбор = Новый Структура("Проект,Комплект",);
	
	ЭтаФорма.ТЗЗадачи.Очистить();
	
	Выборка = ВыборкаПакет[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		Если НЕ (Выборка.КрайТЗ = ПустаяДата ИЛИ Выборка.НачалоТЗ = ПустаяДата) Тогда
			НоваяСтрока = ТЗЗадачи.Добавить();
			НоваяСтрока.Этап 					= "ТЗ";
			НоваяСтрока.Комплект 				= Выборка.Комплект;
			НоваяСтрока.Композиция 				= Выборка.Композиция;
			НоваяСтрока.Проект 					= Выборка.Проект;
			НоваяСтрока.ПродолжительностьВДнях 	= Выборка.ДнейТЗ;
			НоваяСтрока.КоличествоСотрудников 	= 1;
			Если ЗначениеЗаполнено(Выборка.Композиция) Тогда
				НоваяСтрока.ДатаНачалаПлан 			= Выборка.НачалоТЗ;
				НоваяСтрока.ДатаОкончанияПлан 		= Выборка.КрайТЗ;
			Иначе	
				НоваяСтрока.ДатаНачалаПлан 			= Выборка.НачалоТЗ;
				НоваяСтрока.ДатаОкончанияПлан 		= Выборка.КрайТЗ;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ (Выборка.КрайПКД = ПустаяДата ИЛИ Выборка.НачалоПКД = ПустаяДата) Тогда
			НоваяСтрока = ТЗЗадачи.Добавить();
			НоваяСтрока.Этап 					= "ПКД";
			НоваяСтрока.ДатаНачалаПлан 			= Выборка.НачалоПКД;
			НоваяСтрока.ДатаОкончанияПлан 		= Выборка.КрайПКД;
			НоваяСтрока.Комплект 				= Выборка.Комплект;
			НоваяСтрока.Композиция 				= Выборка.Композиция;
			НоваяСтрока.Проект 					= Выборка.Проект;
			НоваяСтрока.ПродолжительностьВДнях 	= Выборка.ДнейПКД;
			НоваяСтрока.КоличествоСотрудников 	= 1;
		КонецЕсли;
		
		Если НЕ (Выборка.КрайРКД = ПустаяДата ИЛИ Выборка.НачалоРКД = ПустаяДата) Тогда
			НоваяСтрока = ТЗЗадачи.Добавить();
			НоваяСтрока.Этап 					= "РКД";
			НоваяСтрока.ДатаНачалаПлан 			= Выборка.НачалоРКД;
			НоваяСтрока.ДатаОкончанияПлан 		= Выборка.КрайРКД;
			НоваяСтрока.Комплект 				= Выборка.Комплект;
			НоваяСтрока.Проект 					= Выборка.Проект;
			НоваяСтрока.ПродолжительностьВДнях 	= Выборка.ДнейРКД;
			НоваяСтрока.КоличествоСотрудников 	= 1;
		КонецЕсли;

		стОтбор.Проект 		= Выборка.Проект;
		стОтбор.Комплект 	= Выборка.Комплект;
		
		НайденыеСтроки = тзКомплекты.НайтиСтроки(стОтбор);
		
		Если НайденыеСтроки.Количество() = 0 Тогда
			
			НС = тзКомплекты.Добавить();
			ЗаполнитьЗначенияСвойств(НС,Выборка);
			Если Выборка.Родитель.Уровень() = 1 Тогда
				НС.Родитель = Выборка.Родитель;
			Иначе	
				НС.Родитель = ПустойКомплект;
			КонецЕсли;
			
		КонецЕсли;
		
		Если спСделки.НайтиПоЗначению(Выборка.Проект) = Неопределено Тогда
			спСделки.Добавить(Выборка.Проект,Строка(Выборка.СделкаНомер) + " " + Выборка.СделкаНаименование);
		КонецЕсли;
		
	КонецЦикла;
	
	Выборка = ВыборкаПакет[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		текДатаНачалаПлан = Выборка.КрайПроизводства - (Выборка.НормаВремениПроизводства * КоличествоСекундВДне);
		Если НЕ (Выборка.КрайПроизводства = ПустаяДата ИЛИ текДатаНачалаПлан = ПустаяДата) Тогда
			НоваяСтрока = ТЗЗадачи.Добавить();
			НоваяСтрока.Этап = "Производство";
			НоваяСтрока.ДатаНачалаПлан 			= текДатаНачалаПлан;
			НоваяСтрока.ДатаОкончанияПлан 		= Выборка.КрайПроизводства;
			НоваяСтрока.Комплект 				= Выборка.ИзделиеКомплект;
			НоваяСтрока.Проект 					= Выборка.Проект;
			НоваяСтрока.Подписать				= Ложь;
			НоваяСтрока.ПродолжительностьВДнях 	= Выборка.НормаВремениПроизводства;    
		КонецЕсли;
		
		Если спСделки.НайтиПоЗначению(Выборка.Проект) = Неопределено Тогда
			спСделки.Добавить(Выборка.Проект,Формат(Выборка.СделкаНомер, "ЧЦ=3; ЧВН=") + " " + Выборка.СделкаНаименование);
		КонецЕсли;
		
		стОтбор.Проект 		= Выборка.Проект;
		стОтбор.Комплект 	= Выборка.Комплект;
		
		НайденыеСтроки = тзКомплекты.НайтиСтроки(стОтбор);
		
		Если НайденыеСтроки.Количество() = 0 Тогда
			НС = тзКомплекты.Добавить();
			ЗаполнитьЗначенияСвойств(НС,Выборка);
			Если Выборка.Родитель.Уровень() = 1 Тогда
				НС.Родитель = Выборка.Родитель;
			Иначе	
				НС.Родитель = ПустойКомплект;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	
	Выборка = ВыборкаПакет[3].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если НЕ (Выборка.КрайМонтажа = ПустаяДата ИЛИ Выборка.НачалоМонтажа = ПустаяДата) Тогда
			НоваяСтрока = ТЗЗадачи.Добавить();
			НоваяСтрока.Этап = "Монтаж";
			НоваяСтрока.ДатаНачалаПлан 			= Выборка.НачалоМонтажа;
			НоваяСтрока.ДатаОкончанияПлан 		= Выборка.КрайМонтажа;
			НоваяСтрока.Проект 					= Выборка.Проект;
			НоваяСтрока.Комплект 				= Выборка.Комплект;
			НоваяСтрока.ПродолжительностьВДнях 	= Выборка.НормаВремениМонтажа;
			НоваяСтрока.КоличествоСотрудников 	= Выборка.КоличествоСотрудниковМонтаж;
			стОтбор.Проект 		= Выборка.Проект;
			стОтбор.Комплект 	= Выборка.Комплект;
			
			НайденыеСтроки = тзКомплекты.НайтиСтроки(стОтбор);
			
			Если НайденыеСтроки.Количество() = 0 Тогда
				НС = тзКомплекты.Добавить();
				ЗаполнитьЗначенияСвойств(НС,Выборка);
				Если Выборка.Родитель.Уровень() = 1 Тогда
					НС.Родитель = Выборка.Родитель;
				Иначе	
					НС.Родитель = ПустойКомплект;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	тзДатыДоговора.Загрузить(ВыборкаПакет[4].Выгрузить());
	
	тзДатыОкончания.Загрузить(ВыборкаПакет[5].Выгрузить());
	
	тзКомплекты.Сортировать("Родитель, КомплектПредставление");
	спСделки.СортироватьПоПредставлению();
	
	
КонецПроцедуры // ЗаполнитьЗадачиДоговора()

#КонецОбласти 
	

#Область СобытияФормы

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПроектПриИзмененииНаСервере();
	ЗаполнитьЦветаЛегенды();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Диаграмма.ОтображатьЛегенду = Ложь;
КонецПроцедуры

#КонецОбласти 
	

#Область Команды

&НаКлиенте
Процедура Обновить(Команда)
	ПроектПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	ОткрытьФорму("Отчет.КонтрольПроекта.Форма.ФормаПечати",
		Новый Структура("Проект,тзЗадачи,тзКомплекты,тзДатыОкончания",Проект,ТЗЗадачи,тзКомплекты,тзДатыОкончания));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	
	стПараметры = Новый Структура("Проект", Проект);
	
	ОткрытьФорму("Отчет.КонтрольПроекта.Форма.ФормаСозданиеДокументов", стПараметры, , , , , ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#КонецОбласти 


#Область СобытиеРеквизитов

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	ПроектПриИзмененииНаСервере();
	ОформлениеФормы();
КонецПроцедуры

&НаКлиенте
Процедура КонструкторПриИзменении(Элемент)
	ПроектПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти 


#Область СобытиеРеквизитовПродолжение

Процедура ПроектПриИзмененииНаСервере()
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	
	ПостроитьДиаграммуГанта(); 
	
	Диаграмма.Обновление = Истина;
КонецПроцедуры 

#КонецОбласти 


#Область СобытияДиаграммы
	
&НаКлиенте
Процедура ДиаграммаПриОкончанииРедактированияИнтервала(Элемент, Интервал, ОтменаРедактирования)
	Если СтрНайти(Интервал.Текст, "Проект") > 0 ИЛИ СтрНайти(Интервал.Текст, "Окончание с запасом") > 0 Тогда	
		ОтменаРедактирования = Истина;
	Иначе
		
		СсылкаКомплект = Интервал.Расшифровка.СсылкаКомплект;
		Если НЕ ЗначениеЗаполнено(СсылкаКомплект) Тогда
			ОтменаРедактирования = Истина;
			Возврат;
		КонецЕсли;
		
		ТипКомплекта = ТипЗнч(СсылкаКомплект);
		Если ТипКомплекта = Тип("СправочникСсылка.ИзделияКомплект") Тогда
			стОтбор = Новый Структура("Комплект", Интервал.Расшифровка.СсылкаКомплект);
		ИначеЕсли ТипКомплекта = Тип("СправочникСсылка.Композиции") Тогда
			стОтбор = Новый Структура("Композиция", Интервал.Расшифровка.СсылкаКомплект);
		Иначе
			Возврат;
		КонецЕсли;
		
		стОтбор.Вставить("Этап", Интервал.Расшифровка.Этап); 
		
		ИдентификаторИнтервала = ТЗЗадачи.НайтиСтроки(стОтбор);
		Если ИдентификаторИнтервала.Количество() = 0 Тогда
			ОтменаРедактирования = Истина;
			Возврат;
		КонецЕсли;
		
		строкаТЗЗадачи 	= ИдентификаторИнтервала[0]; 

		//Интервал.Начало = НачалоДня(Интервал.Начало);
		Интервал.Конец 	= НачалоДня(Интервал.Конец);
		
		Интервал.Начало = омДаты.ДобавитьДниСУчетомВыходных(Интервал.Конец,-строкаТЗЗадачи.ПродолжительностьВДнях);
		
		СекундВдне = 24 * 3600;
		Если Интервал.Конец - Интервал.Начало < СекундВдне Тогда
		
			Интервал.Конец = Интервал.Начало + СекундВдне;	
		                           
		КонецЕсли;
		
		строкаТЗЗадачи.ДатаНачалаПлан 		= Интервал.Начало;
		строкаТЗЗадачи.ДатаОкончанияПлан 	= Интервал.Конец;
		строкаТЗЗадачи.Изменен 				= Истина;
		
		Если ТипЗнч(СсылкаКомплект) = Тип("СправочникСсылка.ИзделияКомплект") Тогда
			СсылкаИзделиеКомплект = СсылкаКомплект;
			СсылкаКомпозиция = Неопределено;
		ИначеЕсли ТипЗнч(СсылкаКомплект) = Тип("СправочникСсылка.Композиции") Тогда
			СсылкаКомпозиция = СсылкаКомплект;	
			СсылкаИзделиеКомплект = Неопределено;
		КонецЕсли; 
		
		стИнтервалы = РасчитатьИнтервалы(Интервал.Расшифровка.Проект, СсылкаИзделиеКомплект, СсылкаКомпозиция); 
		
		Серия = Диаграмма.Серии[0];
		
		МаксИндексСделок = Диаграмма.Точки.Количество() - 1;
		
		Для х = 0 По МаксИндексСделок Цикл
			Если Диаграмма.Точки[х].Значение = Интервал.Расшифровка.Проект Тогда
				текТочка = Диаграмма.Точки[х];
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ЗначениеСерия = Диаграмма.ПолучитьЗначение(текТочка, Серия);
		
		Для каждого х Из ЗначениеСерия Цикл
			х.Начало 	= стИнтервалы.НачалоСделка;
			х.Конец 	= стИнтервалы.КонецСделка;
		КонецЦикла;
		
		МаксИндексТочки = текТочка.Точки.Количество() - 1;
		Для х = 0 По МаксИндексТочки Цикл
			Если текТочка.Точки[х].Значение = СсылкаИзделиеКомплект Тогда
				ЗначениеКомплект = Диаграмма.ПолучитьЗначение(текТочка.Точки[х], Серия);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Функция РасчитатьИнтервалы(Проект, СсылкаКомплект, СсылкаКомпозиция)
	
	стНаименованиеЭтапов = омДиаграммаГантаКлиентСервер.ПолучитНаименованиеЭтапов();

	ПустаяДата = Дата(1,1,1);
		
	стИнтервалы = Новый Структура();
	стИнтервалы.Вставить("НачалоСделка", 		ПустаяДата);	
	стИнтервалы.Вставить("КонецСделка", 		ПустаяДата);	
	стИнтервалы.Вставить("НачалоПроизводство", 	ПустаяДата);	
	стИнтервалы.Вставить("КонецПроизводство", 	ПустаяДата);	
	стИнтервалы.Вставить("НачалоМонтаж", 		ПустаяДата);	
	стИнтервалы.Вставить("КонецМонтаж", 		ПустаяДата);	
	
	стОтбор = Новый Структура("Проект", Проект);
	
	НайденыеСтроки = ТЗЗадачи.НайтиСтроки(стОтбор);
	
	Для каждого х Из НайденыеСтроки Цикл
		КонтрольСделки(стИнтервалы, х, ПустаяДата);
	КонецЦикла;
	
	
	Если СсылкаКомплект = Неопределено Тогда
		стОтбор = Новый Структура("Композиция", СсылкаКомпозиция);
	Иначе
		стОтбор = Новый Структура("Комплект", СсылкаКомплект);
	КонецЕсли;
	
	НайденыеСтроки = ТЗЗадачи.НайтиСтроки(стОтбор);
	
	Для каждого х Из НайденыеСтроки Цикл
		
		Если НЕ стНаименованиеЭтапов.мЭтапыПроизводства.Найти(х.Этап) = Неопределено Тогда
			Если НЕ х.ДатаНачалаПлан = ПустаяДата Тогда
				Если стИнтервалы.НачалоПроизводство = ПустаяДата ИЛИ стИнтервалы.НачалоПроизводство > х.ДатаНачалаПлан Тогда
					стИнтервалы.НачалоПроизводство = х.ДатаНачалаПлан;
				КонецЕсли;	
			ИначеЕсли стИнтервалы.НачалоПроизводство > х.ДатаНачалаПлан Тогда	
				стИнтервалы.НачалоПроизводство = х.ДатаНачалаПлан;
			КонецЕсли;
			
			Если стИнтервалы.КонецПроизводство < х.ДатаОкончанияПлан Тогда	
				стИнтервалы.КонецПроизводство = х.ДатаОкончанияПлан;
			КонецЕсли;	
		ИначеЕсли НЕ стНаименованиеЭтапов.мЭтапыМонтаж.Найти(х.Этап) = Неопределено Тогда
			Если НЕ х.ДатаНачалаПлан = ПустаяДата Тогда
				Если стИнтервалы.НачалоМонтаж = ПустаяДата ИЛИ стИнтервалы.НачалоМонтаж > х.ДатаНачалаПлан Тогда
					стИнтервалы.НачалоМонтаж = х.ДатаНачалаПлан;
				КонецЕсли;	
			ИначеЕсли стИнтервалы.НачалоМонтаж > х.ДатаНачалаПлан Тогда	
				стИнтервалы.НачалоМонтаж = х.ДатаНачалаПлан;
			КонецЕсли;
			
			Если стИнтервалы.КонецМонтаж < х.ДатаОкончанияПлан Тогда	
				стИнтервалы.КонецМонтаж = х.ДатаОкончанияПлан;
			КонецЕсли;
		КонецЕсли;  
	КонецЦикла;
	Возврат стИнтервалы;
	
КонецФункции // РасчитатьИнтервалы() 

&НаКлиенте
Функция КонтрольСделки(стИнтервалы, х, ПустаяДата)
	Если НЕ х.ДатаНачалаПлан = ПустаяДата Тогда
		Если стИнтервалы.НачалоСделка = ПустаяДата ИЛИ стИнтервалы.НачалоСделка > х.ДатаНачалаПлан Тогда
			стИнтервалы.НачалоСделка = х.ДатаНачалаПлан;
		КонецЕсли;	
	ИначеЕсли стИнтервалы.НачалоСделка > х.ДатаНачалаПлан Тогда	
		стИнтервалы.НачалоСделка = х.ДатаНачалаПлан;
	КонецЕсли;
	
	Если стИнтервалы.КонецСделка < х.ДатаОкончанияПлан Тогда	
		стИнтервалы.КонецСделка = х.ДатаОкончанияПлан;
	КонецЕсли;	
КонецФункции // КонтрольСделки()

&НаКлиенте
Процедура ДиаграммаОбработкаРасшифровки(Элемент, Расшифровки, СтандартнаяОбработка, Дата)
	СтандартнаяОбработка = Ложь;
	Если Расшифровки.Количество() > 1 Тогда
		Если НЕ Расшифровки[1] = Неопределено Тогда
			СсылкаКомплект = Расшифровки[1].СсылкаКомплект;
			СсылкаКомплект = ДокументСрокиСделки(СсылкаКомплект, Расшифровки[1].Этап);
			ПоказатьЗначение(,СсылкаКомплект);	
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДокументСрокиСделки(СсылкаКомплект, Этап)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СрокиСделкиКомплекты.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗапускВРаботу.Комплекты КАК СрокиСделкиКомплекты
		|ГДЕ
		|	СрокиСделкиКомплекты.Комплект = &Комплект";
	
	Запрос.УстановитьПараметр("Комплект", СсылкаКомплект); 
	
	Если Этап = "Монтаж" Тогда
	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЗапускВРаботу", "СрокиПроектаМонтаж");	
	
	ИначеЕсли Этап = "Производство" Тогда
	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЗапускВРаботу", "СрокиПроектаПроизводство");	
	
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка;
	КонецЦикла;
	
	Возврат СсылкаКомплект;

КонецФункции // ДокументСрокиСделки()

&НаКлиенте
Процедура ДиаграммаВыбор(Элемент, Значения, СтандартнаяОбработка, Дата)
	Если Тип("ТочкаДиаграммыГанта") = ТипЗнч(Значения) Тогда
		Если Тип("СправочникСсылка.ИзделияКомплект") = ТипЗнч(Значения.Значение) Тогда
			СтандартнаяОбработка = Ложь;
			ПоказатьЗначение(,Значения.Значение);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ДиаграммаПередСворачиванием(Элемент, ИдентификаторЗначения, Отказ)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаПередРазворачиванием(Элемент, ИдентификаторЗначения, Отказ)
	диаграмма.НайтиЗначениеПоИдентификатору(ИдентификаторЗначения)
КонецПроцедуры
#КонецОбласти 

&НаКлиенте
Процедура ОформлениеФормы()

	Элементы.СоздатьДокументы.Доступность = ЗначениеЗаполнено(Проект);	

КонецПроцедуры // ОформлениеФормы()


#Область Запись
	
&НаСервереБезКонтекста
Процедура ЗаписатьНаСервере(мИзмененныеКомплекты)
	мИзделияКомплект 			= Новый Массив;
	мКомпозиция		 			= Новый Массив;
	мКонтроль 					= Новый Массив;
	мКонтрольКомпозиция			= Новый Массив;
	ТипИзделияКомплект 			= Тип("СправочникСсылка.ИзделияКомплект");
	ТипКомпозиция				= Тип("СправочникСсылка.Композиции");
	
	
	ОписаниеЦелоеЧисло 	= Новый ОписаниеТипов("Число", , ,Новый КвалификаторыЧисла(6, 0, ДопустимыйЗнак.Неотрицательный));
	ОписаниеСтрока40 	= Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(40));
	ОписаниеДата 		= Новый ОписаниеТипов("Дата");

	тзКомплекты = Новый ТаблицаЗначений;
	тзКомплекты.Колонки.Добавить("УИД", 				ОписаниеСтрока40);
	тзКомплекты.Колонки.Добавить("Комплект", 			Новый ОписаниеТипов("СправочникСсылка.ИзделияКомплект"));
	тзКомплекты.Колонки.Добавить("Композиция", 			Новый ОписаниеТипов("СправочникСсылка.Композиции"));
	тзКомплекты.Колонки.Добавить("Проект", 				Новый ОписаниеТипов("СправочникСсылка.Проекты"));
	тзКомплекты.Колонки.Добавить("Этап", 				ОписаниеСтрока40);
	тзКомплекты.Колонки.Добавить("ДатаНачалаПлан", 		ОписаниеДата);
	тзКомплекты.Колонки.Добавить("ДатаОкончанияПлан", 	ОписаниеДата);
	
	Для каждого текДанные Из мИзмененныеКомплекты Цикл       
		ТипКомплекта = ТипЗнч(текДанные.Комплект);
		
		Если ТипКомплекта = ТипИзделияКомплект ИЛИ ТипКомпозиция = ТипЗнч(текДанные.Композиция) Тогда
			НС 						= тзКомплекты.Добавить();
			НС.Комплект 			= текДанные.Комплект;
			НС.УИД 					= текДанные.Комплект.УникальныйИдентификатор();      
			НС.Этап					= текДанные.Этап;
			НС.Композиция			= текДанные.Композиция;
			НС.Проект				= текДанные.Проект;
			НС.ДатаНачалаПлан		= текДанные.ДатаНачалаПлан;
			НС.ДатаОкончанияПлан	= текДанные.ДатаОкончанияПлан;
		КонецЕсли;
		
	КонецЦикла;
	
	
	Для каждого текДанные Из тзКомплекты Цикл
		
		ТипКомплекта = типЗнч(текДанные.Комплект);
		
		
		Если ЗначениеЗаполнено(текДанные.Комплект) Тогда
			
			Если ТипКомплекта = ТипИзделияКомплект Тогда
				мИзделияКомплект.Добавить(Новый Структура("Проект,Комплект",текДанные.Проект,текДанные.Комплект));
				
				Если мКонтроль.Найти(текДанные.Комплект) = Неопределено Тогда
					Источник = текДанные.Комплект.ПолучитьОбъект();
					Отказ = Ложь;
					Б24_СМ_РегистрацияИзмененийСервер.ЗарегистрироватьИзменения(Источник, Ложь, Отказ);
					
					мКонтроль.Добавить(текДанные.Комплект);
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(текДанные.Композиция) Тогда
			
			мКомпозиция.Добавить(Новый Структура("Проект,Композиция",текДанные.Проект,	текДанные.Композиция));
			
			Если мКонтрольКомпозиция.Найти(текДанные.Комплект) = Неопределено Тогда
				Источник = текДанные.Композиция.ПолучитьОбъект();
				Отказ = Ложь;
				Б24_СМ_РегистрацияИзмененийСервер.ЗарегистрироватьИзменения(Источник, Ложь, Отказ);
				
				мКонтрольКомпозиция.Добавить(текДанные.Композиция);
			КонецЕсли;
		Иначе
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ИзделияКомплект Из мИзделияКомплект Цикл 
		
		МенеджерЗаписей = РегистрыСведений.ДатыКомплекта.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.Проект = ИзделияКомплект.Проект;
		МенеджерЗаписей.Комплект = ИзделияКомплект.Комплект;
		МенеджерЗаписей.Прочитать();
		
		НужнаЗаписи = Ложь;
		
		Для каждого текДанные Из мИзмененныеКомплекты Цикл
			Если НЕ текДанные.Комплект = ИзделияКомплект.Комплект Тогда
				Продолжить;	
			КонецЕсли;	
			Если текДанные.Этап 		= "ТЗ" Тогда
				МенеджерЗаписей.НачалоТЗ 	= текДанные.ДатаНачалаПлан;	
				МенеджерЗаписей.КрайТЗ 		= текДанные.ДатаОкончанияПлан;
				НужнаЗаписи					= Истина;
			ИначеЕсли текДанные.Этап 	= "ПКД" Тогда
				МенеджерЗаписей.НачалоПКД 	= текДанные.ДатаНачалаПлан;	
				МенеджерЗаписей.КрайПКД 	= текДанные.ДатаОкончанияПлан;
				НужнаЗаписи					= Истина;
			ИначеЕсли текДанные.Этап 	= "РКД" Тогда
				МенеджерЗаписей.НачалоРКД 	= текДанные.ДатаНачалаПлан;	
				МенеджерЗаписей.КрайРКД 	= текДанные.ДатаОкончанияПлан;
				НужнаЗаписи					= Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НужнаЗаписи Тогда
			МенеджерЗаписей.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ИзделияКомплект Из мИзделияКомплект Цикл
		
		МенеджерЗаписей = РегистрыСведений.ДатыКомплектаПроизводство.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.Проект 		= ИзделияКомплект.Проект;
		МенеджерЗаписей.Комплект 	= ИзделияКомплект.Комплект;
		МенеджерЗаписей.Прочитать();
		
		НужнаЗаписи = Ложь;
		
		Для каждого текДанные Из мИзмененныеКомплекты Цикл
			Если НЕ (текДанные.Комплект = ИзделияКомплект.Комплект И текДанные.Этап = "Производство") Тогда
				Продолжить;	
			КонецЕсли;
			
			МенеджерЗаписей.НачалоПроизводства	= текДанные.ДатаНачалаПлан;	
			МенеджерЗаписей.КрайПроизводства	= текДанные.ДатаОкончанияПлан;
			НужнаЗаписи							= Истина;
			
		КонецЦикла;
		
		Если НужнаЗаписи Тогда
			МенеджерЗаписей.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ИзделияКомплект Из мИзделияКомплект Цикл
		
		МенеджерЗаписей = РегистрыСведений.ДатыКомплектаМонтаж.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.Проект 		= ИзделияКомплект.Проект;
		МенеджерЗаписей.Комплект 	= ИзделияКомплект.Комплект;
		МенеджерЗаписей.Прочитать();
		
		НужнаЗаписи = Ложь;
		
		Для каждого текДанные Из мИзмененныеКомплекты Цикл
			Если НЕ текДанные.Комплект = ИзделияКомплект.Комплект Тогда
				Продолжить;	
			КонецЕсли;	
			Если текДанные.Этап = "Монтаж" Тогда
				МенеджерЗаписей.НачалоМонтажа 	= текДанные.ДатаНачалаПлан;	
				МенеджерЗаписей.КрайМонтажа 	= текДанные.ДатаОкончанияПлан;	
				НужнаЗаписи						= Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НужнаЗаписи Тогда
			МенеджерЗаписей.Записать();
		КонецЕсли;
		
	КонецЦикла; 
	
	Для каждого ИзделияКомпозиция Из мКомпозиция Цикл
		
		
		МенеджерЗаписей = РегистрыСведений.ДатыКомплекта.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.Проект 		= ИзделияКомпозиция.Проект;
		МенеджерЗаписей.Комплект 	= ИзделияКомпозиция.Композиция;
		МенеджерЗаписей.Прочитать();
		
		НужнаЗаписи = Ложь;
		
		Для каждого текДанные Из мИзмененныеКомплекты Цикл
			Если НЕ текДанные.Композиция = ИзделияКомпозиция.Композиция Тогда
				Продолжить;	
			КонецЕсли;	
			Если текДанные.Этап 		= "ТЗ" Тогда
				МенеджерЗаписей.НачалоТЗ 	= текДанные.ДатаНачалаПлан;	
				МенеджерЗаписей.КрайТЗ 		= текДанные.ДатаОкончанияПлан;
				НужнаЗаписи					= Истина;
			ИначеЕсли текДанные.Этап 	= "ПКД" Тогда
				МенеджерЗаписей.НачалоПКД 	= текДанные.ДатаНачалаПлан;	
				МенеджерЗаписей.КрайПКД 	= текДанные.ДатаОкончанияПлан;
				НужнаЗаписи					= Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НужнаЗаписи Тогда
			МенеджерЗаписей.Записать();
		КонецЕсли;
		
		
	КонецЦикла; 
	
	//РегистрыСведений.ДанныеКомпозиций.РасчитатьПКДКопозицииНаОснованииКомплектов(тзКомплекты.ВыгрузитьКолонку("Комплект"));
	
КонецПроцедуры 

&НаКлиенте
Процедура Записать(Команда)
	мИзмененныеКомплекты = Новый Массив;
	
	Если ЭтаФорма.СинхронизироватьВсе Тогда
		НайденыеСтроки = ТЗЗадачи;	
	Иначе
		НайденыеСтроки = ТЗЗадачи.НайтиСтроки(Новый Структура("Изменен", Истина));
	КонецЕсли;
	
	Для каждого х Из НайденыеСтроки Цикл 
	    стДанные = Новый Структура("Проект, Комплект ,Композиция, Этап, ДатаНачалаПлан, ДатаОкончанияПлан", );
		ЗаполнитьЗначенияСвойств(стДанные, х);
		
		мИзмененныеКомплекты.Добавить(стДанные);	
		х.Изменен = Ложь;
	КонецЦикла;
	
	Если мИзмененныеКомплекты.Количество() > 0 Тогда
		ЗаписатьНаСервере(мИзмененныеКомплекты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Цвет

Процедура ЗаполнитьЦветаЛегенды()

	тзЦвета = омДиаграммаГанта.ПолучитьТаблицуЦветов();
	Для каждого х Из тзЦвета Цикл
		Если х.НаименованиеЭтапа = "ТЗ" Тогда
			Элементы.ДекорацияТЗ.ЦветФона = х.Цвет;	
		ИначеЕсли х.НаименованиеЭтапа = "РКД" Тогда
			Элементы.ДекорацияРКД.ЦветФона = х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "ПКД" Тогда
			Элементы.ДекорацияПКД.ЦветФона = х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "Производство" И НЕ х.ЭтоГруппа Тогда
			Элементы.ДекорацияПроизводствоГр.ЦветФона = х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "Монтаж" И НЕ х.ЭтоГруппа Тогда
			Элементы.ДекорацияМонтажГр.ЦветФона = х.Цвет;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ЗаполнитьЦветаЛегенды()

&НаКлиенте
Процедура ДиаграммаТаблицаПриАктивизацииСтроки(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти 
