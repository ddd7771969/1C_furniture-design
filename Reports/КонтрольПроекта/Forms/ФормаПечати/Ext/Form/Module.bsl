&НаКлиенте
Перем стКоэффициенты, стИнтервал, 	КоличествоСекундВДне;


&НаСервере
Процедура ДоЗаполнитьПредставления()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Проекты.Ссылка КАК Проект,
		|	Проекты.Наименование КАК Наименование,
		|	Проекты.Номер КАК Номер,
		|	Проекты.Префикс КАК Префикс
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	Проекты.Ссылка В(&Ссылка)";
	
	Запрос.УстановитьПараметр("Ссылка", омТЧ_НаКлиентеСервере.КолонкуВМассив(тзКомплекты, "Проект"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	
	стОтбор = Новый Структура("Проект", );
	
	Пока Выборка.Следующий() Цикл
		стОтбор.Проект = Выборка.Проект;
		
		НайденыеСтроки = тзКомплекты.НайтиСтроки(стОтбор);
		
		стрПроект = Формат(Выборка.Номер,"ЧЦ=2; ЧВН=") + "(" + Выборка.Префикс + ") " + Выборка.Наименование;
		
		Для каждого х Из НайденыеСтроки Цикл
			х.ПроектПредставление = стрПроект;
		КонецЦикла;
	КонецЦикла;
	

КонецПроцедуры // ДоЗаполнитьПредставления()
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	МасштабНаПечать = 100;
	Проект = Параметры.Проект; 
	ТЗЗадачи.Загрузить(Параметры.тзЗадачи.Выгрузить());
	тзДатыОкончания.Загрузить(Параметры.тзДатыОкончания.Выгрузить());
	тзКомплекты.Загрузить(Параметры.тзКомплекты.Выгрузить());
	ДоЗаполнитьПредставления();
	
	тзКомплекты.Сортировать("ПроектПредставление, Родитель,КомплектПредставление");
	
	ПустаяДата 		= Дата(1,1,1);                    
	
	ДатаНачала 		= ПустаяДата;
	ДатаОкончания 	= ПустаяДата;
	
	Для каждого х Из ЭтаФорма.ТЗЗадачи Цикл
		х.Этап = СтрЗаменить(х.Этап,"МонтажКомплекта","Монтаж");	
		
		Если ДатаОкончания < х.ДатаОкончанияПлан Тогда
			ДатаОкончания = х.ДатаОкончанияПлан;
		КонецЕсли;
		
	    Если х.ДатаНачалаПлан > ПустаяДата Тогда
			Если ДатаНачала = ПустаяДата Тогда
				ДатаНачала = х.ДатаНачалаПлан;
			ИначеЕсли ДатаНачала > х.ДатаНачалаПлан Тогда
				ДатаНачала = х.ДатаНачалаПлан;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	текПроект 	= Неопределено;
	текРодитель = Неопределено;
	
	НомерСтроки = 3;
	
	Для каждого х Из тзКомплекты Цикл
		Если НЕ текПроект = х.Проект Тогда
			х.СтрокаПроекта = НомерСтроки;
			НомерСтроки 	= НомерСтроки + 1;
			текПроект 		= х.Проект;
		КонецЕсли;

		Если НЕ текРодитель = х.Родитель И ЗначениеЗаполнено(х.Родитель) Тогда
			х.СтрокаРодитель 	= НомерСтроки;
			НомерСтроки 		= НомерСтроки + 1;
			текРодитель 		= х.Родитель;
		КонецЕсли;
		
		х.СтрокаКомплект = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	ЭтаФорма.тзЦвета.Загрузить(омДиаграммаГанта.ПолучитьТаблицуЦветов());

	омМесяц.ЗаполнитьСписокМесяцами(ЭтаФорма.спМесяцы);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИнтервалДиаграммы(тзЗадачи)
	
	ПустаяДата = Дата(1,1,1);
	стИнтервал = Новый Структура("ДатаНачала,ДатаОкончания,мДаты",ПустаяДата,ПустаяДата, Новый Массив);
	
	Для каждого х Из тзЗадачи Цикл
		Если стИнтервал.ДатаОкончания < х.ДатаОкончанияПлан Тогда
			стИнтервал.ДатаОкончания = х.ДатаОкончанияПлан;
		КонецЕсли;
		
	    Если х.ДатаНачалаПлан > ПустаяДата Тогда
			Если стИнтервал.ДатаНачала = ПустаяДата Тогда
				стИнтервал.ДатаНачала = х.ДатаНачалаПлан;
			ИначеЕсли стИнтервал.ДатаНачала > х.ДатаНачалаПлан Тогда
				стИнтервал.ДатаНачала = х.ДатаНачалаПлан;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ДеньНеделиДатаНачала = ДеньНедели(стИнтервал.ДатаНачала);
	
	Если Интервалы = 0 И ДеньНеделиДатаНачала > 1 Тогда
		стИнтервал.ДатаНачала = стИнтервал.ДатаНачала - (ДеньНеделиДатаНачала - 1) * КоличествоСекундВДне;
	КонецЕсли;
	
	текДата = стИнтервал.ДатаНачала;

	Пока текДата <= стИнтервал.ДатаОкончания Цикл
		стИнтервал.мДаты.Добавить(текДата);
		
	    текДата = текДата + КоличествоСекундВДне; 
	КонецЦикла;
	
	Возврат стИнтервал;
КонецФункции // ПолучитьИнтервалДиаграммы()

&НаКлиенте
Процедура ПолучитьКоэффициенты()

	КоличествоМиллиметровВПикселе = омТабличныйДокументСерверПовтор.ПолучитьКоличествоМиллиметровВПикселе();
	КоличествоДней = Окр((Период.ДатаОкончания - Период.ДатаНачала) / (24 * 3600), 0);
	мИнформацияОЭкране = ПолучитьИнформациюЭкрановКлиента();
	
	Если мИнформацияОЭкране = Неопределено Тогда
		DPI = 60;
		ШиринаЭкрана = 600;
	Иначе
		DPI = мИнформацияОЭкране[0].DPI;
		ШиринаЭкрана = мИнформацияОЭкране[0].Ширина;
	КонецЕсли;
	
	стКоэффициенты =  Новый Структура("КоличествоМиллиметровВПикселе,КоличествоДней,DPI,ШиринаЭкрана,КоличествоИнтервалов"
						,КоличествоМиллиметровВПикселе,КоличествоДней,DPI,ШиринаЭкрана,0);
КонецПроцедуры // ПолучитьКоэффициенты()

&НаКлиенте
Процедура МасштабПриИзменении(Элемент)
	ВывестиДиаграмму();
КонецПроцедуры
 
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	КоличествоСекундВДне = 24 * 3600;
	ВывестиДиаграмму();
КонецПроцедуры

&НаСервере
Процедура ОчиститьТаблицу()

	ЭтаФорма.ПолеВывода.Очистить(); 

КонецПроцедуры // ОчиститьТаблицу()

&НаКлиенте
Процедура ВывестиДиаграмму()
	
	стИнтервал = ПолучитьИнтервалДиаграммы(тзЗадачи);

	ЗаполнитьТаблицыДанных(стИнтервал);
	
	Период.ДатаНачала 		= стИнтервал.ДатаНачала;
	Период.ДатаОкончания 	= стИнтервал.ДатаОкончания;

	Если НЕ ПолеВывода.ВысотаТаблицы = 0 Тогда
		ОчиститьТаблицу();	
	КонецЕсли;
	
	ПолучитьКоэффициенты();
	Если стКоэффициенты.КоличествоДней = 0 Тогда
		Возврат;
	КонецЕсли;

	УстановитьШиринуКолонок();
	
	ПолеВывода.ФиксацияСлева = 3;
	
	ВывестиШапку();
	
	НомерСтроки = 3;
	
	Если тзДаты.Количество() = 0 Тогда
	
		Возврат;	
	
	КонецЕсли;
	
	ВывестиДанные(); 
	
КонецПроцедуры 

&НаКлиенте
Процедура ВывестиДанные()

	МаксимальныйНомерДаты = тзДаты[тзДаты.Количество() - 1].НомерДаты;
	
	текПроект 	= Неопределено;
	текРодиель	= Неопределено;
	
	ЦветБелый 	= Новый Цвет(255,255,255);
	стЦвета = Новый Структура("ПКД, ТЗ, РКД, Производство, Монтаж, Пересечение, КрайняяДата, Конструирование",
								ЦветБелый, ЦветБелый, ЦветБелый, ЦветБелый, ЦветБелый, ЦветБелый, ЦветБелый, ЦветБелый);
	
	ЦветПроекта			= ЦветБелый;
	ЦветРодитель 		= ЦветБелый;
	КомпозицияЭлемент 	= ЦветБелый;
	
	Для каждого х Из тзЦвета Цикл
		Если х.НаименованиеЭтапа = "Проект" Тогда
			ЦветПроекта 	= х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "Композиция" Тогда
			ЦветРодитель = х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "КомпозицияЭлемент" Тогда
			КомпозицияЭлемент = х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "ПКД" Тогда
			стЦвета.ПКД 	= х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "ТЗ" Тогда
			стЦвета.ТЗ 		= х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "РКД" Тогда
			стЦвета.РКД 	= х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "Производство" Тогда
			стЦвета.Производство 	= х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "Монтаж" Тогда
			стЦвета.Монтаж 	= х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "Пересечение" Тогда
			стЦвета.Пересечение 	= х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "КрайняяДата" Тогда
			стЦвета.КрайняяДата 	= х.Цвет;
		ИначеЕсли х.НаименованиеЭтапа = "Конструирование" Тогда
			стЦвета.Конструирование = х.Цвет;
		КонецЕсли;	
	КонецЦикла; 
	
	смещениеКолонок = 3;  
	
	МакНомерСроки = 0;
	

	стПараметровОбласти = омТабличныйДокументКлиентСервер.ПолучитьСтруктуруПараметров();
	стПараметровОбласти.ЦветРамки 		= "210210210";
	стПараметровОбласти.ГраницаСправа 	= Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 0);
	стПараметровОбласти.ГраницаСлева 	= Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 0);
	
	стПараметровОбластиПроект = омТабличныйДокументКлиентСервер.ПолучитьСтруктуруПараметров();
	стПараметровОбластиПроект.НомерКолонки 				= 2;
	стПараметровОбластиПроект.ЦветРамки 				= "210210210";
	стПараметровОбластиПроект.ЦветФона 					= ЦветПроекта;
	стПараметровОбластиПроект.ГоризонтальноеПоложение 	= ГоризонтальноеПоложение.Лево;
	
	стПараметровОбластиКомп = омТабличныйДокументКлиентСервер.ПолучитьСтруктуруПараметров();
	стПараметровОбластиКомп.НомерКолонки 				= 2;
	стПараметровОбластиКомп.ЦветРамки 					= "210210210";
	стПараметровОбластиКомп.ЦветФона 					= ЦветРодитель;
	стПараметровОбластиКомп.ГоризонтальноеПоложение 	= ГоризонтальноеПоложение.Лево;
	
	стПараметровОбластиКонстр = омТабличныйДокументКлиентСервер.ПолучитьСтруктуруПараметров();
	стПараметровОбластиКонстр.НомерКолонки 				= 3;
	стПараметровОбластиКонстр.ГоризонтальноеПоложение 	= ГоризонтальноеПоложение.Лево;
	
	стОтбор 		= Новый Структура("ОбъектДанных", );
	стОтборДата		= Новый Структура("Дата", );
	стОтборИзделие 	= Новый Структура("ИзделиеКомплект",);

	
	Для каждого текКомплект Из тзКомплекты Цикл 
		
		Если текКомплект.СтрокаПроекта > 0  Тогда
			КешЦвета 				= Неопределено; 
			стПараметровОбластиПроект.Текст 					= Строка(текКомплект.ПроектПредставление);
			стПараметровОбластиПроект.Расшифровка				= текКомплект.Проект;
			стПараметровОбластиПроект.НомерСтроки 				= текКомплект.СтрокаПроекта;
			омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбластиПроект, КешЦвета);
			
			стПараметровОбластиКонстр.Текст 					= "";
			стПараметровОбластиКонстр.Расшифровка				= Неопределено;
			стПараметровОбластиКонстр.НомерСтроки 				= текКомплект.СтрокаПроекта;
			стПараметровОбластиКонстр.ЦветФона 					= ЦветПроекта;
			омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбластиКонстр, КешЦвета);
			
			стОтбор.ОбъектДанных = текКомплект.Проект;
			
			НайденыеСтроки = тзВывод.НайтиСтроки(стОтбор);
			
			стПараметровОбласти.Текст					= "";
			стПараметровОбласти.Расшифровка				= Неопределено;
			стПараметровОбласти.ЦветФона 				= ЦветПроекта;
			
			ВыводОбъекта(НайденыеСтроки, текКомплект, КешЦвета, стПараметровОбласти, смещениеКолонок);
		КонецЕсли; 
		
		Если текКомплект.СтрокаРодитель > 0  Тогда
			КешЦвета 				= Неопределено; 
			стПараметровОбластиКомп.Текст 						= Строка(текКомплект.Родитель);
			стПараметровОбластиКомп.НомерСтроки 				= текКомплект.СтрокаРодитель;
			омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбластиКомп, КешЦвета);
			
			стПараметровОбластиКонстр.Текст 					= "";
			стПараметровОбластиКонстр.НомерСтроки 				= текКомплект.СтрокаРодитель;
			стПараметровОбластиКонстр.ЦветФона 					= ЦветРодитель;
			омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбластиКонстр, КешЦвета);
			
		КонецЕсли;
		
		МакНомерСроки = Макс(МакНомерСроки, текКомплект.СтрокаКомплект);
		
		КешЦвета 				= Неопределено; 
		стПараметровОбласти.Расшифровка				= текКомплект.Комплект;
		стПараметровОбласти.НомерСтроки 			= текКомплект.СтрокаКомплект;
		стПараметровОбласти.НомерКолонки 			= 2;
		стПараметровОбласти.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
		
		Если ЗначениеЗаполнено(текКомплект.Родитель) Тогда
			стПараметровОбласти.Текст 					= "  " + Строка(текКомплект.Комплект);
			стПараметровОбласти.ЦветФона 				= КомпозицияЭлемент;
		Иначе
			стПараметровОбласти.Текст 					= Строка(текКомплект.Комплект);
			стПараметровОбласти.ЦветФона 				= ЦветБелый;
		КонецЕсли;
		омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти, КешЦвета);
		
		стПараметровОбластиКонстр.Текст 					= Строка(текКомплект.Конструктор);
		стПараметровОбластиКонстр.Расшифровка				= текКомплект.Конструктор;
		стПараметровОбластиКонстр.НомерСтроки 				= текКомплект.СтрокаКомплект;
		стПараметровОбластиКонстр.ЦветФона 					= ЦветБелый;
		омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбластиКонстр, КешЦвета);
		
		КешЦвета 				= Неопределено;
		
		стОтбор.ОбъектДанных = текКомплект.Комплект;
		НайденыеСтроки = тзВывод.НайтиСтроки(стОтбор);
		
		стПараметровОбласти.Текст = "";
		ВыводОбъекта(НайденыеСтроки, текКомплект, КешЦвета, стПараметровОбласти, смещениеКолонок, стЦвета);
		
		стОтборИзделие.ИзделиеКомплект = текКомплект.Комплект; 
		НайденыеСтроки = тзДатыОкончания.НайтиСтроки(стОтборИзделие);
		
		Если НайденыеСтроки.Количество() > 0 Тогда
			
			
			стПараметровОбласти.Текст 					= "";
			стПараметровОбласти.Расшифровка				= Неопределено;
			стПараметровОбласти.НомерСтроки 			= текКомплект.СтрокаКомплект;
			стПараметровОбласти.ЦветФона 				= стЦвета.КрайняяДата;
			
			стОтборДата.Дата = НайденыеСтроки[0].ОкончаниеСЗапасом;
			строкиДата = тзДаты.НайтиСтроки(стОтборДата);
			Если строкиДата.Количество() > 0 Тогда
				стПараметровОбласти.НомерКолонки 			= строкиДата[0].НомерДаты + смещениеКолонок;
				омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти);
			КонецЕсли;
			
		КонецЕсли; 
		
	КонецЦикла;  
	
	ВывестиИтоги(МаксимальныйНомерДаты, МакНомерСроки, смещениеКолонок, стЦвета, ЦветБелый);
	
КонецПроцедуры // ВывестиДанные(МаксимальныйНомерДаты) 

&НаКлиенте
Процедура ВывестиИтоги(МаксимальныйНомерДаты, МакНомерСроки, смещениеКолонок, стЦвета, ЦветБелый)

	стПараметровОбласти = омТабличныйДокументКлиентСервер.ПолучитьСтруктуруПараметров();
	стПараметровОбласти.ЦветРамки 		= "210210210";
	стПараметровОбласти.НомерКолонки 	= 3;
	стПараметровОбласти.ГраницаСправа 	= Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 0);
	стПараметровОбласти.ГраницаСлева 	= Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 0);

	стОтбор = Новый Структура("НомерДаты", );
	
	НомерСроки = МакНомерСроки + 3;
	стСтроки = Новый Структура();
	
	стСтроки.Вставить("Конструирование", НомерСроки);
	НомерСроки = НомерСроки + 1;
	
	стСтроки.Вставить("РКД", НомерСроки);
	НомерСроки = НомерСроки + 1;
	
	стСтроки.Вставить("ПКД", НомерСроки);
	НомерСроки = НомерСроки + 1;
	
	стСтроки.Вставить("Производство", НомерСроки);
	НомерСроки = НомерСроки + 1;
	
	стСтроки.Вставить("Монтаж", НомерСроки);
	
	стДанные = Неопределено;
	ОбнулитьИтогДня(стДанные);
	
	стМакс = Неопределено;
	ОбнулитьИтогДня(стМакс);
	
	Для х = 1 По МаксимальныйНомерДаты Цикл
		стОтбор.НомерДаты = х;
		
		Конструирование = 0;
		
		НайденыеСтроки = тзВывод.НайтиСтроки(стОтбор);
		
		Для каждого текКомплект Из НайденыеСтроки Цикл
			Если Тип("Структура") = ТипЗнч(текКомплект.СтруктураДанных) Тогда
				Продолжить;	
			КонецЕсли;
		
			Для каждого текСобытие Из текКомплект.СтруктураДанных Цикл
				стДанные[текСобытие.Значение.ВидОбъекта] = стДанные[текСобытие.Значение.ВидОбъекта] + текСобытие.Значение.КоличествоСотрудников;
				Если текСобытие.Значение.ВидОбъекта = "РКД" ИЛИ текСобытие.Значение.ВидОбъекта = "ПКД" Тогда
					стДанные.Конструирование = стДанные.Конструирование + текСобытие.Значение.КоличествоСотрудников	
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		Для каждого данныеСтроки Из стСтроки Цикл
			
			Ключ = данныеСтроки.Ключ;
			
			
			стПараметровОбласти.ЦветФона 	= ПолучитьЦветЭтапа(Ключ, стЦвета);
			стПараметровОбласти.Текст 		= стДанные[Ключ];
			стПараметровОбласти.НомерСтроки = стСтроки[Ключ];
			стПараметровОбласти.НомерКолонки = х + смещениеКолонок;
			омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти);
			
			стМакс[Ключ] = Макс(стМакс[Ключ], стДанные[Ключ]); 
			
			
		КонецЦикла;
		
		стПараметровОбласти.ЦветФона 	= ПолучитьЦветЭтапа(Ключ, стЦвета);
		стПараметровОбласти.Текст 		= стДанные[Ключ];
		стПараметровОбласти.НомерСтроки = стСтроки[Ключ];
		стПараметровОбласти.НомерКолонки = х + смещениеКолонок;
		омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти);
		
		ОбнулитьИтогДня(стДанные);
	КонецЦикла; 
	
	стПараметровОбласти.ДлинаОбласти 			= 2;
	стПараметровОбласти.НомерКолонки 			= 2;
	стПараметровОбласти.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
	
	Для каждого х Из стСтроки Цикл
		стПараметровОбласти.ЦветФона 	= ПолучитьЦветЭтапа(х.Ключ, стЦвета);
		стПараметровОбласти.Текст 		= х.Ключ + " (" + стМакс[х.Ключ] + ")";
		стПараметровОбласти.НомерСтроки = стСтроки[х.Ключ];
		омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти); 
	КонецЦикла;
	
	

КонецПроцедуры // ВывестиИтоги(МаксимальныйНомерДаты) 

&НаКлиенте
Процедура ОбнулитьИтогДня(стДанные = Неопределено)

	Если стДанные = Неопределено Тогда
		стДанные = Новый Структура("ТЗ, РКД, ПКД, Производство, Монтаж, Конструирование"
									, 0, 0, 0, 0, 0, 0);
	Иначе
		стДанные.ТЗ 				= 0;
		стДанные.РКД 				= 0;
		стДанные.ПКД 				= 0;
		стДанные.Производство 		= 0;
		стДанные.Монтаж 			= 0;
		стДанные.Конструирование 	= 0;
	КонецЕсли;
	
	
КонецПроцедуры // ОбнулитьИтогДня(стДанные)()

&НаКлиенте
Процедура ВыводОбъекта(НайденыеСтроки, текКомплект, КешЦвета, стПараметровОбласти, смещениеКолонок, стЦвета = Неопределено)
	Для каждого текДата Из найденыеСтроки Цикл
		Если текКомплект.СтрокаПроекта > 0 И ТипЗнч(текДата.СтруктураДанных) = Тип("Структура") Тогда
			стПараметровОбласти.НомерСтроки 	= текКомплект.СтрокаПроекта;	
			стПараметровОбласти.НомерКолонки 	= текДата.НомерДаты + смещениеКолонок;
			омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти, КешЦвета); 
		Иначе
			КешЦвета 				= Неопределено;
			Если текДата.СтруктураДанных.Количество() = 0 Тогда
				Продолжить;
			ИначеЕсли текДата.СтруктураДанных.Количество() > 1 Тогда
				стПараметровОбласти.ЦветФона 		= стЦвета.Пересечение;
			Иначе	
				стПараметровОбласти.ЦветФона 		= ПолучитьЦветЭтапа(текДата.СтруктураДанных[0].Значение.ВидОбъекта, стЦвета);
			КонецЕсли;
			стПараметровОбласти.НомерСтроки 	= текКомплект.СтрокаКомплект;	
			стПараметровОбласти.НомерКолонки 	= текДата.НомерДаты + смещениеКолонок;
			омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти, КешЦвета); 
		КонецЕсли;
		
	КонецЦикла;	
	
	
	
КонецПроцедуры // ВыводОбъекта()  

&НаКлиенте
Функция ПолучитьЦветЭтапа(Этап, стЦвета)

	Возврат стЦвета[Этап];	

КонецФункции // ПолучитьЦветЭтапа()

&НаКлиенте
Функция ПолучитьИзДатыКлюч(текДата)

	Возврат "_" + Формат(День(текДата),"ЧЦ=2; ЧВН=; ЧГ=0") + Формат(Месяц(текДата),"ЧЦ=2; ЧВН=; ЧГ=0") + Формат(Год(текДата),"ЧЦ=4; ЧВН=; ЧГ=0");	

КонецФункции // ПолучитьИздатыКлюч(Дата)()

&НаКлиенте
Процедура ВывестиШапку()
	
	стПараметровОбласти = омТабличныйДокументКлиентСервер.ПолучитьСтруктуруПараметров();
	стПараметровОбласти.НомерКолонки 	= 2;
	стПараметровОбласти.НомерСтроки 	= 1;
	стПараметровОбласти.ВысотаОбласти 	= 2;
	стПараметровОбласти.Полужирный 		= Истина;
	стПараметровОбласти.Текст 			= "Комплекты"; 
	
	КешЦвета = Неопределено;
	омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти, КешЦвета);
	
	стПараметровОбласти.НомерКолонки 	= 3;
	стПараметровОбласти.Текст 			= "Конструктор"; 
	омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти, КешЦвета);
	
	НомерКолонки = 4;
	стПараметровОбласти.РазмерШрифта 	= 6;
	стПараметровОбласти.НомерСтроки 	= 2;
	стПараметровОбласти.ВысотаОбласти 	= 1;
	
	стПараметровОбластиМесяц = омТабличныйДокументКлиентСервер.ПолучитьСтруктуруПараметров();
	стПараметровОбластиМесяц.Полужирный 	= Истина;
	стПараметровОбластиМесяц.НомерКолонки 	= НомерКолонки;
	
	НачалоКолонки = НомерКолонки;
	НомерДаты = -1;
	
	стПараметровОбластиРамка = омТабличныйДокументКлиентСервер.ПолучитьСтруктуруПараметров();
	стПараметровОбластиРамка.ЦветРамки 		= "230230230";
	стПараметровОбластиРамка.Объединять 	= Ложь;
	КоличествоСтрок = ПолучитьВысотуТаблицы();
	
	
	Если тзДаты.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли; 
	
	текМесяц = тзДаты[0].Месяц;
	КешЦветаРамка = Неопределено;
	
	Для каждого х Из тзДаты Цикл
		
		
		Если НЕ текМесяц = х.Месяц Тогда
			стПараметровОбластиМесяц.ДлинаОбласти 	= НомерКолонки - НачалоКолонки;		
			стПараметровОбластиМесяц.Текст 			= спМесяцы.НайтиПоЗначению(Месяц(текМесяц)).Представление; 
			омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбластиМесяц, КешЦвета);
			
			стПараметровОбластиМесяц.НомерКолонки 	= НомерКолонки;
			НачалоКолонки 							= НомерКолонки;  
			текМесяц = х.Месяц;
		КонецЕсли;

		Если НомерДаты = х.НомерДаты Тогда
		
			Продолжить;	
		
		КонецЕсли;
		
		стПараметровОбласти.НомерКолонки 	= НомерКолонки;
		стПараметровОбласти.Текст 			= ?(НеОтображатьЧисла,"",х.Представление);
		
		омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбласти, КешЦвета);
		
		НомерДаты = х.НомерДаты;
		
		стПараметровОбластиРамка.НомерКолонки 	= НомерКолонки;
		стПараметровОбластиРамка.НомерСтроки 	= 3;
		стПараметровОбластиРамка.ВысотаОбласти  = КоличествоСтрок;
		
		омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбластиРамка, КешЦветаРамка);
		
		НомерКолонки = НомерКолонки + 1;
	КонецЦикла;
	
	Если НЕ НомерКолонки = НачалоКолонки Тогда
		стПараметровОбластиМесяц.ДлинаОбласти 	= НомерКолонки - НачалоКолонки;		
		стПараметровОбластиМесяц.Текст 			= спМесяцы.НайтиПоЗначению(Месяц(текМесяц)).Представление; 
		омТабличныйДокументКлиентСервер.СформироватьОбластьТабличногоДокумента(ПолеВывода, стПараметровОбластиМесяц, КешЦвета);
	КонецЕсли;
	
	ПолеВывода.ФиксацияСверху = 2;

КонецПроцедуры // ВывестиШапку()


Функция ПолучитьВысотуТаблицы()

	мПроекты 	= Новый Массив;
	мРодитель 	= Новый Массив;
	
	Для каждого х Из тзКомплекты Цикл
		Если мПроекты.Найти(х.Проект) = Неопределено Тогда
			мПроекты.Добавить(х.Проект);
		КонецЕсли;
		Если мРодитель.Найти(х.Родитель) = Неопределено Тогда
			мРодитель.Добавить(х.Родитель);
		КонецЕсли;
	КонецЦикла;
	
    Возврат тзКомплекты.Количество() + мПроекты.Количество() + мРодитель.Количество() + 2;
	
КонецФункции // ПолучитьВысотуТаблицы()


&НаКлиенте
Функция УстановитьШиринуКолонок()
	
	Если ЭтаФорма.Интервалы = 0 Тогда
		КоличествоИнтервалов = стКоэффициенты.КоличествоДней / 7 + 2;
		КоличествоИнтервалов = ?(КоличествоИнтервалов - Цел(КоличествоИнтервалов) = 0, КоличествоИнтервалов, Цел(КоличествоИнтервалов) + 1);
	Иначе
		КоличествоИнтервалов = стКоэффициенты.КоличествоДней; 
	КонецЕсли;
	
	стКоэффициенты.КоличествоИнтервалов = КоличествоИнтервалов;
	
	мШиринаКолонок = Новый Массив(КоличествоИнтервалов + 3);
	мШиринаКолонок[0] = 2;
	мШиринаКолонок[1] = 40;
	мШиринаКолонок[2] = 12;
	
	ШиринаКолонок = (стКоэффициенты.КоличествоМиллиметровВПикселе * стКоэффициенты.ШиринаЭкрана / (КоличествоИнтервалов * 3)) * (МасштабНаПечать / 100) * 1.2; 
	
	ШиринаКолонок = ?(Интервалы = 0, 1, 4) * ШиринаКолонок;
	Для х = 3 По КоличествоИнтервалов + 2 Цикл
		мШиринаКолонок[х] = ШиринаКолонок;
	КонецЦикла;
	
	омТабличныйДокументКлиентСервер.УстановитьШиринуКолонок(ЭтаФорма.ПолеВывода, мШиринаКолонок);
	
КонецФункции // УстановитьШиринуКолонок()

&НаКлиенте
Процедура НеОтображатьЧислаПриИзменении(Элемент)
	ВывестиДиаграмму();
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалыПриИзменении(Элемент)
	ВывестиДиаграмму();
КонецПроцедуры

Функция ЗаполнитьТаблицыДанных(стИнтервал)

	тзДаты.Очистить();
	тзВывод.Очистить();
	
	ДатаНачала   	= стИнтервал.ДатаНачала;
	ДатаОкончания 	= стИнтервал.ДатаОкончания;
	
	Если НЕ (ЗначениеЗаполнено(ДатаНачала) И (ЗначениеЗаполнено(ДатаОкончания))) Тогда
		Возврат 0;	
	КонецЕсли;
	
	МаксимальныйНомерДаты = ЗаполнитьТаблицуДата(ДатаНачала, ДатаОкончания);
	
	ЗаполнитьТаблицуВывода();
	
	Возврат МаксимальныйНомерДаты;
	
КонецФункции // ЗаполнитьТаблицыДанных()

Процедура ЗаполнитьТаблицуВывода()
	
	стОтбор 				= Новый Структура("НомерДаты, ОбъектДанных", );
	стОтборДата 			= Новый Структура("Дата", );
	КоличествоСекундВДне 	= 24 * 3600;
	
	мКешСделок 				= Новый Массив;
	
	Для каждого строка_тзЗадач Из ТЗЗадачи Цикл
		ЗаполнитьПроект(строка_тзЗадач, стОтбор, стОтборДата, КоличествоСекундВДне, мКешСделок);
		ЗаполнитьКомплект(строка_тзЗадач, стОтбор, стОтборДата, КоличествоСекундВДне);	
	КонецЦикла;
	
	ДоЗаполнитьПроекты(мКешСделок, стОтборДата, стОтбор);
	
КонецПроцедуры // ЗаполнитьТаблицуВывода()

Процедура ДоЗаполнитьПроекты(мКешСделок, стОтборДата, стОтбор)

	Для каждого данныеСдеки Из мКешСделок Цикл
		СтруктураДанных = Новый Структура("ВидОбъекта, ЗначениеОбъекта","Проект", данныеСдеки.Проект);
		
		стОтборДата.Дата 	= данныеСдеки.ДатаМинимум;
		НомерДатыМин 		= тзДаты.НайтиСтроки(стОтборДата)[0].НомерДаты;
		
		стОтборДата.Дата 	= данныеСдеки.ДатаМаксимум;
		НомерДатыМакс 		= тзДаты.НайтиСтроки(стОтборДата)[0].НомерДаты;
		
		стОтбор.ОбъектДанных = данныеСдеки.Проект;
		
		Для НомерДаты = НомерДатыМин По НомерДатыМакс Цикл
			стОтбор.НомерДаты = НомерДаты; 
			найденыеСтроки = тзВывод.НайтиСтроки(стОтбор);
			
			Если найденыеСтроки.Количество() = 0 Тогда
				НС = тзВывод.Добавить();
				НС.НомерДаты 	= НомерДаты;
				НС.ОбъектДанных = стОтбор.ОбъектДанных;
				НС.СтруктураДанных = Новый Структура("ВидОбъекта, ЗначениеОбъекта", "Проект", стОтбор.ОбъектДанных);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;		
	
КонецПроцедуры // ДоЗаполнитьПроекты(мКешСделок)

Процедура ЗаполнитьКомплект(строка_тзЗадач, стОтбор, стОтборДата, КоличествоСекундВДне)
	
	стОтбор.ОбъектДанных = строка_тзЗадач.Комплект;

	датаНачала = строка_тзЗадач.ДатаНачалаПлан;
	
	Если датаНачала < тзДаты[0].Дата Тогда
		датаНачала = тзДаты[0].Дата;	
	КонецЕсли;
	
	Если строка_тзЗадач.ДатаОкончанияПлан > Дата(1, 1, 1) Тогда
		
		Пока датаНачала <= строка_тзЗадач.ДатаОкончанияПлан Цикл
			стОтборДата.Дата = датаНачала;
			НомерДаты = тзДаты.НайтиСтроки(стОтборДата)[0].НомерДаты;
			стОтбор.НомерДаты = НомерДаты;
			
			найденыеСтроки = тзВывод.НайтиСтроки(стОтбор);
			
			Если найденыеСтроки.Количество() = 0 Тогда
				
				НС = тзВывод.Добавить();
				НС.НомерДаты 	= НомерДаты;
				НС.ОбъектДанных = стОтбор.ОбъектДанных;
				спДанные = Новый СписокЗначений;
				НС.СтруктураДанных = спДанные;
			Иначе
				НС = найденыеСтроки[0];	
			КонецЕсли;
			
			ДанныеУжеЗаполнены = Ложь;
			
			Для каждого текСписок Из НС.СтруктураДанных Цикл
				Если текСписок.Значение.ВидОбъекта = строка_тзЗадач.Этап Тогда
					ДанныеУжеЗаполнены = Истина; 
					текСписок.Значение.КоличествоСотрудников = текСписок.Значение.КоличествоСотрудников + строка_тзЗадач.КоличествоСотрудников;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если НЕ ДанныеУжеЗаполнены Тогда
				стДанных = Новый Структура("ВидОбъекта, ЗначениеОбъекта, КоличествоСотрудников", строка_тзЗадач.Этап, строка_тзЗадач.Комплект, строка_тзЗадач.КоличествоСотрудников);
				
				НС.СтруктураДанных.Добавить(стДанных); 
			КонецЕсли;
			
			датаНачала = датаНачала + КоличествоСекундВДне;
		КонецЦикла;
		
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьКомплект(строка_тзЗадач, стОтбор, стОтборДата, КоличествоСекундВДне)

Процедура ЗаполнитьПроект(строка_тзЗадач, стОтбор, стОтборДата, КоличествоСекундВДне, мКешСделок)
	
	стОтбор.ОбъектДанных = строка_тзЗадач.Проект;
	
	КэшНайден = Ложь;
	Для каждого х Из мКешСделок Цикл
		Если х.Проект = стОтбор.ОбъектДанных Тогда
			КэшНайден 		= Истина;
			ЭлементМассива 	= х;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ КэшНайден Тогда
		ЭлементМассива 	= Новый Структура("Проект, ДатаМинимум, ДатаМаксимум",стОтбор.ОбъектДанных, Дата(1,1,1), Дата(1,1,1));
		мКешСделок.Добавить(ЭлементМассива);
	КонецЕсли;
	
	датаНачала = строка_тзЗадач.ДатаНачалаПлан;
	
	Если ЭлементМассива.ДатаМинимум = Неопределено И ЗначениеЗаполнено(датаНачала) Тогда
		ЭлементМассива.ДатаМинимум = датаНачала;
	ИначеЕсли ЭлементМассива.ДатаМинимум > датаНачала ИЛИ НЕ ЗначениеЗаполнено(ЭлементМассива.ДатаМинимум) Тогда	
		ЭлементМассива.ДатаМинимум = датаНачала;
	КонецЕсли;
	
	Если ЭлементМассива.ДатаМаксимум < строка_тзЗадач.ДатаОкончанияПлан Тогда
		ЭлементМассива.ДатаМаксимум = строка_тзЗадач.ДатаОкончанияПлан;
	КонецЕсли;
	
	Если тзДаты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если датаНачала < тзДаты[0].Дата Тогда
		датаНачала = тзДаты[0].Дата;	
	КонецЕсли;
	
	Если строка_тзЗадач.ДатаОкончанияПлан > Дата(1,1,1) Тогда
		
		Пока датаНачала <= строка_тзЗадач.ДатаОкончанияПлан Цикл
			
			стОтборДата.Дата = датаНачала;
			НомерДаты = тзДаты.НайтиСтроки(стОтборДата)[0].НомерДаты;
			стОтбор.НомерДаты = НомерДаты;
			
			найденыеСтроки = тзВывод.НайтиСтроки(стОтбор);
			
			Если найденыеСтроки.Количество() = 0 Тогда
				
				НС = тзВывод.Добавить();
				НС.НомерДаты 	= НомерДаты;
				НС.ОбъектДанных = стОтбор.ОбъектДанных;
				НС.СтруктураДанных = Новый Структура("ВидОбъекта, ЗначениеОбъекта","Проект", стОтбор.ОбъектДанных);
			КонецЕсли;
			
			датаНачала = датаНачала + КоличествоСекундВДне;
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры // ЗаполнитьПроект(строка_тзЗадач, стОтбор, стОтборДата, КоличествоСекундВДне, мКешСделок)

Функция ЗаполнитьТаблицуДата(ДатаНачала, ДатаОкончания)

	КоличествоСекундВДне = 24 * 3600; 
	текНомерДаты = 1;
	Если Интервалы = 0 Тогда
		
		Смещение = (ДатаНачала - НачалоНедели(ДатаНачала)) / КоличествоСекундВДне;
	
		ДатаНачала = ДатаНачала - КоличествоСекундВДне * Смещение;
		Представление = День(ДатаНачала);
		
		текСч = 1;
		Пока ДатаНачала <= ДатаОкончания Цикл
			НС = тзДаты.Добавить();
			НС.Дата 			= ДатаНачала;
			НС.НомерДаты 		= текНомерДаты;
			НС.Представление 	= Представление;
			НС.Месяц 			= НачалоМесяца(НС.Дата);
			
			ДатаНачала = ДатаНачала + КоличествоСекундВДне;
			текСч = текСч + 1;
			
			Если текСч = 8 Тогда
				текСч = 1;
				текНомерДаты = текНомерДаты + 1;
				Представление = День(ДатаНачала);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Пока ДатаНачала <= ДатаОкончания Цикл
			НС = тзДаты.Добавить();
			НС.Дата 			= ДатаНачала;
			НС.НомерДаты 		= текНомерДаты;
			НС.Представление 	= День(ДатаНачала);
			НС.Месяц 			= НачалоМесяца(НС.Дата);
			
			ДатаНачала = ДатаНачала + КоличествоСекундВДне;
			текНомерДаты = текНомерДаты + 1;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецФункции // ЗаполнитьТаблицуДата()

