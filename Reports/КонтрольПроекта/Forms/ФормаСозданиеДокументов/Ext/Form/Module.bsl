
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Проект = Параметры.Проект;
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыНаСервере()
	
	ПолеВывода.Очистить();
	
	стДанные = СоздатьПлановаяГотовностьПомещений();
	СоздатьЗадачиСоздатьМодельГабаритногоЧертежа(стДанные);
	
	ВывестиЛог(стДанные);

КонецПроцедуры 

Процедура СоздатьЗадачиСоздатьМодельГабаритногоЧертежа(стДанные)

	стДанные.Вставить("Задачи", Новый Массив);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
			|	ДатыКомплекта.Комплект.ГабаритныйЧертеж КАК ГабаритныйЧертеж,
			|	ДатыКомплекта.НачалоПКД КАК НачалоПКД
			|ПОМЕСТИТЬ втГЧ
			|ИЗ
			|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
			|ГДЕ
			|	ДатыКомплекта.Проект = &Проект
			|	И ДатыКомплекта.НачалоПКД > ДАТАВРЕМЯ(1, 1, 1)
			|	И НЕ ДатыКомплекта.Комплект.ГабаритныйЧертеж = ЗНАЧЕНИЕ(Справочник.ГабаритныеЧертежи.ПустаяСсылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МИНИМУМ(втГЧ.НачалоПКД) КАК НачалоПКД,
			|	втГЧ.ГабаритныйЧертеж КАК ГабаритныйЧертеж
			|ПОМЕСТИТЬ втМинГЧ
			|ИЗ
			|	втГЧ КАК втГЧ
			|
			|СГРУППИРОВАТЬ ПО
			|	втГЧ.ГабаритныйЧертеж
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(СоздатьМодельГабаритногоЧертежа.Ссылка, НЕОПРЕДЕЛЕНО) КАК Задача,
			|	втМинГЧ.ГабаритныйЧертеж КАК ГабаритныйЧертеж,
			|	втМинГЧ.НачалоПКД КАК НачалоПКД,
			|	втМинГЧ.ГабаритныйЧертеж.Проект КАК Проект,
			|	втМинГЧ.ГабаритныйЧертеж.Наименование КАК ГабаритныйЧертежНаименование
			|ИЗ
			|	втМинГЧ КАК втМинГЧ
			|		ЛЕВОЕ СОЕДИНЕНИЕ Задача.СоздатьМодельГабаритногоЧертежа КАК СоздатьМодельГабаритногоЧертежа
			|		ПО втМинГЧ.ГабаритныйЧертеж = СоздатьМодельГабаритногоЧертежа.ГабаритныйЧертеж
			|ГДЕ
			|	ЕСТЬNULL(СоздатьМодельГабаритногоЧертежа.КрайняяДатаИсполнения, ДАТАВРЕМЯ(1, 1, 1)) <> втМинГЧ.НачалоПКД
			|	И НЕ ЕСТЬNULL(СоздатьМодельГабаритногоЧертежа.Выполнена, ЛОЖЬ)";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Выборка = Запрос.Выполнить().Выбрать();

	Исполнитель = Неопределено;
	
	ЗадачаСсылка = Неопределено;
	
	Пока Выборка.Следующий() Цикл
		
		Если Исполнитель = Неопределено И Выборка.Задача = Неопределено Тогда
			Исполнитель = омЗадачи.ПолучитьИсполнителя(Перечисления.ОбъектыЗадач.ПроверятьГабаритныеЧерчежи);
		КонецЕсли;
		
		Описание = СоздатьИзменитьЗадачу(Выборка, Исполнитель, ЗадачаСсылка);
		
		стДанные.Задачи.Добавить(Новый Структура("Документ, Действие", ЗадачаСсылка, Описание));
		
	КонецЦикла;
	
КонецПроцедуры 

Функция СоздатьИзменитьЗадачу(Выборка, Исполнитель, ЗадачаСсылка)

	Если Выборка.Задача = Неопределено Тогда
	
		Задача = Задачи.СоздатьМодельГабаритногоЧертежа.СоздатьЗадачу();
		Задача.ГабаритныйЧертеж = Выборка.ГабаритныйЧертеж;
		Задача.Дата 			= ТекущаяДата();
		Задача.Исполнитель 		= Исполнитель;
		Задача.Проект 			= Выборка.Проект;
		Задача.Наименование 	= Выборка.ГабаритныйЧертежНаименование + ".Создание";

	Иначе
	
		Задача = Выборка.Задача.ПолучитьОбъект();	
	
	КонецЕсли;
	
	Задача.КрайняяДатаИсполнения = Выборка.НачалоПКД;
	
    Задача.Записать();
	
	ЗадачаСсылка = Задача.Ссылка;
	
	Возврат ?(Выборка.Задача = Неопределено, "Создана ", "Изменена ") + Задача.Наименование
			+ ". Крайняя дата " + Формат(Выборка.НачалоПКД, "ДФ=dd.MM.yy");
	
	
КонецФункции // СоздатьИзменитьЗадачу(Выборка, Исполнитель)

Функция СоздатьПлановаяГотовностьПомещений()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МИНИМУМ(ЕСТЬNULL(ДатыКомплекта.НачалоРКД, ДАТАВРЕМЯ(1, 1, 1))) КАК НачалоРКД,
		|	Помещения.Ссылка КАК Помещение
		|ИЗ
		|	Справочник.Помещения КАК Помещения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИзделияКомплект.ИзделияЭлемент КАК ИзделияКомплектИзделияЭлемент
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|			ПО ИзделияКомплектИзделияЭлемент.Ссылка = ДатыКомплекта.Комплект
		|				И (ЕСТЬNULL(ДатыКомплекта.НачалоРКД, ДАТАВРЕМЯ(1, 1, 1)) > ДАТАВРЕМЯ(1, 1, 1))
		|		ПО (ИзделияКомплектИзделияЭлемент.ИзделиеЭлемент.Помещения = Помещения.Ссылка)
		|ГДЕ
		|	Помещения.Проект = &Проект
		|	И НЕ Помещения.ЭтоГруппа
		|
		|СГРУППИРОВАТЬ ПО
		|	Помещения.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Помещения.НомерПомещения,
		|	Помещения.Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановаяГотовностьПомещений.Ссылка КАК Ссылка,
		|	ПлановаяГотовностьПомещений.Утвержден КАК Утвержден
		|ИЗ
		|	Документ.ПлановаяГотовностьПомещений КАК ПлановаяГотовностьПомещений
		|ГДЕ
		|	ПлановаяГотовностьПомещений.Проект = &Проект
		|	И ПлановаяГотовностьПомещений.АвтоматическоеСоздание";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[1].Выбрать();
	
	обПлановаяГотовностьПомещений = Неопределено; 
	УтвержденПлановаяГотовностьПомещений = Ложь;
	
	Пока Выборка.Следующий() Цикл
		обПлановаяГотовностьПомещений 			= Выборка.Ссылка.ПолучитьОбъект();
		УтвержденПлановаяГотовностьПомещений 	= Выборка.Утвержден;
	КонецЦикла; 
	
	НужнаЗапись 	= Ложь;
	
	Если обПлановаяГотовностьПомещений = Неопределено Тогда
		
		обПлановаяГотовностьПомещений 							= Документы.ПлановаяГотовностьПомещений.СоздатьДокумент();
		обПлановаяГотовностьПомещений.Автор 					= ПараметрыСеанса.ТекущийПользователь;
		обПлановаяГотовностьПомещений.Дата						= ТекущаяДата();
		обПлановаяГотовностьПомещений.АвтоматическоеСоздание 	= Истина;
		обПлановаяГотовностьПомещений.Проект					= Проект;
		
		ДействиеДокумента = "Документ создан";
		
		НовыйДокумент 	= Истина;
	
	Иначе
		
		НовыйДокумент 	= Ложь;
		ДействиеДокумента = "Документ найден";
		
	КонецЕсли;
	
	Выборка = Результат[0].Выбрать();
	
	соДействиеКомплекта = Новый Соответствие;
	
	стОтбор = Новый Структура("Помещение", );
	
	Пока Выборка.Следующий() Цикл
		
		стОтбор.Помещение = Выборка.Помещение;
		
		НайденыеСтроки = обПлановаяГотовностьПомещений.Помещения.НайтиСтроки(стОтбор);
		
		Если НайденыеСтроки.Количество() = 0 Тогда
			
			НС = обПлановаяГотовностьПомещений.Помещения.Добавить();
			
			НС.Помещение 		= Выборка.Помещение;
			НС.ДатаГотовности 	= Выборка.НачалоРКД;
			
			соДействиеКомплекта.Вставить(Выборка.Помещение
				, "Дата добавлена " + Формат(Выборка.НачалоРКД, "ДФ=dd.MM.yy"));	
			
			НужнаЗапись = Истина; 
		Иначе
			Для каждого х Из НайденыеСтроки Цикл
				
				СтрокаСДатами = "Текущая дата " + Формат(х.ДатаГотовности, "ДФ=dd.MM.yy") + ", начало РКД " + Формат(Выборка.НачалоРКД, "ДФ=dd.MM.yy"); 
				
				Если х.ДатаГотовности = Выборка.НачалоРКД Тогда
					Продолжить;
				КонецЕсли;
				
				Если УтвержденПлановаяГотовностьПомещений Тогда
					соДействиеКомплекта.Вставить(Выборка.Помещение
						, "Дата не изменена. " + СтрокаСДатами);	
				Иначе
					
					х.ДатаГотовности = Выборка.НачалоРКД;
					соДействиеКомплекта.Вставить(Выборка.Помещение
						, "Дата изменена. " + СтрокаСДатами);	
					
					НужнаЗапись = Истина; 
					
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		
	КонецЦикла;
	
	Если НужнаЗапись Тогда
	
		обПлановаяГотовностьПомещений.Записать(РежимЗаписиДокумента.Проведение);	
	
	КонецЕсли;
	
	Возврат Новый Структура("ПлановаяГотовностьПомещений", 
		Новый Структура("ДействиеДокумента, соДействиеКомплекта, ПлановаяГотовностьПомещений", ДействиеДокумента, соДействиеКомплекта, обПлановаяГотовностьПомещений.Ссылка));
	
КонецФункции // СоздатьПлановаяГотовностьПомещений()

Процедура ВывестиЛог(стДанные)

    стПлановаяГотовностьПомещений = стДанные.ПлановаяГотовностьПомещений;
	
	Макет = Отчеты.КонтрольПроекта.ПолучитьМакет("МакетСозданиеДокументов");
	
	ПолеВывода.Вывести(Макет.ПолучитьОбласть("Шапка"));
	
	обСтрока = Макет.ПолучитьОбласть("Строка");
	
	ПолеВывода.НачатьАвтогруппировкуСтрок();
	обСтрока.Параметры.Документ = стПлановаяГотовностьПомещений.ПлановаяГотовностьПомещений;
	обСтрока.Параметры.Действие = стПлановаяГотовностьПомещений.ДействиеДокумента;
	ПолеВывода.Вывести(обСтрока, 1);                                              
	
	соДействиеКомплекта = стПлановаяГотовностьПомещений.соДействиеКомплекта;
	
	Для каждого х Из соДействиеКомплекта Цикл
		обСтрока.Параметры.Документ = х.Ключ;
		обСтрока.Параметры.Действие = х.Значение;
		ПолеВывода.Вывести(обСтрока, 2);                                              
	КонецЦикла;
	
	ПолеВывода.ЗакончитьАвтогруппировкуСтрок(); 
	
	Для каждого х Из стДанные .Задачи Цикл
		обСтрока.Параметры.Заполнить(х);
		ПолеВывода.Вывести(обСтрока);                                             	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	СоздатьДокументыНаСервере();
КонецПроцедуры
