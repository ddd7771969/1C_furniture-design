
&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	СформироватьОтчетНаСервере();
КонецПроцедуры 

&НаКлиенте
Процедура Обновить(Команда)
	СформироватьОтчетНаСервере();
КонецПроцедуры

Процедура СформироватьОтчетНаСервере()
	
	ПолеВывода.Очистить();
	
	Если Не ЗначениеЗаполнено(Проект) Тогда
		Возврат;	
	КонецЕсли;
	
	
	стТаблицы 	= ПолучитьТаблицуДанных();
	
	ВывестиДанные(стТаблицы);
КонецПроцедуры // СформироватьОтчетНаСервере()

Функция ПолучитьТаблицуДанных()

	тзДанныеЗапрос 	= ЗаполнитьКомплектыИзделий();
	
	тзДанные = РассчитатьПоля(тзДанныеЗапрос);
	
	Возврат Новый Структура("тзДанные", тзДанные);
	
КонецФункции // ПолучитьТаблицуДанных()     

Функция РассчитатьПоля(тзДанныеЗапрос)                                   

	тзДанные = Новый ТаблицаЗначений;
	тзДанные.Колонки.Добавить("Номер");
    тзДанные.Колонки.Добавить("КомплектКомпозиция");
    тзДанные.Колонки.Добавить("КомплектКомпозицияНаименование");
    тзДанные.Колонки.Добавить("Помещение");
    тзДанные.Колонки.Добавить("ПомещениеНаименование");
    тзДанные.Колонки.Добавить("СтатусПомещения");
    тзДанные.Колонки.Добавить("Согласовано");
    тзДанные.Колонки.Добавить("Приоритет");
    тзДанные.Колонки.Добавить("Состояние");
    тзДанные.Колонки.Добавить("Запас");
    тзДанные.Колонки.Добавить("КрайПроектирование");
    тзДанные.Колонки.Добавить("КрайПроизводство");
    тзДанные.Колонки.Добавить("КрайМонтаж");
    тзДанные.Колонки.Добавить("Договор");
    тзДанные.Колонки.Добавить("Кон");
    тзДанные.Колонки.Добавить("Диз");
    тзДанные.Колонки.Добавить("Согл");
    тзДанные.Колонки.Добавить("ЭтоКомпозиция");
	
	НомерКомпозиции 	= 0;
	Номер			 	= 1; 
	
	мКомплект = Новый Массив;
	
	текущаяКомпозиция = Неопределено;
	Для каждого х Из тзДанныеЗапрос Цикл
		
		Если НЕ текущаяКомпозиция = х.Композиция И ЗначениеЗаполнено(х.Композиция) Тогда
			
			Номер = 1;  
			
			НС = тзДанные.Добавить();
			
			НомерКомпозиции 					= НомерКомпозиции + 1;
			текущаяКомпозиция 					= х.Композиция;	
			НС.Номер 							= НомерКомпозиции;
			НС.КомплектКомпозиция 				= х.Композиция;
			НС.КомплектКомпозицияНаименование 	= х.КомпозицииНаименование;
			НС.Помещение 						= х.ПомещениеКомпозиции;
			НС.ПомещениеНаименование		 	= х.ПомещениеКомпозицииНаименование;
			НС.Приоритет 						= х.ПриоритетКомпозиция;
			НС.Состояние		 				= х.ТекущаяСтадияКомпозиция;
			НС.КрайПроектирование 				= х.ВнешняяДатаОкончаниеПроектированияКомпозиции;
			НС.ЭтоКомпозиция					= Истина;
			
			мКомплект.Добавить(х.Композиция);
			
		КонецЕсли;
		
		НС = тзДанные.Добавить();
		
		НС.Номер 							= ?(ЗначениеЗаполнено(х.Композиция), Строка(НомерКомпозиции) + "." + Номер, Номер);
		НС.КомплектКомпозиция 				= х.ИзделиеКомплект;
		НС.КомплектКомпозицияНаименование 	= ?(ЗначениеЗаполнено(х.Композиция), "   ", "") + х.КомплектНаименование;
		НС.Помещение 						= х.Помещение;
		НС.ПомещениеНаименование		 	= х.ПомещениеНаименование;
		НС.Приоритет 						= х.Приоритет;
		НС.Состояние		 				= х.ТекущаяСтадия;
		НС.КрайПроектирование 				= х.ВнешняяДатаОкончаниеПроектирования;
		НС.КрайПроизводство 				= х.ВнешняяДатаОкончаниеПроизводства;
		НС.КрайМонтаж 						= х.ВнешняяДатаОкончаниеМонтажа;
		НС.ЭтоКомпозиция					= Ложь;
		
		Номер = Номер + 1;
		
		мКомплект.Добавить(х.ИзделиеКомплект);
		
	КонецЦикла;
	
	тзДанные.Индексы.Добавить("КомплектКомпозиция");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(ДанныеКомплекта.Период) КАК Период,
		|	ДанныеКомплекта.Комплект КАК Комплект
		|ИЗ
		|	РегистрСведений.ДанныеКомплекта КАК ДанныеКомплекта
		|ГДЕ
		|	ДанныеКомплекта.Комплект В(&Комплект)
		|	И ДанныеКомплекта.ТекущаяСтадия = &ТекущаяСтадия
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеКомплекта.Комплект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ПриложенияКДоговорамИзделияЗаказчика.Ссылка.ДатаДоговора) КАК ДатаДоговора,
		|	ИзделияКомплектИзделияЭлемент.Ссылка КАК Комплект
		|ИЗ
		|	Справочник.ПриложенияКДоговорам.ИзделияЗаказчика КАК ПриложенияКДоговорамИзделияЗаказчика
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИзделияЗаказчика.ИзделияЭлемент КАК ИзделияЗаказчикаИзделияЭлемент
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИзделияКомплект.ИзделияЭлемент КАК ИзделияКомплектИзделияЭлемент
		|			ПО ИзделияЗаказчикаИзделияЭлемент.ИзделиеЭлемент = ИзделияКомплектИзделияЭлемент.ИзделиеЭлемент
		|		ПО ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика = ИзделияЗаказчикаИзделияЭлемент.Ссылка
		|ГДЕ
		|	ИзделияКомплектИзделияЭлемент.Ссылка В(&Комплект)
		|
		|СГРУППИРОВАТЬ ПО
		|	ИзделияКомплектИзделияЭлемент.Ссылка";
	
	Запрос.УстановитьПараметр("Комплект", мКомплект);
	Запрос.УстановитьПараметр("ТекущаяСтадия", омНастройкаПовтор.ПолучитьСтадиюСогласования());
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[0].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		тзДанные.Найти(Выборка.Комплект, "КомплектКомпозиция").Согласовано = Выборка.Период; 
		
	КонецЦикла;
	
	Выборка = Результат[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		тзДанные.Найти(Выборка.Комплект, "КомплектКомпозиция").Договор = Выборка.ДатаДоговора; 
	КонецЦикла;
	
	Возврат тзДанные;
	
КонецФункции

Функция ЗаполнитьКомплектыИзделий()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК ИзделиеКомплект,
		|	ВЫБОР
		|		КОГДА ИзделияКомплект.Родитель.Композиция ЕСТЬ NULL
		|			ТОГДА """"
		|		ИНАЧЕ ИзделияКомплект.Родитель.Композиция.Наименование
		|	КОНЕЦ КАК КомпозицииНаименование,
		|	ВЫБОР
		|		КОГДА ИзделияКомплект.Родитель.Композиция ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Композиции.ПустаяСсылка)
		|		ИНАЧЕ ИзделияКомплект.Родитель.Композиция
		|	КОНЕЦ КАК Композиция,
		|	ВЫБОР
		|		КОГДА ИзделияКомплект.Родитель.Композиция ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Помещения.ПустаяСсылка)
		|		ИНАЧЕ ИзделияКомплект.Родитель.Композиция.ОсновноеПомещение
		|	КОНЕЦ КАК ПомещениеКомпозиции,
		|	ВЫБОР
		|		КОГДА ИзделияКомплект.Родитель.Композиция.ОсновноеПомещение ЕСТЬ NULL
		|			ТОГДА """"
		|		ИНАЧЕ ИзделияКомплект.Родитель.Композиция.ОсновноеПомещение.Наименование
		|	КОНЕЦ КАК ПомещениеКомпозицииНаименование,
		|	ИзделияКомплект.ОсновноеПомещение КАК Помещение,
		|	ВЫБОР
		|		КОГДА ИзделияКомплект.ОсновноеПомещение = ЗНАЧЕНИЕ(Справочник.Помещения.ПустаяСсылка)
		|			ТОГДА """"
		|		ИНАЧЕ ИзделияКомплект.ОсновноеПомещение.Наименование
		|	КОНЕЦ КАК ПомещениеНаименование,
		|	ИзделияКомплект.Наименование КАК КомплектНаименование,
		|	ДанныеКомплектаСрезПоследних.Приоритет КАК Приоритет,
		|	ДанныеКомплектаСрезПоследних.ТекущаяСтадия.ПолноеНаименование КАК ТекущаяСтадия,
		|	ДанныеКомплектаСрезПоследних1.Приоритет КАК ПриоритетКомпозиция,
		|	ДанныеКомплектаСрезПоследних1.ТекущаяСтадия.ПолноеНаименование КАК ТекущаяСтадияКомпозиция,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаОкончаниеМонтажа КАК ВнешняяДатаОкончаниеМонтажа,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаОкончаниеПроектирования КАК ВнешняяДатаОкончаниеПроектирования,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаОкончаниеПроизводства КАК ВнешняяДатаОкончаниеПроизводства,
		|	ДанныеКомплектаСрезПоследних1.ВнешняяДатаОкончаниеПроектирования КАК ВнешняяДатаОкончаниеПроектированияКомпозиции
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних КАК ДанныеКомплектаСрезПоследних
		|		ПО (ДанныеКомплектаСрезПоследних.Комплект = ИзделияКомплект.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних КАК ДанныеКомплектаСрезПоследних1
		|		ПО (ДанныеКомплектаСрезПоследних1.Комплект = ИзделияКомплект.Родитель.Композиция)
		|ГДЕ
		|	ИзделияКомплект.Проект = &Проект
		|	И НЕ ИзделияКомплект.ЭтоГруппа
		|
		|УПОРЯДОЧИТЬ ПО
		|	КомпозицииНаименование,
		|	КомплектНаименование";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Возврат Запрос.Выполнить().Выгрузить();
	

КонецФункции // ЗаполнитьКомплектыИзделий()

Процедура ВывестиДанные(стТаблицы)

	
	ПустаяДата = Дата(1,1,1);
	
	Таб = ПолеВывода;
	Таб.Очистить();
	
	Макет = Отчеты.СостояниеПроекта.ПолучитьМакет("Макет");
	
	Таб.Вывести(Макет.ПолучитьОбласть("Шапка"));
	Таб.ФиксацияСверху 	= Таб.ВысотаТаблицы; 
	Таб.ФиксацияСлева	= 3;
	
    обСтрока 		= Макет.ПолучитьОбласть("Строка"); 
    обКомпозиция 	= Макет.ПолучитьОбласть("Композиция"); 
	
	Для каждого текСтрока Из стТаблицы.тзДанные Цикл
		
		текОбласть = ?(текСтрока.ЭтоКомпозиция, обКомпозиция, обСтрока); 
		
		текОбласть.Параметры.Заполнить(текСтрока); 
		
		Таб.Вывести(текОбласть);
	КонецЦикла; 
	
КонецПроцедуры // ВывестиДанные()
