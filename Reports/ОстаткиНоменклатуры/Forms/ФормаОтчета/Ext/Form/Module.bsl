
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если РольДоступна("ЦеныЗакупки") Тогда
		ЭтаФорма.ПоказыватьСуммы = Истина;
	Иначе
	   ЭтаФорма.Элементы.ПоказыватьСуммы.Видимость = Ложь;
   КонецЕсли;
   
   ЭтаФорма.Дата = ТекущаяДата();
   
   СформироватьОтчет();
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчет()
	тзДанные = ПолучитьДанные();
	ВывестиДанные(тзДанные);

КонецПроцедуры // СформироватьОтчет() 

Процедура ВывестиДанные(тзДанные)

	Таб = ЭтаФорма.ПолеВывода;
	Таб.Очистить();
	
	Макет = Отчеты.ОстаткиНоменклатуры.ПолучитьМакет("Макет");
	
	Таб.Вывести(Макет.ПолучитьОбласть("Шапка|Основная"));
	
	обСтрока 	= Макет.ПолучитьОбласть("Строка|Основная"); 
	обСклад 	= Макет.ПолучитьОбласть("Склад|Основная"); 
	обЛГраница  = Макет.ПолучитьОбласть("Строка|ЛеваяГраница"); 
	
	Если ЭтаФорма.ПоказыватьСуммы Тогда
		Таб.Присоединить(Макет.ПолучитьОбласть("Шапка|КолонкаСумма"));
		обСумма = Макет.ПолучитьОбласть("Строка|КолонкаСумма");
		обСуммаС = Макет.ПолучитьОбласть("Склад|КолонкаСумма");
		Если НЕ ЭтаФорма.ОбъектВКолонке И ЭтаФорма.ПоказыватьОбъект Тогда 
			обСуммаО = Макет.ПолучитьОбласть("Объект|КолонкаСумма");
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтаФорма.ОбъектВКолонке Тогда
		Таб.Присоединить(Макет.ПолучитьОбласть("Шапка|КолонкаОбъект"));
		тзДанные.Сортировать("Склад,Номенклатура,Объект");
		обОбъектКолонка = Макет.ПолучитьОбласть("Строка|КолонкаОбъект");
		обОбъектКолонкаС = Макет.ПолучитьОбласть("Склад|КолонкаОбъект");
	ИначеЕсли ЭтаФорма.ПоказыватьОбъект Тогда
		тзДанные.Сортировать("Объект,Склад,Номенклатура"); 
		обОбъект = Макет.ПолучитьОбласть("Объект|Основная");
	Иначе
		тзДанные.Свернуть("Склад,Номенклатура,СтатьяЗатрат,ЕдиницаИзмерения","Количество,СуммаСНДС");
		тзДанные.Сортировать("Склад,Номенклатура"); 
	КонецЕсли;
	
	текОбъкет = Неопределено; 
	текСклад  = Неопределено;
	
	стОтборОбъект 		= Новый Структура("Объект", );
	стОтборОбъектСклад 	= Новый Структура("Объект,Склад", );
	стОтборСклад 		= Новый Структура("Склад", );
	
	Для каждого х Из тзДанные Цикл
		
		Если ЭтаФорма.ПоказыватьОбъект И НЕ ЭтаФорма.ОбъектВКолонке Тогда
			
			Если НЕ текОбъкет = х.Проект Тогда
				текОбъкет = х.Проект;
				обОбъект.Параметры.Объект = х.Проект;
				Таб.Вывести(обОбъект);	
				Если ЭтаФорма.ПоказыватьСуммы Тогда
					стОтборОбъект.Объект = текОбъкет; 
					обСуммаО.Параметры.Сумма = ПолучитьСумму(тзДанные,стОтборОбъект);
					Таб.Присоединить(обСуммаО);
				КонецЕсли;		
				текСклад  = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ текСклад = х.Склад Тогда
			текСклад = х.Склад; 
			обСклад.Параметры.Склад = текСклад;
			Таб.Вывести(обСклад);
			
			Если ЭтаФорма.ПоказыватьСуммы Тогда
				Если ЭтаФорма.ПоказыватьОбъект И НЕ ЭтаФорма.ОбъектВКолонке Тогда
					стОтборОбъектСклад.Объект = текОбъкет;
					стОтборОбъектСклад.Склад = текСклад;
					обСуммаС.Параметры.Сумма = ПолучитьСумму(тзДанные,стОтборОбъектСклад);
				Иначе
					стОтборСклад.Склад = текСклад;
					обСуммаС.Параметры.Сумма = ПолучитьСумму(тзДанные,стОтборСклад);
				КонецЕсли;
				Таб.Присоединить(обСуммаС);
			КонецЕсли;
			Если ЭтаФорма.ОбъектВКолонке Тогда  
				Таб.Присоединить(обОбъектКолонкаС);	
			КонецЕсли;
		КонецЕсли;
		
		обСтрока.Параметры.Заполнить(х);
		Таб.Вывести(обСтрока);
		Если ЭтаФорма.ПоказыватьСуммы Тогда
			обСумма.Параметры.Заполнить(х);
			Таб.Присоединить(обСумма);	
		КонецЕсли;
		Если ЭтаФорма.ОбъектВКолонке Тогда  
			обОбъектКолонка.Параметры.Заполнить(х);
			Таб.Присоединить(обОбъектКолонка);	
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ВывестиДанные()

&НаСервере
Функция ПолучитьСумму(тзДанные,стОтборОбъект)
	
	текСумма = 0;
	
	Для каждого х Из тзДанные.НайтиСтроки(стОтборОбъект) Цикл
	
		текСумма = текСумма + х.СуммаСНДС;	
	
	КонецЦикла;
 	
    Возврат текСумма;
КонецФункции // ПолучитьСумму(тзДанные,стОтборОбъект)()
 
&НаСервере
Функция ПолучитьДанные()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваровНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровНаСкладахОстатки.Склад КАК Склад,
		|	ОстаткиТоваровНаСкладахОстатки.КоличествоОстаток КАК Количество,
		|	ОстаткиТоваровНаСкладахОстатки.СуммаСНДСОстаток КАК СуммаСНДС,
		|	ОстаткиТоваровНаСкладахОстатки.Номенклатура.СтатьяЗатрат КАК СтатьяЗатрат,
		|	ОстаткиТоваровНаСкладахОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровНаСкладах.Остатки(
		|			&Дата,
		|			ВЫБОР
		|					КОГДА &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ Склад = &Склад
		|				КОНЕЦ
		|				И ВЫБОР
		|					КОГДА &СтатьяЗатрат = ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ Номенклатура.СтатьяЗатрат = &СтатьяЗатрат
		|				КОНЕЦ) КАК ОстаткиТоваровНаСкладахОстатки";
	
	Запрос.УстановитьПараметр("Дата", КонецДня(ЭтаФорма.Дата));
	Запрос.УстановитьПараметр("Склад", ЭтаФорма.Склад);
	Запрос.УстановитьПараметр("СтатьяЗатрат", ЭтаФорма.СтатьяЗатрат);
	
	Возврат Запрос.Выполнить().Выгрузить();
	

КонецФункции // ПолучитьДанные()

&НаКлиенте
Процедура Обновить(Команда)
   СформироватьОтчет();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьОбъектПриИзменении(Элемент)
	ЭтаФорма.ОбъектВКолонке = Ложь;
	Элементы.ОбъектВКолонке.Доступность = ЭтаФорма.ПоказыватьОбъект;
КонецПроцедуры


&НаКлиенте
Процедура СделкаПриИзменении(Элемент)
   СформироватьОтчет();
КонецПроцедуры


&НаКлиенте
Процедура СкладПриИзменении(Элемент)
   СформироватьОтчет();
КонецПроцедуры

