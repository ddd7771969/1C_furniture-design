#Область СобытиеРеквизитов

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	ПроектПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти 


#Область СобытиеРеквизитовПродолжение

Процедура ПроектПриИзмененииНаСервере()
	
	ПостроитьДиаграммуГанта(); 
	
КонецПроцедуры 

#КонецОбласти 


#Область ПостроениеяДиаграммы

Процедура ПостроитьДиаграммуГанта() 
	
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	
	Если НЕ ЗначениеЗаполнено(Проект) Тогда
		Возврат;	
	КонецЕсли;

	ПустаяДата = Дата(1,1,1);
	
	тзДатыПроекта 			= омГантНаСервере.ПолучитьДатыПроекта(Проект);
	ПолучитьКомплектыДляДобавления(тзДатыПроекта, ПустаяДата);
	тзИнтервалыПомещения 	= омГантНаСервере.ПолучитьИнтервалыПомещения(тзДатыПроекта);
	тзЦвета 				= омДиаграммаГанта.ПолучитьТаблицуЦветов();
	ДлинаРазрываДляГанта	= Проект.ДлинаРазрываДляГанта;
	
	Серия = Диаграмма.УстановитьСерию("План");

	
	стОтбор = Новый Структура("Помещение", );
	
	Для каждого ЭлементПомещение Из тзИнтервалыПомещения Цикл
		
		стОтбор.Помещение	= ЭлементПомещение.Помещение;
		ПроектТочка 		= ВывестиПомещение(ЭлементПомещение, Серия, тзЦвета, стОтбор);
		
		Для каждого строкаКомплкет Из тзДатыПроекта.НайтиСтроки(стОтбор) Цикл
			стТочкаКомплект = ВывестиКомплект(строкаКомплкет, ЭлементПомещение.Помещение, Серия, тзЦвета);
		КонецЦикла;
		
	КонецЦикла; 
	
	Элементы.ГруппаКомплектыДляДобавления.Видимость = спКомплектыДляДобавления.Количество() > 0;
	
	Диаграмма.Обновление = Истина;

КонецПроцедуры

Функция ВывестиКомплект(СтрокаКомплкет, Помещение, Серия, тзЦвета)
	
	НаименованиеКомплект 	= СтрокаКомплкет.НаименованиеКомплект;
	СсылкаКомплект 			= СтрокаКомплкет.Комплект;
	
	КомплектТочка 			= Диаграмма.УстановитьТочку(СсылкаКомплект, Помещение);
	КомплектТочка.Текст 	= НаименованиеКомплект;
	КомплектТочка.Картинка 	= БиблиотекаКартинок.БыстрыйДоступДобавить;
	
	стДанные = Новый Структура("СсылкаЗначение, Этап, ДатаНачалаПлан, ДатаОкончанияПлан, Помещение"
		, СсылкаКомплект, "Конструирование", СтрокаКомплкет.НачалоПроектирования, СтрокаКомплкет.ОкончаниеПроектирования, СтрокаКомплкет.Помещение);
	ВывестиТочку(КомплектТочка, Серия, стДанные, СсылкаКомплект, тзЦвета,,,,,Истина);
	
	стДанные.Этап 				= "Согласования";
	стДанные.ДатаНачалаПлан 	= СтрокаКомплкет.НачалоСогласования;
	стДанные.ДатаОкончанияПлан 	= СтрокаКомплкет.ОкончаниеСогласования;
	ВывестиТочку(КомплектТочка, Серия, стДанные, СсылкаКомплект, тзЦвета,,,,,Истина);
	
	стДанные.Этап 				= "Производство";
	стДанные.ДатаНачалаПлан 	= СтрокаКомплкет.НачалоПроизводства;
	стДанные.ДатаОкончанияПлан 	= СтрокаКомплкет.ОкончаниеПроизводства;
	ВывестиТочку(КомплектТочка, Серия, стДанные, СсылкаКомплект, тзЦвета,,,,,Истина);
	
	стДанные.Этап 				= "Монтаж";
	стДанные.ДатаНачалаПлан 	= СтрокаКомплкет.НачалоМонтажа;
	стДанные.ДатаОкончанияПлан 	= СтрокаКомплкет.ОкончаниеМонтажа;
	ВывестиТочку(КомплектТочка, Серия, стДанные, СсылкаКомплект, тзЦвета,,,,,Истина);
	
КонецФункции // ВывестиСделку()

Процедура ВывестиКрайниеТочки(СсылкаКомплект,КомплектТочка,Серия,ЦветКрайнейТочки)
	//стОтбор = Новый Структура("ИзделиеКомплект", СсылкаКомплект);
	//НайденыеСтроки = тзДатыОкончания.НайтиСтроки(стОтбор);
	//Если НайденыеСтроки.Количество() = 0 Тогда
	//	Возврат;
	//КонецЕсли; 
	//
	//ПочтиСуткиВСекундах = 24 * 3600; //Ширина крайней точки

	//ДанныеДатыЭтап = Новый Структура("ДатаНачалаПлан,ДатаОкончанияПлан,Этап",НайденыеСтроки[0].ОкончаниеПроизводства,НайденыеСтроки[0].ОкончаниеПроизводства + ПочтиСуткиВСекундах,"Окончание производства");
	//
	//ДанныеДатыЭтап.ДатаНачалаПлан 		= НайденыеСтроки[0].ОкончаниеМонтаж;
	//ДанныеДатыЭтап.ДатаОкончанияПлан 	= ДанныеДатыЭтап.ДатаНачалаПлан + ПочтиСуткиВСекундах;
	//ДанныеДатыЭтап.Этап 				= "Окончание монтаж";
	//ВывестиТочку(КомплектТочка, Серия, ДанныеДатыЭтап, Неопределено, Неопределено,Ложь,Ложь,ЦветКрайнейТочки);
	//
	//ДанныеДатыЭтап.ДатаНачалаПлан 		= НайденыеСтроки[0].ОкончаниеСЗапасом;
	//ДанныеДатыЭтап.ДатаОкончанияПлан 	= ДанныеДатыЭтап.ДатаНачалаПлан + ПочтиСуткиВСекундах;
	//ДанныеДатыЭтап.Этап 				= "Окончание с запасом";
	//ВывестиТочку(КомплектТочка, Серия, ДанныеДатыЭтап, Неопределено, Неопределено,Ложь,Ложь,ЦветКрайнейТочки,Ложь,Ложь);
	
КонецПроцедуры // ВывестиКрайниеТочки(СтрокаКомплкет,Серия,тзЦвета)

Функция ВывестиПомещение(ЭлементПомещение, Серия, тзЦвета, стОтбор)
	
	СсылкаПомещение = ЭлементПомещение.Помещение; 
	
	ПомещениеТочка 			= Диаграмма.Точки.Добавить();
	ПомещениеТочка.Текст 	= ЭлементПомещение.ПомещениеНаименование;
	ПомещениеТочка.Значение = СсылкаПомещение;
	ПомещениеТочка.Картинка = БиблиотекаКартинок.Помещение;
	
	стДанные = Новый Структура("СсылкаЗначение, Этап, ДатаНачалаПлан, ДатаОкончанияПлан, Помещение"
		, СсылкаПомещение, "Помещение", ЭлементПомещение.НачалоПроектирования, ЭлементПомещение.ОкончаниеМонтажа, ЭлементПомещение.Помещение);
	//
	//ВывестиТочку(ПомещениеТочка, Серия, стДанные, СсылкаПомещение, тзЦвета, Ложь, , , Истина);
	
	//стДанные.Этап 				= "Согласования";
	//стДанные.ДатаНачалаПлан 	= ЭлементПомещение.НачалоСогласования;
	//стДанные.ДатаОкончанияПлан 	= ЭлементПомещение.ОкончаниеСогласования;
	//ВывестиТочку(ПомещениеТочка, Серия, стДанные, СсылкаПомещение, тзЦвета, Ложь, , , Истина);
	//
	//стДанные.Этап 				= "Производство";
	//стДанные.ДатаНачалаПлан 	= ЭлементПомещение.НачалоПроизводства;
	//стДанные.ДатаОкончанияПлан 	= ЭлементПомещение.ОкончаниеПроизводства;
	//ВывестиТочку(ПомещениеТочка, Серия, стДанные, СсылкаПомещение, тзЦвета, Ложь, , , Истина);
	//
	//стДанные.Этап 				= "Монтаж";
	//стДанные.ДатаНачалаПлан 	= ЭлементПомещение.НачалоМонтажа;
	//стДанные.ДатаОкончанияПлан 	= ЭлементПомещение.ОкончаниеМонтажа;
	//ВывестиТочку(ПомещениеТочка, Серия, стДанные, СсылкаПомещение, тзЦвета, Ложь, , , Истина);
	
	ВывестиТочку(ПомещениеТочка, Серия, стДанные, СсылкаПомещение, тзЦвета, Ложь, , , Истина, ,ЭлементПомещение.ПомещениеНаименование);
	
	Возврат ПомещениеТочка; 
	
КонецФункции // ВывестиСделку()

Функция ВывестиТочку(Точка, Серия, ДанныеДатыЭтап, СсылкаЗначение, тзЦвета, Редактирование = Истина, ЕстьРасшифровка = Истина
		, ТекущийЦвет = Неопределено, ЭтоГруппа = Ложь, СократитьНаименование = Ложь, ТекстСообщения = Неопределено)

	ПустаяДата = Дата(1, 1, 1);
	Если ДанныеДатыЭтап.ДатаНачалаПлан = ПустаяДата Тогда
	
		Возврат Неопределено;	
	
	КонецЕсли;
		
	Значение = Диаграмма.ПолучитьЗначение(Точка, Серия);
	Значение.Редактирование = Редактирование;
	
	Интервал 				= Значение.Добавить();
	Интервал.Начало 		= ДанныеДатыЭтап.ДатаНачалаПлан;
	Интервал.Конец 			= ?(ДанныеДатыЭтап.ДатаОкончанияПлан - ДанныеДатыЭтап.ДатаНачалаПлан < 24 * 3600, ДанныеДатыЭтап.ДатаНачалаПлан + 24 * 3600, ДанныеДатыЭтап.ДатаОкончанияПлан);
	Интервал.Текст 			= ?(ТекстСообщения = Неопределено, ДанныеДатыЭтап.Этап, ТекстСообщения);
	
	Если НЕ ТекущийЦвет = Неопределено Тогда
		Интервал.Цвет			= ТекущийЦвет;
	ИначеЕсли НЕ тзЦвета = Неопределено Тогда
		Интервал.Цвет			= омДиаграммаГанта.ПолучитьЦвет(ДанныеДатыЭтап.Этап, тзЦвета, ЭтоГруппа);
	КонецЕсли;
	Если ЕстьРасшифровка Тогда
		Интервал.Расшифровка 	= Новый Структура("Этап, Ссылка, Помещение", ДанныеДатыЭтап.Этап, ДанныеДатыЭтап.СсылкаЗначение, ДанныеДатыЭтап.Помещение);
	Иначе
		Интервал.Расшифровка 	= Неопределено;
	КонецЕсли;
	
	Если СократитьНаименование Тогда
		Интервал.Текст = Лев(Интервал.Текст, 1);
	КонецЕсли;
	
	УстановитьТекстЗначения(Значение.Текст, ДанныеДатыЭтап.ДатаНачалаПлан, ДанныеДатыЭтап.ДатаОкончанияПлан, ДанныеДатыЭтап.Этап); 
	
	Возврат Интервал;
КонецФункции // ВывестиТочку()

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекстЗначения(Текст, ДатаНачала, ДатаОкончания, Этап)
	Текст = Этап + " " + Формат(ДатаНачала, "ДФ=dd.MM.yy") + " - " + Формат(ДатаОкончания, "ДФ=dd.MM.yy");	
КонецПроцедуры

#КонецОбласти 


#Область РасчетДанныхДиаграммы

Процедура ПолучитьКомплектыДляДобавления(тзДатыПроекта, ПустаяДата)

    спКомплектыДляДобавления.Очистить();
	
	Для каждого х Из тзДатыПроекта Цикл
		
		Если х.НачалоМонтажа = ПустаяДата ИЛИ х.НачалоПроизводства = ПустаяДата ИЛИ х.НачалоПроектирования = ПустаяДата Тогда
		
			спКомплектыДляДобавления.Добавить(х.Комплект, х.НаименованиеКомплект);	
		
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти


#Область ИзменениеРеквизитов

&НаКлиенте
Процедура спКомплектыДляДобавленияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура спКомплектыДляДобавленияПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура спКомплектыДляДобавленияПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаОбработкаРасшифровки(Элемент, Расшифровки, СтандартнаяОбработка, Дата)

	СтандартнаяОбработка = Ложь;
	Если Расшифровки.Количество() > 1 Тогда
		Если НЕ Расшифровки[1] = Неопределено Тогда
			ПоказатьЗначение(,Расшифровки[1].Ссылка);	
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаВыбор(Элемент, Значения, СтандартнаяОбработка, Дата)
	Если Тип("ТочкаДиаграммыГанта") = ТипЗнч(Значения) Тогда
		Если Тип("СправочникСсылка.ИзделияКомплект") = ТипЗнч(Значения.Значение)
			ИЛИ Тип("СправочникСсылка.Композиции") = ТипЗнч(Значения.Значение) Тогда
			СтандартнаяОбработка = Ложь;
			ПоказатьЗначение(,Значения.Значение);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаПриОкончанииРедактированияИнтервала(Элемент, Интервал, ОтменаРедактирования)

	СекундВСутках = 3600 * 24;
	
	Этап 			= Интервал.Расшифровка.Этап;
	Комплект 		= Интервал.Расшифровка.Ссылка;
	Помещение 		= Интервал.Расшифровка.Помещение;
	РазрывВСекундах	= ДлинаРазрываДляГанта * СекундВСутках;
	
	Начало 	= Интервал.Начало;
	Конец 	= Интервал.Конец;
	
	КомплектТочка 	= Диаграмма.УстановитьТочку(Комплект, Помещение);
	Серия 			= Диаграмма.УстановитьСерию("План");
	Значение 		= Диаграмма.ПолучитьЗначение(КомплектТочка, Серия);
	Значение.Редактирование = Истина; 
	
	Диаграмма.Обновление = Ложь;
	
	стДанныеДляЗаписи = Новый Структура("ВнешняяДатаНачалоМонтажа, ВнешняяДатаНачалоСогласования, ВнешняяДатаНачалоПроектирования, ВнешняяДатаНачалоПроизводства, ВнешняяДатаОкончаниеМонтажа, ВнешняяДатаОкончаниеСогласования, ВнешняяДатаОкончаниеПроектирования, ВнешняяДатаОкончаниеПроизводства", );			
	стДанныеДляЗаписи.Вставить("Комплект", Комплект);  
	
	Для каждого текЗначение Из Значение Цикл
		Если текЗначение.Расшифровка.Этап = "Конструирование" Тогда
			текЗначениеКонструирование 	= текЗначение;
		ИначеЕсли текЗначение.Расшифровка.Этап = "Согласования" Тогда
			текЗначениеСогласования 	= текЗначение;
		ИначеЕсли текЗначение.Расшифровка.Этап = "Производство" Тогда
			текЗначениеПроизводство 	= текЗначение;
		ИначеЕсли текЗначение.Расшифровка.Этап = "Монтаж" Тогда
			текЗначениеМонтаж 			= текЗначение;
		КонецЕсли;	
	КонецЦикла;
	
	стДанныеДляЗаписи.ВнешняяДатаНачалоМонтажа 				= текЗначениеМонтаж.Начало; 
	стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования 		= текЗначениеКонструирование.Начало; 
	стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства 		= текЗначениеПроизводство.Начало;
	стДанныеДляЗаписи.ВнешняяДатаНачалоСогласования 		= текЗначениеСогласования.Начало;
	стДанныеДляЗаписи.ВнешняяДатаОкончаниеМонтажа 			= текЗначениеМонтаж.Конец; 
	стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования 	= текЗначениеКонструирование.Конец; 
	стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства 		= текЗначениеПроизводство.Конец;
	стДанныеДляЗаписи.ВнешняяДатаОкончаниеСогласования 		= текЗначениеСогласования.Конец;
	
	ДлинаКонструирования 	= текЗначениеКонструирование.Конец 	- текЗначениеКонструирование.Начало;
	ДлинаСогласования 		= текЗначениеСогласования.Конец 	- текЗначениеСогласования.Начало;
	ДлинаПроизводства 		= текЗначениеПроизводство.Конец 	- текЗначениеПроизводство.Начало;
	ДлинаМонтажа 			= текЗначениеМонтаж.Конец 			- текЗначениеМонтаж.Начало;
	
	ДлинаКонструирования 	= ?(ДлинаКонструирования 	< СекундВСутках, СекундВСутках, ДлинаКонструирования);
	ДлинаСогласования 		= ?(ДлинаСогласования 		< СекундВСутках, СекундВСутках, ДлинаСогласования);
	ДлинаПроизводства 		= ?(ДлинаПроизводства 		< СекундВСутках, СекундВСутках, ДлинаПроизводства);
	ДлинаМонтажа 			= ?(ДлинаМонтажа 			< СекундВСутках, СекундВСутках, ДлинаМонтажа);
	
	Если Этап = "Конструирование" Тогда
		
		ПолучитьСмещениеЭтапов(Начало, РазрывВСекундах, СекундВСутках, ДлинаКонструирования, ДлинаСогласования, ДлинаПроизводства, ДлинаМонтажа, стДанныеДляЗаписи);
		
	ИначеЕсли Этап = "Согласования" Тогда

		стДанныеДляЗаписи.ВнешняяДатаНачалоСогласования 		= Начало; 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеСогласования 		= Конец; 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования	= стДанныеДляЗаписи.ВнешняяДатаНачалоСогласования 		- РазрывВСекундах;
		стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования		= стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования 	- ДлинаКонструирования;
		
		ПолучитьСмещениеЭтапов(стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования, РазрывВСекундах, СекундВСутках, ДлинаКонструирования, ДлинаСогласования, ДлинаПроизводства, ДлинаМонтажа, стДанныеДляЗаписи);

	ИначеЕсли Этап = "Производство" Тогда
		
		стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства 		= Начало; 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства 		= Конец; 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеСогласования		= стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства 		- РазрывВСекундах;
		стДанныеДляЗаписи.ВнешняяДатаНачалоСогласования			= стДанныеДляЗаписи.ВнешняяДатаОкончаниеСогласования 	- ДлинаСогласования;
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования	= стДанныеДляЗаписи.ВнешняяДатаНачалоСогласования		- РазрывВСекундах;
		стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования 		= стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования	- ДлинаКонструирования;
		
		ПолучитьСмещениеЭтапов(стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования, РазрывВСекундах, СекундВСутках, ДлинаКонструирования, ДлинаСогласования, ДлинаПроизводства, ДлинаМонтажа, стДанныеДляЗаписи);

	Иначе
		
		стДанныеДляЗаписи.ВнешняяДатаНачалоМонтажа 				= Начало; 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеМонтажа 			= Конец; 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства		= стДанныеДляЗаписи.ВнешняяДатаНачалоМонтажа 			- РазрывВСекундах;
		стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства			= стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства 	- ДлинаПроизводства;
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеСогласования		= стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства 		- РазрывВСекундах;
		стДанныеДляЗаписи.ВнешняяДатаНачалоСогласования			= стДанныеДляЗаписи.ВнешняяДатаОкончаниеСогласования 	- ДлинаСогласования;
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования	= стДанныеДляЗаписи.ВнешняяДатаНачалоСогласования		- РазрывВСекундах;
		стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования 		= стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования	- ДлинаКонструирования;
		
		ПолучитьСмещениеЭтапов(стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования, РазрывВСекундах, СекундВСутках, ДлинаКонструирования, ДлинаСогласования, ДлинаПроизводства, ДлинаМонтажа, стДанныеДляЗаписи);
		
	КонецЕсли;
	
	текЗначениеКонструирование.Начало 	= стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования;
	текЗначениеКонструирование.Конец 	= стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования;
	текЗначениеСогласования.Начало 		= стДанныеДляЗаписи.ВнешняяДатаНачалоСогласования;
	текЗначениеСогласования.Конец 		= стДанныеДляЗаписи.ВнешняяДатаОкончаниеСогласования;
	текЗначениеПроизводство.Начало 		= стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства;
	текЗначениеПроизводство.Конец 		= стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства;
	текЗначениеМонтаж.Начало 			= стДанныеДляЗаписи.ВнешняяДатаНачалоМонтажа;
	текЗначениеМонтаж.Конец 			= стДанныеДляЗаписи.ВнешняяДатаОкончаниеМонтажа; 
	
	ЗаписатьНаСервере(стДанныеДляЗаписи);

	Попытка
		
		Помещение = текЗначениеКонструирование.Значение.Точка.Родитель.Значение;
		
	Исключение
		
		Помещение = Неопределено;
		
	КонецПопытки;
	
	Если ЗначениеЗаполнено(Помещение) Тогда
		
		КомплектТочка 	= Диаграмма.УстановитьТочку(Помещение);
		Значение 		= Диаграмма.ПолучитьЗначение(КомплектТочка, Серия);
		Значение.Редактирование = Истина; 
		
		мДанныеПомещения = ПолучитьДанныеОПомещенииНаСервере(Проект, Помещение);
		
		Если мДанныеПомещения.Количество() > 0 Тогда
			
			Для каждого текЗначение Из Значение Цикл
				//Если текЗначение.Расшифровка.Этап = "Конструирование" Тогда
					текЗначение.Начало 	= мДанныеПомещения[0].НачалоПроектирования;
				//	текЗначение.Конец 	= мДанныеПомещения[0].ОкончаниеПроектирования;
				//ИначеЕсли текЗначение.Расшифровка.Этап = "Согласования" Тогда
				//	текЗначение.Начало 	= мДанныеПомещения[0].НачалоСогласования;
				//	текЗначение.Конец 	= мДанныеПомещения[0].ОкончаниеСогласования;
				//ИначеЕсли текЗначение.Расшифровка.Этап = "Производство" Тогда
				//	текЗначение.Начало 	= мДанныеПомещения[0].НачалоПроизводства;
				//	текЗначение.Конец 	= мДанныеПомещения[0].ОкончаниеПроизводства;
				//ИначеЕсли текЗначение.Расшифровка.Этап = "Монтаж" Тогда
				//	текЗначение.Начало 	= мДанныеПомещения[0].НачалоМонтажа;
					текЗначение.Конец 	= мДанныеПомещения[0].ОкончаниеМонтажа;
				//КонецЕсли;	
			КонецЦикла;
			
		КонецЕсли;
		
		Значение.Редактирование = Ложь;
		
	КонецЕсли;
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

Функция ПолучитьСмещениеЭтапов(Начало, РазрывВСекундах, СекундВСутках, ДлинаКонструирования, ДлинаСогласования, ДлинаПроизводства, ДлинаМонтажа, стДанныеДляЗаписи)

		стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования 		= ПолучитьНачалоНедели(Начало, СекундВСутках); 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования 	= ПолучитьКонецНедели(Начало + ДлинаКонструирования, СекундВСутках); 
		стДанныеДляЗаписи.ВнешняяДатаНачалоСогласования 		= стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования 	+ РазрывВСекундах;
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеСогласования 		= ПолучитьКонецНедели(стДанныеДляЗаписи.ВнешняяДатаНачалоСогласования  		+ ДлинаСогласования, СекундВСутках);
		стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства 		= стДанныеДляЗаписи.ВнешняяДатаОкончаниеСогласования  	+ РазрывВСекундах;
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства 		= ПолучитьКонецНедели(стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства  		+ ДлинаПроизводства, СекундВСутках);
		стДанныеДляЗаписи.ВнешняяДатаНачалоМонтажа		 		= стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства  	+ РазрывВСекундах;
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеМонтажа 			= ПолучитьКонецНедели(стДанныеДляЗаписи.ВнешняяДатаНачалоМонтажа  			+ ДлинаМонтажа, 	 СекундВСутках);

КонецФункции // ПолучитьСмещениеЭтапов()

Функция ПолучитьНачалоНедели(текДата, СекундВСутках)

	Возврат НачалоДня(текДата - (ДеньНедели(текДата) - 1) * СекундВСутках);
	
КонецФункции // получитьКонецНедели()

Функция ПолучитьКонецНедели(текДата, СекундВСутках)
	
	Возврат КонецДня(текДата + (7 - ДеньНедели(текДата)) * СекундВСутках);
	
КонецФункции // получитьКонецНедели()

&НаСервереБезКонтекста
Функция ПолучитьДанныеОПомещенииНаСервере(Проект, Помещение)

	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(омГантНаСервере.ПолучитьИнтервалыПомещения(омГантНаСервере.ПолучитьДатыПроекта(Проект, Помещение)));
	

КонецФункции // ПолучитьДанныеОПомещенииНаСервере(Помещение)()

&НаСервереБезКонтекста
Процедура ЗаписатьНаСервере(стДанныеДляЗаписи)

		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ *
			|ИЗ
			|	РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект = &Комплект) КАК ДанныеКомплекта";
		
		Запрос.УстановитьПараметр("Комплект", стДанныеДляЗаписи.Комплект);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Период = ТекущаяДата();
		
		НаборЗаписей = РегистрыСведений.ДанныеКомплекта.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Период);
		НаборЗаписей.Отбор.Комплект.Установить(стДанныеДляЗаписи.Комплект);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		НоваяЗапись.Период = Период; 
		ЗаполнитьЗначенияСвойств(НоваяЗапись, стДанныеДляЗаписи);
		
		НоваяЗапись.Автор = ПараметрыСеанса.ТекущийПользователь;
		
		НаборЗаписей.Записать();
		

КонецПроцедуры

#КонецОбласти 

#Область ДейстаияФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ДатаДобавления 				= ТекущаяДата();
	Диаграмма.ОтображатьЛегенду = Ложь;
КонецПроцедуры

#КонецОбласти


#Область Команды

&НаКлиенте
Процедура ДобавитьКомплекты(Команда)
	ДобавитьКомплектыНаСервере();
КонецПроцедуры
	
#КонецОбласти


#Область ИсполнениеКоманды

&НаСервере
Процедура ДобавитьКомплектыНаСервере()
	
	СекундВСутках  	= 3600 * 24;
	РазрывВСекундах	= ДлинаРазрываДляГанта * СекундВСутках;
	ДлинаЭтапа  	= СекундВСутках * 10;
	Период			= ТекущаяДата();
	Автор 			= ПараметрыСеанса.ТекущийПользователь;
	
	ВнешняяДатаОкончаниеПроектирования = ДатаДобавления + ДлинаЭтапа; 
	
	Для каждого х Из спКомплектыДляДобавления Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект = &Комплект) КАК ДанныеКомплекта";
		
		Запрос.УстановитьПараметр("Комплект", х.Значение);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		НаборЗаписей = РегистрыСведений.ДанныеКомплекта.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Период);
		НаборЗаписей.Отбор.Комплект.Установить(х.Значение);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		НоваяЗапись.Период 								= Период; 
		НоваяЗапись.Комплект 							= х.Значение; 
		НоваяЗапись.Автор 								= Автор;
		НоваяЗапись.ВнешняяДатаНачалоПроектирования 	= ДатаДобавления;
		НоваяЗапись.ВнешняяДатаОкончаниеПроектирования 	= ВнешняяДатаОкончаниеПроектирования;
		НоваяЗапись.ВнешняяДатаНачалоСогласования 		= НоваяЗапись.ВнешняяДатаОкончаниеПроектирования 	+ РазрывВСекундах;
		НоваяЗапись.ВнешняяДатаОкончаниеСогласования 	= НоваяЗапись.ВнешняяДатаНачалоСогласования 		+ ДлинаЭтапа;
		НоваяЗапись.ВнешняяДатаНачалоПроизводства 		= НоваяЗапись.ВнешняяДатаОкончаниеСогласования 		+ РазрывВСекундах;
		НоваяЗапись.ВнешняяДатаОкончаниеПроизводства 	= НоваяЗапись.ВнешняяДатаНачалоПроизводства 		+ ДлинаЭтапа;
		НоваяЗапись.ВнешняяДатаНачалоМонтажа 			= НоваяЗапись.ВнешняяДатаОкончаниеПроизводства     	+ РазрывВСекундах;
		НоваяЗапись.ВнешняяДатаОкончаниеМонтажа 		= НоваяЗапись.ВнешняяДатаНачалоМонтажа 				+ ДлинаЭтапа;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
	Элементы.ГруппаКомплектыДляДобавления.Видимость = Ложь;
	
	ПостроитьДиаграммуГанта();	
	
КонецПроцедуры

#КонецОбласти


&НаКлиенте
Процедура Печать(Команда)
	ОткрытьФорму("Отчет.ПланДляВнешнегоПользователя.Форма.ФормаПечати",
		Новый Структура("Проект, МассивЗадачи", Проект, ПолучитьМассивДанных(Проект)));
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьМассивДанных(Проект)                                                                                                  

	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(омГантНаСервере.ПолучитьДатыПроекта(Проект));	

КонецФункции // ПолучитьМассивДанных()
