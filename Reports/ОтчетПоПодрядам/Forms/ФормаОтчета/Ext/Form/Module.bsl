&НаКлиенте
Процедура Обновить(Команда)
	Если ТолькоВПодряде Тогда
		ПустаяДата 				= Дата(1, 1, 1);
		Период.ДатаНачала 		= ПустаяДата;
		Период.ДатаОкончания 	= ПустаяДата;
	Иначе
		Период.ДатаОкончания 	= ТекущаяДата();
		Период.ДатаНачала 		= Период.ДатаОкончания - 60 * 24 * 3600;
	КонецЕсли;
	
	Элементы.Период.Доступность = НЕ ТолькоВПодряде;
	
	ОбновитьНаСервере();
КонецПроцедуры


Процедура ОбновитьНаСервере()
	
	Запрос = СозданиеЗапроса();
	
	Запрос.УстановитьПараметр("Подрядчик", 	Подрядчик);
	Запрос.УстановитьПараметр("Проект", 	Сделка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	тзОбещания = РезультатЗапроса[2].Выгрузить();
	тзОбещания.Индексы.Добавить("Подрядчик, ИзделиеКомплект");	
	
	ВыборкаПодрядчик = РезультатЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Макет = Отчеты.ОтчетПоПодрядам.ПолучитьМакет("Макет");
	ПолеВвода.Очистить();
	
	ПолеВвода.Вывести(Макет.ПолучитьОбласть("Шапка"));
	
	ОбластьСтрока 		= Макет.ПолучитьОбласть("Строка");
	ОбластьПодрядчик 	= Макет.ПолучитьОбласть("Подрядчик");
	
	ПустаяДата = Дата(1, 1, 1);
	
	Пока ВыборкаПодрядчик.Следующий() Цикл
		ВыборкаПодрядчикНаименование= ВыборкаПодрядчик.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПодрядчикНаименование.Следующий() Цикл
			ОбластьПодрядчик.Параметры.Заполнить(ВыборкаПодрядчикНаименование);
			ПолеВвода.Вывести(ОбластьПодрядчик); 
			
			ВыборкаКомплекты = ВыборкаПодрядчикНаименование.Выбрать();
			Пока ВыборкаКомплекты.Следующий() Цикл
				ОбластьСтрока.Параметры.Заполнить(ВыборкаКомплекты);
				//Если НЕ ТолькоВПодряде Тогда
				//	Если ВыборкаКомплекты.Приход = 1 Тогда
				//		ОбластьСтрока.Параметры.ДатаВозврата = ПустаяДата;
				//		ОбластьСтрока.Параметры.ДатаПередачи = ВыборкаКомплекты.Период;
				//	Иначе
				//		ОбластьСтрока.Параметры.ДатаВозврата = ВыборкаКомплекты.Период;
				//		ОбластьСтрока.Параметры.ДатаПередачи = ПустаяДата;
				//	КонецЕсли;
				//
				//КонецЕсли;
				ПолеВвода.Вывести(ОбластьСтрока); 
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	
КонецПроцедуры // ПолучитьДанныеДляВывода()

Функция СозданиеЗапроса()
	
	Если ТолькоВПодряде Тогда
		Возврат СоздатьЗапросВПодряде();
	КонецЕсли;
	
	Возврат СоздатьЗапросНеТолькоВПодряде();
КонецФункции // СозданиеЗапроса()

Функция СоздатьЗапросВПодряде()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КомплектыУПодрядчикаОстатки.Подрядчик КАК Подрядчик,
		|	КомплектыУПодрядчикаОстатки.ИзделиеКомплект КАК ИзделиеКомплект,
		|	КомплектыУПодрядчикаОстатки.ИзделиеКомплект.Наименование КАК ИзделиеКомплектНаименование,
		|	КомплектыУПодрядчикаОстатки.Подрядчик.Наименование КАК ПодрядчикНаименование
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрНакопления.КомплектыУПодрядчика.Остатки(
		|			,
		|			ВЫБОР
		|					КОГДА &Подрядчик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ Подрядчик = &Подрядчик
		|				КОНЕЦ
		|				И ВЫБОР
		|					КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ИзделиеКомплект.Проект = &Проект
		|				КОНЕЦ) КАК КомплектыУПодрядчикаОстатки
		|ГДЕ
		|	КомплектыУПодрядчикаОстатки.КоличествоОстаток > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(КомплектыУПодрядчика.Период) КАК ДатаПередачи,
		|	вт.Подрядчик КАК Подрядчик,
		|	вт.ИзделиеКомплект КАК ИзделиеКомплект,
		|	вт.ИзделиеКомплектНаименование КАК ИзделиеКомплектНаименование,
		|	вт.ПодрядчикНаименование КАК ПодрядчикНаименование,
		|	ОбещаниеПодрядчикаСрезПоследних.ДатаОбещания КАК ДатаОжидания
		|ИЗ
		|	вт КАК вт
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КомплектыУПодрядчика КАК КомплектыУПодрядчика
		|		ПО вт.Подрядчик = КомплектыУПодрядчика.Подрядчик
		|			И вт.ИзделиеКомплект = КомплектыУПодрядчика.ИзделиеКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбещаниеПодрядчика.СрезПоследних КАК ОбещаниеПодрядчикаСрезПоследних
		|		ПО вт.Подрядчик = ОбещаниеПодрядчикаСрезПоследних.Подрядчик
		|			И вт.ИзделиеКомплект = ОбещаниеПодрядчикаСрезПоследних.ИзделиеКомплект
		|ГДЕ
		|	КомплектыУПодрядчика.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|СГРУППИРОВАТЬ ПО
		|	вт.Подрядчик,
		|	вт.ИзделиеКомплект,
		|	вт.ИзделиеКомплектНаименование,
		|	вт.ПодрядчикНаименование,
		|	ОбещаниеПодрядчикаСрезПоследних.ДатаОбещания
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПодрядчикНаименование,
		|	Подрядчик,
		|	ИзделиеКомплектНаименование
		|ИТОГИ ПО
		|	Подрядчик,
		|	ПодрядчикНаименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбещаниеПодрядчика.ДатаОбещания КАК ДатаОбещания,
		|	ОбещаниеПодрядчика.ВозвратНаСклад КАК ВозвратНаСклад,
		|	ОбещаниеПодрядчика.Регистратор.Комментарий КАК Комментарий,
		|	вт.ИзделиеКомплект КАК ИзделиеКомплект,
		|	вт.Подрядчик КАК Подрядчик
		|ИЗ
		|	вт КАК вт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбещаниеПодрядчика КАК ОбещаниеПодрядчика
		|		ПО вт.Подрядчик = ОбещаниеПодрядчика.Подрядчик
		|			И вт.ИзделиеКомплект = ОбещаниеПодрядчика.ИзделиеКомплект
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОбещаниеПодрядчика.Период";
	
	
	Возврат Запрос;
	
КонецФункции // СоздатьЗапросВПодряде()

Функция СоздатьЗапросНеТолькоВПодряде()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА КомплектыУПодрядчикаОбороты.КоличествоПриход = 1
		|				ТОГДА КомплектыУПодрядчикаОбороты.Период
		|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|		КОНЕЦ) КАК ДатаПередачи,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА КомплектыУПодрядчикаОбороты.КоличествоРасход = 1
		|				ТОГДА КомплектыУПодрядчикаОбороты.Период
		|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|		КОНЕЦ) КАК ДатаВозврата,
		|	КомплектыУПодрядчикаОбороты.Подрядчик КАК Подрядчик,
		|	КомплектыУПодрядчикаОбороты.ИзделиеКомплект КАК ИзделиеКомплект,
		|	КомплектыУПодрядчикаОбороты.Подрядчик.Наименование КАК ПодрядчикНаименование,
		|	КомплектыУПодрядчикаОбороты.ИзделиеКомплект.Наименование КАК ИзделиеКомплектНаименование,
		|	КомплектыУПодрядчикаОбороты.Регистратор.Комментарий КАК Комментарий
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрНакопления.КомплектыУПодрядчика.Обороты(
		|			&ДатаНачала,
		|			&ДатаОкончания,
		|			Регистратор,
		|			ВЫБОР
		|					КОГДА &Подрядчик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ Подрядчик = &Подрядчик
		|				КОНЕЦ
		|				И ВЫБОР
		|					КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ИзделиеКомплект.Проект = &Проект
		|				КОНЕЦ) КАК КомплектыУПодрядчикаОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	КомплектыУПодрядчикаОбороты.Регистратор.Комментарий,
		|	КомплектыУПодрядчикаОбороты.Подрядчик,
		|	КомплектыУПодрядчикаОбороты.ИзделиеКомплект,
		|	КомплектыУПодрядчикаОбороты.Подрядчик.Наименование,
		|	КомплектыУПодрядчикаОбороты.ИзделиеКомплект.Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.ДатаПередачи КАК ДатаПередачи,
		|	вт.Подрядчик КАК Подрядчик,
		|	вт.ИзделиеКомплект КАК ИзделиеКомплект,
		|	вт.ПодрядчикНаименование КАК ПодрядчикНаименование,
		|	вт.ИзделиеКомплектНаименование КАК ИзделиеКомплектНаименование,
		|	вт.Комментарий КАК Комментарий,
		|	ОбещаниеПодрядчикаСрезПоследних.ДатаОбещания КАК ДатаОжидания,
		|	вт.ДатаВозврата КАК ДатаВозврата
		|ИЗ
		|	вт КАК вт
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбещаниеПодрядчика.СрезПоследних КАК ОбещаниеПодрядчикаСрезПоследних
		|		ПО вт.Подрядчик = ОбещаниеПодрядчикаСрезПоследних.Подрядчик
		|			И вт.ИзделиеКомплект = ОбещаниеПодрядчикаСрезПоследних.ИзделиеКомплект
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПодрядчикНаименование,
		|	Подрядчик,
		|	ИзделиеКомплектНаименование
		|ИТОГИ ПО
		|	Подрядчик,
		|	ПодрядчикНаименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбещаниеПодрядчика.ДатаОбещания КАК ДатаОбещания,
		|	ОбещаниеПодрядчика.Период КАК Период,
		|	ОбещаниеПодрядчика.Регистратор.Комментарий КАК РегистраторКомментарий,
		|	вт.Подрядчик КАК Подрядчик,
		|	вт.ИзделиеКомплект КАК ИзделиеКомплект
		|ИЗ
		|	вт КАК вт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбещаниеПодрядчика КАК ОбещаниеПодрядчика
		|		ПО вт.Подрядчик = ОбещаниеПодрядчика.Подрядчик
		|			И вт.ИзделиеКомплект = ОбещаниеПодрядчика.ИзделиеКомплект
		|
		|СГРУППИРОВАТЬ ПО
		|	вт.Подрядчик,
		|	вт.ИзделиеКомплект,
		|	ОбещаниеПодрядчика.ДатаОбещания,
		|	ОбещаниеПодрядчика.Период,
		|	ОбещаниеПодрядчика.Регистратор.Комментарий";
	
	Запрос.УстановитьПараметр("ДатаНачала",  	Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	
	Возврат Запрос;

КонецФункции // СоздатьЗапросВПодряде()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТолькоВПодряде = Истина;
	ОбновитьНаСервере();
КонецПроцедуры

