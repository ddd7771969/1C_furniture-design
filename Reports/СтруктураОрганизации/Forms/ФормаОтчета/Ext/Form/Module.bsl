
&НаСервере
Процедура ОбновитьНаСервере()
	
	
	СтрокаHTML = ПолучитьЗагаловок();
	НазваниеОрганизации = ПолучитьНаименованиеОрганизации();
	
	СтрокаHTML = СтрокаHTML + "
		|	<h2>" + НазваниеОрганизации + "</h2>
		|<ul id=""myUL"">"; 
	
	стДанные 		= ПолучитьПодразделения();
	тзПодразделение = стДанные.Подразделения;
	тзСотрудники 	= стДанные.Сотрудники;
	тзСотрудники.Индексы.Добавить("Подразделение");
	
	стОтбор = Новый Структура("Родитель", Справочники.Подразделения.ПустаяСсылка());
	стОтборСотрудники = Новый Структура("Подразделение");
	
	НайденыеСтроки = тзПодразделение.НайтиСтроки(стОтбор);
	
	СтрокаЗакрытия = "";
	
	Для каждого х Из НайденыеСтроки Цикл
		
		стОтборСотрудники.Подразделение = х.Ссылка; 
		стОтбор.Родитель = х.Ссылка;
		
		НайденыеСтрокиПодчиненые = тзПодразделение.НайтиСтроки(стОтбор);
		
		Если НайденыеСтрокиПодчиненые.Количество() > 0 Тогда
			
			СтрокаHTML	= СтрокаHTML + "
			|<li><span class=""caret"">" + х.Наименование + " (" + х.Руководитель + ")</span>
			|<ul class=""nested"">";
			
			ВывестиСотрудников(СтрокаHTML, тзСотрудники, стОтборСотрудники);

			ЗаполнитьПодчиненые(СтрокаHTML, тзПодразделение, тзСотрудники, стОтборСотрудники, НайденыеСтрокиПодчиненые, стОтбор);
			
			СтрокаHTML	= СтрокаHTML + "</ul>";
			
		Иначе
			
			СтрокаHTML	= СтрокаHTML + "
			|<li>" + х.Наименование + " (" + х.Руководитель + ")";

			ВывестиСотрудников(СтрокаHTML, тзСотрудники, стОтборСотрудники);
			
		КонецЕсли;

		СтрокаHTML	= СтрокаHTML + "</li>";
		
	КонецЦикла;
	
	СтрокаHTML = СтрокаHTML + "</ul>" + ПолучитьОкончание();
	
	ПолеВывода = СтрокаHTML; 
	
	ЗаполнитьСотрудниковБезПодразделения();
	
КонецПроцедуры 

Процедура ВывестиСотрудников(СтрокаHTML, тзСотрудники, стОтборСотрудники)
	
	СтрокаHTML = СтрокаHTML + " <i>";
	ПерваяСтрока = Истина;
	Для каждого х Из тзСотрудники.НайтиСтроки(стОтборСотрудники) Цикл
		
		Если ПерваяСтрока Тогда
			ПерваяСтрока = Ложь;	
		Иначе	
			СтрокаHTML = СтрокаHTML + ", ";
		КонецЕсли;
		
		СтрокаHTML = СтрокаHTML + х.Наименование;
		
	КонецЦикла;
	
	СтрокаHTML = СтрокаHTML + " </i>";
КонецПроцедуры


Процедура ЗаполнитьСотрудниковБезПодразделения()
	
	СотрудникиБезПодразделения.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Ссылка,
	|	Сотрудники.Наименование КАК Наименование,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Подразделения.Подразделение) КАК Подразделение
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Подразделения КАК Подразделения
	|		ПО (Подразделения.Сотрудник = Сотрудники.Ссылка)
	|ГДЕ
	|	НЕ Сотрудники.ПометкаУдаления
	|	И НЕ Сотрудники.ЭтоГруппа
	|
	|СГРУППИРОВАТЬ ПО
	|	Сотрудники.Ссылка,
	|	Сотрудники.Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт.Ссылка КАК Значение,
	|	вт.Наименование КАК Представление
	|ИЗ
	|	вт КАК вт
	|ГДЕ
	|	вт.Подразделение = 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Представление";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СотрудникиБезПодразделения.Добавить(Выборка.Значение, Выборка.Представление);
	КонецЦикла; 
	
КонецПроцедуры


Процедура ЗаполнитьПодчиненые(СтрокаHTML, тзПодразделение, тзСотрудники, стОтборСотрудники, НайденыеСтроки, стОтбор)

	Для каждого х Из НайденыеСтроки Цикл
		
		стОтборСотрудники.Подразделение = х.Ссылка; 

		стОтбор.Родитель = х.Ссылка;
		НайденыеСтрокиПодчиненые = тзПодразделение.НайтиСтроки(стОтбор);
		
		Если НайденыеСтрокиПодчиненые.Количество() > 0 Тогда
			
			СтрокаHTML	= СтрокаHTML + "
			|<li><span class=""caret"">" + х.Наименование +  + " (" + х.Руководитель + ")</span>
			|<ul class=""nested"">";
			
			ВывестиСотрудников(СтрокаHTML, тзСотрудники, стОтборСотрудники);
			
			ЗаполнитьПодчиненые(СтрокаHTML, тзПодразделение, тзСотрудники, стОтборСотрудники, НайденыеСтрокиПодчиненые, стОтбор);
			
			СтрокаHTML	= СтрокаHTML + "</ul>";
			
		Иначе
			
			СтрокаHTML	= СтрокаHTML + "
			|<li>" + х.Наименование + " (" + х.Руководитель + ")";

			ВывестиСотрудников(СтрокаHTML, тзСотрудники, стОтборСотрудники);
			
		КонецЕсли;

		СтрокаHTML	= СтрокаHTML + "</li>";
		
		
	КонецЦикла;
	
КонецПроцедуры


Функция ПолучитьПодразделения()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Подразделения.Ссылка КАК Ссылка,
		|	Подразделения.Родитель КАК Родитель,
		|	Подразделения.Руководитель КАК Руководитель,
		|	Подразделения.Наименование КАК Наименование
		|ИЗ
		|	Справочник.Подразделения КАК Подразделения
		|ГДЕ
		|	НЕ Подразделения.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Подразделения.Подразделение КАК Подразделение,
		|	Сотрудники.Наименование КАК Наименование,
		|	Сотрудники.Ссылка КАК Ссылка
		|ИЗ
		|	РегистрСведений.Подразделения КАК Подразделения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО Подразделения.Сотрудник = Сотрудники.Ссылка
		|ГДЕ
		|	НЕ Сотрудники.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	
	Результат = Запрос.ВыполнитьПакет();
	
	Возврат Новый Структура("Подразделения, Сотрудники", Результат[0].Выгрузить(), Результат[1].Выгрузить());
	
КонецФункции // ПолучитьПодразделения()


&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьНаСервере();
КонецПроцедуры


Функция ПолучитьНаименованиеОрганизации()

	ОбъектОтчета = Отчеты.ОбщиеНастройки.Создать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПроекта.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.НастройкиПроекта КАК НастройкиПроекта
		|ГДЕ
		|	НастройкиПроекта.ИмяНастройки = &ИмяНастройки
		|	И НастройкиПроекта.НомерНастройки = 0
		|	И НастройкиПроекта.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ИмяНастройки", ОбъектОтчета.ПолучитьНазваниеОрганизации());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Значение;
	КонецЦикла;
	
    Возврат "";
	
КонецФункции // ПолучитьНаименованиеОрганизации()

Функция ПолучитьЗагаловок()

	Возврат "<!DOCTYPE html>
			|<html>
			|<head>
			|<meta name=""viewport"" content=""width=device-width, initial-scale=1"">
			|<style>
			|ul, #myUL {
			|  list-style-type: none;
			|}
			|
			|#myUL {
			|  margin: 0;
			|  padding: 0;
			|}
			|
			|.caret {
			|  cursor: pointer;
			|  -webkit-user-select: none; /* Safari 3.1+ */
			|  -moz-user-select: none; /* Firefox 2+ */
			|  -ms-user-select: none; /* IE 10+ */
			|  user-select: none;
			|}
			|
			|.caret::before {
			|  content: ""\25B6"";
			|  color: black;
			|  display: inline-block;
			|  margin-right: 6px;
			|}
			|
			|.caret-down::before {
			|  -ms-transform: rotate(90deg); /* IE 9 */
			|  -webkit-transform: rotate(90deg); /* Safari */'
			|  transform: rotate(90deg);  
			|}
			|
			|.nested {
			|  display: none;
			|}
			|
			|.active {
			|  display: block;
			|}
			|</style>
			|</head>
			|<body>";
	
КонецФункции // ПолучитьЗагаловок()

Функция ПолучитьОкончание()
	
	Возврат "
	|<script>
	|var toggler = document.getElementsByClassName(""caret"");
	|var i;
	|
	|for (i = 0; i < toggler.length; i++) {
	|  toggler[i].addEventListener(""click"", function() {
	|    this.parentElement.querySelector("".nested"").classList.toggle(""active"");
	|    this.classList.toggle(""caret-down"");
	|  });
	|}
	|</script>
	|
	|</body>
	|</html>"; 
	
КонецФункции // ПолучитьОкончание()

&НаКлиенте
Процедура СотрудникиБезПодразделенияПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиБезПодразделенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиБезПодразделенияПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	ПоказатьЗначение(, Элементы.СотрудникиБезПодразделения.ТекущиеДанные.Значение);
КонецПроцедуры
