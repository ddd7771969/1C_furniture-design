#Область СобытиеРеквизитов

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	ПроектПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти 



#Область СобытиеРеквизитовПродолжение

Процедура ПроектПриИзмененииНаСервере()
	
	ПостроитьДиаграммуГанта(); 
	
КонецПроцедуры 

#КонецОбласти 


#Область ПостроениеяДиаграммы

Процедура ПостроитьДиаграммуГанта() 
	
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();

	ПустаяДата = Дата(1,1,1);
	
	тзДатыПроекта 			= ПолучитьДатыПроекта();
	ПолучитьКомплектыДляДобавления(тзДатыПроекта, ПустаяДата);
	тзИнтервалыПомещения 	= ПолучитьИнтервалыПомещения(ПустаяДата, тзДатыПроекта);
	тзЦвета 				= омДиаграммаГанта.ПолучитьТаблицуЦветов();
	
	Серия = Диаграмма.УстановитьСерию("План");

	
	стОтбор = Новый Структура("Помещение", );
	
	Для каждого ЭлементПомещение Из тзИнтервалыПомещения Цикл
		
		стОтбор.Помещение	= ЭлементПомещение.Помещение;
		ПроектТочка 		= ВывестиПомещение(ЭлементПомещение, Серия, тзЦвета, стОтбор);
		
		Для каждого строкаКомплкет Из тзДатыПроекта.НайтиСтроки(стОтбор) Цикл
			стТочкаКомплект = ВывестиКомплект(строкаКомплкет, ЭлементПомещение.Помещение, Серия, тзЦвета);
		КонецЦикла;
		
	КонецЦикла; 
	
	Элементы.ГруппаКомплектыДляДобавления.Видимость = спКомплектыДляДобавления.Количество() > 0;
	
	Диаграмма.Обновление = Истина;

КонецПроцедуры

Функция ВывестиКомплект(СтрокаКомплкет, Помещение, Серия, тзЦвета)
	
	НаименованиеКомплект 	= СтрокаКомплкет.НаименованиеКомплект;
	СсылкаКомплект 			= СтрокаКомплкет.Комплект;
	
	КомплектТочка 			= Диаграмма.УстановитьТочку(СсылкаКомплект, Помещение);
	КомплектТочка.Текст 	= НаименованиеКомплект;
	КомплектТочка.Картинка 	= БиблиотекаКартинок.БыстрыйДоступДобавить;
	
	стДанные = Новый Структура("СсылкаЗначение, Этап, ДатаНачалаПлан, ДатаОкончанияПлан, Помещение"
		, СсылкаКомплект, "Конструирование", СтрокаКомплкет.НачалоПроектирования, СтрокаКомплкет.ОкончаниеПроектирования, СтрокаКомплкет.Помещение);
	ВывестиТочку(КомплектТочка, Серия, стДанные, СсылкаКомплект, тзЦвета);
	
	стДанные.Этап 				= "Производство";
	стДанные.ДатаНачалаПлан 	= СтрокаКомплкет.НачалоПроизводства;
	стДанные.ДатаОкончанияПлан 	= СтрокаКомплкет.ОкончаниеПроизводства;
	ВывестиТочку(КомплектТочка, Серия, стДанные, СсылкаКомплект, тзЦвета);
	
	стДанные.Этап 				= "Монтаж";
	стДанные.ДатаНачалаПлан 	= СтрокаКомплкет.НачалоМонтажа;
	стДанные.ДатаОкончанияПлан 	= СтрокаКомплкет.ОкончаниеМонтажа;
	ВывестиТочку(КомплектТочка, Серия, стДанные, СсылкаКомплект, тзЦвета);
	
КонецФункции // ВывестиСделку()

Процедура ВывестиКрайниеТочки(СсылкаКомплект,КомплектТочка,Серия,ЦветКрайнейТочки)
	//стОтбор = Новый Структура("ИзделиеКомплект", СсылкаКомплект);
	//НайденыеСтроки = тзДатыОкончания.НайтиСтроки(стОтбор);
	//Если НайденыеСтроки.Количество() = 0 Тогда
	//	Возврат;
	//КонецЕсли; 
	//
	//ПочтиСуткиВСекундах = 24 * 3600; //Ширина крайней точки

	//ДанныеДатыЭтап = Новый Структура("ДатаНачалаПлан,ДатаОкончанияПлан,Этап",НайденыеСтроки[0].ОкончаниеПроизводства,НайденыеСтроки[0].ОкончаниеПроизводства + ПочтиСуткиВСекундах,"Окончание производства");
	//
	//ДанныеДатыЭтап.ДатаНачалаПлан 		= НайденыеСтроки[0].ОкончаниеМонтаж;
	//ДанныеДатыЭтап.ДатаОкончанияПлан 	= ДанныеДатыЭтап.ДатаНачалаПлан + ПочтиСуткиВСекундах;
	//ДанныеДатыЭтап.Этап 				= "Окончание монтаж";
	//ВывестиТочку(КомплектТочка, Серия, ДанныеДатыЭтап, Неопределено, Неопределено,Ложь,Ложь,ЦветКрайнейТочки);
	//
	//ДанныеДатыЭтап.ДатаНачалаПлан 		= НайденыеСтроки[0].ОкончаниеСЗапасом;
	//ДанныеДатыЭтап.ДатаОкончанияПлан 	= ДанныеДатыЭтап.ДатаНачалаПлан + ПочтиСуткиВСекундах;
	//ДанныеДатыЭтап.Этап 				= "Окончание с запасом";
	//ВывестиТочку(КомплектТочка, Серия, ДанныеДатыЭтап, Неопределено, Неопределено,Ложь,Ложь,ЦветКрайнейТочки,Ложь,Ложь);
	
КонецПроцедуры // ВывестиКрайниеТочки(СтрокаКомплкет,Серия,тзЦвета)

Функция ВывестиПомещение(ЭлементПомещение, Серия, тзЦвета, стОтбор)
	
	СсылкаПомещение = ЭлементПомещение.Помещение; 
	
	ПомещениеТочка 			= Диаграмма.Точки.Добавить();
	ПомещениеТочка.Текст 	= ЭлементПомещение.ПомещениеНаименование;
	ПомещениеТочка.Значение = СсылкаПомещение;
	ПомещениеТочка.Картинка = БиблиотекаКартинок.Помещение;
	
	стДанные = Новый Структура("СсылкаЗначение, Этап, ДатаНачалаПлан, ДатаОкончанияПлан, Помещение"
		, СсылкаПомещение, "Конструирование", ЭлементПомещение.НачалоПроектирования, ЭлементПомещение.ОкончаниеПроектирования, ЭлементПомещение.Помещение);
	
	ВывестиТочку(ПомещениеТочка, Серия, стДанные, СсылкаПомещение, тзЦвета, Ложь, , , Истина);
	
	стДанные.Этап 				= "Производство";
	стДанные.ДатаНачалаПлан 	= ЭлементПомещение.НачалоПроизводства;
	стДанные.ДатаОкончанияПлан 	= ЭлементПомещение.ОкончаниеПроизводства;
	ВывестиТочку(ПомещениеТочка, Серия, стДанные, СсылкаПомещение, тзЦвета, Ложь, , , Истина);
	
	стДанные.Этап 				= "Монтаж";
	стДанные.ДатаНачалаПлан 	= ЭлементПомещение.НачалоМонтажа;
	стДанные.ДатаОкончанияПлан 	= ЭлементПомещение.ОкончаниеМонтажа;
	ВывестиТочку(ПомещениеТочка, Серия, стДанные, СсылкаПомещение, тзЦвета, Ложь, , , Истина);
	
	Возврат ПомещениеТочка; 
	
КонецФункции // ВывестиСделку()

Функция ВывестиТочку(Точка, Серия, ДанныеДатыЭтап, СсылкаЗначение, тзЦвета, Редактирование = Истина, ЕстьРасшифровка = Истина
		, ТекущийЦвет = Неопределено, ЭтоГруппа = Ложь, СократитьНаименование = Ложь)

	ПустаяДата = Дата(1, 1, 1);
	Если ДанныеДатыЭтап.ДатаНачалаПлан = ПустаяДата Тогда
	
		Возврат Неопределено;	
	
	КонецЕсли;
		
	Значение = Диаграмма.ПолучитьЗначение(Точка, Серия);
	Значение.Редактирование = Редактирование;
	
	Интервал 				= Значение.Добавить();
	Интервал.Начало 		= ДанныеДатыЭтап.ДатаНачалаПлан;
	Интервал.Конец 			= ?(ДанныеДатыЭтап.ДатаОкончанияПлан - ДанныеДатыЭтап.ДатаНачалаПлан < 24 * 3600, ДанныеДатыЭтап.ДатаНачалаПлан + 24 * 3600, ДанныеДатыЭтап.ДатаОкончанияПлан);
	Интервал.Текст 			= ДанныеДатыЭтап.Этап;
	
	Если НЕ ТекущийЦвет = Неопределено Тогда
		Интервал.Цвет			= ТекущийЦвет;
	ИначеЕсли НЕ тзЦвета = Неопределено Тогда
		Интервал.Цвет			= омДиаграммаГанта.ПолучитьЦвет(ДанныеДатыЭтап.Этап, тзЦвета, ЭтоГруппа, СократитьНаименование);
	КонецЕсли;
	Если ЕстьРасшифровка Тогда
		Интервал.Расшифровка 	= Новый Структура("Этап, Ссылка, Помещение", ДанныеДатыЭтап.Этап, ДанныеДатыЭтап.СсылкаЗначение, ДанныеДатыЭтап.Помещение);
	Иначе
		Интервал.Расшифровка 	= Неопределено;
	КонецЕсли;
	
	Если СократитьНаименование Тогда
		Интервал.Текст = Лев(Интервал.Текст, 1);
	КонецЕсли;
	
	УстановитьТекстЗначения(Значение.Текст, ДанныеДатыЭтап.ДатаНачалаПлан, ДанныеДатыЭтап.ДатаОкончанияПлан, ДанныеДатыЭтап.Этап); 
	
	Возврат Интервал;
КонецФункции // ВывестиТочку()

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекстЗначения(Текст, ДатаНачала, ДатаОкончания, Этап)
	Текст = Этап + " " + Формат(ДатаНачала, "ДФ=dd.MM.yy") + " - " + Формат(ДатаОкончания, "ДФ=dd.MM.yy");	
КонецПроцедуры

#КонецОбласти 


#Область РасчетДанныхДиаграммы

Функция ПолучитьИнтервалыПомещения(ПустаяДата, тзДатыПроекта)
	
	тзИнтервалыПомещения = Новый ТаблицаЗначений;
	тзИнтервалыПомещения.Колонки.Добавить("Помещение", 					Новый ОписаниеТипов("СправочникСсылка.Помещения"));
	тзИнтервалыПомещения.Колонки.Добавить("ПомещениеНаименование", 		Новый ОписаниеТипов("Строка"));
	тзИнтервалыПомещения.Колонки.Добавить("НачалоПроектирования", 		Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("ОкончаниеПроектирования", 	Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("НачалоПроизводства", 		Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("ОкончаниеПроизводства", 		Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("НачалоМонтажа", 				Новый ОписаниеТипов("Дата"));
	тзИнтервалыПомещения.Колонки.Добавить("ОкончаниеМонтажа", 			Новый ОписаниеТипов("Дата"));
    тзИнтервалыПомещения.Индексы.Добавить("Помещение");
	
	Для каждого х Из тзДатыПроекта Цикл
		
		НайденаяСтрока = тзИнтервалыПомещения.Найти(х.Помещение);
		
		Если НайденаяСтрока = Неопределено Тогда
		
			ЗаполнитьЗначенияСвойств(тзИнтервалыПомещения.Добавить(), х);
		    Продолжить;
	
		КонецЕсли;
		
		АнализДатыНачала("НачалоПроектирования", 		ПустаяДата, х, НайденаяСтрока);
		АнализДатыНачала("НачалоПроизводства", 			ПустаяДата, х, НайденаяСтрока);
		АнализДатыНачала("НачалоМонтажа", 				ПустаяДата, х, НайденаяСтрока);
		
		АнализДатыОкончания("ОкончаниеПроектирования", 	х, НайденаяСтрока);
		АнализДатыОкончания("ОкончаниеПроизводства", 	х, НайденаяСтрока);
		АнализДатыОкончания("ОкончаниеМонтажа", 		х, НайденаяСтрока);
		
		
	КонецЦикла;
	
	Возврат тзИнтервалыПомещения; 

КонецФункции 

Процедура АнализДатыОкончания(ИмяКолонки, СтрокаДатыПроекта, СтрокаИнтервалыПомещения)
	
	Если СтрокаИнтервалыПомещения[ИмяКолонки] < СтрокаДатыПроекта[ИмяКолонки] Тогда
		
		СтрокаИнтервалыПомещения[ИмяКолонки] = СтрокаДатыПроекта[ИмяКолонки];
		
	КонецЕсли;
	
КонецПроцедуры

Процедура АнализДатыНачала(ИмяКолонки, ПустаяДата, СтрокаДатыПроекта, СтрокаИнтервалыПомещения)
	
	Если СтрокаДатыПроекта[ИмяКолонки] = ПустаяДата Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрокаИнтервалыПомещения[ИмяКолонки] = ПустаяДата Тогда
		
		СтрокаИнтервалыПомещения[ИмяКолонки] = СтрокаДатыПроекта[ИмяКолонки];
		
	ИначеЕсли СтрокаИнтервалыПомещения[ИмяКолонки] > СтрокаДатыПроекта[ИмяКолонки] Тогда
		
		СтрокаИнтервалыПомещения[ИмяКолонки] = СтрокаДатыПроекта[ИмяКолонки];
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДатыПроекта()   	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаНачалоМонтажа, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоМонтажа,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаНачалоПроектирования, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоПроектирования,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаНачалоПроизводства, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоПроизводства,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаОкончаниеМонтажа, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеМонтажа,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаОкончаниеПроектирования, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеПроектирования,
		|	ЕСТЬNULL(ДанныеКомплекта.ВнешняяДатаОкончаниеПроизводства, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеПроизводства,
		|	ИзделияКомплект.ОсновноеПомещение КАК Помещение,
		|	ИзделияКомплект.ОсновноеПомещение.Наименование КАК ПомещениеНаименование,
		|	ИзделияКомплект.Наименование КАК НаименованиеКомплект,
		|	ИзделияКомплект.Ссылка КАК Комплект
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних(, ) КАК ДанныеКомплекта
		|		ПО (ДанныеКомплекта.Комплект = ИзделияКомплект.Ссылка)
		|ГДЕ
		|	ИзделияКомплект.Проект = &Проект
		|	И НЕ ИзделияКомплект.ЭтоГруппа
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИзделияКомплект.ОсновноеПомещение.Наименование,
		|	ИзделияКомплект.Наименование";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьДатыПроекта()

Процедура ПолучитьКомплектыДляДобавления(тзДатыПроекта, ПустаяДата)

    спКомплектыДляДобавления.Очистить();
	
	Для каждого х Из тзДатыПроекта Цикл
		
		Если х.НачалоМонтажа = ПустаяДата ИЛИ х.НачалоПроизводства = ПустаяДата ИЛИ х.НачалоПроектирования = ПустаяДата Тогда
		
			спКомплектыДляДобавления.Добавить(х.Комплект, х.НаименованиеКомплект);	
		
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти 

#Область ИзменениеРеквизитов

&НаКлиенте
Процедура спКомплектыДляДобавленияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура спКомплектыДляДобавленияПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура спКомплектыДляДобавленияПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаОбработкаРасшифровки(Элемент, Расшифровки, СтандартнаяОбработка, Дата)

	СтандартнаяОбработка = Ложь;
	Если Расшифровки.Количество() > 1 Тогда
		Если НЕ Расшифровки[1] = Неопределено Тогда
			ПоказатьЗначение(,Расшифровки[1].Ссылка);	
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаВыбор(Элемент, Значения, СтандартнаяОбработка, Дата)
	Если Тип("ТочкаДиаграммыГанта") = ТипЗнч(Значения) Тогда
		Если Тип("СправочникСсылка.ИзделияКомплект") = ТипЗнч(Значения.Значение) Тогда
			СтандартнаяОбработка = Ложь;
			ПоказатьЗначение(,Значения.Значение);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаПриОкончанииРедактированияИнтервала(Элемент, Интервал, ОтменаРедактирования)
	Этап 		= Интервал.Расшифровка.Этап;
	Комплект 	= Интервал.Расшифровка.Ссылка;
	Помещение 	= Интервал.Расшифровка.Помещение;
	
	Начало 	= Интервал.Начало;
	Конец 	= Интервал.Конец;
	
	СекундВСутках = 3600 * 24;
	
	КомплектТочка 	= Диаграмма.УстановитьТочку(Комплект, Помещение);
	Серия 			= Диаграмма.УстановитьСерию("План");
	Значение 		= Диаграмма.ПолучитьЗначение(КомплектТочка, Диаграмма.УстановитьСерию("План"));
	Значение.Редактирование = Истина; 
	
	Диаграмма.Обновление = Ложь;
	
	стДанныеДляЗаписи = Новый Структура("ВнешняяДатаНачалоМонтажа, ВнешняяДатаНачалоПроектирования, ВнешняяДатаНачалоПроизводства, ВнешняяДатаОкончаниеМонтажа, ВнешняяДатаОкончаниеПроектирования, ВнешняяДатаОкончаниеПроизводства", );			
	стДанныеДляЗаписи.Вставить("Комплект", Комплект);  
	
	Для каждого текЗначение Из Значение Цикл
		Если текЗначение.Расшифровка.Этап = "Конструирование" Тогда
			текЗначениеКонструирование 	= текЗначение;
		ИначеЕсли текЗначение.Расшифровка.Этап = "Производство" Тогда
			текЗначениеПроизводство 	= текЗначение;
		ИначеЕсли текЗначение.Расшифровка.Этап = "Монтаж" Тогда
			текЗначениеМонтаж 			= текЗначение;
		КонецЕсли;	
	КонецЦикла;
	
	стДанныеДляЗаписи.ВнешняяДатаНачалоМонтажа 				= текЗначениеМонтаж.Начало; 
	стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования 		= текЗначениеКонструирование.Начало; 
	стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства 		= текЗначениеПроизводство.Начало;
	стДанныеДляЗаписи.ВнешняяДатаОкончаниеМонтажа 			= текЗначениеМонтаж.Конец; 
	стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования 	= текЗначениеКонструирование.Конец; 
	стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства 		= текЗначениеПроизводство.Конец;
	
	Если Этап = "Конструирование" Тогда
		
		стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования 		= Начало; 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования 	= Конец; 
		стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства 		= Конец;
		
		Если текЗначениеПроизводство.Конец - Конец <= СекундВСутках Тогда
			
			текЗначениеПроизводство.Конец = Конец + СекундВСутках;	
			стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства 	= текЗначениеПроизводство.Конец; 
			стДанныеДляЗаписи.ВнешняяДатаНачалоМонтажа 			= стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства;
			
			Если текЗначениеМонтаж.Конец - текЗначениеПроизводство.Конец <= СекундВСутках Тогда
				
				текЗначениеМонтаж.Конец = текЗначениеПроизводство.Конец + СекундВСутках;	
				стДанныеДляЗаписи.ВнешняяДатаОкончаниеМонтажа 	= текЗначениеМонтаж.Конец; 
				
			КонецЕсли;
			текЗначениеМонтаж.Начало = текЗначениеПроизводство.Конец;
		КонецЕсли;
		
		текЗначениеПроизводство.Начало = Конец;
		
	ИначеЕсли Этап = "Производство" Тогда
		стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства 		= Начало; 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства 		= Конец; 
		
		Если Начало - текЗначениеКонструирование.Начало < СекундВСутках Тогда
			
			текЗначениеКонструирование.Начало 					= Начало - СекундВСутках; 	
			стДанныеДляЗаписи.ВнешняяДатаНачалоПроектирования 	= текЗначениеКонструирование.Начало; 
			
		КонецЕсли;
		
		текЗначениеКонструирование.Конец 						= Начало; 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроектирования 	= текЗначениеКонструирование.Конец;
		
		Если текЗначениеМонтаж.Конец - Конец < СекундВСутках  Тогда
		
			текЗначениеМонтаж.Конец 					= Конец + СекундВСутках; 	
			стДанныеДляЗаписи.ВнешняяДатаНачалоМонтажа 	= текЗначениеМонтаж.Конец; 
		
		КонецЕсли;
		
		текЗначениеМонтаж.Начало 						= Конец;
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеМонтажа 	= текЗначениеМонтаж.Начало; 
		
		
	Иначе
		
		стДанныеДляЗаписи.ВнешняяДатаНачалоМонтажа 				= Начало; 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеМонтажа 			= Конец; 
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства 		= Начало; 
		
		Если Начало - текЗначениеПроизводство.Начало < СекундВСутках Тогда
			
			текЗначениеПроизводство.Начало 						= Начало - СекундВСутках; 	
			стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства 	= текЗначениеКонструирование.Начало;
			
			Если текЗначениеПроизводство.Начало - текЗначениеКонструирование.Начало < СекундВСутках Тогда
			
				текЗначениеКонструирование.Начало = текЗначениеПроизводство.Начало - СекундВСутках;	
				стДанныеДляЗаписи.ВнешняяДатаНачалоПроизводства = текЗначениеКонструирование.Начало; 
			
			КонецЕсли;
			
			текЗначениеКонструирование.Конец = текЗначениеПроизводство.Начало;
			стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства 	= текЗначениеКонструирование.Конец;
		КонецЕсли;
		
		текЗначениеПроизводство.Конец 						= Начало; 	
		стДанныеДляЗаписи.ВнешняяДатаОкончаниеПроизводства 	= текЗначениеПроизводство.Конец;
		
	КонецЕсли;	
	
	Диаграмма.Обновление = Истина;
	
	ЗаписатьНаСервере(стДанныеДляЗаписи);
	
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ЗаписатьНаСервере(стДанныеДляЗаписи)

		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДанныеКомплекта.Конструктор КАК Конструктор,
			|	ДанныеКомплекта.ТекущаяСтадия КАК ТекущаяСтадия
			|ИЗ
			|	РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект = &Комплект) КАК ДанныеКомплекта";
		
		Запрос.УстановитьПараметр("Комплект", стДанныеДляЗаписи.Комплект);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Период = ТекущаяДата();
		
		НаборЗаписей = РегистрыСведений.ДанныеКомплекта.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Период);
		НаборЗаписей.Отбор.Комплект.Установить(стДанныеДляЗаписи.Комплект);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		НоваяЗапись.Период = Период; 
		ЗаполнитьЗначенияСвойств(НоваяЗапись, стДанныеДляЗаписи);
		
		НоваяЗапись.Автор = ПараметрыСеанса.ТекущийПользователь;
		
		НаборЗаписей.Записать();
		

КонецПроцедуры


&НаКлиенте
Процедура ДиаграммаТаблицаТочкаПриИзменении(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти 

#Область ДейстаияФормы


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ДатаДобавления = ТекущаяДата();
КонецПроцедуры

#КонецОбласти  

#Область Команды

&НаКлиенте
Процедура ДобавитьКомплекты(Команда)
	ДобавитьКомплектыНаСервере();
КонецПроцедуры
	
#КонецОбласти

#Область ИсполнениеКоманды

&НаСервере
Процедура ДобавитьКомплектыНаСервере()
	
	ДлинаЭтапа  	= 3600 * 24 * 10;
	Период			= ТекущаяДата();
	Автор 			= ПараметрыСеанса.ТекущийПользователь;
	
	Для каждого х Из спКомплектыДляДобавления Цикл
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДанныеКомплекта.Комплект КАК Комплект,
			|	ДанныеКомплекта.Автор КАК Автор,
			|	ДанныеКомплекта.ВнешняяДатаНачалоМонтажа КАК ВнешняяДатаНачалоМонтажа,
			|	ДанныеКомплекта.ВнешняяДатаНачалоПроектирования КАК ВнешняяДатаНачалоПроектирования,
			|	ДанныеКомплекта.ВнешняяДатаНачалоПроизводства КАК ВнешняяДатаНачалоПроизводства,
			|	ДанныеКомплекта.ВнешняяДатаОкончаниеМонтажа КАК ВнешняяДатаОкончаниеМонтажа,
			|	ДанныеКомплекта.ВнешняяДатаОкончаниеПроектирования КАК ВнешняяДатаОкончаниеПроектирования,
			|	ДанныеКомплекта.ВнешняяДатаОкончаниеПроизводства КАК ВнешняяДатаОкончаниеПроизводства,
			|	ДанныеКомплекта.Конструктор КАК Конструктор,
			|	ДанныеКомплекта.ТекущаяСтадия КАК ТекущаяСтадия
			|ИЗ
			|	РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект = &Комплект) КАК ДанныеКомплекта";
		
		Запрос.УстановитьПараметр("Комплект", х.Значение);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		НаборЗаписей = РегистрыСведений.ДанныеКомплекта.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Период);
		НаборЗаписей.Отбор.Комплект.Установить(х.Значение);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		НоваяЗапись.Период = Период; 
		НоваяЗапись.Комплект = х.Значение; 
		
		НоваяЗапись.Автор 	= Автор;
		НоваяЗапись.ВнешняяДатаНачалоПроектирования 	= ДатаДобавления;
		НоваяЗапись.ВнешняяДатаОкончаниеПроектирования 	= НоваяЗапись.ВнешняяДатаНачалоПроектирования 	+ ДлинаЭтапа;
		НоваяЗапись.ВнешняяДатаНачалоПроизводства 		= НоваяЗапись.ВнешняяДатаОкончаниеПроектирования;
		НоваяЗапись.ВнешняяДатаОкончаниеПроизводства 	= НоваяЗапись.ВнешняяДатаНачалоПроизводства 	+ ДлинаЭтапа;
		НоваяЗапись.ВнешняяДатаНачалоМонтажа 			= НоваяЗапись.ВнешняяДатаОкончаниеПроизводства;
		НоваяЗапись.ВнешняяДатаОкончаниеМонтажа 		= НоваяЗапись.ВнешняяДатаНачалоМонтажа 			+ ДлинаЭтапа;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
	Элементы.ГруппаКомплектыДляДобавления.Видимость = Ложь;
	
	ПостроитьДиаграммуГанта();	
	
КонецПроцедуры

#КонецОбласти


