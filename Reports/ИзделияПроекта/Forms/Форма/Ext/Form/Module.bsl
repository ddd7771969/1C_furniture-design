
&НаКлиенте
Процедура СделкаПриИзменении(Элемент)
	ВывестиДанныеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Проект) Тогда
		ВывестиДанныеНаСервере();	
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ВывестиДанныеНаСервере()
	
	Таб = ЭтаФорма.ПолеВывода;
	Таб.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Проект) Тогда
		Возврат;	
	КонецЕсли;
	
	мИзделияЗаказчикаВТаблице 	= Новый Массив;
	мКомплектыИзделийВТаблице 	= Новый Массив;
	мГЧБезКомплекта 			= Новый Массив;
	мЭлементыВТаблице 			= Новый Массив;
	
	стДанные = ПолучитьДанные();
	
	ЦветОшибка 	= Новый Цвет(255, 100, 110);
	ЦветБелый 	= Новый Цвет(255, 255, 255);
	
	
	Если ЭтаФорма.Порядок = 0 Тогда
		Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("Макет");
	Иначе
		Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("Макет1");
	КонецЕсли;
	
	обШапка = Макет.ПолучитьОбласть("Шапка");
	обСтрока = Макет.ПолучитьОбласть("Строка");
	
	обШапка.Параметры.Проект 		= Проект;
	обШапка.Параметры.Сотрудники 	= Справочники.Проекты.ПолучитьСотрудниковСделки(Проект).СотрудникиСтрокой;
	
	Таб.Вывести(обШапка); 
	Таб.ФиксацияСверху = Таб.ВысотаТаблицы;
	
	текЭлемент1 = Неопределено;
	КоличествоЭлемент1 = 0;
	Для каждого текДанные Из стДанные.тзДанные Цикл
		
		Если НЕ текЭлемент1 = текДанные.Элемент1 Тогда
			
			Если НЕ текЭлемент1 = Неопределено Тогда
				
				Если КоличествоЭлемент1 > 1 Тогда
					
					текСтрока = Таб.ВысотаТаблицы;
					Если ЭтаФорма.Порядок = 0 Тогда
						
						ИмяОбласти = "R" + текСтрока + "C2:R" + (текСтрока - КоличествоЭлемент1 + 1) + "C2"; 
						Таб.Область(ИмяОбласти).Объединить();
						ИмяОбласти = "R" + текСтрока + "C3:R" + (текСтрока - КоличествоЭлемент1 + 1) + "C3"; 
						Таб.Область(ИмяОбласти).Объединить();
						
					Иначе
						ИмяОбласти = "R" + текСтрока + "C2:R" + (текСтрока - КоличествоЭлемент1 + 1) + "C3"; 
						Таб.Область(ИмяОбласти).Объединить();
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли; 
			КоличествоЭлемент1 = текДанные.КоличествоЭлемент1;
			текЭлемент1 = текДанные.Элемент1;
			
		КонецЕсли;
		
		Если ЭтаФорма.Порядок = 0 Тогда
			
			НайденаяСтрока = стДанные.тзКонтрольИзделия.Найти(текДанные.Элемент1,"Ссылка");
			Если НЕ НайденаяСтрока = Неопределено Тогда
				НайденаяСтрока.Найден = Истина;
			КонецЕсли;
			
			НайденаяСтрока = стДанные.тзКонтрольКомплект.Найти(текДанные.Элемент2,"Ссылка");
			Если НЕ НайденаяСтрока = Неопределено Тогда
				НайденаяСтрока.Найден = Истина;
			КонецЕсли;
			
			Если мИзделияЗаказчикаВТаблице.Найти(текДанные.Элемент1) = Неопределено Тогда
				мИзделияЗаказчикаВТаблице.Добавить(текДанные.Элемент1);
			КонецЕсли;
			
			Если мКомплектыИзделийВТаблице.Найти(текДанные.Элемент2) = Неопределено Тогда
				мКомплектыИзделийВТаблице.Добавить(текДанные.Элемент2);
			КонецЕсли;
			
			Если мЭлементыВТаблице.Найти(текДанные.ИзделиеЭлемент) = Неопределено Тогда
				мЭлементыВТаблице.Добавить(текДанные.ИзделиеЭлемент);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(текДанные.ГабаритныйЧертеж) ИЛИ текДанные.БезГабаритногоЧертежа Тогда
				обСтрока.Области.ГабаритныйЧертеж.ЦветФона = ЦветБелый;	
			Иначе	
				обСтрока.Области.ГабаритныйЧертеж.ЦветФона = ЦветОшибка;	
			КонецЕсли;
			
		Иначе
			
			НайденаяСтрока = стДанные.тзКонтрольКомплект.Найти(текДанные.Элемент1,"Ссылка");
			Если НЕ НайденаяСтрока = Неопределено Тогда
				НайденаяСтрока.Найден = Истина;
			КонецЕсли;
			
			НайденаяСтрока = стДанные.тзКонтрольИзделия.Найти(текДанные.Элемент2,"Ссылка");
			Если НЕ НайденаяСтрока = Неопределено Тогда
				НайденаяСтрока.Найден = Истина;
			КонецЕсли;
			
			Если мИзделияЗаказчикаВТаблице.Найти(текДанные.Элемент2) = Неопределено Тогда
				мИзделияЗаказчикаВТаблице.Добавить(текДанные.Элемент2);
			КонецЕсли;
			
			Если мКомплектыИзделийВТаблице.Найти(текДанные.Элемент1) = Неопределено Тогда
				мКомплектыИзделийВТаблице.Добавить(текДанные.Элемент1);
			КонецЕсли;
			
			Если мЭлементыВТаблице.Найти(текДанные.ИзделиеЭлемент) = Неопределено Тогда
				мЭлементыВТаблице.Добавить(текДанные.ИзделиеЭлемент);
			КонецЕсли;
			
		КонецЕсли;
		
		обСтрока.Параметры.Заполнить(текДанные);
		
		Таб.Вывести(обСтрока); 
	КонецЦикла;
	
	обШапка = Макет.ПолучитьОбласть("ЭлементыБезРодителяШ");
	обСтрока = Макет.ПолучитьОбласть("ЭлементыБезРодителяС");

	стОтбор = Новый Структура("Найден", Ложь);      
	
	НайденыеСтроки = Новый Массив;
	НайденыеСтроки1 = Новый Массив;
	НайденыеСтроки2 = Новый Массив;
	
	Если ЭтаФорма.Порядок = 0 Тогда
		
		Для каждого х Из стДанные.тзКонтрольИзделия Цикл
			Если НЕ х.Найден И мИзделияЗаказчикаВТаблице.Найти(х.Ссылка) = Неопределено Тогда
				НайденыеСтроки.Добавить(х);
			КонецЕсли;
		КонецЦикла;
		
		Для каждого х Из стДанные.тзКонтрольКомплект Цикл
			Если НЕ х.Найден И мКомплектыИзделийВТаблице.Найти(х.Ссылка) = Неопределено Тогда
				НайденыеСтроки1.Добавить(х);
			КонецЕсли;
		КонецЦикла;

		Для каждого х Из стДанные.тзГЧБезКомплекта Цикл
			НайденыеСтроки2.Добавить(х);
		КонецЦикла;

	Иначе

		Для каждого х Из стДанные.тзКонтрольИзделия Цикл
			Если НЕ х.Найден И мИзделияЗаказчикаВТаблице.Найти(х.Ссылка) = Неопределено Тогда
				НайденыеСтроки1.Добавить(х);
			КонецЕсли;
		КонецЦикла; 
		
		Для каждого х Из стДанные.тзКонтрольКомплект Цикл
			Если НЕ х.Найден И мКомплектыИзделийВТаблице.Найти(х.Ссылка) = Неопределено Тогда
				НайденыеСтроки.Добавить(х);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	КоличествоИзделийНеНайдено 			= НайденыеСтроки.Количество();	
	КоличествоКомплектНеНайдено 		= НайденыеСтроки1.Количество();	
	КоличествоГЧБезКомплектаНеНайдено 	= НайденыеСтроки2.Количество();	
	
	Если КоличествоИзделийНеНайдено + КоличествоКомплектНеНайдено + КоличествоКомплектНеНайдено > 0 Тогда
		
		Если КоличествоИзделийНеНайдено > 0 Тогда
			обШапка.Параметры.Заголовок = ?(ЭтаФорма.Порядок = 0,"Изделия заказчика", "Изделия комплект");
		КонецЕсли;
		
		Если КоличествоКомплектНеНайдено > 0 Тогда
			обШапка.Параметры.Заголовок1 = ?(ЭтаФорма.Порядок = 0, "Изделия комплект", "Изделия заказчика");
		КонецЕсли;
		
		Если КоличествоГЧБезКомплектаНеНайдено > 0 Тогда
			обШапка.Параметры.Заголовок2 = ?(ЭтаФорма.Порядок = 0, "Габартные чертежы без комплектов", "");
		КонецЕсли;
		
		Таб.Вывести(обШапка); 
		
		МинИндекс = Мин(КоличествоИзделийНеНайдено, КоличествоКомплектНеНайдено, КоличествоГЧБезКомплектаНеНайдено) - 1;
		
		Для х = 0 По МинИндекс Цикл
			
			обСтрока.Параметры.ИзделиеЭлемент 	= НайденыеСтроки[х].Ссылка;
			обСтрока.Параметры.ИзделиеЭлемент1 	= НайденыеСтроки1[х].Ссылка;
			обСтрока.Параметры.ГЧБезКомплекта 	= НайденыеСтроки2[х].Ссылка;
			Таб.Вывести(обСтрока);
			
		КонецЦикла; 
		
		МаксИндекс = Макс(КоличествоИзделийНеНайдено, КоличествоКомплектНеНайдено, КоличествоГЧБезКомплектаНеНайдено) - 1; 
		
		Для х = МинИндекс  + 1 По МаксИндекс Цикл
			
			Если КоличествоИзделийНеНайдено > х Тогда
				обСтрока.Параметры.ИзделиеЭлемент   = НайденыеСтроки[х].Ссылка;
			Иначе
				обСтрока.Параметры.ИзделиеЭлемент   = Неопределено;
			КонецЕсли; 
			
			Если КоличествоКомплектНеНайдено > х Тогда
				обСтрока.Параметры.ИзделиеЭлемент1   = НайденыеСтроки1[х].Ссылка;
			Иначе
				обСтрока.Параметры.ИзделиеЭлемент1   = Неопределено;
			КонецЕсли; 

			Если КоличествоГЧБезКомплектаНеНайдено > х Тогда
				обСтрока.Параметры.Элемент3   = НайденыеСтроки2[х].Ссылка;
			Иначе
				обСтрока.Параметры.Элемент3   = Неопределено;
			КонецЕсли; 

			
			Таб.Вывести(обСтрока);
			
		КонецЦикла;
		
	КонецЕсли;
	
	мЭлементыИ = ЭлементыБезРодителя(стДанные.тзИзделияЭлемент, стДанные.тзИзделияЗаказчикаЭлемент, мЭлементыВТаблице); 
	мЭлементыК = ЭлементыБезРодителя(стДанные.тзИзделияЭлемент, стДанные.тзИзделияКомплект, мЭлементыВТаблице); 
	
	КоличествоИ = мЭлементыИ.Количество() - 1;
	КоличествоК = мЭлементыК.Количество() - 1;

	
	Если КоличествоИ < 0 И ЭтаФорма.Порядок = 0 Тогда
		Возврат;
	ИначеЕсли КоличествоК < 0 И ЭтаФорма.Порядок = 1 Тогда
		Возврат;
	ИначеЕсли КоличествоИ >= 0 И КоличествоК >= 0 Тогда	
		обШапка.Параметры.Заголовок = "Элементы без изделия";
		обШапка.Параметры.Заголовок1 = "Элементы без комплекта";
		Таб.Вывести(обШапка);
		
		МаксКол = ?(КоличествоИ > КоличествоК, КоличествоИ, КоличествоК);
		
		Для х = 0 По МаксКол Цикл
			Если х > КоличествоИ Тогда
				обСтрока.Параметры.ИзделиеЭлемент = Неопределено;
			Иначе
				обСтрока.Параметры.ИзделиеЭлемент = мЭлементыИ[х];
			КонецЕсли;
			Если х > КоличествоК Тогда
				обСтрока.Параметры.ИзделиеЭлемент1 = Неопределено;
			Иначе
				обСтрока.Параметры.ИзделиеЭлемент1 = мЭлементыК[х];
			КонецЕсли;
			Таб.Вывести(обСтрока);
		КонецЦикла;
	ИначеЕсли КоличествоИ > 0 Тогда
		обШапка.Параметры.Заголовок = "Элементы без изделия";
		обШапка.Параметры.Заголовок1 = "";
		Таб.Вывести(обШапка);
		Для х = 0 По КоличествоИ Цикл
			обСтрока.Параметры.ИзделиеЭлемент = мЭлементыИ[х];
			обСтрока.Параметры.ИзделиеЭлемент1 = Неопределено;
			Таб.Вывести(обСтрока);
		КонецЦикла;
	Иначе
		обШапка.Параметры.Заголовок = "Элементы без комплекта";
		обШапка.Параметры.Заголовок1 = "";
		Таб.Вывести(обШапка);
		Для х = 0 По КоличествоК Цикл
			обСтрока.Параметры.ИзделиеЭлемент = мЭлементыК[х];
			обСтрока.Параметры.ИзделиеЭлемент1 = Неопределено;
			Таб.Вывести(обСтрока);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // ВывестиДанные() 

&НаСервере
Функция ЭлементыБезРодителя(тзИзделияЭлемент, тз, мЭлементыВТаблице)
	
	мЭлементы = Новый Массив;
	
	Для каждого текЭлемент Из тзИзделияЭлемент Цикл 
		
		Если НЕ мЭлементыВТаблице.Найти(текЭлемент.ИзделиеЭлемент) = Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		
		НайденаяСтрока = тз.Найти(текЭлемент.ИзделиеЭлемент,"ИзделиеЭлемент");
		
		Если НайденаяСтрока = Неопределено Тогда
			
			мЭлементы.Добавить(текЭлемент.ИзделиеЭлемент);	
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат мЭлементы;
КонецФункции // ЭлементыБезИзделий()

&НаСервере
Функция ПолучитьДанные()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияЗаказчика.Ссылка КАК Ссылка,
		|	ИСТИНА КАК Найден,
		|	ЕСТЬNULL(ДанныеИзделияЗаказчика.Актуальность, ЛОЖЬ) КАК Актуальность,
		|	ЕСТЬNULL(ДанныеИзделияЗаказчика.НаименованиеСАртикулом, """") КАК НаименованиеСАртикулом,
		|	СУММА(ВЫБОР
		|			КОГДА ПриложенияКДоговорамИзделияЗаказчика.Ссылка.Отказ
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ) КАК Количество
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	Справочник.ИзделияЗаказчика КАК ИзделияЗаказчика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеИзделияЗаказчика КАК ДанныеИзделияЗаказчика
		|		ПО (ДанныеИзделияЗаказчика.ИзделиеЗаказчика = ИзделияЗаказчика.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПриложенияКДоговорам.ИзделияЗаказчика КАК ПриложенияКДоговорамИзделияЗаказчика
		|		ПО (ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика = ИзделияЗаказчика.Ссылка)
		|ГДЕ
		|	ИзделияЗаказчика.Проект = &Проект
		|	И НЕ ИзделияЗаказчика.ЭтоГруппа
		|
		|СГРУППИРОВАТЬ ПО
		|	ИзделияЗаказчика.Ссылка,
		|	ЕСТЬNULL(ДанныеИзделияЗаказчика.Актуальность, ЛОЖЬ),
		|	ЕСТЬNULL(ДанныеИзделияЗаказчика.НаименованиеСАртикулом, """")
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.Ссылка КАК Ссылка,
		|	вт.Найден КАК Найден,
		|	вт.Актуальность КАК Актуальность,
		|	вт.НаименованиеСАртикулом КАК НаименованиеСАртикулом
		|ИЗ
		|	вт КАК вт
		|ГДЕ
		|	вт.Количество > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ГабаритныеЧертежи.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ГабаритныеЧертежи КАК ГабаритныеЧертежи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИзделияКомплект КАК ИзделияКомплект
		|		ПО (ИзделияКомплект.ГабаритныйЧертеж = ГабаритныеЧертежи.Ссылка)
		|ГДЕ
		|	ИзделияКомплект.Ссылка ЕСТЬ NULL
		|	И ГабаритныеЧертежи.Проект = &Проект
		|	И НЕ ГабаритныеЧертежи.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	Результат = Запрос.ВыполнитьПакет();
	
	
	тзКонтрольИзделия 	= Результат[1].Выгрузить();
	тзГЧБезКомплекта 	= Результат[2].Выгрузить();
	
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК Ссылка,
		|	ЛОЖЬ КАК Найден
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|ГДЕ
		|	ИзделияКомплект.Проект = &Проект
		|	И НЕ ИзделияКомплект.ЭтоГруппа
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИзделияКомплект.Наименование";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	тзКонтрольКомплект = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияЗаказчикаИзделияЭлемент.Ссылка КАК ИзделиеЗаказчика,
		|	ИзделияЗаказчикаИзделияЭлемент.ИзделиеЭлемент КАК ИзделиеЭлемент
		|ИЗ
		|	Справочник.ИзделияЗаказчика.ИзделияЭлемент КАК ИзделияЗаказчикаИзделияЭлемент
		|ГДЕ
		|	ИзделияЗаказчикаИзделияЭлемент.Ссылка.Проект = &Проект
		|	И НЕ ИзделияЗаказчикаИзделияЭлемент.Ссылка.ЭтоГруппа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзделияКомплектИзделияЭлемент.Ссылка КАК ИзделиеКомплект,
		|	ИзделияКомплектИзделияЭлемент.ИзделиеЭлемент КАК ИзделиеЭлемент,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Конструктор КАК Конструктор,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.ГабаритныйЧертеж КАК ГабаритныйЧертеж,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Родитель.Композиция КАК Композиция,
		|	ДатыКомплекта.БезЗамера КАК БезЗамера,
		|	ЕСТЬNULL(ДатыКомплекта.БезГабаритногоЧертежа, ЛОЖЬ) КАК БезГабаритногоЧертежа,
		|	ЕСТЬNULL(ДатыКомплекта.Код_в_PDM, ЛОЖЬ) КАК Код_в_PDM
		|ИЗ
		|	Справочник.ИзделияКомплект.ИзделияЭлемент КАК ИзделияКомплектИзделияЭлемент
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|		ПО ИзделияКомплектИзделияЭлемент.Ссылка = ДатыКомплекта.Комплект
		|ГДЕ
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Проект = &Проект
		|	И НЕ ИзделияКомплектИзделияЭлемент.Ссылка.ЭтоГруппа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзделияЭлемент.Ссылка КАК ИзделиеЭлемент,
		|	ВЫБОР
		|		КОГДА АктуальныеИзделияКомплектыЭлементы.ИзделиеКомплектЭлемент ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК АктуальностьЭ
		|ИЗ
		|	Справочник.ИзделияЭлемент КАК ИзделияЭлемент
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АктуальныеИзделияКомплектыЭлементы КАК АктуальныеИзделияКомплектыЭлементы
		|		ПО (АктуальныеИзделияКомплектыЭлементы.ИзделиеКомплектЭлемент = ИзделияЭлемент.Ссылка)
		|ГДЕ
		|	ИзделияЭлемент.Проект = &Проект
		|	И НЕ ИзделияЭлемент.ЭтоГруппа
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА АктуальныеИзделияКомплектыЭлементы.ИзделиеКомплектЭлемент ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ,
		|	ИзделияЭлемент.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзделияЗаказчика.Ссылка КАК ИзделиеЗаказчика,
		|	ДанныеИзделияЗаказчика.НаименованиеСАртикулом КАК НаименованиеСАртикулом,
		|	ДанныеИзделияЗаказчика.Актуальность КАК Актуальность,
		|	СУММА(ВЫБОР
		|			КОГДА ПриложенияКДоговорамИзделияЗаказчика.Ссылка.Отказ
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ) КАК Количество
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	Справочник.ИзделияЗаказчика КАК ИзделияЗаказчика
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеИзделияЗаказчика КАК ДанныеИзделияЗаказчика
		|		ПО ИзделияЗаказчика.Ссылка = ДанныеИзделияЗаказчика.ИзделиеЗаказчика
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПриложенияКДоговорам.ИзделияЗаказчика КАК ПриложенияКДоговорамИзделияЗаказчика
		|		ПО (ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика = ИзделияЗаказчика.Ссылка)
		|ГДЕ
		|	ИзделияЗаказчика.Проект = &Проект
		|	И НЕ ИзделияЗаказчика.ЭтоГруппа
		|
		|СГРУППИРОВАТЬ ПО
		|	ИзделияЗаказчика.Ссылка,
		|	ДанныеИзделияЗаказчика.НаименованиеСАртикулом,
		|	ДанныеИзделияЗаказчика.Актуальность
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.ИзделиеЗаказчика КАК ИзделиеЗаказчика,
		|	вт.НаименованиеСАртикулом КАК НаименованиеСАртикулом,
		|	вт.Актуальность КАК Актуальность
		|ИЗ
		|	вт КАК вт
		|ГДЕ
		|	вт.Количество > 0";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	тзИзделияЗаказчикаЭлемент	= РезультатЗапроса[0].Выгрузить();
	тзИзделияКомплект 			= РезультатЗапроса[1].Выгрузить();
	тзИзделияЭлемент 			= РезультатЗапроса[2].Выгрузить();
	тзИзделияЗаказчика			= РезультатЗапроса[4].Выгрузить();

	тзИзделияЗаказчикаЭлемент.Индексы.Добавить("ИзделиеЗаказчика");
	тзИзделияЗаказчика.Индексы.Добавить("ИзделиеЗаказчика");
	тзИзделияКомплект.Индексы.Добавить("ИзделиеЭлемент");
	тзИзделияЭлемент.Индексы.Добавить("ИзделиеЭлемент");
	
	тзИзделияЗаказчика.Сортировать("НаименованиеСАртикулом");
	тзИзделияЭлемент.Сортировать("ИзделиеЭлемент");
	Если ЭтаФорма.Порядок = 0 Тогда
		тзИзделияЗаказчикаЭлемент.Сортировать("ИзделиеЭлемент"); 
		тзИзделияКомплект.Сортировать("ИзделиеЭлемент"); 
	Иначе
		тзИзделияЗаказчикаЭлемент.Сортировать("ИзделиеЗаказчика"); 
		тзИзделияКомплект.Сортировать("ИзделиеКомплект"); 
	КонецЕсли;
	
	ОписаниеТипаБулево = Новый ОписаниеТипов("Булево");
	
	тзДанные = Новый ТаблицаЗначений;
	тзДанные.Колонки.Добавить("Актуальность");
	тзДанные.Колонки.Добавить("Элемент1");
	тзДанные.Колонки.Добавить("стрЭлемент1");
	тзДанные.Колонки.Добавить("КоличествоЭлемент1");
	тзДанные.Колонки.Добавить("АктуальностьЭ");
	тзДанные.Колонки.Добавить("ИзделиеЭлемент");
	тзДанные.Колонки.Добавить("Элемент2");
	тзДанные.Колонки.Добавить("стрЭлемент2");
	тзДанные.Колонки.Добавить("Конструктор");
	тзДанные.Колонки.Добавить("ГабаритныйЧертеж");
	тзДанные.Колонки.Добавить("Композация");
	тзДанные.Колонки.Добавить("БезЗамера", ОписаниеТипаБулево);
	тзДанные.Колонки.Добавить("БезГабаритногоЧертежа", ОписаниеТипаБулево);
	тзДанные.Колонки.Добавить("Код_в_PDM");
	тзДанные.Колонки.Добавить("КодВыгрузкиВБитрикс");    

	стОтборИзделиеЗаказчика = Новый Структура("ИзделиеЗаказчика", 	);
	стОтборИзделияЭлемент 	= Новый Структура("ИзделиеЭлемент", 	);
	стОтборИзделиеКомплект 	= Новый Структура("ИзделиеКомплект", 	);
	
	Если ЭтаФорма.Порядок = 0 Тогда 
		
		Для каждого текИзделие Из тзИзделияЗаказчика Цикл 
			стОтборИзделиеЗаказчика.ИзделиеЗаказчика = текИзделие.ИзделиеЗаказчика;
			
			НайденыеСтроки = тзИзделияЗаказчикаЭлемент.НайтиСтроки(стОтборИзделиеЗаказчика);
			КоличествоЭлемент1 = НайденыеСтроки.Количество();
			Если КоличествоЭлемент1 = 0 Тогда
				
				НС = тзДанные.Добавить();
				НС.Актуальность 		= текИзделие.Актуальность;
				НС.Элемент1 			= текИзделие.ИзделиеЗаказчика;
				НС.стрЭлемент1 			= текИзделие.НаименованиеСАртикулом;
				НС.КоличествоЭлемент1 	= КоличествоЭлемент1;
				
			Иначе
				
				Для каждого текИзделиеЭлемент Из НайденыеСтроки Цикл
					
					стОтборИзделияЭлемент.ИзделиеЭлемент = текИзделиеЭлемент.ИзделиеЭлемент;	
					НайденыеСтрокиИзделияЭлемент = тзИзделияЭлемент.НайтиСтроки(стОтборИзделияЭлемент);
					Для каждого текИзделиеЭлемент Из НайденыеСтрокиИзделияЭлемент Цикл
						
						НайденыеСтрокиКомплекты = тзИзделияКомплект.НайтиСтроки(стОтборИзделияЭлемент);
						Если НайденыеСтрокиКомплекты.Количество() = 0 Тогда
						
							НС = тзДанные.Добавить();
							НС.Актуальность 		= текИзделие.Актуальность;
							НС.Элемент1 			= текИзделие.ИзделиеЗаказчика;
							НС.стрЭлемент1 			= текИзделие.НаименованиеСАртикулом;
							НС.КоличествоЭлемент1 	= КоличествоЭлемент1;
							НС.АктуальностьЭ 		= текИзделиеЭлемент.АктуальностьЭ;
							НС.ИзделиеЭлемент 		= текИзделиеЭлемент.ИзделиеЭлемент;
							НС.Элемент2 			= Неопределено;
							НС.стрЭлемент2 			= "";
							НС.Конструктор 			= Неопределено;
						
						Иначе
							
							Для каждого текКомплект Из НайденыеСтрокиКомплекты Цикл
								
								НС = тзДанные.Добавить();
							
								ЗаполнитьЗначенияСвойств(НС, текКомплект);
							
								НС.Актуальность 		= текИзделие.Актуальность;
								НС.Элемент1 			= текИзделие.ИзделиеЗаказчика;
								НС.стрЭлемент1 			= текИзделие.НаименованиеСАртикулом;
								НС.КоличествоЭлемент1 	= КоличествоЭлемент1;
								НС.АктуальностьЭ 		= текИзделиеЭлемент.АктуальностьЭ;
								НС.ИзделиеЭлемент 		= текИзделиеЭлемент.ИзделиеЭлемент;
								НС.Элемент2 			= текКомплект.ИзделиеКомплект;
								НС.стрЭлемент2 			= Строка(текКомплект.ИзделиеКомплект);
								
							КонецЦикла;	
							
						КонецЕсли;

					КонецЦикла;
					
				КонецЦикла;
				
			КонецЕсли;  
			
		КонецЦикла;
		
		Для каждого х Из тзКонтрольИзделия Цикл
			
			Если тзДанные.Найти(х.Ссылка, "Элемент1") = Неопределено Тогда
				
				НС 						= тзДанные.Добавить();
				НС.Актуальность 		= х.Актуальность;
				НС.Элемент1 			= х.Ссылка;
				НС.стрЭлемент1 			= х.НаименованиеСАртикулом;
				НС.КоличествоЭлемент1 	= 0;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		мИзделияКомплект = омТЧ_НаКлиентеСервере.КолонкуВМассив(тзИзделияКомплект,"ИзделиеКомплект");
		
		Для каждого текИзделиеКомплект Из мИзделияКомплект Цикл 
			
			стОтборИзделиеКомплект.ИзделиеКомплект = текИзделиеКомплект;	

			НайденыеСтрокиИзделияЭлемент = тзИзделияКомплект.НайтиСтроки(стОтборИзделиеКомплект);
			КоличествоЭлемент1 = НайденыеСтрокиИзделияЭлемент.Количество();
			Для каждого текИзделиеЭлемент Из НайденыеСтрокиИзделияЭлемент Цикл
				стОтборИзделияЭлемент.ИзделиеЭлемент = текИзделиеЭлемент.ИзделиеЭлемент;
				НайденыеСтрокиИзделияЗаказчика = тзИзделияЗаказчикаЭлемент.НайтиСтроки(стОтборИзделияЭлемент);
				
				Для каждого текИзделияЗаказчика Из НайденыеСтрокиИзделияЗаказчика Цикл
					
					ИзделиеЗаказчика 	= тзИзделияЗаказчика.Найти(текИзделияЗаказчика.ИзделиеЗаказчика,"ИзделиеЗаказчика");
					АктуальностьЭ 	= тзИзделияЭлемент.Найти(текИзделиеЭлемент.ИзделиеЭлемент,"ИзделиеЭлемент").АктуальностьЭ;
					НС = тзДанные.Добавить();
					НС.Актуальность = ИзделиеЗаказчика.Актуальность;
					НС.Элемент1 = текИзделиеКомплект;
					НС.стрЭлемент1 = Строка(текИзделиеКомплект);
					НС.КоличествоЭлемент1 = КоличествоЭлемент1;
					НС.АктуальностьЭ = АктуальностьЭ;
					НС.ИзделиеЭлемент = текИзделиеЭлемент.ИзделиеЭлемент;
					НС.Элемент2 = текИзделияЗаказчика.ИзделиеЗаказчика;
					НС.стрЭлемент2 = ИзделиеЗаказчика.НаименованиеСАртикулом;
					
					ЗаполнитьЗначенияСвойств(НС, текИзделиеКомплект);
				КонецЦикла;
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Новый Структура("тзДанные,тзИзделияЭлемент,тзИзделияЗаказчикаЭлемент,тзИзделияКомплект,тзКонтрольИзделия,тзКонтрольКомплект,тзГЧБезКомплекта"
			,тзДанные, тзИзделияЭлемент, тзИзделияЗаказчикаЭлемент, тзИзделияКомплект, тзКонтрольИзделия
			, тзКонтрольКомплект, тзГЧБезКомплекта);
	
КонецФункции // ПолучитьДанные() 

&НаКлиенте
Процедура Обновить(Команда)
	ВывестиДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьКонструктораПриИзменении(Элемент)
	ВывестиДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьКрайниеДатыПриИзменении(Элемент)
	ВывестиДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьСуммыПриИзменении(Элемент)
	ВывестиДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПорядокПриИзменении(Элемент)
	ВывестиДанныеНаСервере();
КонецПроцедуры
