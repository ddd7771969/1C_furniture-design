
&НаКлиенте
Процедура ИмяФайлаЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
     
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Фильтр = НСтр("ru = 'Файл csv'") + " (*.csv)|*.csv";
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПродолжитьЗагрузкуИзФайла", ЭтотОбъект);
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(ОписаниеОповещения, ДиалогВыбораФайла);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗагрузкуИзФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	тзДанныеФайла.Очистить();
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;

	
	ПолноеИмяФайла = ВыбранныеФайлы[0];

	текФайл = Новый ЧтениеТекста(ПолноеИмяФайла,КодировкаТекста.UTF8); 
	СтрокаФайла = текФайл.ПрочитатьСтроку();
	//а не был ли файл пуст?
	Если СтрокаФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	мНаименованиеКолонок = Новый Массив(5);
	
	Если ЕстьЗаголовок Тогда
		ЗаполнитьЗаголовкиТаблицы(СтрокаФайла, мНаименованиеКолонок);
	Иначе	
		мНаименованиеКолонок[0] 	= "артикул";		
		мНаименованиеКолонок[1] 	= "наименование";		
		мНаименованиеКолонок[2] 	= "количество";		
		мНаименованиеКолонок[3] 	= "определённый";		
		мНаименованиеКолонок[4] 	= "ссылка";		
		ЗаполнитьСтрокуТаблицы(СтрокаФайла, мНаименованиеКолонок);
	КонецЕсли;
	
	СтрокаФайла = текФайл.ПрочитатьСтроку();
	Пока СтрокаФайла <> Неопределено Цикл
		ЗаполнитьСтрокуТаблицы(СтрокаФайла, мНаименованиеКолонок);
		СтрокаФайла = текФайл.ПрочитатьСтроку();
	КонецЦикла;
	
	текФайл.Закрыть();
	
	ЗаполнитьТекущуюНоменклатуру();
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьТекущуюНоменклатуру()

	мАртикул 		= Новый Массив;
	мНаименование 	= Новый Массив;
	
	Для каждого х Из тзДанныеФайла Цикл
		Если х.Определённый Тогда
			мАртикул.Добавить(х.Артикул);
		Иначе
			мНаименование.Добавить(х.Наименование);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	Номенклатура.Артикул КАК Артикул
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Артикул В(&Артикул)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПредварительнаяНоменклатура.Ссылка КАК ПредварительнаяНоменклатура,
		|	ПредварительнаяНоменклатура.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ПредварительнаяНоменклатура КАК ПредварительнаяНоменклатура
		|ГДЕ
		|	ПредварительнаяНоменклатура.Наименование В(&Наименование)";
	
	Запрос.УстановитьПараметр("Артикул", мАртикул);
	Запрос.УстановитьПараметр("Наименование", мНаименование);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[0].Выбрать();
	
	стОтбор = Новый Структура("Определённый,Артикул",Истина );
	Пока Выборка.Следующий() Цикл
		стОтбор.Артикул = Выборка.Артикул;
		НайденыеСтроки 	= тзДанныеФайла.НайтиСтроки(стОтбор);
		
		Для каждого х Из НайденыеСтроки Цикл
			 	
			х.Номенклатура = Выборка.Номенклатура;	
		
		КонецЦикла;
	КонецЦикла;
	
	Выборка = РезультатЗапроса[1].Выбрать();
	
	стОтбор = Новый Структура("Определённый,Наименование",Ложь );
	Пока Выборка.Следующий() Цикл
		стОтбор.Наименование = Выборка.Наименование;
		НайденыеСтроки 	= тзДанныеФайла.НайтиСтроки(стОтбор);
		
		Для каждого х Из НайденыеСтроки Цикл
			 	
			х.ПредварительнаяНоменклатура = Выборка.ПредварительнаяНоменклатура;	
		
		КонецЦикла;
	КонецЦикла;
	

КонецПроцедуры // ЗаполнитьТекущуюНоменклатуру()


&НаКлиенте
Процедура ЗаполнитьСтрокуТаблицы(СтрокаФайла, мНаименованиеКолонок)
	мДанныеСтроки = омСsvКлиентСервер.РазобратьСтрокуCSV(СтрокаФайла);
	Если мДанныеСтроки.Количество() < 5 Тогда
		Возврат;
	КонецЕсли;
	НС = ЭтаФорма.тзДанныеФайла.Добавить();
	Для  х = 0 По 4 Цикл 
		Если мНаименованиеКолонок[х] = "количество" Тогда
			НС[мНаименованиеКолонок[х]] = Число(омРаботаССтроками.ОставитьТолькоЦифры(мДанныеСтроки[х]));
		ИначеЕсли мНаименованиеКолонок[х] = "определённый" Тогда
			НС[мНаименованиеКолонок[х]] = ?(мДанныеСтроки[х] = "0", Ложь, Истина);
		Иначе
			НС[мНаименованиеКолонок[х]] = мДанныеСтроки[х]; 
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьСтрокуТаблицы()


&НаКлиенте
Процедура ЗаполнитьЗаголовкиТаблицы(СтрокаФайла, мНаименованиеКолонок)
	
	мДанныеСтроки = омСsvКлиентСервер.РазобратьСтрокуCSV(СтрокаФайла,, Истина);
	
	Если мДанныеСтроки.Количество() < 5 Тогда
		Возврат;
	КонецЕсли;
	
	Для х = 0 По 4 Цикл
		Если мДанныеСтроки[х] = "определённый(0/1)" Тогда
			мНаименованиеКолонок[х] = "определённый";		
		Иначе
			мНаименованиеКолонок[х] = мДанныеСтроки[х];		
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьЗаголовок()

&НаСервере
Процедура ЗаполнитьСпискиНоменклатуры()
	
	//Артикул;Наименование;Количество;определённый(0/1);Ссылка
	
	//Ошибка = Ложь;
	//Количество = омРаботаССтроками.ОставитьТолькоЦифры(текСписок[2].Значение, Ошибка);
	//
	//Если Ошибка ИЛИ Количество = 0 Тогда
	//	Возврат;
	//КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСпискиУсловий(омСsvКлиент.РазобратьСтрокуCSV(Строка))()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЭтаФорма.ЕстьЗаголовок = Истина;
КонецПроцедуры

&НаКлиенте
Процедура тзДанныеФайлаПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура тзДанныеФайлаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаСервере
Функция СоздатьПотребностьНаСервере()
	Док = Документы.ПотребностьВМатериалах.СоздатьДокумент();
	Док.Дата = ТекущаяДата();
	Док.Комментарий = Комментарий;
	Док.Автор = ПараметрыСеанса.ТекущийПользователь;
	
	мНоменклатура = Новый Массив;
	
	Для каждого х Из тзДанныеФайла Цикл
		Если х.Определённый Тогда
			
			мНоменклатура.Добавить(х.Номенклатура);
			
			НС = Док.Материалы.Добавить();
			НС.Коэффициент 		= 1;
			НС.Комментарий 		= х.Комментарий;
			НС.Количество 		= х.Количество;
			НС.ИзделиеКомплект 	= ИзделиеКомплект;
			НС.Номенклатура 	= х.Номенклатура;
			НС.URL 				= х.Ссылка;
		Иначе
			
			НС = Док.ПредварительныеНоменклатуры.Добавить();
			НС.Количество 					= х.Количество;
			НС.Комментарий 					= х.Комментарий;
			НС.ПредварительнаяНоменклатура 	= х.ПредварительнаяНоменклатура;
			НС.ИзделиеКомплект 				= ИзделиеКомплект;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если мНоменклатура.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка В(&Ссылка)";
		
		Запрос.УстановитьПараметр("Ссылка", мНоменклатура);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
        стОтбор = Новый Структура("Номенклатура", );
		
		Пока Выборка.Следующий() Цикл
			стОтбор.Номенклатура = Выборка.Номенклатура;
			
			НайденыеСтроки = Док.Материалы.НайтиСтроки(стОтбор);
			
			Для каждого х Из НайденыеСтроки Цикл
				х.ЕдиницыИзмерения = Выборка.ОсновнаяЕдиницаИзмерения;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Док.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат Док.Ссылка;
КонецФункции

&НаКлиенте
Процедура СоздатьПотребность(Команда)
	Если КонтрольЗаполнения() Тогда
		ПоказатьЗначение(,СоздатьПотребностьНаСервере());
		ЭтаФорма.Закрыть();
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Функция КонтрольЗаполнения()

	БезОшибок = Истина;	
	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.ИзделиеКомплект) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнен комплект";
		Сообщение.Поле = "ИзделиеКомплект";
		Сообщение.УстановитьДанные(ЭтаФорма);
		Сообщение.Сообщить();
		
	    БезОшибок = Ложь;
	КонецЕсли;
	
	МаксимальныйИндекс = тзДанныеФайла.Количество() - 1;
	
	Если МаксимальныйИндекс = -1 Тогда
		ПоказатьПредупреждение(,"Нет данных для потребности");
	    Возврат Ложь;
	КонецЕсли;
	
	Для х = 0 По МаксимальныйИндекс Цикл
		текСтрока = тзДанныеФайла[х];
		Если текСтрока.Определённый И НЕ ЗначениеЗаполнено(текСтрока.Номенклатура) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнена номенклатура";
			Сообщение.Поле = "тзДанныеФайла[" + х + "].Номенклатура";
			Сообщение.УстановитьДанные(ЭтаФорма);
			Сообщение.Сообщить();
			
			БезОшибок = Ложь;
		ИначеЕсли НЕ (текСтрока.Определённый ИЛИ ЗначениеЗаполнено(текСтрока.ПредварительнаяНоменклатура)) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнена номенклатура";
			Сообщение.Поле = "тзДанныеФайла[" + х + "].ПредварительнаяНоменклатура";
			Сообщение.УстановитьДанные(ЭтаФорма);
			Сообщение.Сообщить();
			
			БезОшибок = Ложь;
		КонецЕсли;
		
	
	КонецЦикла;
	
	
	Возврат БезОшибок;
КонецФункции // КонтрольЗаполнения()


&НаКлиенте
Процедура тзДанныеФайлаПередНачаломИзменения(Элемент, Отказ)
	ТД = Элементы.тзДанныеФайла.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ТД.Определённый И Элемент.ТекущийЭлемент.Имя = "тзДанныеФайлаПредварительнаяНоменклатура" Тогда
		Отказ = Истина;
		Возврат;
	ИначеЕсли НЕ ТД.Определённый И Элемент.ТекущийЭлемент.Имя = "тзДанныеФайлаНоменклатура" Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
КонецПроцедуры
 

