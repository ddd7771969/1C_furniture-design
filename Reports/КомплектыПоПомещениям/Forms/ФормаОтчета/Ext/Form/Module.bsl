
&НаСервере
Процедура СделкаПриИзмененииНаСервере()
	ПолеВывода.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Помещения.Ссылка КАК Помещение,
		|	ИзделияКомплектИзделияЭлемент.Ссылка КАК Комплект,
		|	ЕСТЬNULL(ДатыКомплекта.ДнейПКД, 0) КАК ДнейПКД,
		|	ЕСТЬNULL(ДатыКомплекта.ДнейРКД, 0) КАК ДнейРКД,
		|	ЕСТЬNULL(ДатыКомплекта.ДнейТЗ, 0) КАК ДнейТЗ,
		|	ЕСТЬNULL(ДатыКомплектаМонтаж.НормаВремениМонтажа, 0) КАК СрокМонтажа,
		|	ЕСТЬNULL(ДатыКомплектаПроизводство.НормаВремениПроизводства, 0) КАК СрокПроизводства,
		|	ПлановаяГотовностьПомещенийСрезПоследних.ДатаГотовности КАК ДатаПлан
		|ИЗ
		|	Справочник.Помещения КАК Помещения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИзделияКомплект.ИзделияЭлемент КАК ИзделияКомплектИзделияЭлемент
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|			ПО ИзделияКомплектИзделияЭлемент.Ссылка = ДатыКомплекта.Комплект
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплектаМонтаж КАК ДатыКомплектаМонтаж
		|			ПО ИзделияКомплектИзделияЭлемент.Ссылка = ДатыКомплектаМонтаж.Комплект
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплектаПроизводство КАК ДатыКомплектаПроизводство
		|			ПО ИзделияКомплектИзделияЭлемент.Ссылка = ДатыКомплектаПроизводство.Комплект
		|		ПО Помещения.Ссылка = ИзделияКомплектИзделияЭлемент.ИзделиеЭлемент.Помещения
		|			И (ИзделияКомплектИзделияЭлемент.Основное)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановаяГотовностьПомещений.СрезПоследних КАК ПлановаяГотовностьПомещенийСрезПоследних
		|		ПО (ПлановаяГотовностьПомещенийСрезПоследних.Помещение = Помещения.Ссылка)
		|ГДЕ
		|	Помещения.Проект = &Проект
		|ИТОГИ
		|	СУММА(ДнейПКД),
		|	СУММА(ДнейРКД),
		|	СУММА(ДнейТЗ),
		|	СУММА(СрокМонтажа),
		|	СУММА(СрокПроизводства),
		|	МАКСИМУМ(ДатаПлан)
		|ПО
		|	Помещение";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаПомещение = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Макет = Отчеты.КомплектыПоПомещениям.ПолучитьМакет("Макет");
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Сделка = Проект;
	ПолеВывода.Вывести(Область);
	
	ПолеВывода.ФиксацияСверху = ПолеВывода.ВысотаТаблицы;
	
	ОбластьПомешение = Макет.ПолучитьОбласть("Помещение");
	Область = Макет.ПолучитьОбласть("Строка");
	
	Пока ВыборкаПомещение.Следующий() Цикл
		ОбластьПомешение.Параметры.Заполнить(ВыборкаПомещение);
		ПолеВывода.Вывести(ОбластьПомешение);
	
		ВыборкаДетальныеЗаписи = ВыборкаПомещение.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Область.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
			ПолеВывода.Вывести(Область);
		КонецЦикла;
	КонецЦикла;
	
	ПолеВывода.Вывести(Макет.ПолучитьОбласть("Подвал"));
	
КонецПроцедуры

&НаКлиенте
Процедура СделкаПриИзменении(Элемент)
	СделкаПриИзмененииНаСервере();
КонецПроцедуры
