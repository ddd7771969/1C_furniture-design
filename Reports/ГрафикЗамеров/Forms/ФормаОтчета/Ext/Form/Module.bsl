Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТридцатьДней = 30 * 24 * 3600;
	
	Интервал.ДатаНачала 	= НачалоДня(ТекущаяДата() - ТридцатьДней);
	Интервал.ДатаОкончания	= КонецДня(Интервал.ДатаНачала + 3 * ТридцатьДней);
	
	ОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	
	ОбновитьНаСервере();
	
КонецПроцедуры 

Процедура ОбновитьНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыездНаЗамерГабаритныеЧертежи.Помещение КАК Помещение,
		|	ВыездНаЗамерГабаритныеЧертежи.Ссылка.ДатаВыездаПлан КАК ДатаВыездаПлан,
		|	ВыездНаЗамерГабаритныеЧертежи.Ссылка.ДатаВыездаФакт КАК ДатаВыездаФакт,
		|	ВыездНаЗамерГабаритныеЧертежи.Ссылка.Исполнитель КАК Исполнитель,
		|	ВыездНаЗамерГабаритныеЧертежи.Ссылка.СокращенноеНазвание КАК СокращенноеНазвание,
		|	ВыездНаЗамерГабаритныеЧертежи.Ссылка КАК Документ,
		|	ВыездНаЗамерГабаритныеЧертежи.Ссылка.Проект КАК Проект,
		|	ВыездНаЗамерГабаритныеЧертежи.Ссылка.Комментарий КАК Комментарий
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	Документ.ВыездНаЗамер.ГабаритныеЧертежи КАК ВыездНаЗамерГабаритныеЧертежи
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ВыездНаЗамерГабаритныеЧертежи.Ссылка.Проект = &Проект
		|		КОНЕЦ
		|	И ВыездНаЗамерГабаритныеЧертежи.Ссылка.ДатаВыездаПлан МЕЖДУ &ДатаН И &ДатаК
		|	И ВыездНаЗамерГабаритныеЧертежи.Ссылка.Проведен
		|	И ВЫБОР
		|			КОГДА ВыездНаЗамерГабаритныеЧертежи.Ссылка.ДатаВыездаФакт > ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ВыездНаЗамерГабаритныеЧертежи.ЗамерПроизведен
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаявкаНаЗамерПомещения.Помещение,
		|	ЗаявкаНаЗамерПомещения.Ссылка.ДатаВыездаПлан,
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка),
		|	ЗаявкаНаЗамерПомещения.Ссылка.СокращенноеНазвание,
		|	ЗаявкаНаЗамерПомещения.Ссылка,
		|	ЗаявкаНаЗамерПомещения.Ссылка.Проект,
		|	ЗаявкаНаЗамерПомещения.Ссылка.Комментарий
		|ИЗ
		|	Документ.ЗаявкаНаЗамер.Помещения КАК ЗаявкаНаЗамерПомещения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыездНаЗамер КАК ВыездНаЗамер
		|		ПО ЗаявкаНаЗамерПомещения.Ссылка = ВыездНаЗамер.Основание
		|			И (ВыездНаЗамер.Проведен)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЗаявкаНаЗамерПомещения.Ссылка.Проект = &Проект
		|		КОНЕЦ
		|	И ЗаявкаНаЗамерПомещения.Ссылка.ДатаВыездаПлан МЕЖДУ &ДатаН И &ДатаК
		|	И ЗаявкаНаЗамерПомещения.Ссылка.Проведен
		|	И ВыездНаЗамер.Ссылка ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОзнакомительныйВыездПомещения.Помещение,
		|	ОзнакомительныйВыездПомещения.Ссылка.ДатаВыездаПлан,
		|	ОзнакомительныйВыездПомещения.Ссылка.ДатаВыездаФакт,
		|	ОзнакомительныйВыездПомещения.Ссылка.Исполнитель,
		|	ОзнакомительныйВыездПомещения.Ссылка.СокращенноеНазвание,
		|	ОзнакомительныйВыездПомещения.Ссылка,
		|	ОзнакомительныйВыездПомещения.Ссылка.Проект,
		|	ОзнакомительныйВыездПомещения.Ссылка.Комментарий
		|ИЗ
		|	Документ.ОзнакомительныйВыезд.Помещения КАК ОзнакомительныйВыездПомещения
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ОзнакомительныйВыездПомещения.Ссылка.Проект = &Проект
		|		КОНЕЦ
		|	И ОзнакомительныйВыездПомещения.Ссылка.ДатаВыездаПлан МЕЖДУ &ДатаН И &ДатаК
		|	И ОзнакомительныйВыездПомещения.Ссылка.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.ДатаВыездаПлан КАК ДатаВыездаПлан,
		|	вт.ДатаВыездаФакт КАК ДатаВыездаФакт,
		|	вт.Исполнитель КАК Исполнитель,
		|	вт.СокращенноеНазвание КАК СокращенноеНазвание,
		|	вт.Документ КАК Документ,
		|	вт.Проект КАК Проект,
		|	вт.Помещение.Наименование КАК Помещения,
		|	вт.Документ.Автор КАК Постановщик,
		|	вт.Комментарий КАК Комментарий
		|ИЗ
		|	вт КАК вт
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаВыездаПлан,
		|	СокращенноеНазвание,
		|	Документ,
		|	вт.Помещение.Наименование";
	
	Запрос.УстановитьПараметр("ДатаК", Интервал.ДатаОкончания);
	Запрос.УстановитьПараметр("ДатаН", Интервал.ДатаНачала);
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	тзДанные = Новый ТаблицаЗначений;
	тзДанные.Колонки.Добавить("ДатаВыездаПлан");
	тзДанные.Колонки.Добавить("ДатаВыездаФакт");
	тзДанные.Колонки.Добавить("Исполнитель");
	тзДанные.Колонки.Добавить("СокращенноеНазвание");
	тзДанные.Колонки.Добавить("Документ");
	тзДанные.Колонки.Добавить("Проект");
	тзДанные.Колонки.Добавить("Помещения", Новый ОписаниеТипов("Строка"));
	тзДанные.Колонки.Добавить("Постановщик");
	тзДанные.Колонки.Добавить("Комментарий");
	
	Пока Выборка.Следующий() Цикл
		
		НайденаяСтрока = тзДанные.Найти(Выборка.Документ, "Документ");
		
		Если НайденаяСтрока = Неопределено Тогда
		
			ЗаполнитьЗначенияСвойств(тзДанные.Добавить(), Выборка);	
		
		Иначе
		
			НайденаяСтрока.Помещения = НайденаяСтрока.Помещения + ", " + Выборка.Помещения;
			
		КонецЕсли;
	КонецЦикла;

	Макет = Отчеты.ГрафикЗамеров.ПолучитьМакет("Макет");
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Проект = Проект;
	
	ПолеВывода.Очистить();
	
	ПолеВывода.Вывести(Область);
	Область 	= Макет.ПолучитьОбласть("Строка");
	ОбластьВ 	= Макет.ПолучитьОбласть("Строка1");
	ОбластьО 	= Макет.ПолучитьОбласть("Строка2"); 
	
	ПустаяДата 		= Дата(1, 1, 1);
	ТД				= ТекущаяДата();
	
	текДокумент = Неопределено;
	
    Для каждого ВыборкаДокумент Из тзДанные Цикл
	
			
		Если ВыборкаДокумент.ДатаВыездаФакт > ПустаяДата Тогда
			текОбласть = ОбластьВ;	
		ИначеЕсли ВыборкаДокумент.ДатаВыездаПлан < ТД Тогда
			текОбласть = ОбластьО;	
		Иначе
			текОбласть = Область;	
		КонецЕсли;
		
		текОбласть.Параметры.Заполнить(ВыборкаДокумент);

		Если ВыборкаДокумент.ДатаВыездаФакт = Дата(1, 1, 1) Тогда
			текОбласть.Параметры.Дата = ВыборкаДокумент.ДатаВыездаПлан;	
		Иначе
			текОбласть.Параметры.Дата = ВыборкаДокумент.ДатаВыездаФакт;	
		КонецЕсли;

		ПолеВывода.Вывести(текОбласть);
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры



