
&НаСервере
Процедура СформироватьОтчет()
	Таб = ЭтаФорма.ПолеВывода;
	Таб.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Объект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипУчета = 0 Тогда
		тзДанные = ПолучитьданныеОтчетаСЗ();
	Иначе
		тзДанные = ПолучитьданныеОтчета();
	КонецЕсли;
	
	стДанные = ПодготовитьДанные(тзДанные);
	
	ВывестиДанные(стДанные);
КонецПроцедуры 

&НаСервере
Процедура ВывестиДанные(стДанные)
	Таб = ЭтаФорма.ПолеВывода;
	
	мКолонки 			= Новый Массив;
	мКолонкиОсновные	= Новый Массив;
	Итого = 0;
	стрНачальныеКолонки = стДанные.стрНачальныеКолонки;
	
	Макет = Отчеты.Смета.ПолучитьМакет("Макет");
	
	обОсновной = Макет.ПолучитьОбласть("Шапка|Основной"); 
	обОсновной.Параметры.Объект = ЭтаФорма.Объект;
	
	Таб.Вывести(обОсновной);
	
	обМатериал = Макет.ПолучитьОбласть("Шапка|Материал");
	ПервыйМатериал = Истина;
	Для каждого Колонка Из стДанные.тзДанныеОсновные.Колонки Цикл
		Если СтрНайти(стрНачальныеКолонки,Колонка.Имя) > 0 Тогда
			Продолжить;	
		КонецЕсли;
		мКолонкиОсновные.Добавить(Колонка.Имя);
		обМатериал.Параметры.Материал = Колонка.Заголовок;
	    Если ПервыйМатериал Тогда
		    обМатериал.Параметры.Основные = "Основной";
			ПервыйМатериал = Ложь;
		Иначе
		    обМатериал.Параметры.Основные = "";
		КонецЕсли;
		Таб.Присоединить(обМатериал);
	КонецЦикла;
	Таб.Присоединить(Макет.ПолучитьОбласть("Шапка|Итого"));
	
	Для каждого Колонка Из стДанные.тзДанные.Колонки Цикл
		Если СтрНайти("ИзделияЭлемент",Колонка.Имя) > 0 Тогда
			Продолжить;	
		КонецЕсли;
		мКолонки.Добавить(Колонка.Имя);
		обМатериал.Параметры.Материал = Колонка.Заголовок;
		обМатериал.Параметры.Основные = "";
		Таб.Присоединить(обМатериал);
	КонецЦикла;
	Таб.Присоединить(Макет.ПолучитьОбласть("Шапка|Край")); 
	
	обОсновной 	= Макет.ПолучитьОбласть("Строка|Основной"); 
	обМатериал 	= Макет.ПолучитьОбласть("Строка|Материал");
	обИтого 	= Макет.ПолучитьОбласть("Строка|Итого");
	обИтог_О 	= Макет.ПолучитьОбласть("Итог|Основной");
	обИтогМ	 	= Макет.ПолучитьОбласть("Итог|Материал");
	обИтогИ	 	= Макет.ПолучитьОбласть("Итог|Итого");
	
	Для каждого текСтрока Из стДанные.тзДанныеОсновные Цикл
		ИтогоВСтроке = 0;
		обОсновной.Параметры.Заполнить(текСтрока);
		Таб.Вывести(обОсновной);
		
		Для каждого имяКолонки Из мКолонкиОсновные Цикл
			обМатериал.Параметры.Сумма = текСтрока[имяКолонки];
			Таб.Присоединить(обМатериал);
			ИтогоВСтроке = ИтогоВСтроке + текСтрока[имяКолонки];
		КонецЦикла;
		обИтого.Параметры.Итого = ИтогоВСтроке;
		Итого = Итого + ИтогоВСтроке;
		Таб.Присоединить(обИтого);
		найденаяСтрока = стДанные.тзДанные.Найти(текСтрока.ИзделияЭлемент,"ИзделияЭлемент");
		Для каждого имяКолонки Из мКолонки Цикл
			обМатериал.Параметры.Сумма = найденаяСтрока[имяКолонки];
			Таб.Присоединить(обМатериал);
		КонецЦикла;
	КонецЦикла;
	Таб.Вывести(обИтог_О);
	Для каждого имяКолонки Из мКолонкиОсновные Цикл
		обИтогМ.Параметры.Сумма = стДанные.тзДанныеОсновные.Итог(имяКолонки);
		Таб.Присоединить(обИтогМ);
	КонецЦикла;
	обИтогИ.Параметры.Итого = Итого;
	Таб.Присоединить(обИтогИ);
	Для каждого имяКолонки Из мКолонки Цикл
		обИтогМ.Параметры.Сумма = стДанные.тзДанные.Итог(имяКолонки);
		Таб.Присоединить(обИтогМ);
	КонецЦикла;
КонецПроцедуры // ВывестиДанные(Таб, тзДанные)

&НаСервере
Функция ПодготовитьДанные(тзДанныеНеСортированные)
	тзДанныеОсновные 	= Новый ТаблицаЗначений;
	тзДанные 			= Новый ТаблицаЗначений;
	
	мКолонки 			= Новый Массив;
	мКолонкиОсновные	= Новый Массив;
	мИзделияЗаказчика 	= Новый Массив; 
	
	мКолонкиОсновные.Добавить("Номер");
	мКолонкиОсновные.Добавить("Помещения");
	мКолонкиОсновные.Добавить("СтрокаИзделияЗаказчика");
	мКолонкиОсновные.Добавить("ИзделияЭлемент");
	мКолонкиОсновные.Добавить("Размер");
	мКолонкиОсновные.Добавить("Количество");
	мКолонкиОсновные.Добавить("Единица");
	
	стрНачальныеКолонки = "Помещения,СтрокаИзделияЗаказчика,ИзделияЭлемент,Размер";
	
	Для каждого ИмяКолонки Из мКолонкиОсновные Цикл
		тзДанныеОсновные.Колонки.Добавить(ИмяКолонки);
	КонецЦикла;
	
	тзДанные.Колонки.Добавить("ИзделияЭлемент"); 
	
	тзДанныеОсновные.Индексы.Добавить("ИзделияЭлемент");
	тзДанные.Индексы.Добавить("ИзделияЭлемент");
	
	ОписаниеЧисло = новый ОписаниеТипов("Число");
	
	Номер = 1;
	Для каждого текСтрока Из тзДанныеНеСортированные Цикл
		Если мИзделияЗаказчика.Найти(текСтрока.ИзделияЭлемент) = Неопределено Тогда
			Если текСтрока.Основной Тогда
				НайденаяСтрока = тзДанныеОсновные.Добавить(); 
				НайденаяСтрока.Номер = Номер;
				ЗаполнитьЗначенияСвойств(НайденаяСтрока,текСтрока,стрНачальныеКолонки);
				ЗаполнитьЗначенияСвойств(тзДанные.Добавить(),текСтрока,"ИзделияЭлемент");
			Иначе
				НайденаяСтрока = тзДанные.Добавить(); 
				ЗаполнитьЗначенияСвойств(НайденаяСтрока,текСтрока,"ИзделияЭлемент");
				НС = тзДанныеОсновные.Добавить();
				ЗаполнитьЗначенияСвойств(НС,текСтрока,стрНачальныеКолонки);
				НС.Номер = Номер;
			КонецЕсли;
			Номер = Номер + 1;
			мИзделияЗаказчика.Добавить(текСтрока.ИзделияЭлемент); 
		Иначе
			Если текСтрока.Основной Тогда
				НайденаяСтрока = тзДанныеОсновные.Найти(текСтрока.ИзделияЭлемент,"ИзделияЭлемент");
			Иначе
				НайденаяСтрока = тзДанные.Найти(текСтрока.ИзделияЭлемент,"ИзделияЭлемент");
			КонецЕсли;
		КонецЕсли;	
		НаименованиеБезПробелов = омРаботаССтроками.ВернутьТолькоБуквыИЦифры(текСтрока.ТипыЗатратНаименование); 
		Если ПустаяСтрока(НаименованиеБезПробелов) Тогда
			НаименованиеБезПробелов = "БезУчета";
			ТипыЗатратНаименование  = "Без учета";
		Иначе	
		   	ТипыЗатратНаименование = текСтрока.ТипыЗатратНаименование; 
		КонецЕсли;

		Если текСтрока.Основной Тогда
			
			Если мКолонкиОсновные.Найти(НаименованиеБезПробелов) = Неопределено Тогда
				тзДанныеОсновные.Колонки.Добавить(НаименованиеБезПробелов,ОписаниеЧисло,ТипыЗатратНаименование); 
				мКолонкиОсновные.Добавить(НаименованиеБезПробелов);
			КонецЕсли;
			
		Иначе
			
			Если мКолонки.Найти(НаименованиеБезПробелов) = Неопределено Тогда
				тзДанные.Колонки.Добавить(НаименованиеБезПробелов,ОписаниеЧисло,ТипыЗатратНаименование);
				мКолонки.Добавить(НаименованиеБезПробелов);
			КонецЕсли;
			
		КонецЕсли;
		НайденаяСтрока[НаименованиеБезПробелов] = текСтрока.Сумма + НайденаяСтрока[НаименованиеБезПробелов];	
		
	КонецЦикла;
	
	стрНачальныеКолонки = стрНачальныеКолонки + "Номер,Количество,Единица";
	
	Возврат Новый Структура("тзДанные,тзДанныеОсновные,стрНачальныеКолонки",тзДанные,тзДанныеОсновные,стрНачальныеКолонки);
КонецФункции // ПолучитьТаблицуИКолонки(тзДанные)

&НаСервере
Функция ПолучитьданныеОтчета()
	
	
	//Запрос = Новый Запрос;
	//Запрос.Текст = 
	//	"ВЫБРАТЬ
	//	|	Смета.ИзделияЗаказчика КАК ИзделияЗаказчика,
	//	|	Смета.ВидЗатрат КАК ВидЗатрат,
	//	|	Смета.Основной КАК Основной,
	//	|	Смета.ТипыЗатрат КАК ТипыЗатрат,
	//	|	СУММА(Смета.Сумма) КАК Сумма,
	//	|	Смета.ИзделияЗаказчика.РазмерПоТЗ КАК Размер,
	//	|	Смета.ИзделияЗаказчика.Помещения КАК Помещения,
	//	|	Смета.ИзделияЗаказчика.Наименование КАК СтрокаИзделияЗаказчика,
	//	|	Смета.Фурнитура КАК Фурнитура,
	//	|	Смета.ВидЗатрат.Наименование КАК ВидЗатратНаименование,
	//	|	Смета.ТипыЗатрат.Наименование КАК ТипыЗатратНаименование
	//	|ИЗ
	//	|	РегистрНакопления.Смета КАК Смета
	//	|ГДЕ
	//	|	Смета.Объект = &Объект
	//	|
	//	|СГРУППИРОВАТЬ ПО
	//	|	Смета.Основной,
	//	|	Смета.ИзделияЗаказчика,
	//	|	Смета.ТипыЗатрат,
	//	|	Смета.ВидЗатрат,
	//	|	Смета.ИзделияЗаказчика.РазмерПоТЗ,
	//	|	Смета.ИзделияЗаказчика.Помещения,
	//	|	Смета.ИзделияЗаказчика.Наименование,
	//	|	Смета.Фурнитура
	//	|
	//	|УПОРЯДОЧИТЬ ПО
	//	|	СтрокаИзделияЗаказчика";
	//
	//Запрос.УстановитьПараметр("Объект", ЭтаФорма.Объект);
	//
	//Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции // ПолучитьданныеОтчета()

&НаСервере
Функция ПолучитьданныеОтчетаСЗ()
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Смета.ИзделияЭлемент КАК ИзделияЭлемент,
		|	Смета.ВидЗатрат КАК ВидЗатрат,
		|	Смета.Основной КАК Основной,
		|	Смета.СтатьяЗатрат КАК ТипыЗатрат,
		|	СУММА(Смета.Сумма) КАК Сумма,
		|	Смета.ИзделияЭлемент.РазмерПоТЗ КАК Размер,
		|	Смета.ИзделияЭлемент.Помещения КАК Помещения,
		|	Смета.ИзделияЭлемент.Наименование КАК СтрокаИзделияЗаказчика,
		|	Смета.Фурнитура КАК Фурнитура,
		|	Смета.ВидЗатрат.Наименование КАК ВидЗатратНаименование,
		|	Смета.СтатьяЗатрат.Наименование КАК ТипыЗатратНаименование
		|ИЗ
		|	РегистрНакопления.Смета КАК Смета
		|ГДЕ
		|	Смета.Объект = &Объект
		|
		|СГРУППИРОВАТЬ ПО
		|	Смета.Основной,
		|	Смета.ИзделияЭлемент,
		|	Смета.ВидЗатрат,
		|	Смета.СтатьяЗатрат,
		|	Смета.ИзделияЭлемент.РазмерПоТЗ,
		|	Смета.ИзделияЭлемент.Помещения,
		|	Смета.ИзделияЭлемент.Наименование,
		|	Смета.Фурнитура,
		|	Смета.ВидЗатрат.Наименование,
		|	Смета.СтатьяЗатрат.Наименование
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтрокаИзделияЗаказчика";
	
	Запрос.УстановитьПараметр("Объект", ЭтаФорма.Объект);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции // ПолучитьданныеОтчета()



&НаКлиенте
Процедура ОбъектПриИзменении(Элемент)
	СформироватьОтчет();
КонецПроцедуры

&НаКлиенте
Процедура НаТекущуюДатуПриИзменении(Элемент)
	СформироватьОтчет();
КонецПроцедуры
