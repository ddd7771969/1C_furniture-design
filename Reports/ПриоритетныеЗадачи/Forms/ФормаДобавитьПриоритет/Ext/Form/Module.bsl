
&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Проект) Тогда
	
		Элементы.Комплект.ТолькоПросмотр = Ложь;
		Элементы.Комплект.СписокВыбора.ЗагрузитьЗначения(ПолучитьКомплектыПроекта(Проект));
	
	Иначе
	
		Элементы.Комплект.СписокВыбора.Очистить();
		Элементы.Комплект.ТолькоПросмотр = Истина;
	
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКомплектыПроекта(Проект)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|ГДЕ
		|	ИзделияКомплект.Проект = &Проект
		|	И НЕ ИзделияКомплект.ЭтоГруппа
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИзделияКомплект.Наименование";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции // ПолучитьКомплектыПроекта()

&НаКлиенте
Процедура КомплектПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Комплект) Тогда
	
		стДанные = ПолучитьДанныеКомплекта(Комплект);
		ЗаполнитьЗначенияСвойств(ЭтаФорма, стДанные);
		Элементы.Приоритет.СписокВыбора.ЗагрузитьЗначения(стДанные.ДопустимыеПриоритеты);
	
	КонецЕсли;
	
	ВидимостьЭлементов();
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ПолучитьДанныеКомплекта(Комплект)

	стДанные = Новый Структура("ДопустимыеПриоритеты, ТекущийПриоритет, ТекущаяСтадия, Конструктор", Новый Массив);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеКомплектаСрезПоследних.Приоритет КАК ТекущийПриоритет,
		|	ДанныеКомплектаСрезПоследних.ТекущаяСтадия КАК ТекущаяСтадия,
		|	ДанныеКомплектаСрезПоследних.Конструктор КАК Конструктор
		|ИЗ
		|	РегистрСведений.ДанныеКомплекта.СрезПоследних КАК ДанныеКомплектаСрезПоследних
		|ГДЕ
		|	ДанныеКомплектаСрезПоследних.Комплект = &Комплект";
	
	Запрос.УстановитьПараметр("Комплект", Комплект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл

		ЗаполнитьЗначенияСвойств(стДанные, Выборка);
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(стДанные.ТекущаяСтадия) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПриоритетыСтадииКомплекта.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Приоритеты.СтадииКомплекта КАК ПриоритетыСтадииКомплекта
		|ГДЕ
		|	ПриоритетыСтадииКомплекта.Стадия = &Стадия";
		
		Запрос.УстановитьПараметр("Стадия", стДанные.ТекущаяСтадия);
		
		стДанные.ДопустимыеПриоритеты = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	КонецЕсли;
	

	Возврат стДанные;

КонецФункции // ПолучитьДанныеКомплекта()

&НаКлиенте
Процедура ВидимостьЭлементов()

	Элементы.ГруппаДанныеКомплекта.Видимость = ЗначениеЗаполнено(Комплект);	

КонецПроцедуры // ВидимостьЭлементов()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВидимостьЭлементов();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьПриоритетНаСервере(Комплект, Приоритет)
	
	стПараметры = Новый Структура("Комплект, Приоритет", Комплект, Приоритет);

	РегистрыСведений.ДанныеКомплекта.ИзменитьДанныеКомплекта(стПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПриоритет(Команда)
	
	Если ТекущийПриоритет = Приоритет Тогда
		
		ПоказатьПредупреждение(, "Текущий приоритет равен приоритету для установки.");
		Возврат;
		
	КонецЕсли;
	
	ТекстВопроса = "Для комплекта " + Комплект;
	Если ЗначениеЗаполнено(Приоритет) Тогда
		ТекстВопроса = ТекстВопроса + " будет установлен приоритет " + Приоритет;	
	Иначе
		ТекстВопроса = ТекстВопроса + " будет сброшен приоритет";	
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопрсаОСменеПриоритета", Этаформа);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопрсаОСменеПриоритета(Результат, Параметры) Экспорт
    Если НЕ Результат = КодВозвратаДиалога.Да Тогда
        Возврат;
    КонецЕсли;

	УстановитьПриоритетНаСервере(Комплект, Приоритет);

    Закрыть();
	
КонецПроцедуры