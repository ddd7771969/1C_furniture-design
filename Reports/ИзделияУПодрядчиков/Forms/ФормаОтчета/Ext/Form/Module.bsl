
&НаСервере
Процедура ОбновитьНаСервере()
	ЭтаФорма.ПолеВывода.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияУПодрядчикаОстатки.Подрядчик КАК Подрядчик,
		|	ИзделияУПодрядчикаОстатки.ИзделиеЭлемент КАК ИзделиеЭлемент,
		|	ИзделияУПодрядчикаОстатки.ИзделиеЭлемент.Проект КАК Проект
		|ИЗ
		|	РегистрНакопления.ИзделияУПодрядчика.Остатки(
		|			,
		|			ВЫБОР
		|				КОГДА &Подрядчик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ Подрядчик = &Подрядчик
		|			КОНЕЦ) КАК ИзделияУПодрядчикаОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИзделияУПодрядчикаОстатки.Подрядчик,
		|	ИзделияУПодрядчикаОстатки.Подрядчик.Наименование,
		|	ИзделияУПодрядчикаОстатки.ИзделиеЭлемент.Наименование";
	
	Запрос.УстановитьПараметр("Подрядчик", Подрядчик);
	
	тзОстатки = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияУПодрядчикаПлан.Подрядчик КАК Подрядчик,
		|	ИзделияУПодрядчикаПлан.ИзделиеЭлемент КАК ИзделиеЭлемент,
		|	ИзделияУПодрядчикаПлан.ДатаВозвратаПлан КАК ДатаПлан,
		|	МАКСИМУМ(ИзделияУПодрядчикаПлан.ДатаПередачи) КАК ДатаПередачи
		|ИЗ
		|	РегистрСведений.ИзделияУПодрядчикаПлан КАК ИзделияУПодрядчикаПлан
		|ГДЕ
		|	ИзделияУПодрядчикаПлан.ИзделиеЭлемент В(&ИзделиеЭлемент)
		|	И НЕ ИзделияУПодрядчикаПлан.ДатаВозвратаПлан = ДАТАВРЕМЯ(1, 1, 1)
		|	И ВЫБОР
		|			КОГДА &Подрядчик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ИзделияУПодрядчикаПлан.Подрядчик = &Подрядчик
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	ИзделияУПодрядчикаПлан.ИзделиеЭлемент,
		|	ИзделияУПодрядчикаПлан.Подрядчик,
		|	ИзделияУПодрядчикаПлан.ДатаВозвратаПлан";
	
	Запрос.УстановитьПараметр("ИзделиеЭлемент", тзОстатки.ВыгрузитьКолонку("ИзделиеЭлемент"));
	Запрос.УстановитьПараметр("Подрядчик", Подрядчик);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	тзОстатки.Колонки.Добавить("ДатаПлан", Новый ОписаниеТипов("Дата"));
	тзОстатки.Колонки.Добавить("ДатаПередачи", Новый ОписаниеТипов("Дата"));
	
	Если тзОстатки.Количество() > 30 Тогда
		тзОстатки.Индексы.Добавить("Подрядчик,ИзделиеЭлемент");
	КонецЕсли;
	
	стОтбор = Новый Структура("Подрядчик,ИзделиеЭлемент", );
	Пока Выборка.Следующий() Цикл
    	ЗаполнитьЗначенияСвойств(стОтбор,Выборка);
		
		Для каждого стрОстатки Из тзОстатки.НайтиСтроки(стОтбор) Цикл
			стрОстатки.ДатаПлан 	= Выборка.ДатаПлан;
			стрОстатки.ДатаПередачи = Выборка.ДатаПередачи;
		КонецЦикла;
	КонецЦикла; 
	
	текПодрядчик = Неопределено;
	ТД = КонецДня(ТекущаяДата());
	
	Макет = Отчеты.ИзделияУПодрядчиков.ПолучитьМакет("Макет");
	обШапка 	= Макет.ПолучитьОбласть("Шапка");
	обПодрядчик = Макет.ПолучитьОбласть("Подрядчик");
	обСтрокаО 	= Макет.ПолучитьОбласть("Строка|Основная");
	обСтрокаД 	= Макет.ПолучитьОбласть("Строка|ДатаВозврат");
	обСтрокаДК 	= Макет.ПолучитьОбласть("План|ДатаВозврат"); 
	
	обШапка.Параметры.ДатаФормирования = "Дата формирования " + Формат(ТекущаяДата(),"ДЛФ=DDT");
	ЭтаФорма.ПолеВывода.Вывести(обШапка);
	
	Для каждого текОстаток Из тзОстатки Цикл
		Если НЕ текПодрядчик = текОстаток.Подрядчик Тогда
			обПодрядчик.Параметры.Подрядчик = текОстаток.Подрядчик; 
			ЭтаФорма.ПолеВывода.Вывести(обПодрядчик);
			текПодрядчик = текОстаток.Подрядчик;
		КонецЕсли;
		
		обСтрокаО.Параметры.Заполнить(текОстаток);
		ЭтаФорма.ПолеВывода.Вывести(обСтрокаО);
		
		текОбласть = ?(ТД < текОстаток.ДатаПлан, обСтрокаД, обСтрокаДК); 
		текОбласть.Параметры.ДатаПлан = текОстаток.ДатаПлан;
		ЭтаФорма.ПолеВывода.Присоединить(текОбласть);
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПодрядчикПриИзменении(Элемент)
	ОбновитьНаСервере();
КонецПроцедуры
