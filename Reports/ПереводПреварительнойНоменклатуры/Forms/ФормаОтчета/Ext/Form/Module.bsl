 
&НаСервереБезКонтекста
Функция СделкаПриИзмененииНаСервере(Сделка, ИзделиеКомплект, мНоменклатураПредварительная, мИзделияСделки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПотребностьПредварительнойНоменклатурыОстатки.ИзделиеКомплект КАК ИзделиеКомплект,
		|	ПотребностьПредварительнойНоменклатурыОстатки.ПредварительнаяНоменклатура КАК ПредварительнаяНоменклатура,
		|	ПотребностьПредварительнойНоменклатурыОстатки.КоличествоОстаток КАК Количество,
		|	ПотребностьПредварительнойНоменклатурыОстатки.ПредварительнаяНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ПотребностьПредварительнойНоменклатурыОстатки.ИзделиеКомплект.Конструктор КАК Конструктор,
		|	0 КАК Выдано,
		|	ПотребностьПредварительнойНоменклатурыОстатки.ИзделиеКомплект.ОсновноеПомещение КАК Помещение
		|ИЗ
		|	РегистрНакопления.ПотребностьПредварительнойНоменклатуры.Остатки(
		|			,
		|			&Проект = Проект
		|				И ВЫБОР
		|					КОГДА &ИзделиеКомплект = ЗНАЧЕНИЕ(Справочник.ИзделияКомплект.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ИзделиеКомплект = &ИзделиеКомплект
		|				КОНЕЦ) КАК ПотребностьПредварительнойНоменклатурыОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПотребностьПредварительнойНоменклатурыОстатки.ПредварительнаяНоменклатура.Наименование";
	
	Запрос.УстановитьПараметр("Проект", Сделка);
	Запрос.УстановитьПараметр("ИзделиеКомплект", ИзделиеКомплект);
	
	тзНоменклатура = Запрос.Выполнить().Выгрузить(); 
	
	мИзделияСделки = омТЧ_НаКлиентеСервере.КолонкуВМассив(тзНоменклатура, "ИзделиеКомплект");
	
	тзНоменклатураПредварительная = Запрос.Выполнить().Выгрузить();
	тзНоменклатураПредварительная.Свернуть("ПредварительнаяНоменклатура, ЕдиницаИзмерения","Количество,Выдано");
	
	мНоменклатураПредварительная = ОбщегоНазначения.ТаблицаЗначенийВМассив(тзНоменклатураПредварительная); 
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(тзНоменклатура);
	
КонецФункции

&НаКлиенте
Процедура СделкаПриИзменении(Элемент)
	ПриИзмененииФильтров();
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииФильтров()
	ЭтаФорма.тзПолныеДанные.Очистить();
	ЭтаФорма.тзПредварительнаяНоменклатура.Очистить();
	ЭтаФорма.тзНоменклатура.Очистить();
	ЭтаФорма.тзРезультат.Очистить();

	мНоменклатураПредварительная = Неопределено;   
	
	Если ЗначениеЗаполнено(ЭтаФорма.Проект) Тогда
		мИзделияСделки = Неопределено;
		
		мДанные = СделкаПриИзмененииНаСервере(ЭтаФорма.Проект, ЭтаФорма.ИзделиеКомплект, мНоменклатураПредварительная, мИзделияСделки);
		Для каждого х Из мДанные Цикл
			ЗаполнитьЗначенияСвойств(ЭтаФорма.тзПолныеДанные.Добавить(),х);
		КонецЦикла; 
		Элементы.ИзделиеКомплект.СписокВыбора.ЗагрузитьЗначения(мИзделияСделки);
	Иначе
		Элементы.ИзделиеКомплект.СписокВыбора.Очистить();
		ИзделиеКомплект = ПредопределенноеЗначение("Справочник.ИзделияКомплект.ПустаяСсылка");
	КонецЕсли;
	
	ВывестиНоменклатуру(мНоменклатураПредварительная, ЭтаФорма.ПолеВывода);
КонецПроцедуры // ИриИзмененииФильтров()



&НаСервереБезКонтекста
Процедура ВывестиНоменклатуру(мНоменклатураПредварительная, ПолеВывода)
	ПолеВывода.Очистить(); 
	
	Если мНоменклатураПредварительная = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Макет = Отчеты.ПереводПреварительнойНоменклатуры.ПолучитьМакет("Макет");
	
	ПолеВывода.Вывести(Макет.ПолучитьОбласть("Шапка"));
	
	обСтрока = Макет.ПолучитьОбласть("Строка");
	
	Для каждого х Из мНоменклатураПредварительная Цикл 
		обСтрока.Параметры.Заполнить(х);
		обСтрока.Параметры.стДанныеКоличество 	= Новый Структура("ПредварительнаяНоменклатура,Количество",х.ПредварительнаяНоменклатура,х.Количество);
		ПолеВывода.Вывести(обСтрока);
	КонецЦикла;
КонецПроцедуры // ВывестиНоменклатуру()

&НаКлиенте
Процедура ПолеВыводаПриАктивизации(Элемент)
	текПредварительнаяНоменклатура = Элемент.ТекущаяОбласть.Расшифровка;
	
	мДанные = Новый Массив;
	
	Для каждого х Из ЭтаФорма.тзПолныеДанные Цикл
		Если НЕ х.ПредварительнаяНоменклатура = текПредварительнаяНоменклатура Тогда
			Продолжить;
		КонецЕсли;
		
		текКоличество = х.Количество - х.Выдано;
		
		Если текКоличество <= 0 Тогда
			Продолжить;
		КонецЕсли;

		стДанные = Новый Структура("ИзделиеКомплект,ЕдиницаИзмерения,Конструктор,Помещение", );
		стДанные.Вставить("Количество", текКоличество); 
		ЗаполнитьЗначенияСвойств(стДанные, х); 
		
		мДанные.Добавить(стДанные);
	
	КонецЦикла;
	ВывестиИзделияКомплект(текПредварительнаяНоменклатура, мДанные, ЭтаФорма.ПоИзделиям);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВывестиИзделияКомплект(текПредварительнаяНоменклатура, мДанные, ПоИзделиям)
	ПоИзделиям.Очистить();
	
	Макет = Отчеты.ПереводПреварительнойНоменклатуры.ПолучитьМакет("МакетИзделия");
	
	обШапка = Макет.ПолучитьОбласть("Шапка");
	обШапка.Параметры.ПредварительнаяНоменклатура = текПредварительнаяНоменклатура;
	ПоИзделиям.Вывести(обШапка);
	
	обСтрока = Макет.ПолучитьОбласть("Строка");
	
	Для каждого х Из мДанные Цикл
		обСтрока.Параметры.Заполнить(х);
		обСтрока.Параметры.стКоличество = Новый Структура("ПредварительнаяНоменклатура,ИзделиеКомплект,Количество"
															,	текПредварительнаяНоменклатура
															,	х.ИзделиеКомплект
															, 	х.Количество);
		ПоИзделиям.Вывести(обСтрока);
	КонецЦикла;
КонецПроцедуры // ВывестиИзделияКомплект()
 
&НаКлиенте
Процедура Перенести(Команда)
	МаксимальныйНомерСборки = 0;
	
	Для каждого х Из ЭтаФорма.тзРезультат Цикл
		Если МаксимальныйНомерСборки < х.НомерСборки Тогда
			МаксимальныйНомерСборки = х.НомерСборки; 
		КонецЕсли;
	КонецЦикла;
	
	МаксимальныйНомерСборки = МаксимальныйНомерСборки + 1;
	
	КоличествоСтрокПредварительной 	= тзПредварительнаяНоменклатура.Количество() 	- 1;
	КоличествоСтрокНоменклатура 	= тзНоменклатура.Количество() 					- 1;
	
	МаксимальноеКоличествоСтрок = ?(КоличествоСтрокПредварительной < КоличествоСтрокНоменклатура, КоличествоСтрокНоменклатура, КоличествоСтрокПредварительной);
	
	Для текИндекс = 0 По МаксимальноеКоличествоСтрок Цикл
		НС 				= тзРезультат.Добавить();
		НС.НомерСборки 	= МаксимальныйНомерСборки;
		
		Если текИндекс <= КоличествоСтрокПредварительной Тогда
			НС.ПредварительнаяНоменклатура 	= тзПредварительнаяНоменклатура[текИндекс].ПредварительнаяНоменклатура; 
			НС.КоличествоПН 				= тзПредварительнаяНоменклатура[текИндекс].Количество; 
			НС.ЕдиницаИзмеренияПН 			= тзПредварительнаяНоменклатура[текИндекс].ЕдиницаИзмерения; 
			НС.ИзделиеКомплектПН			= тзПредварительнаяНоменклатура[текИндекс].ИзделиеКомплект; 
		КонецЕсли;
		
		Если текИндекс <= КоличествоСтрокНоменклатура Тогда
			НС.Номенклатура 	= тзНоменклатура[текИндекс].Номенклатура; 
			НС.Количество 		= тзНоменклатура[текИндекс].Количество; 
			НС.ЕдиницаИзмерения	= тзНоменклатура[текИндекс].ЕдиницаИзмерения; 
			НС.ИзделиеКомплект	= тзНоменклатура[текИндекс].ИзделиеКомплект; 
		КонецЕсли;
	КонецЦикла;
	
	тзПредварительнаяНоменклатура.Очистить();
	тзНоменклатура.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ПолеВыводаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	Если Тип("Структура") = ТипЗнч(Расшифровка) Тогда
		СтандартнаяОбработка = Ложь;
		Для каждого х Из ЭтаФорма.тзПолныеДанные Цикл
			Если НЕ х.ПредварительнаяНоменклатура = Расшифровка.ПредварительнаяНоменклатура Тогда
				Продолжить;	
			КонецЕсли;
			
			текКоличество = х.Количество - х.Выдано;
			
			Если текКоличество <= 0 Тогда
				Продолжить;	
			КонецЕсли;
			
			х.Выдано = х.Количество;
			
			НС = ЭтаФорма.тзПредварительнаяНоменклатура.Добавить();
			ЗаполнитьЗначенияСвойств(НС, х);

			НС.Количество = текКоличество;	
		КонецЦикла;	
		
		ВывестиПредварительнуюНоменклатуруСУчетомВыдачи();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура тзПредварительнаяНоменклатураПередУдалением(Элемент, Отказ)
	текПредварительнаяНоменклатура 	= Элемент.ТекущиеДанные.ПредварительнаяНоменклатура;
	текИзделиеКомплект 				= Элемент.ТекущиеДанные.ИзделиеКомплект;
    текКоличество 					= Элемент.ТекущиеДанные.Количество;
	
	Для каждого х Из тзПолныеДанные Цикл

		Если х.ПредварительнаяНоменклатура = текПредварительнаяНоменклатура И
			х.ИзделиеКомплект = текИзделиеКомплект Тогда
		
			Если текКоличество > х.Выдано Тогда
				текКоличество = текКоличество - х.Выдано;
				х.Выдано = 0;
			Иначе
				х.Выдано = х.Выдано - текКоличество;
				Прервать;
			КонецЕсли;	
		КонецЕсли;	
		
	КонецЦикла;
	
	ВывестиПредварительнуюНоменклатуруСУчетомВыдачи();
	
КонецПроцедуры 

&НаКлиенте
Процедура ВывестиПредварительнуюНоменклатуруСУчетомВыдачи()
	мДанные = Новый Массив;
	
	Для каждого х Из ЭтаФорма.тзПолныеДанные Цикл
		текКоличество = х.Количество - х.Выдано;
		
		Если текКоличество <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		флНайденаНоменклатура = Ложь;
		
		Для каждого текДанные Из мДанные Цикл
			Если текДанные.ПредварительнаяНоменклатура = х.ПредварительнаяНоменклатура Тогда
				текДанные.Количество = текДанные.Количество + текКоличество;	
				флНайденаНоменклатура = Истина;
			    Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если флНайденаНоменклатура Тогда
			Продолжить;
		КонецЕсли;
		
		стДанные = Новый Структура("ПредварительнаяНоменклатура,ЕдиницаИзмерения,Конструктор,Помещение", );
		ЗаполнитьЗначенияСвойств(стДанные, х); 
		стДанные.Вставить("Количество", текКоличество);
		
		мДанные.Добавить(стДанные);
		
	КонецЦикла;
	
	
	ВывестиНоменклатуру(мДанные, ЭтаФорма.ПолеВывода)
КонецПроцедуры // ВывестиПредварительнуюНоменклатуруСУчетомВыдачи()


&НаКлиенте
Процедура тзНоменклатураНоменклатураПриИзменении(Элемент)
	Элемент.Родитель.ТекущиеДанные.ЕдиницаИзмерения = омЕдиницыИзмерения.ПолучитьОсновнуюЕдиницуИзмерения(Элемент.Родитель.ТекущиеДанные.Номенклатура);
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановкаВидимости();
КонецПроцедуры


&НаКлиенте
Процедура РаспределитьКомплекты(Команда)
	Если тзПредварительнаяНоменклатура.Количество() = 0 Тогда
		ПоказатьПредупреждение(,"Нет данных для распределения.");
		Возврат;
	КонецЕсли;
	
	стРезультат = ПроверкаОдинКомплект();
	Если стРезультат.Результат Тогда
		Для каждого х Из тзНоменклатура Цикл
			х.ИзделиеКомплект = стРезультат.ИзделиеКомплект;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ стРезультат.Результат Тогда
		стРезультат = ПроверкаОднаПредварительнаяНоменклатура();
		Если стРезультат.Результат Тогда
			мРеквизиты = Новый Массив(2);
			мРеквизиты[0] = "Номенклатура";
			мРеквизиты[1] = "ЕдиницаИзмерения";
			
			мРесурсы = Новый Массив(1);
			мРесурсы[0] = "Количество";
			
			мДанныеНоменклатура = Новый Массив;
			
			Для каждого х Из тзНоменклатура Цикл
				стДанныеНоменклатура = Новый Структура("Номенклатура,ЕдиницаИзмерения,Количество",);
				ЗаполнитьЗначенияСвойств(стДанныеНоменклатура, х); 
				мДанныеНоменклатура.Добавить(стДанныеНоменклатура);
			КонецЦикла;
			
			мДанныеНоменклатура = омТабличныйДокументКлиентСервер.СвернутьМассивСтуктур(мДанныеНоменклатура, мРеквизиты, мРесурсы);
			
			тзНоменклатура.Очистить();
			
			МаксимальныйИндекс 	= стРезультат.КоличествоЭлементов - 1;
			ОбщиеКоличество 	= стРезультат.ОбщиеКоличество;
			мДанные 			= стРезультат.мДанные;
			
			Если ОбщиеКоличество > 0 Тогда
				Для каждого данныеНомеклатура  Из мДанныеНоменклатура Цикл
					
					текКоличество = данныеНомеклатура.Количество;
					
					Для х = 0 По МаксимальныйИндекс Цикл
						НС = тзНоменклатура.Добавить();
						НС.Номенклатура 	= данныеНомеклатура.Номенклатура;
						НС.ЕдиницаИзмерения = данныеНомеклатура.ЕдиницаИзмерения;
						НС.ИзделиеКомплект 	= мДанные[х].ИзделиеКомплект;
						Если х = МаксимальныйИндекс Тогда
							НС.Количество = текКоличество;
						Иначе
							НС.Количество = мДанные[х].Количество / ОбщиеКоличество * данныеНомеклатура.Количество;
							текКоличество = текКоличество - НС.Количество;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры 

&НаКлиенте
Функция ПроверкаОдинКомплект()
	
	текИзделиеКомплект = тзПредварительнаяНоменклатура[0].ИзделиеКомплект;
	ОдинаковыйИзделиеКомплект = Истина;
	
	Для каждого х Из тзПредварительнаяНоменклатура Цикл
		Если НЕ текИзделиеКомплект = х.ИзделиеКомплект Тогда
			ОдинаковыйИзделиеКомплект = Ложь;	
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура("Результат, ИзделиеКомплект", ОдинаковыйИзделиеКомплект, текИзделиеКомплект);	
	
КонецФункции // ПроверкаОдинКомплект()


&НаКлиенте
Функция ПроверкаОднаПредварительнаяНоменклатура()
	
	текПредварительнаяНоменклатура = тзПредварительнаяНоменклатура[0].ПредварительнаяНоменклатура;
	ОдинаковаяПредварительнаяНоменклатура = Истина;
	
	мДанные = Новый Массив;
	
	ОбщиеКоличество = 0;
	
	КоличествоЭлементов = 0;
	
	Для каждого х Из тзПредварительнаяНоменклатура Цикл
		Если НЕ текПредварительнаяНоменклатура = х.ПредварительнаяНоменклатура Тогда
			ОдинаковаяПредварительнаяНоменклатура = Ложь;	
			Прервать;
		КонецЕсли;
		стДанные = Новый Структура("ИзделиеКомплект,Количество", );
		ЗаполнитьЗначенияСвойств(стДанные, х);
		
		ОбщиеКоличество = ОбщиеКоличество + х.Количество;
		
		мДанные.Добавить(стДанные); 
		
		КоличествоЭлементов = КоличествоЭлементов + 1;
	КонецЦикла;
	
	Возврат Новый Структура("Результат, мДанные, ОбщиеКоличество, КоличествоЭлементов"
	, ОдинаковаяПредварительнаяНоменклатура, мДанные, ОбщиеКоличество, КоличествоЭлементов);	

КонецФункции // ПроверкаОднаПредварительнаяНоменклатура()


&НаСервереБезКонтекста
Функция ПолучитьСделкиДляРаспределения()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПотребностьПредварительнойНоменклатурыОстатки.Проект КАК Проект
		|ИЗ
		|	РегистрНакопления.ПотребностьПредварительнойНоменклатуры.Остатки КАК ПотребностьПредварительнойНоменклатурыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ПотребностьПредварительнойНоменклатурыОстатки.Проект";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Проект");
	
	
КонецФункции


&НаКлиенте
Процедура Обновить(Команда)
	мСделки = ПолучитьСделкиДляРаспределения();
	
	Элементы.Проект.СписокВыбора.ЗагрузитьЗначения(мСделки);
	
	Если мСделки.Найти(ЭтаФорма.Проект) = Неопределено Тогда
		ЭтаФорма.Проект = Неопределено;	
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	мСделки = ПолучитьСделкиДляРаспределения();
	Элементы.Проект.СписокВыбора.ЗагрузитьЗначения(мСделки);
КонецПроцедуры


&НаКлиенте
Процедура ИзделиеКомплектПриИзменении(Элемент)
	ПриИзмененииФильтров();
	УстановкаВидимости();
КонецПроцедуры


&НаКлиенте
Процедура ПоИзделиямОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	Если Тип("Структура") = ТипЗнч(Расшифровка) Тогда
		СтандартнаяОбработка = Ложь;
		Для каждого х Из тзПолныеДанные Цикл
			Если х.ПредварительнаяНоменклатура = Расшифровка.ПредварительнаяНоменклатура 
				И х.ИзделиеКомплект = Расшифровка.ИзделиеКомплект Тогда
				х.Выдано = х.Количество;
				
				НС = ЭтаФорма.тзПредварительнаяНоменклатура.Добавить();
				ЗаполнитьЗначенияСвойств(НС, х);

			КонецЕсли;	
		КонецЦикла;
		ВывестиПредварительнуюНоменклатуруСУчетомВыдачи();
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ПоИзделиямОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	// Вставить содержимое обработчика.
КонецПроцедуры   

&НаКлиенте
Процедура УстановкаВидимости()
	Если ЗначениеЗаполнено(ЭтаФорма.ИзделиеКомплект) Тогда
		Элементы.ПредварительнаяНоменклатураИзделиеКомплект.Видимость 	= Ложь;	
		Элементы.тзНоменклатураИзделиеКомплект.Видимость 				= Ложь;	
		Элементы.тзНоменклатураРаспределитьКомплекты.Видимость 			= Ложь;	
	Иначе	
		Элементы.ПредварительнаяНоменклатураИзделиеКомплект.Видимость 	= Истина;	
		Элементы.тзНоменклатураИзделиеКомплект.Видимость 				= Истина;	
		Элементы.тзНоменклатураРаспределитьКомплекты.Видимость 			= Истина;	
	КонецЕсли;

КонецПроцедуры // УстановкаВидимости()

&НаКлиенте
Процедура тзНоменклатураПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ЭтаФорма.ИзделиеКомплект) Тогда
		ТД = Элементы.тзНоменклатура.ТекущиеДанные;
		Если ТД = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ТД.ИзделиеКомплект = ЭтаФорма.ИзделиеКомплект;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументНаСервере()

	Док = Документы.ПереводПреварительнойНоменклатуры.СоздатьДокумент();
	Док.Дата = ТекущаяДата();
	Для каждого х Из ЭтаФорма.тзРезультат Цикл
		Если ЗначениеЗаполнено(х.ПредварительнаяНоменклатура) Тогда
			НС = Док.ПредварительнаяНомеклатура.Добавить();
			ЗаполнитьЗначенияСвойств(НС, х,,"Количество, ИзделиеКомплект");
			НС.Количество 		= х.КоличествоПН;
			НС.ИзделиеКомплект 	= х.ИзделиеКомплектПН;
		КонецЕсли;
		Если ЗначениеЗаполнено(х.Номенклатура) Тогда
			ЗаполнитьЗначенияСвойств(Док.Номеклатура.Добавить(), х);
		КонецЕсли;
	КонецЦикла;
	
	Док.Записать(РежимЗаписиДокумента.Проведение);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокумент(Команда)
	СоздатьДокументНаСервере(); 
	ПриИзмененииФильтров();
КонецПроцедуры




