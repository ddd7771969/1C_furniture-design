
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.АктПриемкиПомещения") Тогда
		
		// Заполнение шапки
		Проект = ДанныеЗаполнения.Проект;
		Основание = ДанныеЗаполнения.Ссылка; 
		
		// Заполнение ТЧ
		мСтатус = Новый Массив(2);
		мСтатус[0] = Перечисления.СтатусыЗамера.Рабочий;
		мСтатус[1] = Перечисления.СтатусыЗамера.Строительный;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗапускВПроизводствоКомплекты.ИзделиеКомплект КАК Комплект
		|ПОМЕСТИТЬ втЗапущенныеКомплекты
		|ИЗ
		|	Документ.ЗапускВПроизводство.Комплекты КАК ЗапускВПроизводствоКомплекты
		|ГДЕ
		|	ЗапускВПроизводствоКомплекты.Ссылка.Проект = &Проект
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапускВПроизводствоКомплекты.ИзделиеКомплект
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Комплект УНИКАЛЬНО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК ИзделиеКомплект
		|ИЗ
		|	Документ.АктПриемкиПомещения.ГабаритныеЧертежи КАК АктПриемкиПомещенияГабаритныеЧертежи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИзделияКомплект КАК ИзделияКомплект
		|			ЛЕВОЕ СОЕДИНЕНИЕ втЗапущенныеКомплекты КАК втЗапущенныеКомплекты
		|			ПО ИзделияКомплект.Ссылка = втЗапущенныеКомплекты.Комплект
		|		ПО АктПриемкиПомещенияГабаритныеЧертежи.ГабаритныйЧертеж = ИзделияКомплект.ГабаритныйЧертеж
		|ГДЕ
		|	АктПриемкиПомещенияГабаритныеЧертежи.Ссылка = &Ссылка
		|	И АктПриемкиПомещенияГабаритныеЧертежи.НовыйСтатус В(&НовыйСтатус)
		|	И втЗапущенныеКомплекты.Комплект ЕСТЬ NULL";
		
		Запрос.УстановитьПараметр("НовыйСтатус", мСтатус);
		Запрос.УстановитьПараметр("Проект", ДанныеЗаполнения.Проект);
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения.Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Комплекты.Добавить(), Выборка); 
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
