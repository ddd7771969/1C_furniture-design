

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	омЗаполнениеРеквизиов.ЗаполнитьРеквизитыСтандартные(Объект);

	ОбновитьКартинки();
	Элементы.АдресКартинки.РазмерКартинки = РазмерКартинки.АвтоРазмер;
	
	Если ЗначениеЗаполнено(Объект.ИзделиеКомплект) Тогда
		Элементы.ИзделиеКомплект.СписокВыбора.Добавить(Объект.ИзделиеКомплект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзделиеКомплектПриИзмененииНаСервере(ИзделиеКомплект)
	Возврат ИзделиеКомплект.Проект;
КонецФункции

&НаКлиенте
Процедура ИзделиеКомплектПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.ИзделиеКомплект) Тогда
		Объект.Проект = ИзделиеКомплектПриИзмененииНаСервере(Объект.ИзделиеКомплект);
	Иначе
		Объект.Проект = ПредопределенноеЗначение("Справочник.Проекты.ПустаяСсылка");
	КонецЕсли;
КонецПроцедуры


Процедура ОбновитьКартинки()	
	КлючКартинки = РегистрыСведений.КартинкиНоменклатуры.СоздатьКлючЗаписи(Новый Структура("ИзделиеЭлемент", Объект.Ссылка));
	АдресКартинки = ПолучитьНавигационнуюСсылку(КлючКартинки, "Картинка");
КонецПроцедуры

&НаКлиенте
Процедура АдресКартинкиПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры


&НаКлиенте
Асинх Процедура АдресКартинкиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЭтаФорма.Параметры.Ключ.Пустая() Тогда
		ПоказатьПредупреждение(,"Перед добавлением картинки необходимо сохранить изделие.");
		Возврат;	
	КонецЕсли;
	
	Если НЕ ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СсылкаНаФайл") Тогда
		Возврат;	
	КонецЕсли;
	
	мДопустимоеРасширениеФайлов = Новый Массив(3);
	мДопустимоеРасширениеФайлов[0] = ".png";
	мДопустимоеРасширениеФайлов[1] = ".jpg";
	мДопустимоеРасширениеФайлов[2] = ".jpeg";
	
	ПолноеИмя = ПараметрыПеретаскивания.Значение.Файл.ПолноеИмя;
	ФайлКартинки = Новый Файл(ПолноеИмя);
	РасширениеФайла = НРег(ФайлКартинки.Расширение);
	Если мДопустимоеРасширениеФайлов.Найти(РасширениеФайла) = Неопределено Тогда
		
		СтрокаДопустимыеФорматы = "";
		
		Для каждого х Из мДопустимоеРасширениеФайлов Цикл
		 	Если НЕ ПустаяСтрока(СтрокаДопустимыеФорматы) Тогда
				СтрокаДопустимыеФорматы = СтрокаДопустимыеФорматы + ", ";
			КонецЕсли;
			
			СтрокаДопустимыеФорматы = СтрокаДопустимыеФорматы + х;
		КонецЦикла;
		
		СтрокаДопустимыеФорматы = СтрокаДопустимыеФорматы + ".";
		
		ПоказатьПредупреждение(,"Не допустимый тип файла. Возможные варианты " + СтрокаДопустимыеФорматы);
		Возврат;
		
	КонецЕсли;

	
	РазмерФайла = Окр(ФайлКартинки.Размер() / (1024*1024),1);
	МаксимальныйРазмерФайла = Окр(ПолучитьМаксимальныйРазмерФайла() / (1024*1024),1);
	Если РазмерФайла > МаксимальныйРазмерФайла Тогда
		ПоказатьПредупреждение(,"Размер файла " + РазмерФайла + " Мб. Максимальный допустимый размер файла " + МаксимальныйРазмерФайла + "Мб.");
		Возврат;	
	КонецЕсли; 
	
	ПомещенныйФайл = Ждать ПоместитьФайлНаСерверАсинх(,,,ПолноеИмя);
	
	ЗаписатьФайлКартинкиНаСервере(Объект.Ссылка, ПомещенныйФайл.Адрес, ПолноеИмя);
	
	ПодключитьОбработчикОжидания("ПослеЗагрузкиКартинки",0.5,Истина);
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ПолучитьМаксимальныйРазмерФайла()

	Возврат РаботаСФайлами.МаксимальныйРазмерФайла();	

КонецФункции // ПолучитьМаксимальныйРазмерФайла()


&НаСервереБезКонтекста
Функция ЗаписатьФайлКартинкиНаСервере(РекламацияПодряда, АдресВХ, ИмяФайла)

	МЗ = РегистрыСведений.КартинкиНоменклатуры.СоздатьМенеджерЗаписи();
	МЗ.ИзделиеЭлемент 	= РекламацияПодряда;
	МЗ.ИмяФайла 		= ИмяФайла;
	МЗ.Картинка 		= Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресВХ));
	МЗ.Записать();
	
КонецФункции

&НаКлиенте
Процедура ПослеЗагрузкиКартинки() Экспорт

	ОбновитьКартинки();	
	Элементы.АдресКартинки.Обновить();

КонецПроцедуры // ПослеЗагрузкиКартинки()

&НаКлиенте
Процедура ИзделиеКомплектНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Объект.ВозвратСПодряда) Тогда
	   Элементы.ИзделиеКомплект.СписокВыбора.ЗагрузитьЗначения(ПолучитьКомплектыВозрата(Объект.ВозвратСПодряда));
	Иначе
		ПоказатьПредупреждение(, "Не заполнен документ возврата с подряда");
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКомплектыВозрата(ВозвратСПодряда)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВозвратСПодрядаИзделияКомплекты.ИзделиеКомплект КАК ИзделиеКомплект
		|ИЗ
		|	Документ.ВозвратСПодряда.ИзделияКомплекты КАК ВозвратСПодрядаИзделияКомплекты
		|ГДЕ
		|	ВозвратСПодрядаИзделияКомплекты.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВозвратСПодрядаИзделияКомплекты.ИзделиеКомплект.Наименование";
	
	Запрос.УстановитьПараметр("Ссылка", ВозвратСПодряда);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИзделиеКомплект");
	
КонецФункции // ПолучитьКомплектыВозрата()


