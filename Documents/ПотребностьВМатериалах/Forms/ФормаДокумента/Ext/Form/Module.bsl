#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	омЗаполнениеРеквизиов.ЗаполнитьРеквизитыСтандартные(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаголовкиЗакладок("");	
КонецПроцедуры

#КонецОбласти

#Область СобытияРеквизитов

&НаКлиенте
Процедура ИзделиеКомплектПриИзменении(Элемент)
	Для каждого х Из Объект.Материалы Цикл
		х.ИзделиеКомплект = Объект.ИзделиеКомплект;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти


#Область СобытияРеквизитовТЧ

&НаКлиенте
Процедура МатериалыНоменклатураПриИзменении(Элемент)
	омЕдиницыКлиент.ЗаполнитьЕдиницуПриИзмененииМатериала(Элементы.Материалы.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура МатериалыЕдиницыИзмеренияПриИзменении(Элемент)
	омЕдиницыКлиент.ЗаполнитьКоэффициентПриИзмененииЕдиницы(Элементы.Материалы.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура МатериалыЕдиницыИзмеренияНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ТД = Элементы.Материалы.ТекущиеДанные;
	Элемент.СписокВыбора.ЗагрузитьЗначения(омЕдиницыИзмерения.ЕдиницыИзмеренийНоменклатурыДляВыбора(ТД.Номенклатура));
КонецПроцедуры

&НаКлиенте
Процедура МатериалыПриИзменении(Элемент)
	ЗаголовкиЗакладок("Номенклатура");	
КонецПроцедуры

&НаКлиенте
Процедура ИзделиеКомплектНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораКомплекта",ЭтаФорма);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",Ложь);
	
	НеПоказыватьВВыборе = Новый Массив;
	
	ПараметрыФормы.Вставить("НеПоказыватьВВыборе",ПолучитьСуществующиеКобмплекты());
	
	ОткрытьФорму("Справочник.ИзделияКомплект.ФормаВыбора",ПараметрыФормы,ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
	МатериалыПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ОпределеннаяНоменклатураНоменклатураПриИзменении(Элемент)
	Для каждого х Из Объект.ОпределеннаяНоменклатура Цикл
		Если ЗначениеЗаполнено(х.Номенклатура) Тогда
			НС = Объект.Материалы.Добавить();
			НС.ЕдиницыИзмерения	= омЕдиницыИзмерения.ПолучитьОсновнуюЕдиницуИзмерения(х.Номенклатура);
			НС.Номенклатура 	= х.Номенклатура;
            НС.Коэффициент  	= 1;
            НС.Количество  		= х.Количество;
            НС.URL  			= х.URL;
			
			Объект.ОпределеннаяНоменклатура.Удалить(х);
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ЗаголовкиЗакладок("Не опознаная");	
	ЗаголовкиЗакладок("Номенклатура");	
	
КонецПроцедуры

&НаКлиенте
Процедура НеОпределеннаяНоменклатураПредварительнаяНоменклатураПриИзменении(Элемент)
	Для каждого х Из Объект.НеОпределеннаяНоменклатура Цикл
		Если ЗначениеЗаполнено(х.ПредварительнаяНоменклатура) Тогда
			НС = Объект.ПредварительныеНоменклатуры.Добавить();
			НС.ПредварительнаяНоменклатура 	= х.ПредварительнаяНоменклатура;
            НС.Количество  					= х.Количество;
			
			Объект.НеОпределеннаяНоменклатура.Удалить(х);
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ЗаголовкиЗакладок("Не опознаная");	
	ЗаголовкиЗакладок("Предварительная");	
КонецПроцедуры


#КонецОбласти 

&НаКлиенте
Процедура ПослеВыбораКомплекта(Значение, ДополнительныйПараметр) Экспорт

    Если Значение = Неопределено Тогда  
        Возврат;
	КонецЕсли;
	
	Объект.ИзделиеКомплект = Значение;

КонецПроцедуры


&НаСервереБезКонтекста
Функция ПолучитьСуществующиеКобмплекты()	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПотребностьВМатериалах.ИзделиеКомплект КАК ИзделиеКомплект
		|ИЗ
		|	Документ.ПотребностьВМатериалах КАК ПотребностьВМатериалах
		|
		|СГРУППИРОВАТЬ ПО
		|	ПотребностьВМатериалах.ИзделиеКомплект";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИзделиеКомплект");
	
	
КонецФункции // ПолучитьСуществующиеКобмплекты()

&НаКлиенте
Процедура ЗаголовкиЗакладок(ИмяЗакладки = "")

	Если ИмяЗакладки = "" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаНоменклатура"		,"Номенклатура"		,Объект.Материалы);
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаПредварительная"	,"Предварительная"	,Объект.ПредварительныеНоменклатуры);
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаНеОпознаная"		,"Не опознаная"		,Объект.НеОпределеннаяНоменклатура, Объект.ОпределеннаяНоменклатура);
	ИначеЕсли ИмяЗакладки = "Номенклатура" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаНоменклатура"		,ИмяЗакладки		,Объект.Материалы);
	ИначеЕсли ИмяЗакладки = "Предварительная" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаПредварительная"	,ИмяЗакладки		,Объект.ПредварительныеНоменклатуры);
	ИначеЕсли ИмяЗакладки = "Не опознаная" Тогда
		омОформлениеФормы.ЗаголовокЗакладки(Элементы,"ГруппаНеОпознаная"		,ИмяЗакладки		,Объект.НеОпределеннаяНоменклатура, Объект.ОпределеннаяНоменклатура);
	КонецЕсли;

КонецПроцедуры // ЗаголовкиЗакладок()


&НаКлиенте
Процедура ПредварительныеНоменклатурыПриИзменении(Элемент)
	ЗаголовкиЗакладок("Предварительная");	
КонецПроцедуры


&НаКлиенте
Процедура ОпределннаяНоменклатураПриИзменении(Элемент)
	ЗаголовкиЗакладок("Не опознаная");	
КонецПроцедуры


&НаКлиенте
Процедура НеОпределннаяНоменклатураПриИзменении(Элемент)
	ЗаголовкиЗакладок("Не опознаная");	
КонецПроцедуры

#Область Загрузка
	
&НаКлиенте
Процедура Загрузить(Команда)
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Фильтр = НСтр("ru = 'Файл csv'") + " (*.csv)|*.csv";
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПродолжитьЗагрузкуИзФайла", ЭтотОбъект);
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(ОписаниеОповещения, ДиалогВыбораФайла);	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗагрузкуИзФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьЗаголовок = Истина;
	
	ПолноеИмяФайла = ВыбранныеФайлы[0];

	текФайл = Новый ЧтениеТекста(ПолноеИмяФайла,КодировкаТекста.UTF8); 
	СтрокаФайла = текФайл.ПрочитатьСтроку();
	//а не был ли файл пуст?
	Если СтрокаФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	мНаименованиеКолонок = Новый Массив(5);
	
	Если ЕстьЗаголовок Тогда
		ЗаполнитьЗаголовкиТаблицы(СтрокаФайла, мНаименованиеКолонок);
	Иначе	
		мНаименованиеКолонок[0] 	= "артикул";		
		мНаименованиеКолонок[1] 	= "наименование";		
		мНаименованиеКолонок[2] 	= "количество";		
		мНаименованиеКолонок[3] 	= "определённый";		
		мНаименованиеКолонок[4] 	= "ссылка";		
		ЗаполнитьСтрокуТаблицы(СтрокаФайла, мНаименованиеКолонок);
	КонецЕсли;
	
	СтрокаФайла = текФайл.ПрочитатьСтроку();
	Пока СтрокаФайла <> Неопределено Цикл
		ЗаполнитьСтрокуТаблицы(СтрокаФайла, мНаименованиеКолонок);
		СтрокаФайла = текФайл.ПрочитатьСтроку();
	КонецЦикла;
	
	текФайл.Закрыть();
	
	ЗаполнитьТекущуюНоменклатуру();
	
	ЗаголовкиЗакладок("");	

	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаполнитьСтрокуТаблицы(СтрокаФайла, мНаименованиеКолонок)
	мДанныеСтроки = омСsvКлиентСервер.РазобратьСтрокуCSV(СтрокаФайла);
	Если мДанныеСтроки.Количество() < 5 Тогда
		Возврат;
	КонецЕсли;
	НС = ЭтаФорма.тзДанныеФайла.Добавить();
	Для  х = 0 По 4 Цикл 
		Если мНаименованиеКолонок[х] = "количество" Тогда
			НС[мНаименованиеКолонок[х]] = Число(омРаботаССтроками.ОставитьТолькоЦифры(мДанныеСтроки[х]));
		ИначеЕсли мНаименованиеКолонок[х] = "определённый" Тогда
			НС[мНаименованиеКолонок[х]] = ?(мДанныеСтроки[х] = "0", Ложь, Истина);
		Иначе
			НС[мНаименованиеКолонок[х]] = мДанныеСтроки[х]; 
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьСтрокуТаблицы()


&НаКлиенте
Процедура ЗаполнитьЗаголовкиТаблицы(СтрокаФайла, мНаименованиеКолонок)
	
	мДанныеСтроки = омСsvКлиентСервер.РазобратьСтрокуCSV(СтрокаФайла,, Истина);
	
	Если мДанныеСтроки.Количество() < 5 Тогда
		Возврат;
	КонецЕсли;
	
	Для х = 0 По 4 Цикл
		Если мДанныеСтроки[х] = "определённый(0/1)" Тогда
			мНаименованиеКолонок[х] = "определённый";		
		Иначе
			мНаименованиеКолонок[х] = мДанныеСтроки[х];		
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьЗаголовок()


&НаСервере
Процедура ЗаполнитьТекущуюНоменклатуру()

	мАртикул 		= Новый Массив;
	мНаименование 	= Новый Массив;
	
	Для каждого х Из тзДанныеФайла Цикл
		Если х.Определённый Тогда
			мАртикул.Добавить(х.Артикул);
		Иначе
			мНаименование.Добавить(х.Наименование);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	Номенклатура.Артикул КАК Артикул,
		|	Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Артикул В(&Артикул)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПредварительнаяНоменклатура.Ссылка КАК ПредварительнаяНоменклатура,
		|	ПредварительнаяНоменклатура.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ПредварительнаяНоменклатура КАК ПредварительнаяНоменклатура
		|ГДЕ
		|	ПредварительнаяНоменклатура.Наименование В(&Наименование)";
	
	Запрос.УстановитьПараметр("Артикул", мАртикул);
	Запрос.УстановитьПараметр("Наименование", мНаименование);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[0].Выбрать();
	
	стОтбор = Новый Структура("Определённый,Артикул",Истина );
	Пока Выборка.Следующий() Цикл
		стОтбор.Артикул = Выборка.Артикул;
		НайденыеСтроки 	= тзДанныеФайла.НайтиСтроки(стОтбор);
		
		Для каждого х Из НайденыеСтроки Цикл
			НС = Объект.Материалы.Добавить();
			НС.ЕдиницыИзмерения	= Выборка.ЕдиницаИзмерения;
			НС.Номенклатура 	= Выборка.Номенклатура;
            НС.Коэффициент  	= 1;
            НС.Количество  		= х.Количество;
            НС.URL  			= х.Ссылка;
			
			х.Найдена = Истина;
		КонецЦикла;
	КонецЦикла;
	
	Выборка = РезультатЗапроса[1].Выбрать();
	
	стОтбор = Новый Структура("Определённый,Наименование",Ложь );
	Пока Выборка.Следующий() Цикл
		стОтбор.Наименование = Выборка.Наименование;
		НайденыеСтроки 	= тзДанныеФайла.НайтиСтроки(стОтбор);
		
		Для каждого х Из НайденыеСтроки Цикл
			НС = Объект.ПредварительныеНоменклатуры.Добавить();
			НС.ПредварительнаяНоменклатура 	= Выборка.ПредварительнаяНоменклатура;;
            НС.Коэффициент  				= 1;
			
			х.Найдена = Истина;
		КонецЦикла;
	КонецЦикла;
	
    стОтбор = Новый Структура("Найдена,Определённый",Ложь, Ложь);
	
	НайденыеСтроки 	= тзДанныеФайла.НайтиСтроки(стОтбор);
	Для каждого х Из НайденыеСтроки Цикл
		НС = Объект.НеОпределеннаяНоменклатура.Добавить();
		НС.Комментарий 	= х.Наименование;
		НС.Количество 	= х.Количество;
	КонецЦикла;

	стОтбор.Определённый = Истина;
	НайденыеСтроки 	= тзДанныеФайла.НайтиСтроки(стОтбор);
	Для каждого х Из НайденыеСтроки Цикл
		НС = Объект.ОпределеннаяНоменклатура.Добавить();
		НС.Комментарий 	= х.Наименование;;
		НС.Количество 	= х.Количество;
		НС.URL 			= х.Ссылка;
	КонецЦикла;
	
	тзДанныеФайла.Очистить();      
	
КонецПроцедуры // ЗаполнитьТекущуюНоменклатуру()

#КонецОбласти
