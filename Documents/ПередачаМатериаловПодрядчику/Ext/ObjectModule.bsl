
Процедура ОбработкаПроведения(Отказ, Режим)
	Запрос = Новый запрос;
	Запрос.Текст = "ВЫБРАТЬ
		              |	ОстаткиТоваровНаСкладахОстатки.Номенклатура КАК Номенклатура,
		              |	ОстаткиТоваровНаСкладахОстатки.Склад КАК Склад,
		              |	ОстаткиТоваровНаСкладахОстатки.КоличествоОстаток КАК Количество,
		              |	ОстаткиТоваровНаСкладахОстатки.СуммаСНДСОстаток КАК СуммаСНДС
		              |ИЗ
		              |	РегистрНакопления.ОстаткиТоваровНаСкладах.Остатки(&Дата, Номенклатура В (&МассивМатериалы)) КАК ОстаткиТоваровНаСкладахОстатки";
	
	Запрос.УстановитьПараметр("Дата", Новый Граница(ЭтотОбъект.МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Склад", ЭтотОбъект.Склад);
	Запрос.УстановитьПараметр("МассивМатериалы", Материалы.ВыгрузитьКолонку("Номенклатура"));
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	стОтбор = Новый Структура("Номенклатура", );
	
	Движения.ОстаткиТоваровНаСкладах.Записывать = Истина;
	Для Каждого ТекСтрокаМатериалы Из Материалы Цикл 
		
		стОтбор.Номенклатура = ТекСтрокаМатериалы.Номенклатура;
		НайденныеСтроки = ТЗ.НайтиСтроки(стОтбор);
		
		Количество = ТекСтрокаМатериалы.Количество * ТекСтрокаМатериалы.Коэффициент; 
		

		Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		    Если НайденнаяСтрока.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Движение = Движения.ОстаткиТоваровНаСкладах.Добавить();
			Движение.ВидДвижения 		= ВидДвиженияНакопления.Расход;
			Движение.Номенклатура 		= ТекСтрокаМатериалы.Номенклатура;
			Движение.Период 			= Дата;
			Движение.Склад 				= Склад;
			
			Если НайденнаяСтрока.Количество >= Количество Тогда
				Движение.СуммаСНДС 	= Окр((НайденнаяСтрока.СуммаСНДС / НайденнаяСтрока.Количество) * Количество, 2);
				Движение.Количество = Количество;
				
				НайденнаяСтрока.СуммаСНДС  = НайденнаяСтрока.СуммаСНДС  - Движение.СуммаСНДС;
				НайденнаяСтрока.Количество = НайденнаяСтрока.Количество - Количество;
				Количество = 0;
				Прервать;
			Иначе
				Движение.СуммаСНДС 	= НайденнаяСтрока.СуммаСНДС;
				Движение.Количество = НайденнаяСтрока.Количество;
				
				Количество 			= Количество - НайденнаяСтрока.Количество;
				
				НайденнаяСтрока.Количество = 0;
				НайденнаяСтрока.СуммаСНДС  = 0;
			КонецЕсли;
			
		КонецЦикла; 
		
		Если Количество > 0 Тогда
			Движение = Движения.ОстаткиТоваровНаСкладах.Добавить();
			Движение.ВидДвижения 		= ВидДвиженияНакопления.Расход;
			Движение.Номенклатура 		= ТекСтрокаМатериалы.Номенклатура;
			Движение.Период 			= Дата;
			Движение.Склад 				= Склад;
			Движение.Количество			= Количество;
			Движение.СуммаСНДС			= 0;
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.Записать(); 
КонецПроцедуры
