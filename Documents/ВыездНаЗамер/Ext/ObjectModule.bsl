
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ ЗначениеЗаполнено(Номер) Тогда
		Префикс = Проект.Префикс + "-";
		УстановитьНовыйНомер(Префикс); 
	КонецЕсли;
	
	СокращенноеНазвание = омЗаполнениеРеквизиов.ПолучитьСокращенноеНазвание(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаявкаНаЗамер") Тогда

		Если НЕ ДанныеЗаполнения.Проведен Тогда
		
			Сообщить("Нельзя создавать Выезд на замер пна основании не проведенной заявки");
			СтандартнаяОбработка = Ложь;
			Возврат;
		
		КонецЕсли; 
		
		Основание 		= ДанныеЗаполнения.Ссылка;
		Проект 			= ДанныеЗаполнения.Проект;
		Автор 			= ПараметрыСеанса.ТекущийПользователь; 
		ДатаВыездаПлан 	= ДанныеЗаполнения.ДатаВыездаПлан;
			
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Помещения.Ссылка КАК Помещение,
			|	ГабаритныеЧертежиПомещения.Ссылка КАК ГабаритныйЧертеж
			|ИЗ
			|	Справочник.Помещения КАК Помещения
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГабаритныеЧертежи.Помещения КАК ГабаритныеЧертежиПомещения
			|		ПО (ГабаритныеЧертежиПомещения.Помещение = Помещения.Ссылка)
			|ГДЕ
			|	Помещения.Ссылка В(&Ссылка)
			|
			|СГРУППИРОВАТЬ ПО
			|	ГабаритныеЧертежиПомещения.Ссылка,
			|	Помещения.Ссылка
			|
			|УПОРЯДОЧИТЬ ПО
			|	Помещения.Наименование,
			|	ГабаритныеЧертежиПомещения.Ссылка.Наименование";
		
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения.Помещения.ВыгрузитьКолонку("Помещение"));
		
		ГабаритныеЧертежи.Загрузить(Запрос.Выполнить().Выгрузить());
	
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗадачуПроверки(Выборка, Исполнитель)
	
	ЗадачаПроверки = Задачи.ПроверитьГабаритныеЧертежи.СоздатьЗадачу();
	ЗадачаПроверки.Дата 					= ТекущаяДата();
	ЗадачаПроверки.Основание 				= Ссылка;
	ЗадачаПроверки.ГабаритныйЧертеж 		= Выборка.ГабаритныйЧертеж;
	ЗадачаПроверки.Исполнитель 				= Исполнитель;
	ЗадачаПроверки.Наименование				= Выборка.ГабаритныйЧертежНаименование + ".Проверка";	
	ЗадачаПроверки.Проект					= Выборка.Проект;	
	ЗадачаПроверки.КрайняяДатаИсполнения	= ЗадачаПроверки.Дата + 7 * 24 * 3600;	
	ЗадачаПроверки.БылаКорректировка		= 0;	
	
	ЗадачаПроверки.Записать();

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыездНаЗамерГабаритныеЧертежи.ГабаритныйЧертеж КАК ГабаритныйЧертеж,
		|	ВыездНаЗамерГабаритныеЧертежи.ГабаритныйЧертеж.Наименование КАК ГабаритныйЧертежНаименование,
		|	ВыездНаЗамерГабаритныеЧертежи.ГабаритныйЧертеж.Проект КАК Проект
		|ИЗ
		|	Документ.ВыездНаЗамер.ГабаритныеЧертежи КАК ВыездНаЗамерГабаритныеЧертежи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Задача.ПроверитьГабаритныеЧертежи КАК ПроверитьГабаритныеЧертежи
		|		ПО (ВыездНаЗамерГабаритныеЧертежи.Ссылка = (ВЫРАЗИТЬ(ПроверитьГабаритныеЧертежи.Основание КАК Документ.ВыездНаЗамер)))
		|			И ВыездНаЗамерГабаритныеЧертежи.ГабаритныйЧертеж = ПроверитьГабаритныеЧертежи.ГабаритныйЧертеж
		|ГДЕ
		|	ВыездНаЗамерГабаритныеЧертежи.Ссылка В(&Ссылка)
		|	И ВыездНаЗамерГабаритныеЧертежи.ЗамерПроизведен
		|	И ПроверитьГабаритныеЧертежи.Ссылка ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыездНаЗамерГабаритныеЧертежи.ГабаритныйЧертеж,
		|	ВыездНаЗамерГабаритныеЧертежи.ГабаритныйЧертеж.Наименование,
		|	ВыездНаЗамерГабаритныеЧертежи.ГабаритныйЧертеж.Проект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НазначениеАвтораДляЗадачи.Исполнитель КАК Исполнитель
		|ИЗ
		|	Справочник.НазначениеАвтораДляЗадачи КАК НазначениеАвтораДляЗадачи
		|ГДЕ
		|	НазначениеАвтораДляЗадачи.ОбъектЗадачи = ЗНАЧЕНИЕ(Перечисление.ОбъектыЗадач.ПроверятьГабаритныеЧерчежи)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.ВыполнитьПакет(); 
	
	Исполнитель = Неопределено;
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		Исполнитель = Выборка.Исполнитель;
	КонецЦикла;
	
	
	Выборка = Результат[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		СоздатьЗадачуПроверки(Выборка, Исполнитель);
	КонецЦикла; 
	
	ДокументыВыездНаЗамер();
	
КонецПроцедуры

Процедура ДокументыВыездНаЗамер()

	мДокументы = Новый Массив(2);
	мДокументы[0] = Ссылка;
	мДокументы[1] = Основание;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДокументыДляВыезда.Документы КАК Документ
		|ИЗ
		|	РегистрСведений.ДокументыДляВыезда КАК ДокументыДляВыезда
		|ГДЕ
		|	ДокументыДляВыезда.Документы В(&Документы)";
	
	Запрос.УстановитьПараметр("Документы", мДокументы);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ЕстьВРегистреСсылка 	= Ложь;
	ЕстьВРегистреОснование 	= Ложь;
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Документ = Ссылка Тогда
			ЕстьВРегистреСсылка = Истина;
		Иначе
			ЕстьВРегистреОснование 	= Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьВРегистреОснование Тогда
	
		НаборЗаписей = РегистрыСведений.ДокументыДляВыезда.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Документы.Установить(Основание);
			
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	Если ЕстьВРегистреСсылка И ЗначениеЗаполнено(ДатаВыездаФакт) Тогда
	
		НаборЗаписей = РегистрыСведений.ДокументыДляВыезда.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Документы.Установить(Ссылка);
			
		НаборЗаписей.Записать();
		
	ИначеЕсли НЕ ЗначениеЗаполнено(ДатаВыездаФакт) Тогда
	
		МенеджерЗаписей = РегистрыСведений.ДокументыДляВыезда.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.Документы 		= Ссылка;
		МенеджерЗаписей.Исполнитель		= Исполнитель;
		МенеджерЗаписей.ДатаВыезда 		= ДатаВыездаПлан;
		МенеджерЗаписей.ДатаСоздания 	= Дата;
		
	    МенеджерЗаписей.Записать(); 
		
		Для каждого х Из Исполнители Цикл
			
			МенеджерЗаписей = РегистрыСведений.ДокументыДляВыезда.СоздатьМенеджерЗаписи();
			МенеджерЗаписей.Документы 		= Ссылка;
			МенеджерЗаписей.Исполнитель		= х.Исполнитель;
			МенеджерЗаписей.ДатаВыезда 		= ДатаВыездаПлан;
			МенеджерЗаписей.ДатаСоздания 	= Дата;
			
			МенеджерЗаписей.Записать(); 
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если ЗначениеЗаполнено(Основание) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗаявкаНаЗамер.ДатаВыездаПлан КАК ДатаВыездаПлан,
			|	ЗаявкаНаЗамер.Дата КАК Дата,
			|	ЗаявкаНаЗамер.Проведен КАК Проведен
			|ИЗ
			|	Документ.ЗаявкаНаЗамер КАК ЗаявкаНаЗамер
			|ГДЕ
			|	ЗаявкаНаЗамер.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Основание);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если ЗначениеЗаполнено(Выборка.Проведен) Тогда
				
				МенеджерЗаписей = РегистрыСведений.ДокументыДляВыезда.СоздатьМенеджерЗаписи();
				МенеджерЗаписей.Документы 		= Основание; 
				МенеджерЗаписей.ДатаВыезда 		= Выборка.ДатаВыездаПлан;
				МенеджерЗаписей.ДатаСоздания 	= Выборка.Дата;
				
				МенеджерЗаписей.Записать();
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли; 
	
	НаборЗаписей = РегистрыСведений.ДокументыДляВыезда.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документы.Установить(Ссылка);
	
	НаборЗаписей.Записать();
		
 
КонецПроцедуры
 

