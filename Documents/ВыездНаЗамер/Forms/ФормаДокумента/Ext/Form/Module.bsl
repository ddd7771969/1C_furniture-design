&НаКлиенте
Перем НачальноеЗначениеПлановойДаты;

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НачальноеЗначениеПлановойДаты = Объект.ДатаВыездаПлан;
	Элементы.ПоказатьИсториюИзмененияДатыПлановой.Видимость = Объект.ПричинаИзменени.Количество() > 0;
	
КонецПроцедуры

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
	
		Если ЗначениеЗаполнено(Объект.Основание) Тогда
			
			Если КонтрольДубляОснования() Тогда
			
				Отказ = Истина;
				Возврат;
			
			КонецЕсли;	
			
		КонецЕсли;
	
	КонецЕсли;
	омЗаполнениеРеквизиов.ЗаполнитьРеквизитыСтандартные(Объект); 
		
КонецПроцедуры

Функция КонтрольДубляОснования()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыездНаЗамер.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ВыездНаЗамер КАК ВыездНаЗамер
		|ГДЕ
		|	ВыездНаЗамер.Основание = &Основание";
	
	Запрос.УстановитьПараметр("Основание", Объект.Основание);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Сообщить("Для заявки " + Объект.Основание + " уже есть Выезд на замер " + Выборка.Ссылка);
		Возврат Истина;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции // КонтрольДубляОснования()

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзменениеЗамеров");

КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсеЧертежи(Команда)
	ОтметитьЧертежи(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсеЧертежи(Команда)
	ОтметитьЧертежи(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьЧертежи(Значение)

	Для каждого х Из Объект.ГабаритныеЧертежи Цикл
		х.ЗамерПроизведен = Значение;	
	КонецЦикла;
	
КонецПроцедуры // ОтметитьЧертежи()

&НаКлиенте
Процедура ГабаритныеЧертежиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	
	Если Объект.ГабаритныеЧертежи.Количество() = 0 Тогда
		НеПоказыватьГЧ = Неопределено;
	Иначе
		НеПоказыватьГЧ = Новый Массив;
		
		Для каждого х Из Объект.ГабаритныеЧертежи Цикл
			НеПоказыватьГЧ.Добавить(х.ГабаритныйЧертеж);
		КонецЦикла;
		
	КонецЕсли;
	
	омГабаритныйЧертежНаКлиенте.ОткрытьПодборГЧ(Объект, ЭтаФорма, , , НеПоказыватьГЧ, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораГЧ(Результат, ДополнительныеПараметры) Экспорт	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;                         
	
	Если ДополнительныеПараметры = Неопределено Тогда
		
		ГабаритныеЧертежи = ПолучитьПомещения(Результат);
		
		Для каждого ПомещениеГЧ Из ГабаритныеЧертежи Цикл
			НС = Объект.ГабаритныеЧертежи.Добавить();
			НС.ГабаритныйЧертеж = ПомещениеГЧ.ГЧ;
			НС.Помещение 		= ПомещениеГЧ.Помещение;
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры // ПослеВыбораЭлементовСделки() 

&НаСервереБезКонтекста
Функция ПолучитьПомещения(ГабаритныеЧертежи)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГабаритныеЧертежиПомещения.Помещение КАК Помещение,
		|	ГабаритныеЧертежиПомещения.Ссылка КАК ГЧ
		|ИЗ
		|	Справочник.ГабаритныеЧертежи.Помещения КАК ГабаритныеЧертежиПомещения
		|ГДЕ
		|	ГабаритныеЧертежиПомещения.Ссылка В(&ГабаритныйЧертеж)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГабаритныеЧертежиПомещения.Помещение.Наименование,
		|	ГабаритныеЧертежиПомещения.Ссылка.Наименование";
	
	Запрос.УстановитьПараметр("ГабаритныйЧертеж", ГабаритныеЧертежи);
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
КонецФункции // ПолучитьПомещения()

&НаКлиенте
Процедура ГабаритныеЧертежиЗамерПроизведенПриИзменении(Элемент)
	ТД = Элементы.ГабаритныеЧертежи.ТекущиеДанные;
	
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТД.ЗамерПроизведен Тогда
		стОтбор = Новый Структура("ГабаритныйЧертеж, ЗамерПроизведен", ТД.ГабаритныйЧертеж, Ложь);
		
		Для каждого х Из Объект.ГабаритныеЧертежи.НайтиСтроки(стОтбор) Цикл
			х.ЗамерПроизведен = Истина;
		КонецЦикла;
	
	КонецЕсли;
КонецПроцедуры

#Область ПричинаИзмененияДаты
	
&НаКлиенте
Процедура ДатаВыездаПланПриИзменении(Элемент)
	
	Если НЕ НачальноеЗначениеПлановойДаты = Объект.ДатаВыездаПлан И НачальноеЗначениеПлановойДаты > Дата(1, 1, 1) Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВводаПричиныИзмененияДаты", ЭтаФорма);
		ПоказатьВводСтроки(ОписаниеОповещения, "", "Причина изменения даты", 250, Истина);
	   	
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеВводаПричиныИзмененияДаты(ПричинаИзменения, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ПричинаИзменения) Тогда
	
		НС = Объект.ПричинаИзменени.Добавить();
		НС.Дата 			= ТекущаяДата();
		НС.ДатаПредыдущая	= НачальноеЗначениеПлановойДаты;
		НС.ПричинаИзменения = ПричинаИзменения;
		НС.Автор 			= омСотрудники.ПолучитьТекущегоПользователя();
		
		НачальноеЗначениеПлановойДаты = Объект.ДатаВыездаПлан;	
		
	Иначе
		
		Объект.ДатаВыездаПлан = НачальноеЗначениеПлановойДаты;
		ПоказатьПредупреждение(, "Не указана причина переноса даты.");
	
	КонецЕсли;
	
КонецПроцедуры // ПослеВводаПричиныИзмененияДаты()


&НаКлиенте
Процедура ПоказатьИсториюИзмененияДатыПлановой(Команда)
	ОткрытьФорму("ОбщаяФорма.ИсторияИзмененияДатыВыезда", Новый Структура("ДокументВыезда", Объект.Ссылка));
КонецПроцедуры

#КонецОбласти

