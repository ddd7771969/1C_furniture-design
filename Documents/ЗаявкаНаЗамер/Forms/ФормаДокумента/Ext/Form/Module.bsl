&НаКлиенте
Перем НачальноеЗначениеПлановойДаты;

&НаКлиенте
Процедура ПомещенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	
	НеПоказыватьВВыборе = Новый Массив;
	
	Для каждого х Из Объект.Помещения Цикл
		НеПоказыватьВВыборе.Добавить(х.Помещение);	
	КонецЦикла;
	
	омПомещениеКлиент.ОткрытьПодборПомещения(Объект, ЭтаФорма, , , НеПоказыватьВВыборе, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПомещения(Результат, ДополнительныйПараметр) Экспорт	
	
	Если Результат = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ДополнительныйПараметр = Неопределено Тогда
		
		Для каждого Помещение Из Результат Цикл
			
			НС = Объект.Помещения.Добавить();
			НС.Помещение = Помещение;
			
		КонецЦикла;
		
	Иначе 
		Если Результат.Количество() = 0 Тогда
		
			Возврат;	
		
		КонецЕсли;
		
		ДополнительныйПараметр.ТД.Помещение = Результат[0];
		
	КонецЕсли; 

КонецПроцедуры // ПослеВыбораЭлементовСделки()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	омЗаполнениеРеквизиов.ЗаполнитьРеквизитыСтандартные(Объект);
КонецПроцедуры


&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ИзменениеЗамеров");
КонецПроцедуры

#Область ПричинаИзмененияДаты
	
&НаКлиенте
Процедура ДатаВыездаПланПриИзменении(Элемент)
	
	Если НЕ НачальноеЗначениеПлановойДаты = Объект.ДатаВыездаПлан И НачальноеЗначениеПлановойДаты > Дата(1, 1, 1)  Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВводаПричиныИзмененияДаты", ЭтаФорма);
		ПоказатьВводСтроки(ОписаниеОповещения, "", "Причина изменения даты", 250, Истина);
	   	
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеВводаПричиныИзмененияДаты(ПричинаИзменения, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ПричинаИзменения) Тогда
	
		НС = Объект.ПричинаИзменени.Добавить();
		НС.Дата 			= ТекущаяДата();
		НС.ДатаПредыдущая	= НачальноеЗначениеПлановойДаты;
		НС.ПричинаИзменения = ПричинаИзменения;
		НС.Автор 			= омСотрудники.ПолучитьТекущегоПользователя();
		
		НачальноеЗначениеПлановойДаты = Объект.ДатаВыездаПлан;	
		
	Иначе
		
		Объект.ДатаВыездаПлан = НачальноеЗначениеПлановойДаты;
		ПоказатьПредупреждение(, "Не указана причина переноса даты.");
	
	КонецЕсли;

КонецПроцедуры // ПослеВводаПричиныИзмененияДаты(ПричинаИзменения, ДополнительныеПараметры)


&НаКлиенте
Процедура ПоказатьИсториюИзмененияДатыПлановой(Команда)
	ОткрытьФорму("ОбщаяФорма.ИсторияИзмененияДатыВыезда", Новый Структура("ДокументВыезда", Объект.Ссылка));
КонецПроцедуры


#КонецОбласти


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НачальноеЗначениеПлановойДаты = Объект.ДатаВыездаПлан;
	Элементы.ПоказатьИсториюИзмененияДатыПлановой.Видимость = Объект.ПричинаИзменени.Количество() > 0;
	
КонецПроцедуры


