
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтаФорма.Объект = Параметры.ИзделиеЭлемент.Объект;
	ЭтаФорма.Единица = Параметры.Номенклатура.ЕдиницаИзмерения;
	ЭтаФорма.Номенклатура = Параметры.Номенклатура;
	ОбновитьЦены();
КонецПроцедуры

&НаКлиенте
Процедура ОбъектПриИзменении(Элемент)
	ОбновитьЦены();
КонецПроцедуры


&НаСервере
Процедура ОбновитьЦены()
	Таб = ЭтаФорма.ТаблицаВывода;
	
	Таб.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Объект) Тогда
	
		Возврат;	
	
	КонецЕсли;  
	
	Макет = Документы.ПредварительнаяСмета.ПолучитьМакет("МакетВыборЦены");
	
	Таб.Вывести(Макет.ПолучитьОбласть("Шапка"));
	
	текОбласть = Макет.ПолучитьОбласть("Строка");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦеныПредварительнойСметы.Цена КАК Цена,
		|	ЦеныПредварительнойСметы.ПредварительнаяСмета.Изделие КАК ИзделиеЭлемент
		|ИЗ
		|	РегистрСведений.ЦеныПредварительнойСметы КАК ЦеныПредварительнойСметы
		|ГДЕ
		|	ЦеныПредварительнойСметы.Объект = &Объект
		|	И ЦеныПредварительнойСметы.ПредварительнаяНоменклатура = &ПредварительнаяНоменклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЦеныПредварительнойСметы.ПредварительнаяСмета.Изделие.Наименование";
	
	Запрос.УстановитьПараметр("Объект", ЭтаФорма.Объект);
	Запрос.УстановитьПараметр("ПредварительнаяНоменклатура", ЭтаФорма.Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		текОбласть.Параметры.Заполнить(Выборка);
		Таб.Вывести(текОбласть);
	КонецЦикла;
	

КонецПроцедуры // ОбновитьЦены()

&НаКлиенте
Процедура ТаблицаВыводаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	Если ТипЗнч(Расшифровка) = Тип("Число") Тогда
		ЭтаФорма.Закрыть(Расшифровка);  
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры
