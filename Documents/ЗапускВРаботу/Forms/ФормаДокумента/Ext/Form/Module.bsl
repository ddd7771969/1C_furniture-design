&НаКлиенте
Перем мИзменений, мИзмененийМатериалы, ЕстьУдалениеКомплекта, СтруктураСдвигНормаДни, МассивДанныехДляВыгрузки, ИндексВыгрузки, ВсеОбъектыВыгружены;

&НаКлиенте
Перем ВремяЗадержкиПриВыгрузке, мСтруктурМатериалыПроектаНаКлиенте;

#Область СобытияФормы

Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьНачальноеЗначениеТЧ();
	ДобавитьКолонкиМатериалы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВремяЗадержкиПриВыгрузке 	= 65;
	ЕстьУдалениеКомплекта 		= Ложь;
	СтруктураСдвигНормаДни 		= ПолучитьДанныеСдвигНормаДни();
	КодПроекта 					= ПолучитьКодПроекта(Объект.Проект);
	
	Если Параметры.Ключ.Пустая() Тогда
	
		ВсеОбъектыВыгружены		= Ложь;
	
	Иначе
	
		ВсеОбъектыВыгружены		= КонтрольВыгрузкиВPM();
	
	КонецЕсли; 
	
	Если НЕ Объект.ДокументЗакрыт = ВсеОбъектыВыгружены Тогда
	
		Объект.ДокументЗакрыт = ВсеОбъектыВыгружены;	
	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		
		Элементы.ПриложениеКДоговору.ТолькоПросмотр = Истина;
		Если НЕ ЗначениеЗаполнено(Объект.ПриложениеКДоговору) Тогда
			Объект.ПриложениеКДоговору = ПолучитьПриложениеКДоговору(Объект.Основание, Объект.Проект);
		КонецЕсли;
			
	Иначе
		Элементы.Основание.Видимость = Ложь;
	КонецЕсли;
	ДоступностьКнопкиСоздатьИВыгрузить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если мИзменений.Количество() > 0 Тогда 
		
		ДатаНачала = НачалоДня(?(Объект.Дата = Дата(1, 1, 1), ТекущаяДата(), Объект.Дата)); 
		
		ЗаписатьВРегистрНаСервере(мИзменений, Объект.Проект, ДатаНачала);
		
	КонецЕсли;
	
	Если мИзмененийМатериалы.Количество() > 0 Тогда 
		
		ЗаписатьВМатериалыКомплектаНаСервере(мИзмененийМатериалы, Объект.Ссылка, мСтруктурМатериалыПроектаНаКлиенте);
		
	КонецЕсли;
	
	Если ЕстьУдалениеКомплекта Тогда
		мКомплекты = Новый Массив;
		Для каждого х Из Объект.Комплекты Цикл
			мКомплекты.Добавить(х.Комплект);
		КонецЦикла;
		ПроверитьУдалениеКомплекта(мКомплекты, Объект.Проект);
	КонецЕсли; 
	
	ЗаполнитьНачальноеЗначениеТЧ();
	ДобавитьКолонкиМатериалы();
	
	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		
		Оповестить("ИзменениеЗамеров", Объект.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	омЗаполнениеРеквизиов.ЗаполнитьРеквизитыСтандартные(Объект);
	УстановитьФлагиПодписи();
	
	Если НЕ (РольДоступна("АдминистраторСистемы") И РольДоступна("ПолныеПрава")) Тогда
		
		Элементы.КомплектыЗаполнитьКомплекты.Видимость 		= Ложь;
		Элементы.КомплектыСортировать.Видимость 			= Ложь;
		Элементы.ФормаВыгрузитьВPDM.Видимость 				= Ложь;
		Элементы.ФормаСоздатьЗадачиПланирования.Видимость 	= Ложь;
		
		Если НЕ РольДоступна("ГлавныйКонструктор") Тогда
			Элементы.ВедущийКонструкторПодписать.Доступность = Ложь;
		КонецЕсли; 
		
		Если НЕ РольДоступна("Дизайнер") Тогда
			Элементы.ДизайнерПодписать.Доступность = Ложь;
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СобытиеФормыПродолжение

	
#КонецОбласти

&НаСервереБезКонтекста
Функция ПолучитьПриложениеКДоговору(Основание, Проект)
	
	Проект = Основание.Проект;
	
	Возврат Основание.ПриложениеКДоговору;	

КонецФункции // ПолучитьПриложениеКДоговору(Объект.Основание)()


#Область СобытиеРеквизитов

&НаКлиенте
Процедура ВедущийКонструкторПодписатьПриИзменении(Элемент)
	
	Если ЭтаФорма.ВедущийКонструкторПодписать Тогда
		Объект.ВедущийКонструкторДатаПодписи 	= ТекущаяДата();
		Объект.ВедущийКонструкторПодписьАвтор 	= ПолучитьТекущегоПользователя();
	Иначе	
		Объект.ВедущийКонструкторДатаПодписи 	= Дата(1,1,1);
		Объект.ВедущийКонструкторПодписьАвтор	= ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	КонецЕсли;
	
	ДоступностьКнопкиСоздатьИВыгрузить();  
	
КонецПроцедуры

&НаКлиенте
Процедура ДизайнерПодписатьПриИзменении(Элемент)

	Если ЭтаФорма.ДизайнерПодписать Тогда
		Объект.ДизайнерДатаПодписи 	= ТекущаяДата();
		Объект.ДизайнерПодписьАвтор = ПолучитьТекущегоПользователя();
	Иначе	
		Объект.ДизайнерДатаПодписи 	= Дата(1,1,1);
		Объект.ДизайнерПодписьАвтор	= ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	КонецЕсли;
	
	ДоступностьКнопкиСоздатьИВыгрузить();
	
КонецПроцедуры

 &НаКлиенте
Процедура ПриложениеКДоговоруПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.ПриложениеКДоговору) Тогда
		Объект.Проект = ПолучитьПроектПоПриложению(Объект.ПриложениеКДоговору);
	Иначе
		Объект.Проект = ПредопределенноеЗначение("Справочник.Проекты.ПустаяСсылка");
	КонецЕсли;
КонецПроцедуры

 &НаКлиенте
Процедура ПриложениеКДоговоруНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	Если Объект.Комплекты.Количество() > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаОПриложениеКДоговору", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещения, "При смене приложения к договору табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет,,,"Приложение к договору");		
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомплектыТипКомплектаПриИзменении(Элемент)
	ТД = ПриИзменениеДатыТЧ();
КонецПроцедуры

&НаКлиенте
Процедура КомплектыКатегорияКонструктораПриИзменении(Элемент)
	ТД = ПриИзменениеДатыТЧ();
КонецПроцедуры

&НаКлиенте
Процедура КомплектыГОСТПриИзменении(Элемент)
	ТД = ПриИзменениеДатыТЧ();
КонецПроцедуры

#КонецОбласти 

#Область СобытиеРеквизитовПродолжение

&НаСервереБезКонтекста
Функция ПолучитьПроектПоПриложению(ПриложениеКДоговору)

	Возврат ПриложениеКДоговору.Проект;	

КонецФункции // ПолучитьПроектПоПриложению(ПриложениеКДоговору)
 

&НаКлиенте
Процедура ПослеВопросаОПриложениеКДоговору(Результат, ДополнительныйПараметр) Экспорт
	Если Результат = КодВозвратаДиалога.ДА Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораПриложенияКДоговорам",	ЭтаФорма);
		
		ОткрытьФорму("Справочник.ПриложенияКДоговорам.ФормаВыбора",,ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПриложенияКДоговорам(Значение, ДополнительныйПараметр) Экспорт
	
    Если Значение = Неопределено Тогда  ///Если ничего не выбрать - вернется пустое значение (Неопределено)
        Возврат;
	КонецЕсли;
	
	Объект.ПриложениеКДоговору = Значение;
	Объект.Комплекты.Очистить(); 
	Объект.Проект = ПолучитьПроектПоПриложению(Объект.ПриложениеКДоговору);
	
	Если ЗначениеЗаполнено(Объект.Проект) Тогда
	
		ДобавитьКолонкиМатериалы();	
	
	КонецЕсли;
	
КонецПроцедуры

	

#КонецОбласти 

#Область СобытияТЧ

&НаКлиенте
Процедура КомплектыПередУдалением(Элемент, Отказ)
	ЕстьУдалениеКомплекта = Истина;
КонецПроцедуры

Функция ЗаполнитьНачальноеЗначениеТЧ(мКомплекты = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК Комплект
		|ПОМЕСТИТЬ втКомплекты
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|ГДЕ
		|	ИзделияКомплект.Ссылка В(&мКомплекты)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ДатыКомплекта.КрайПКД, ДАТАВРЕМЯ(1, 1, 1)) КАК КрайПКД,
		|	ЕСТЬNULL(ДатыКомплекта.КрайРКД, ДАТАВРЕМЯ(1, 1, 1)) КАК КрайРКД,
		|	ЕСТЬNULL(ДатыКомплекта.ДнейПКД, ДАТАВРЕМЯ(1, 1, 1)) КАК ДнейПКД,
		|	ЕСТЬNULL(ДатыКомплекта.ДнейРКД, ДАТАВРЕМЯ(1, 1, 1)) КАК ДнейРКД,
		|	ЕСТЬNULL(ДатыКомплекта.НормаВремени_ПКД, ДАТАВРЕМЯ(1, 1, 1)) КАК НормаВремени_ПКД,
		|	ЕСТЬNULL(ДатыКомплекта.НормаВремени_РКД, ДАТАВРЕМЯ(1, 1, 1)) КАК НормаВремени_РКД,
		|	ЕСТЬNULL(ДатыКомплекта.НормаДоработки_ПКД, ДАТАВРЕМЯ(1, 1, 1)) КАК НормаДоработки_ПКД,
		|	ЕСТЬNULL(ДатыКомплекта.НормаДоработки_РКД, ДАТАВРЕМЯ(1, 1, 1)) КАК НормаДоработки_РКД,
		|	ЛОЖЬ КАК Изменен,
		|	ДатыКомплекта.БезЗамера КАК БезЗамера,
		|	ВЫБОР
		|		КОГДА втКомплекты.Комплект.Родитель.Композиция.ГабаритныйЧертеж ЕСТЬ NULL
		|			ТОГДА втКомплекты.Комплект.ГабаритныйЧертеж
		|		ИНАЧЕ втКомплекты.Комплект.Родитель.Композиция.ГабаритныйЧертеж
		|	КОНЕЦ КАК ГабаритныйЧертеж,
		|	Композиции.Ссылка КАК Композиция,
		|	ДатыКомплекта.БезГабаритногоЧертежа КАК БезГабаритногоЧертежа,
		|	втКомплекты.Комплект КАК Комплект,
		|	ЕСТЬNULL(ДатыКомплекта.ГОСТ, ЛОЖЬ) КАК ГОСТ,
		|	ЕСТЬNULL(ДатыКомплекта.КатегорияКонструктора, 0) КАК КатегорияКонструктора,
		|	ЕСТЬNULL(ДатыКомплекта.ТипКомплекта, ЗНАЧЕНИЕ(Справочник.ТипыКомплектов.ПустаяСсылка)) КАК ТипКомплекта
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|		ПРАВОЕ СОЕДИНЕНИЕ втКомплекты КАК втКомплекты
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Композиции КАК Композиции
		|			ПО втКомплекты.Комплект.Родитель.Композиция = Композиции.Ссылка
		|		ПО (втКомплекты.Комплект = ДатыКомплекта.Комплект)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.КрайПКД КАК КрайПКД,
		|	вт.КрайРКД КАК КрайРКД,
		|	вт.ДнейПКД КАК ДнейПКД,
		|	вт.ДнейРКД КАК ДнейРКД,
		|	вт.НормаВремени_ПКД КАК НормаВремени_ПКД,
		|	вт.НормаВремени_РКД КАК НормаВремени_РКД,
		|	вт.Изменен КАК Изменен,
		|	вт.БезЗамера КАК БезЗамера,
		|	вт.Композиция КАК Композиция,
		|	вт.БезГабаритногоЧертежа КАК БезГабаритногоЧертежа,
		|	вт.Комплект КАК Комплект,
		|	вт.ГабаритныйЧертеж КАК ГабаритныйЧертеж,
		|	вт.НормаДоработки_ПКД КАК НормаДоработки_ПКД,
		|	вт.НормаДоработки_РКД КАК НормаДоработки_РКД,
		|	вт.ГОСТ КАК ГОСТ,
		|	вт.КатегорияКонструктора КАК КатегорияКонструктора,
		|	вт.ТипКомплекта КАК ТипКомплекта
		|ИЗ
		|	вт КАК вт";
		
	
	Запрос.УстановитьПараметр("мКомплекты", ?(мКомплекты = Неопределено, Объект.Комплекты.Выгрузить().ВыгрузитьКолонку("Комплект"), мКомплекты)); 
	
	Выборка = Запрос.Выполнить().Выбрать();
	стОтбор = Новый Структура("Комплект", );
	
	Пока Выборка.Следующий() Цикл
		
		стОтбор.Комплект = Выборка.Комплект;
		
		НайденыеСтроки = Объект.Комплекты.НайтиСтроки(стОтбор);
		
		Для каждого х Из НайденыеСтроки Цикл
			
			ЗаполнитьЗначенияСвойств(х, Выборка);
			
		КонецЦикла;
		
	КонецЦикла;  
	
	
КонецФункции // ЗаполнитьНачальноеЗначениеТЧ()
	
Функция ПолучитьДанныеКомплекта(Комплект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплектаПроизводство.Комплект КАК КомплектИзделиеКомплект,
		|	МИНИМУМ(ДатыКомплектаПроизводство.НачалоПроизводства) КАК НачалоПроизводства
		|ПОМЕСТИТЬ втНачало
		|ИЗ
		|	РегистрСведений.ДатыКомплектаПроизводство КАК ДатыКомплектаПроизводство
		|ГДЕ
		|	ДатыКомплектаПроизводство.Комплект В(&Комплект)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыКомплектаПроизводство.Комплект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ДатыКомплекта.ДнейПКД, ДАТАВРЕМЯ(1, 1, 1)) КАК ДнейПКД,
		|	ЕСТЬNULL(ДатыКомплекта.ДнейРКД, ДАТАВРЕМЯ(1, 1, 1)) КАК ДнейРКД,
		|	ЕСТЬNULL(ДатыКомплекта.НормаВремени_ПКД, ДАТАВРЕМЯ(1, 1, 1)) КАК НормаВремени_ПКД,
		|	ЕСТЬNULL(ДатыКомплекта.НормаВремени_РКД, ДАТАВРЕМЯ(1, 1, 1)) КАК НормаВремени_РКД,
		|	ЕСТЬNULL(ДатыКомплекта.НормаДоработки_ПКД, ДАТАВРЕМЯ(1, 1, 1)) КАК НормаДоработки_ПКД,
		|	ЕСТЬNULL(ДатыКомплекта.НормаДоработки_РКД, ДАТАВРЕМЯ(1, 1, 1)) КАК НормаДоработки_РКД,
		|	ЛОЖЬ КАК Изменен,
		|	ЕСТЬNULL(втНачало.НачалоПроизводства, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоПроизводства,
		|	ИзделияКомплект.Ссылка КАК Комплект,
		|	ВЫБОР
		|		КОГДА ИзделияКомплект.Родитель.ГабаритныйЧертеж = ЗНАЧЕНИЕ(Справочник.ГабаритныеЧертежи.ПустаяСсылка)
		|				ИЛИ ИзделияКомплект.Родитель.ГабаритныйЧертеж ЕСТЬ NULL
		|			ТОГДА ИзделияКомплект.ГабаритныйЧертеж
		|		ИНАЧЕ ИзделияКомплект.Родитель.ГабаритныйЧертеж
		|	КОНЕЦ КАК ГабаритныйЧертеж,
		|	ИзделияКомплект.Родитель.Композиция КАК Композиция,
		|	ЕСТЬNULL(ДатыКомплекта.ГОСТ, ЛОЖЬ) КАК ГОСТ,
		|	ЕСТЬNULL(ДатыКомплекта.КатегорияКонструктора, 0) КАК КатегорияКонструктора,
		|	ЕСТЬNULL(ДатыКомплекта.ТипКомплекта, ЗНАЧЕНИЕ(Справочник.ТипыКомплектов.ПустаяСсылка)) КАК ТипКомплекта
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|			ЛЕВОЕ СОЕДИНЕНИЕ втНачало КАК втНачало
		|			ПО ДатыКомплекта.Комплект = втНачало.КомплектИзделиеКомплект
		|		ПО (ДатыКомплекта.Комплект = ИзделияКомплект.Ссылка)
		|ГДЕ
		|	ИзделияКомплект.Ссылка = &Комплект
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатыКомплекта.Комплект.Наименование";
		
		
	Запрос.УстановитьПараметр("Комплект", Комплект);
	тзДанныеКомплека = Запрос.Выполнить().Выгрузить();
	
	ПустаяДата = Дата(1,1,1);
	Если тзДанныеКомплека.Количество() = 0 Тогда
		стрВозврата = Новый Структура("Комплект, ДнейПКД, ДнейРКД, НормаВремени_ПКД, НормаДоработки_ПКД, НормаВремени_РКД, НормаДоработки_РКД, ГабаритныйЧертеж, Композиция, Изменен, НачалоПроизводства, ГОСТ, КатегорияКонструктора, ТипКомплекта"
		,Комплект,
		,0
		,0
		,0
		,0
		,0
		,0
		,Неопределено
		,Ложь
		,Неопределено
		,ПустаяДата
		,Ложь
		,0
		,Неопределено);
		
		мВозврата = Новый Массив(1);
		мВозврата[0] = стрВозврата;
		
		Возврат мВозврата;
	Иначе
		Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(тзДанныеКомплека);
	КонецЕсли;
КонецФункции // ЗаполнитьНачальноеЗначениеТЧ()

&НаКлиенте
Процедура КомплектыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Копирование;
	
	Если НЕ ЗначениеЗаполнено(Объект.Проект) Тогда
	    ПоказатьПредупреждение(,"Не заполнена Проект."); 
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКомплекта(Значение, ДополнительныйПараметр) Экспорт

    Если Значение = Неопределено Тогда  
        Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДополнительныйПараметр.ТекущиеДанные, ПолучитьДанныеКомплекта(Значение)[0]);

	
КонецПроцедуры

#Область ПКД

&НаКлиенте
Процедура КомплектыДнейПКДПриИзменении(Элемент)
	ТД = ПриИзменениеДатыТЧ();
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомплектыНормаВремени_ПКДПриИзменении(Элемент)
	ТД = ПриИзменениеДатыТЧ(); 

	ТД.НормаВремени_РКД = ТД.НормаВремени_ПКД;
	ТД.ДнейПКД 			= ТД.НормаВремени_ПКД * (1 + СтруктураСдвигНормаДни.ПлюсДнейПКД / 100);
	ТД.ДнейРКД 			= ТД.НормаВремени_ПКД * (1 + СтруктураСдвигНормаДни.ПлюсДнейРКД / 100);
	
КонецПроцедуры

#КонецОбласти

#Область РКД

&НаКлиенте
Процедура КомплектыДнейРКДПриИзменении(Элемент)
	
	ТД = ПриИзменениеДатыТЧ();
	
КонецПроцедуры

&НаКлиенте
Процедура КомплектыНормаВремени_РКДПриИзменении(Элемент)
	ТД = ПриИзменениеДатыТЧ();
КонецПроцедуры

&НаКлиенте
Процедура КомплектыНормаДоработки_ПКДПриИзменении(Элемент)
	ТД = ПриИзменениеДатыТЧ();
КонецПроцедуры

&НаКлиенте
Процедура КомплектыНормаДоработки_РКДПриИзменении(Элемент)
	ТД = ПриИзменениеДатыТЧ();
КонецПроцедуры


#КонецОбласти


&НаКлиенте
Процедура КомплектыНормаВремени_ТЗПриИзменении(Элемент)
	ПриИзменениеДатыТЧ();
	ТД = Элементы.Комплекты.ТекущиеДанные;
КонецПроцедуры

#КонецОбласти 


#Область СобытияТЧПродолжение

&НаКлиенте
Функция ПриИзменениеДатыТЧ()

	ТД = Элементы.Комплекты.ТекущиеДанные;
	
	Если ТД = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТД.Изменен = Истина;
	
	Возврат ТД
КонецФункции // ПриИзменениеДатыТЧ()


#КонецОбласти 


#Область Команды

&НаКлиенте
Процедура ЗаполнитьКомплекты(Команда)
	Если ЗначениеЗаполнено(Объект.ПриложениеКДоговору) Тогда
		ЗаполнитьКомплектыНаСервере();
	Иначе	
	    ПоказатьПредупреждение(,"Не заполнено приложение к договору.");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВPDM(Команда) 
	
	Оповещение = Новый ОписаниеОповещения("ВыгрузитьВPDMПослеВопроса", ЭтаФорма);
	
	ПоказатьВопрос(Оповещение, "Если продолжить, то будут созданны данные в PDM и документ заблокирован. Продолжить?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры 

&НаКлиенте
Процедура СоздатьКомпозицииИГЧ1(Команда)

	СоздатьКомпозицииИГЧ();
	
КонецПроцедуры


#КонецОбласти

#Область КомандыПродолжение

Процедура ЗаполнитьКомплектыНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КомплектыЗапускВРаботу.Комплект КАК Комплект
		|ПОМЕСТИТЬ втСуществуют
		|ИЗ
		|	Документ.ЗапускВРаботу.Комплекты КАК КомплектыЗапускВРаботу
		|ГДЕ
		|	КомплектыЗапускВРаботу.Ссылка.Проект = &Проект
		|	И КомплектыЗапускВРаботу.Ссылка <> &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзделияКомплектИзделияЭлемент.Ссылка КАК Комплект
		|ПОМЕСТИТЬ втКомплект
		|ИЗ
		|	Справочник.ПриложенияКДоговорам.ИзделияЗаказчика КАК ПриложенияКДоговорамИзделияЗаказчика
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИзделияЗаказчика.ИзделияЭлемент КАК ИзделияЗаказчикаИзделияЭлемент
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИзделияКомплект.ИзделияЭлемент КАК ИзделияКомплектИзделияЭлемент
		|			ПО ИзделияЗаказчикаИзделияЭлемент.ИзделиеЭлемент = ИзделияКомплектИзделияЭлемент.ИзделиеЭлемент
		|		ПО ПриложенияКДоговорамИзделияЗаказчика.ИзделиеЗаказчика = ИзделияЗаказчикаИзделияЭлемент.Ссылка
		|ГДЕ
		|	ПриложенияКДоговорамИзделияЗаказчика.Ссылка = &ПриложениеКДоговору
		|	И НЕ ИзделияКомплектИзделияЭлемент.Ссылка В (&Комплект)
		|
		|СГРУППИРОВАТЬ ПО
		|	ИзделияКомплектИзделияЭлемент.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.Софт.SW) КАК СофтПКД,
		|	ЗНАЧЕНИЕ(Перечисление.Софт.SW) КАК СофтРКД,
		|	ЛОЖЬ КАК БезЗамера,
		|	ЛОЖЬ КАК БезГабаритногоЧертежа,
		|	втКомплект.Комплект.Родитель.Композиция КАК Композиция,
		|	втКомплект.Комплект КАК Комплект
		|ИЗ
		|	втКомплект КАК втКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ втСуществуют КАК втСуществуют
		|		ПО втКомплект.Комплект = втСуществуют.Комплект
		|ГДЕ
		|	втСуществуют.Комплект ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ПриложениеКДоговору", Объект.ПриложениеКДоговору);
	Запрос.УстановитьПараметр("Проект", Объект.Проект);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Комплект", Объект.Комплекты.Выгрузить().ВыгрузитьКолонку("Комплект"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	мКомплекты = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Объект.Комплекты.Добавить(), Выборка);
		мКомплекты.Добавить(Выборка.Комплект);
	КонецЦикла; 
	
	ЗаполнитьНачальноеЗначениеТЧ(мКомплекты);
	
КонецПроцедуры // ЗаполнитьКомплектыНаСервере()

&НаКлиенте
Процедура ВыгрузитьВPDMПослеВопроса(Результат, ДопПараметры) Экспорт 
	
	Если НЕ Результат = КодВозвратаДиалога.Да Тогда
	
		Возврат;	
	
	КонецЕсли;
	
	мКомпозиции = Новый Массив;
	мГЧ 		= Новый Массив;
	мКомплекты 	= Новый Массив;
	
	Для каждого х Из Объект.Комплекты Цикл
		
		Если мКомпозиции.Найти(х.Композиция) = Неопределено И ЗначениеЗаполнено(х.Композиция) Тогда
			мКомпозиции.Добавить(х.Композиция);
		КонецЕсли;
		
		Если мГЧ.Найти(х.ГабаритныйЧертеж) = Неопределено И ЗначениеЗаполнено(х.ГабаритныйЧертеж) И НЕ х.БезГабаритногоЧертежа Тогда
			мГЧ.Добавить(х.ГабаритныйЧертеж);
		КонецЕсли;
		
		Если мКомплекты.Найти(х.Комплект) = Неопределено И ЗначениеЗаполнено(х.Комплект) Тогда
			мКомплекты.Добавить(х.Комплект);
		КонецЕсли;
		
	КонецЦикла;
	
	ПолучитьНеВыгруженные(мКомпозиции, мГЧ, мКомплекты);
	
	МаксимальныйИндексГЧ 			= мГЧ.Количество();
	МаксимальныйИндексКомплекты 	= мКомплекты.Количество();
	МаксимальныйИндексКомпозиции 	= мКомпозиции.Количество(); 
	
	ИндексВыгрузки = 0;
    МассивДанныехДляВыгрузки = Новый Массив;
	
	МаксимальныйИндекс = Макс(МаксимальныйИндексГЧ, МаксимальныйИндексКомплекты, МаксимальныйИндексКомпозиции);
	
	Для х = 0 По МаксимальныйИндекс Цикл
		
		стМассивов = Новый Структура();
		
		Если х < МаксимальныйИндексГЧ Тогда
			стМассивов.Вставить("ГабаритныйЧертеж", мГЧ[х]);
		КонецЕсли;
		
		Если х < МаксимальныйИндексКомплекты Тогда
			стМассивов.Вставить("Комплект", мКомплекты[х]);
		КонецЕсли;
		
		Если х < МаксимальныйИндексКомпозиции Тогда
			стМассивов.Вставить("Композиция", мКомпозиции[х]);
		КонецЕсли;
		
		
		МассивДанныехДляВыгрузки.Добавить(стМассивов);
		
	КонецЦикла; 
	
	Если МаксимальныйИндекс > 0 Тогда
		ВыгрузкаВPDMПоМаскеКомплект();	
	КонецЕсли;
	
	Объект.ДокументЗакрыт = КонтрольВыгрузкиВPM();
	
КонецПроцедуры

#КонецОбласти 

#Область РеквизитыМатериалы

Функция ПолучитьПрефиксКолонок()
	
	Возврат "___";
	
КонецФункции // ПолучитьПрефиксКолонок()

Процедура ДобавитьКолонкиМатериалы()
	
	стДанныеМатериалов = ПолучитьДанныеМатериалов();  //тзМатериалыПоКомплектам, (тзМатериалыПроекта [Материал, НаименованиеДляРазработчиков, НаименованиеВТабличнойЧасти, Реквизит])
	
	тзМатериалыПроекта = стДанныеМатериалов.тзМатериалыПроекта;
	
	мОставленныеКолонки = УдалитьЛишниеКолонки(тзМатериалыПроекта);
	
	МассивРеквизитов = Новый Массив;
	ТипБулево = Новый ОписаниеТипов("Булево");
	
	Для каждого НоваяКолонка из тзМатериалыПроекта Цикл
		
		ИмяКолонки =  НоваяКолонка.НаименованиеДляРазработчиков;
		
		Если мОставленныеКолонки.Найти(ИмяКолонки) = Неопределено Тогда
		
			НовыйРеквизит = Новый РеквизитФормы(ИмяКолонки, ТипБулево, "Объект.Комплекты");
			МассивРеквизитов.Добавить(НовыйРеквизит);
			НоваяКолонка.Реквизит 						= НовыйРеквизит;
			НоваяКолонка.НаименованиеДляРазработчиков 	= ИмяКолонки;

		КонецЕсли;
		
    КонецЦикла;
    
    ИзменитьРеквизиты(МассивРеквизитов);
	
	Для каждого НоваяРеквизит из тзМатериалыПроекта Цикл
		
		Если НоваяРеквизит.Реквизит = Неопределено Тогда
		
			Продолжить;	
		
		КонецЕсли;
		
		НоваяКолонка 				= Элементы.Добавить(НоваяРеквизит.Реквизит.Имя, Тип("ПолеФормы"), Элементы.КомплектыГруппаМатериалы);
		НоваяКолонка.Вид 			= ВидПоляФормы.ПолеФлажка;
		НоваяКолонка.ПутьКДанным 	= НоваяРеквизит.Реквизит.Путь + "." + НоваяРеквизит.Реквизит.Имя;
		НоваяКолонка.Заголовок		= НоваяРеквизит.НаименованиеВТабличнойЧасти;
		
		НоваяКолонка.УстановитьДействие("ПриИзменении", "ПриИзменениеМатериала");
		
	КонецЦикла;
	
	стОтбор = Новый Структура("Комплект", );
	тзМатериалыПоКомплектам = стДанныеМатериалов.тзМатериалыПоКомплектам;
	
	Для каждого текСтрока Из Объект.Комплекты Цикл
		стОтбор.Комплект = текСтрока.Комплект;
		
		Для каждого х Из тзМатериалыПоКомплектам.НайтиСтроки(стОтбор) Цикл
			
			НайденаяСтрока = тзМатериалыПроекта.Найти(х.Материал, "Материал");
			
			Если НайденаяСтрока = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			текСтрока[НайденаяСтрока.НаименованиеДляРазработчиков] = Истина;
			
		КонецЦикла;
	
	КонецЦикла;
	
КонецПроцедуры 

Функция УдалитьЛишниеКолонки(тзМатериалыПроекта) //(тзМатериалыПроекта [Материал, НаименованиеДляРазработчиков, НаименованиеВТабличнойЧасти])

	
	ПерифксКолонки 			= ПолучитьПрефиксКолонок();
	ДлинаПрефикса 			= СтрДлина(ПерифксКолонки);
	НачалоСимволаДляПоиска 	= ДлинаПрефикса + 1;
	мРеквизитыДляУдаления 	= Новый Массив;
	мЭлементыДляУдаления 	= Новый Массив;
	мОставленныеКолонки 	= Новый Массив;
	
	Для каждого ТекущийЭлемент Из Элементы Цикл 
		
		Если НЕ ТипЗнч(ТекущийЭлемент) = Тип("ПолеФормы") Тогда
			Продолжить;	
		КонецЕсли;
	
		Если Лев(ТекущийЭлемент.Имя, ДлинаПрефикса) = ПерифксКолонки Тогда
		
			НайденаяСтрока = тзМатериалыПроекта.Найти(ТекущийЭлемент.Имя, "НаименованиеДляРазработчиков");	
			
			Если НайденаяСтрока = Неопределено Тогда
			
				мРеквизитыДляУдаления.Добавить(ТекущийЭлемент.ПутьКДанным);
			    мЭлементыДляУдаления.Добавить(ТекущийЭлемент);
				
			Иначе
				мОставленныеКолонки.Добавить(ТекущийЭлемент.Имя);
			КонецЕсли;
		
		КонецЕсли;	
	
	КонецЦикла;
	
	Для каждого х Из мЭлементыДляУдаления Цикл
		Элементы.Удалить(х);	
	КонецЦикла;
	
	Если мРеквизитыДляУдаления.Количество() > 0 Тогда
		ИзменитьРеквизиты(, мРеквизитыДляУдаления);	
	КонецЕсли;
	
	Возврат мОставленныеКолонки;
	
КонецФункции //УдалитьЛишниеКолонки

Функция ПолучитьДанныеМатериалов(ТолькоТаблицуКолонок = Ложь)
	
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МатериалыКомплекта.Комплект КАК Комплект,
		|	МатериалыКомплекта.Материал КАК Материал
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрСведений.МатериалыКомплекта КАК МатериалыКомплекта
		|ГДЕ
		|	МатериалыКомплекта.ЗапускВРаботу = &ЗапускВРаботу
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.Комплект КАК Комплект,
		|	вт.Материал КАК Материал
		|ИЗ
		|	вт КАК вт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА вт.Материал ЕСТЬ NULL
		|			ТОГДА ПроектыМатериалыПроекта.Материал
		|		ИНАЧЕ вт.Материал
		|	КОНЕЦ КАК Материал,
		|	&ПрефиксКолонок + ВЫБОР
		|		КОГДА вт.Материал ЕСТЬ NULL
		|			ТОГДА ПроектыМатериалыПроекта.Материал.НаименованиеДляРазработчиков
		|		ИНАЧЕ вт.Материал.НаименованиеДляРазработчиков
		|	КОНЕЦ КАК НаименованиеДляРазработчиков,
		|	ВЫБОР
		|		КОГДА вт.Материал ЕСТЬ NULL
		|			ТОГДА ПроектыМатериалыПроекта.Материал.НаименованиеВТабличнойЧасти
		|		ИНАЧЕ вт.Материал.НаименованиеВТабличнойЧасти
		|	КОНЕЦ КАК НаименованиеВТабличнойЧасти,
		|	НЕОПРЕДЕЛЕНО КАК Реквизит
		|ИЗ
		|	Справочник.Проекты.МатериалыПроекта КАК ПроектыМатериалыПроекта
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт КАК вт
		|		ПО ПроектыМатериалыПроекта.Материал = вт.Материал
		|ГДЕ
		|	ПроектыМатериалыПроекта.Ссылка = &Проект
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА вт.Материал ЕСТЬ NULL
		|			ТОГДА ПроектыМатериалыПроекта.Материал
		|		ИНАЧЕ вт.Материал
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА вт.Материал ЕСТЬ NULL
		|			ТОГДА ПроектыМатериалыПроекта.Материал.НаименованиеВТабличнойЧасти
		|		ИНАЧЕ вт.Материал.НаименованиеВТабличнойЧасти
		|	КОНЕЦ,
		|	&ПрефиксКолонок + ВЫБОР
		|		КОГДА вт.Материал ЕСТЬ NULL
		|			ТОГДА ПроектыМатериалыПроекта.Материал.НаименованиеДляРазработчиков
		|		ИНАЧЕ вт.Материал.НаименованиеДляРазработчиков
		|	КОНЕЦ";	
		
	Запрос.УстановитьПараметр("ЗапускВРаботу", 	Объект.Ссылка);
	Запрос.УстановитьПараметр("Проект", 		Объект.Проект);
	Запрос.УстановитьПараметр("ПрефиксКолонок", ПолучитьПрефиксКолонок());
	
	Результат = Запрос.ВыполнитьПакет();
	
	тзМатериалыПроекта =  Результат[2].Выгрузить();

	Если ТолькоТаблицуКолонок Тогда
	
		Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(тзМатериалыПроекта);
	
	КонецЕсли; 
	
	тзМатериалыПоКомплектам = Результат[1].Выгрузить();
	тзМатериалыПоКомплектам.Индексы.Добавить("Комплект");
	
	Возврат Новый Структура("тзМатериалыПоКомплектам, тзМатериалыПроекта", тзМатериалыПоКомплектам, тзМатериалыПроекта);
	
КонецФункции // ПолучитьДанныеМатериалов()

 &НаКлиенте
Процедура ПриИзменениеМатериала()

	ТД = Элементы.Комплекты.ТекущиеДанные;
	
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТД.Изменен = Истина;
	
КонецПроцедуры // ПриИзменениеДатыТЧ()

&НаСервереБезКонтекста
Процедура ЗаписатьВМатериалыКомплектаНаСервере(мИзмененийМатериалы, ЗапускВРаботу, мСтруктурМатериалыПроектаНаКлиенте)
	
	НаборЗаписей = РегистрыСведений.МатериалыКомплекта.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ЗапускВРаботу.Установить(ЗапускВРаботу);
	
	Для каждого СтруктураДанных Из мИзмененийМатериалы Цикл
		

		Для каждого ЭлементСтруктуры Из СтруктураДанных Цикл
			
			Если ЭлементСтруктуры.Ключ = "Комплект" Тогда
				Продолжить;	
			КонецЕсли;
			
			Если НЕ ЭлементСтруктуры.Значение Тогда
				Продолжить;	
			КонецЕсли;
			
			Материал = Неопределено;
			
			Для каждого х Из мСтруктурМатериалыПроектаНаКлиенте Цикл
				
				Если х.НаименованиеДляРазработчиков = ЭлементСтруктуры.Ключ Тогда
					
					Материал = х.Материал;
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если Материал = Неопределено Тогда
				Продолжить;	
			КонецЕсли;
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.ЗапускВРаботу = ЗапускВРаботу;
			НоваяЗапись.Комплект = СтруктураДанных.Комплект;
			НоваяЗапись.Материал = Материал;
		
		КонецЦикла;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();

КонецПроцедуры // ЗаписатьВМатериалыКомплектаНаСервере(мИзмененийМатериалы, Объект.Ссылка)()

&НаКлиенте
Функция ПолучитьКолонкиМатериала()  //(тзМатериалыПроекта [Материал, НаименованиеДляРазработчиков, НаименованиеВТабличнойЧасти])
	
	ПрефиксКолонок = ПолучитьПрефиксКолонок();
	
	мСтруктурМатериалыПроектаНаКлиенте = ПолучитьДанныеМатериалов(Истина);
	
	стрНаименованиеКолонкок = "Комплект";
	Для каждого х Из мСтруктурМатериалыПроектаНаКлиенте Цикл
		стрНаименованиеКолонкок = стрНаименованиеКолонкок + "," + х.НаименованиеДляРазработчиков;	
	КонецЦикла; 
	
	Возврат стрНаименованиеКолонкок;
	
	
КонецФункции // ПолучитьКолонкиМатериала()

#КонецОбласти

&НаКлиенте
Процедура ДоступностьКнопкиСоздатьИВыгрузить()

	ДоступностьКнопки = Истина;
	
	Если НЕ ВедущийКонструкторПодписать Тогда
	
		ДоступностьКнопки = Ложь;	
	
	ИначеЕсли НЕ ДизайнерПодписать Тогда
	
		ДоступностьКнопки = Ложь;
		
	Иначе
		
		ДоступностьКнопки = НЕ ВсеОбъектыВыгружены;
	
	КонецЕсли; 
	
	Элементы.ФормаВыгрузитьВPDM.Доступность = ДоступностьКнопки;
	
	Элементы.ФормаСоздатьЗадачиПланирования.Видимость = НЕ ДоступностьКнопки И ВедущийКонструкторПодписать И ДизайнерПодписать;

КонецПроцедуры // ДоступностьКнопкиСоздатьИВыгрузить()

Процедура УстановитьФлагиПодписи()
	
	ПустаяДата = Дата(1, 1, 1);
	
	ДизайнерПодписать 			= Объект.ДизайнерДатаПодписи 			> ПустаяДата;
	ВедущийКонструкторПодписать = Объект.ВедущийКонструкторДатаПодписи 	> ПустаяДата;
	
КонецПроцедуры

&НаКлиенте
Функция КонтрольВыгрузкиВPM()

	Для каждого х Из Объект.Комплекты Цикл
	
		Если НЕ ЗначениеЗаполнено(х.ГабаритныйЧертеж) И НЕ х.БезГабаритногоЧертежа Тогда
		
			Возврат Ложь;	
		
		КонецЕсли;	
	
	КонецЦикла;

	Возврат КонтрольВыгрузкиВPMНаСервере(Объект.Ссылка);
	
КонецФункции // КонтрольВыгрузки()

&НаСервереБезКонтекста
Функция КонтрольВыгрузкиВPMНаСервере(Ссылка)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕСТЬNULL(ДанныеГабаритныйЧертеж.Код_в_PDM, 0) КАК ВыгруженВPDM
		|ИЗ
		|	Документ.ЗапускВРаботу.Комплекты КАК ЗапускВРаботуКомплекты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеГабаритныйЧертеж КАК ДанныеГабаритныйЧертеж
		|		ПО ЗапускВРаботуКомплекты.Комплект.ГабаритныйЧертеж = ДанныеГабаритныйЧертеж.ГабаритныйЧертеж
		|ГДЕ
		|	ЗапускВРаботуКомплекты.Ссылка = &Ссылка
		|	И ЗапускВРаботуКомплекты.Комплект.Композиция = ЗНАЧЕНИЕ(Справочник.Композиции.ПустаяСсылка)
		|	И ЕСТЬNULL(ДанныеГабаритныйЧертеж.Код_в_PDM, 0) = 0";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Ложь;
	КонецЦикла;
	
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕСТЬNULL(ДатыКомплекта.Код_в_PDM, 0) КАК ВыгруженВPDM
		|ИЗ
		|	Документ.ЗапускВРаботу.Комплекты КАК ЗапускВРаботуКомплекты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|		ПО ЗапускВРаботуКомплекты.Комплект.Композиция = ДатыКомплекта.Комплект
		|ГДЕ
		|	ЗапускВРаботуКомплекты.Ссылка = &Ссылка
		|	И ЕСТЬNULL(ДатыКомплекта.Код_в_PDM, 0) = 0
		|	И НЕ ЗапускВРаботуКомплекты.Комплект.Композиция = ЗНАЧЕНИЕ(Справочник.Композиции.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Ложь;
	КонецЦикла;
	
	Возврат Истина;
	
	
КонецФункции // КонтрольВыгрузкиВPM(Ссылка)

&НаСервереБезКонтекста
Процедура ЗаписатьВРегистрНаСервере(мИзменений, Проект, ДатаНачала)

	ПустаяДата 		= Дата(1, 1, 1);
	
	мКомплекты = Новый Массив;
	ДнейТЗ = 3;
	
	Для каждого х Из мИзменений Цикл
		
		ТребуетсяРасчетПКД_РКД = Ложь;
		
		МенеджерЗаписей = РегистрыСведений.ДатыКомплекта.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.Комплект = х.Комплект;
		МенеджерЗаписей.Прочитать();
		
		ЗаполнитьЗначенияСвойств(МенеджерЗаписей,х);
		МенеджерЗаписей.Проект 		= Проект;
		МенеджерЗаписей.Комплект 	= х.Комплект;
		
		Если МенеджерЗаписей.НачалоТЗ = ПустаяДата Тогда
			
			КрайТЗ = омДаты.ДобавитьДниСУчетомВыходных(ДатаНачала, ДнейТЗ);
			
			МенеджерЗаписей.ДнейТЗ 		= ДнейТЗ;	
			МенеджерЗаписей.НачалоТЗ 	= ДатаНачала;
			МенеджерЗаписей.КрайТЗ		= КрайТЗ;
			
			ТребуетсяРасчетПКД_РКД = Истина;
			
		КонецЕсли;
		
		Если ТребуетсяРасчетПКД_РКД Тогда
			
			МенеджерЗаписей.НачалоПКД 	= омДаты.ДобавитьДниСУчетомВыходных(КрайТЗ, 					1);
			МенеджерЗаписей.КрайПКД 	= омДаты.ДобавитьДниСУчетомВыходных(МенеджерЗаписей.НачалоПКД, 	МенеджерЗаписей.ДнейПКД);
			МенеджерЗаписей.НачалоРКД 	= омДаты.ДобавитьДниСУчетомВыходных(МенеджерЗаписей.КрайПКД, 	1);
			МенеджерЗаписей.КрайРКД 	= омДаты.ДобавитьДниСУчетомВыходных(МенеджерЗаписей.НачалоРКД,  МенеджерЗаписей.ДнейРКД);
			
		КонецЕсли;
		
		МенеджерЗаписей.Записать();
		
		КомплектОбъект = х.Комплект.ПолучитьОбъект(); 
		
		Если КомплектОбъект.ДополнительныеСвойства.Свойство("ЗагрузкаСБитрикс24") Тогда
			КомплектОбъект.ДополнительныеСвойства.ЗагрузкаСБитрикс24 = Ложь;
		КонецЕсли;
		
		Отказ = Ложь;
		Б24_СМ_РегистрацияИзмененийСервер.ЗарегистрироватьИзменения(КомплектОбъект, Ложь, Отказ);
		
		мКомплекты.Добавить(х.Комплект);
	КонецЦикла; 
	
	РегистрыСведений.ДатыКомплекта.РасчитатьПКДКопозицииНаОснованииКомплектов(мКомплекты);
	
КонецПроцедуры // ЗаписатьВРегистрНаСервере()

&НаСервереБезКонтекста
Процедура ПроверитьУдалениеКомплекта(мКомплекты, Проект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплекта.Комплект КАК Комплект
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|ГДЕ
		|	ДатыКомплекта.Проект = &Проект";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если мКомплекты.Найти(Выборка.Комплект) = Неопределено Тогда
			НаборЗаписей = РегистрыСведений.ДатыКомплекта.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Комплект.Установить(Выборка.Комплект);
			
			НаборЗаписей.Записать();
		КонецЕсли;
	КонецЦикла;
	
	
КонецПроцедуры // 	ПроверитьУдалениеКомплекта(мКомплекты);¶()

&НаСервереБезКонтекста
Функция ПолучитьТекущегоПользователя()
	
	Возврат ПараметрыСеанса.ТекущийПользователь;
	
КонецФункции // ПолучитьТекущегоПользователя()

&НаКлиенте

&НаКлиенте
Процедура КомплектыКомплектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(объект.Проект) Тогда
	
		ПоказатьПредупреждение(,"Не заполнена Проект");
		Возврат;
	
	КонецЕсли;
	
	ДополнительныйПараметр = Новый Структура("ТекущиеДанные", Элемент.Родитель.ТекущиеДанные);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораКомплекта",ЭтаФорма,ДополнительныйПараметр);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",Ложь);
	ПараметрыФормы.Вставить("Проект",Объект.Проект);
	
	НеПоказыватьВВыборе = СуществующиеКомплектыСделки(Объект.Проект, Объект.Ссылка);
	
	Для каждого х Из Объект.Комплекты Цикл
	
		НеПоказыватьВВыборе.Добавить(х.Комплект);	
	
	КонецЦикла;
	ПараметрыФормы.Вставить("НеПоказыватьВВыборе",НеПоказыватьВВыборе);
	
	ОткрытьФорму("Справочник.ИзделияКомплект.ФормаВыбора",ПараметрыФормы,ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СуществующиеКомплектыСделки(Проект, Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СрокиСделкиКомплекты.Комплект КАК Комплект
		|ИЗ
		|	Документ.ЗапускВРаботу.Комплекты КАК СрокиСделкиКомплекты
		|ГДЕ
		|	СрокиСделкиКомплекты.Ссылка.Проект = &Проект
		|	И СрокиСделкиКомплекты.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Комплект");
	
	
КонецФункции // СуществующиеКомплектыСделки()

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	мИзменений 			= Новый Массив;
	мИзмененийМатериалы = Новый Массив;
	
	стрКолонкиМатериала = ПолучитьКолонкиМатериала();
	
	мИменаКолонкиМатериала = СтрРазделить(стрКолонкиМатериала, ",");
	
	Для каждого х Из Объект.Комплекты Цикл
		
		Если х.Изменен Тогда
			
			стСтрока = Новый Структура("Комплект, ДнейПКД, ДнейРКД, НормаВремени_ПКД, НормаВремени_РКД, БезЗамера, БезГабаритногоЧертежа, НормаДоработки_ПКД, НормаДоработки_РКД, Композиция, КатегорияКонструктора, ТипКомплекта, ГОСТ", );
			ЗаполнитьЗначенияСвойств(стСтрока, х);
			мИзменений.Добавить(стСтрока);
			
			х.Изменен = Ложь;
			
		КонецЕсли;
		
		Для каждого ИмяКолонкиМатериал Из мИменаКолонкиМатериала Цикл
			
			Если ИмяКолонкиМатериал = "Комплект" Тогда
			
				Продолжить;	
			
			КонецЕсли;
			
			Если х[ИмяКолонкиМатериал] Тогда
				
				стСтрока = Новый Структура(стрКолонкиМатериала, );
				ЗаполнитьЗначенияСвойств(стСтрока, х);
				мИзмененийМатериалы.Добавить(стСтрока);
				
				Прервать;
				
			КонецЕсли;	
			
		КонецЦикла;
		
		
		

	КонецЦикла;
	
КонецПроцедуры 


&НаСервереБезКонтекста
Функция ПолучитьДанныеСдвигНормаДни()
	мНаименованиеПараметров = Новый Массив;
	ОбъектОтчета = Отчеты.ОбщиеНастройки.Создать();
	ОбъектОтчета.ПолучитСписокРеквизитовДляНормВремени(мНаименованиеПараметров);

	СтруктураСдвигНормаДни = Новый Структура();
	
	Для каждого х Из мНаименованиеПараметров Цикл
		СтруктураСдвигНормаДни.Вставить(х, 0);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПроекта.ИмяНастройки КАК ИмяНастройки,
		|	НастройкиПроекта.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.НастройкиПроекта КАК НастройкиПроекта
		|ГДЕ
		|	НастройкиПроекта.ИмяНастройки В(&мНаименованиеПараметров)";
	
	Запрос.УстановитьПараметр("мНаименованиеПараметров", мНаименованиеПараметров);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтруктураСдвигНормаДни[Выборка.ИмяНастройки] = Выборка.Значение;
	КонецЦикла;
	
	Возврат СтруктураСдвигНормаДни;

КонецФункции // ПолучитьДанныеСдвигНормаДни()

&НаКлиенте
Процедура КомплектыБезЗамераПриИзменении(Элемент)
	ПриИзменениеДатыТЧ();
КонецПроцедуры

&НаСервере
Процедура СоздатьКомпозицииИГЧНаСервере(мКомплекты, мКомпозиции)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Родитель.Наименование КАК РодительНаименование,
		|	ИзделияКомплект.Родитель КАК Родитель,
		|	ИзделияКомплект.Ссылка КАК ИзделиеКомплект
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|ГДЕ
		|	ИзделияКомплект.Ссылка В(&Ссылка)
		|	И ИзделияКомплект.Родитель.Родитель.Родитель.Наименование ЕСТЬ NULL
		|	И НЕ ИзделияКомплект.Родитель.Родитель.Наименование ЕСТЬ NULL
		|	И НЕ ИзделияКомплект.Родитель.Наименование ЕСТЬ NULL
		|	И ИзделияКомплект.Родитель.Композиция = ЗНАЧЕНИЕ(Справочник.Композиции.ПустаяСсылка)
		|	И НЕ ИзделияКомплект.Родитель = ЗНАЧЕНИЕ(Справочник.ИзделияКомплект.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ИзделияКомплект.Родитель,
		|	ИзделияКомплект.Родитель.Наименование,
		|	ИзделияКомплект.Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", мКомплекты);
	
	мДляКомпозиции 	= Новый Массив;
	
	ОписаниеСтрока 				= Новый ОписаниеТипов("Строка");
	ОписаниеИзделияКомплект 	= Новый ОписаниеТипов("СправочникСсылка.ИзделияКомплект");
	ОписаниеКомпозиции 			= Новый ОписаниеТипов("СправочникСсылка.Композиции");
	
	тзДанные = Новый ТаблицаЗначений;
	тзДанные.Колонки.Добавить("ИзделиеКомплект", 		ОписаниеИзделияКомплект);
	тзДанные.Колонки.Добавить("Родитель", 				ОписаниеИзделияКомплект);
	тзДанные.Колонки.Добавить("РодительНаименование",	ОписаниеСтрока);
	тзДанные.Колонки.Добавить("Наименование",			ОписаниеСтрока);
	тзДанные.Колонки.Добавить("Композиция", 			ОписаниеКомпозиции);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если мДляКомпозиции.Найти(Выборка.Родитель) = Неопределено Тогда
			мДляКомпозиции.Добавить(Выборка.Родитель);
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(тзДанные.Добавить(), Выборка);
	КонецЦикла;
	
	СоздатьКомпозиции(мДляКомпозиции, тзДанные, мКомпозиции);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплектИзделияЭлемент.Ссылка КАК ИзделиеКомплект,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Наименование КАК Наименование,
		|	ИзделияКомплектИзделияЭлемент.ИзделиеЭлемент.Помещения КАК Помещения
		|ИЗ
		|	Справочник.ИзделияКомплект.ИзделияЭлемент КАК ИзделияКомплектИзделияЭлемент
		|ГДЕ
		|	ИзделияКомплектИзделияЭлемент.Ссылка.ГабаритныйЧертеж = ЗНАЧЕНИЕ(Справочник.ГабаритныеЧертежи.ПустаяСсылка)
		|	И ИзделияКомплектИзделияЭлемент.Ссылка В(&мКомплекты)
		|	И ИзделияКомплектИзделияЭлемент.Ссылка.Родитель.Родитель.Наименование ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ИзделияКомплектИзделияЭлемент.Ссылка,
		|	ИзделияКомплектИзделияЭлемент.ИзделиеЭлемент.Помещения,
		|	ИзделияКомплектИзделияЭлемент.Ссылка.Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Композиции.Ссылка КАК Композиция,
		|	СТРЗАМЕНИТЬ(Композиции.Наименование, "".Композиция"", """") КАК НаименованиеКомпозиция
		|ИЗ
		|	Справочник.Композиции КАК Композиции
		|ГДЕ
		|	Композиции.Ссылка В(&мКомпозиции)
		|	И Композиции.ГабаритныйЧертеж = ЗНАЧЕНИЕ(Справочник.ГабаритныеЧертежи.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	спрИзделияКомплект.Композиция КАК Композиция,
		|	спрИзделияЭлемент.Помещения КАК Помещения
		|ИЗ
		|	Справочник.ИзделияКомплект КАК спрИзделияКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИзделияКомплект.ИзделияЭлемент КАК тчИзделияКомплектИзделияЭлемент
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИзделияЭлемент КАК спрИзделияЭлемент
		|			ПО тчИзделияКомплектИзделияЭлемент.ИзделиеЭлемент = спрИзделияЭлемент.Ссылка
		|		ПО (тчИзделияКомплектИзделияЭлемент.Ссылка.Родитель = спрИзделияКомплект.Ссылка)
		|ГДЕ
		|	спрИзделияКомплект.Композиция В(&мКомпозиции)";
	
	Запрос.УстановитьПараметр("мКомплекты", 	мКомплекты);
	Запрос.УстановитьПараметр("мКомпозиции", 	мКомпозиции);
	
	Результат = Запрос.ВыполнитьПакет();
	
	РодительГЧ = омГабаритныйЧертежНаСервере.ПолучитьРодительГабаритногоЧертежа(Объект.Проект);
	
	тзКомплекты = Результат[0].Выгрузить();
	тзКомплекты.Индексы.Добавить("ИзделиеКомплект");
	
	мКомплектыДляГЧ = Новый Массив;
	Для каждого х Из тзКомплекты Цикл
		Если мКомплектыДляГЧ.Найти(х.ИзделиеКомплект) = Неопределено Тогда
			мКомплектыДляГЧ.Добавить(х.ИзделиеКомплект);
		КонецЕсли;
	КонецЦикла; 
	
	стОтбор = Новый Структура("ИзделиеКомплект", );
	мПомещения = Новый Массив;
	Для каждого ТекущийКомплект Из мКомплектыДляГЧ Цикл
		мПомещения.Очистить();
		стОтбор.ИзделиеКомплект = ТекущийКомплект;
		
		Для каждого ТекщиеПомещения Из тзКомплекты.НайтиСтроки(стОтбор) Цикл
			Наименование = ТекщиеПомещения.Наименование;
			Если мПомещения.Найти(ТекщиеПомещения.Помещения) = Неопределено Тогда
				мПомещения.Добавить(ТекщиеПомещения.Помещения);
			КонецЕсли;
		КонецЦикла;
		
		СоздатьГЧ(ТекущийКомплект, мПомещения, Наименование, РодительГЧ);
	
	КонецЦикла;
	
	тзПомещения = Результат[2].Выгрузить();
	тзПомещения.Индексы.Добавить("Композиция");
	стОтбор = Новый Структура("Композиция", );
	
	Выборка = Результат[1].Выбрать();
	мПомещения = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		мПомещения.Очистить();
		стОтбор.Композиция = Выборка.Композиция;
		Для каждого х Из тзПомещения.НайтиСтроки(стОтбор) Цикл
			мПомещения.Добавить(х.Помещения);	
		КонецЦикла;
		
		
		СоздатьГЧ(Выборка.Композиция, мПомещения, Выборка.НаименованиеКомпозиция, РодительГЧ);
	
	КонецЦикла;
	
	ЗаполнитьНачальноеЗначениеТЧ();
	
КонецПроцедуры 

Процедура СоздатьГЧ(ТекущийКомплект, мПомещения, Наименование, РодительГЧ)
	
	НайденыйГЧ = Справочники.ГабаритныеЧертежи.НайтиПоНаименованию(Наименование, , РодительГЧ);
	
	Если НЕ ЗначениеЗаполнено(НайденыйГЧ) Тогда
		ОбъектГЧ 				= Справочники.ГабаритныеЧертежи.СоздатьЭлемент();
		ОбъектГЧ.Родитель 		= РодительГЧ;
		ОбъектГЧ.Наименование 	= Наименование + ".ГЧ";
		ОбъектГЧ.Проект 		= Объект.Проект;
		
		Для каждого ТекущиеПомещение Из мПомещения Цикл
			НС = ОбъектГЧ.Помещения.Добавить();
			НС.Помещение = ТекущиеПомещение;
		КонецЦикла;
		
		ОбъектГЧ.Записать();
		
		НайденыйГЧ = ОбъектГЧ.Ссылка;
	КонецЕсли;
	
	ОбъектКомплект = ТекущийКомплект.ПолучитьОбъект();
	ОбъектКомплект.ГабаритныйЧертеж = НайденыйГЧ;
	ОбъектКомплект.Записать();
	
КонецПроцедуры

Процедура СоздатьКомпозиции(мДляКомпозиции, тзДанные, мКомпозиции)
	
	РодительКомпозиции = омКомпозицииСервер.ПолучитьРодительКомпозиции(Объект.Проект); 
	НайденаяКомпозиция = Неопределено;
	
    Для каждого текРодитель Из мДляКомпозиции Цикл
		КомпозицияСоздана = Ложь;
		
		Для каждого ДанныеКомпозиции Из тзДанные Цикл
			Если ДанныеКомпозиции.Родитель = текРодитель Тогда
				Если КомпозицияСоздана Тогда
					ДанныеКомпозиции.Композиция = НайденаяКомпозиция;
				Иначе
					СоздатьКомпозицию(ДанныеКомпозиции, РодительКомпозиции, мКомпозиции);
					НайденаяКомпозиция = ДанныеКомпозиции.Композиция;
					КомпозицияСоздана = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры 

Процедура СоздатьКомпозицию(ДанныеКомпозиции, РодительКомпозиции, мКомпозиции)
	
	НайденаяКомпозиция = Справочники.Композиции.НайтиПоНаименованию(ДанныеКомпозиции.РодительНаименование, , РодительКомпозиции);
	
	Если НЕ ЗначениеЗаполнено(НайденаяКомпозиция) Тогда
		
		ОбъектКомпозиция 				= Справочники.Композиции.СоздатьЭлемент();
		ОбъектКомпозиция.Родитель 		= РодительКомпозиции;
		ОбъектКомпозиция.Наименование 	= ДанныеКомпозиции.РодительНаименование + ".Композиция";
		ОбъектКомпозиция.Проект 		= Объект.Проект;
		ОбъектКомпозиция.Записать();
		
		НайденаяКомпозиция = ОбъектКомпозиция.Ссылка;
		
		Если мКомпозиции.Найти(НайденаяКомпозиция) = Неопределено Тогда
			мКомпозиции.Добавить(НайденаяКомпозиция);
		КонецЕсли;
	КонецЕсли;
	
	ДанныеКомпозиции.Композиция = НайденаяКомпозиция;
	
	ОбъектКомплектРодитель 				= ДанныеКомпозиции.Родитель.ПолучитьОбъект();
	ОбъектКомплектРодитель.Композиция 	= НайденаяКомпозиция;
	ОбъектКомплектРодитель.Записать();

КонецПроцедуры

&НаКлиенте
Процедура СоздатьКомпозицииИГЧ()
	
	мКомплекты = Новый Массив;
	мКомпозиции = Новый Массив;
	мГЧ = Новый Массив;
	
	Для каждого х Из Объект.Комплекты Цикл
		
		Если НЕ х.БезГабаритногоЧертежа Тогда
			мКомплекты.Добавить(х.Комплект);
		КонецЕсли;
		
		Если мКомпозиции.Найти(х.Композиция) = Неопределено И ЗначениеЗаполнено(х.Композиция) Тогда
			мКомпозиции.Добавить(х.Композиция); 
		КонецЕсли;
		
	КонецЦикла;
	
	СоздатьКомпозицииИГЧНаСервере(мКомплекты, мКомпозиции); 
	
КонецПроцедуры

&НаКлиенте
Процедура КомплектыПередНачаломИзменения(Элемент, Отказ)
	Если Элемент.ТекущийЭлемент.Имя = "КомплектыПолеКомпозиции" Тогда
		
		Отказ = Истина;
		
		ТД = Элементы.Комплекты.ТекущиеДанные;
		
		Если НЕ ТД = Неопределено Тогда
			ПоказатьЗначение(, ТД.Композиция);
		КонецЕсли;
	
	ИначеЕсли Элемент.ТекущийЭлемент.Имя = "КомплектыГабаритныйЧертеж" Тогда
		
		Отказ = Истина;
		
		ТД = Элементы.Комплекты.ТекущиеДанные;
		
		Если НЕ ТД = Неопределено Тогда
			ПоказатьЗначение(, ТД.ГабаритныйЧертеж);
		КонецЕсли;
	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаВPDMПоМаскеКомплект() Экспорт
	
	
	стМассивов = МассивДанныехДляВыгрузки[ИндексВыгрузки];
	
	Если стМассивов.Свойство("Комплект") Тогда
		
		стВозврата = омВыгрузкаCRMНаКлиенте.ВыгрузитьВPDM(стМассивов.Комплект, "Комплект");
		Если НЕ ПустаяСтрока(стВозврата.ОписаниеОшибки) Тогда
			Сообщить(стВозврата.ОписаниеОшибки);
		КонецЕсли;
	Иначе 
		
		ИндексВыгрузки = 0;
		ВыгрузкаВPDMПоМаскеКомпозиция();
		Возврат;
		
	КонецЕсли;
	
	ИндексВыгрузки = ИндексВыгрузки + 1;
	
	Если МассивДанныехДляВыгрузки.Количество() > ИндексВыгрузки Тогда
		
		ПодключитьОбработчикОжидания("ВыгрузкаВPDMПоМаскеКомплект", ВремяЗадержкиПриВыгрузке, Истина); 
		
	Иначе
		
		ИндексВыгрузки = 0;
		ПодключитьОбработчикОжидания("ВыгрузкаВPDMПоМаскеКомпозиция", ВремяЗадержкиПриВыгрузке, Истина);
		
	КонецЕсли;
	
КонецПроцедуры // ВыгрузкаВPDMПоМаскеКомплект()

 &НаКлиенте
Процедура ВыгрузкаВPDMПоМаскеКомпозиция() Экспорт
	
	стМассивов = МассивДанныехДляВыгрузки[ИндексВыгрузки];
	Если стМассивов.Свойство("Композиция") Тогда
		
		стВозврата = омВыгрузкаCRMНаКлиенте.ВыгрузитьВPDM(стМассивов.Композиция, "Композиция");
		Если НЕ ПустаяСтрока(стВозврата.ОписаниеОшибки) Тогда
			Сообщить(стВозврата.ОписаниеОшибки);
		КонецЕсли;
		
	Иначе 
		
		ИндексВыгрузки = 0;
		ВыгрузкаВPDMПоМаскеГабаритныйЧерчеж();
		Возврат;
		
	КонецЕсли;
	
	ИндексВыгрузки = ИндексВыгрузки + 1;
	
	Если МассивДанныехДляВыгрузки.Количество() > ИндексВыгрузки Тогда
		
		ПодключитьОбработчикОжидания("ВыгрузкаВPDMПоМаскеКомпозиция", ВремяЗадержкиПриВыгрузке, Истина);

	Иначе
		
		ИндексВыгрузки = 0;
		ПодключитьОбработчикОжидания("ВыгрузкаВPDMПоМаскеГабаритныйЧерчеж", ВремяЗадержкиПриВыгрузке, Истина); 
		
	КонецЕсли;

КонецПроцедуры // ВыгрузкаВPDMПоМаске()

 &НаКлиенте
Процедура ВыгрузкаВPDMПоМаскеГабаритныйЧерчеж() Экспорт
	
	стМассивов = МассивДанныехДляВыгрузки[ИндексВыгрузки];
	Если стМассивов.Свойство("ГабаритныйЧертеж") Тогда
		
		стВозврата = омВыгрузкаCRMНаКлиенте.ВыгрузитьВPDM(стМассивов.ГабаритныйЧертеж, "ГабаритныйЧертеж");
		Если НЕ ПустаяСтрока(стВозврата.ОписаниеОшибки) Тогда
			Сообщить(стВозврата.ОписаниеОшибки);
		КонецЕсли;
		
	Иначе
		Возврат;
	КонецЕсли;

	ИндексВыгрузки = ИндексВыгрузки + 1;
	
	Если МассивДанныехДляВыгрузки.Количество() > ИндексВыгрузки Тогда
		
		ПодключитьОбработчикОжидания("ВыгрузкаВPDMПоМаскеГабаритныйЧерчеж", ВремяЗадержкиПриВыгрузке, Истина);

	КонецЕсли;

КонецПроцедуры // ВыгрузкаВPDMПоМаскеГабаритныйЧерчеж()

&НаСервереБезКонтекста
Процедура ПолучитьНеВыгруженные(мКомпозиции, мГЧ, мКомплекты)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплекта.Комплект КАК ОбъектВыгрузки
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|ГДЕ
		|	ДатыКомплекта.Комплект В(&мКомпозиции)
		|	И ДатыКомплекта.Код_в_PDM > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеГабаритныйЧертеж.ГабаритныйЧертеж КАК ОбъектВыгрузки
		|ИЗ
		|	РегистрСведений.ДанныеГабаритныйЧертеж КАК ДанныеГабаритныйЧертеж
		|ГДЕ
		|	ДанныеГабаритныйЧертеж.ГабаритныйЧертеж В(&мГЧ)
		|	И ДанныеГабаритныйЧертеж.Код_в_PDM > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыКомплекта.Комплект КАК ОбъектВыгрузки
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|ГДЕ
		|	ДатыКомплекта.Комплект В(&мКомплекты)
		|	И ДатыКомплекта.Код_в_PDM > 0";
	
	Запрос.УстановитьПараметр("мКомпозиции", 	мКомпозиции);
	Запрос.УстановитьПараметр("мГЧ", 			мГЧ);
	Запрос.УстановитьПараметр("мКомплекты", 	мКомплекты);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		мКомпозиции.Удалить(мКомпозиции.Найти(Выборка.ОбъектВыгрузки));
	КонецЦикла;
	
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		мГЧ.Удалить(мГЧ.Найти(Выборка.ОбъектВыгрузки));
	КонецЦикла;
	
	Выборка = Результат[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		мКомплекты.Удалить(мКомплекты.Найти(Выборка.ОбъектВыгрузки));
	КонецЦикла;
	
КонецПроцедуры // ПолучитНеВыгруженные()

&НаКлиенте
Процедура ИзменитьВсе(Команда)
	Для каждого х Из Объект.Комплекты Цикл
		х.Изменен = Истина;
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКодПроекта(Проект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеПроекта.Код_в_PDM КАК Код_в_PDM
		|ИЗ
		|	РегистрСведений.ДанныеПроекта КАК ДанныеПроекта
		|ГДЕ
		|	ДанныеПроекта.Проект = &Проект";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Код_в_PDM;
	КонецЦикла;
	
	Возврат 0;

КонецФункции // ПолучитьКодСделки()


&НаКлиенте
Процедура КомплектыНуженГабаритныйЧертежПриИзменении(Элемент)
	ПриИзменениеДатыТЧ();
КонецПроцедуры

#Область Планирование

&НаКлиенте
Процедура СоздатьЗадачиПланирования(Команда)
	
	мКомплектыДляСроков	= Новый Массив;
	мКомплекты 			= Новый Массив;
	мКомпозиции 		= Новый Массив;
	
	Для каждого х Из Объект.Комплекты Цикл
		
		мКомплектыДляСроков.Добавить(х.Комплект);
		
		Если ЗначениеЗаполнено(х.Композиция) Тогда
			
			Если мКомпозиции.Найти(х.Композиция) = Неопределено Тогда
				мКомпозиции.Добавить(х.Композиция);
			КонецЕсли;

		Иначе
			мКомплекты.Добавить(х.Комплект);
		КонецЕсли;

	КонецЦикла; 
	
	СоздатьЗадачиПланированияНаСервере(Объект.Проект, мКомплектыДляСроков, мКомплекты, мКомпозиции, Объект.Ссылка);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьЗадачиПланированияНаСервере(Проект, мКомплектыДляСроков, мКомплекты, мКомпозиции, ЗапускВработу)

	Задачи.ПодготовкаПроекта.СоздатьЗадачиПоЗаполнению(Проект, мКомплектыДляСроков, ЗапускВработу);
	
	ИмяДоскиДляТЗ = омКанбанДоскиНаСервереПовтор.ПолучитьИмяДоски("НаименованиеДоскиДляТЗ");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанКарточки.Ресурс КАК Ресурс
		|ПОМЕСТИТЬ втСуществующие
		|ИЗ
		|	РегистрСведений.КанбанДоски КАК КанбанДоски
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КанбанКарточки КАК КанбанКарточки
		|		ПО КанбанДоски.УИД = КанбанКарточки.Доска
		|ГДЕ
		|	КанбанДоски.Наименование = &НаименованиеДоскиДляТЗ
		|	И (КанбанКарточки.Ресурс В (&мКомплекты)
		|			ИЛИ КанбанКарточки.Ресурс В (&мКомпозиции))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеКомплектаСрезПоследних.Комплект
		|ИЗ
		|	РегистрСведений.ДанныеКомплекта.СрезПоследних КАК ДанныеКомплектаСрезПоследних
		|ГДЕ
		|	НЕ ДанныеКомплектаСрезПоследних.ТекущаяСтадия = ЗНАЧЕНИЕ(Справочник.СтадииКомплекта.ПустаяСсылка)
		|	И (ДанныеКомплектаСрезПоследних.Комплект В (&мКомплекты)
		|			ИЛИ ДанныеКомплектаСрезПоследних.Комплект В (&мКомпозиции))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК Ссылка,
		|	ИзделияКомплект.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ втСуществующие КАК втСуществующие
		|		ПО ИзделияКомплект.Ссылка = втСуществующие.Ресурс
		|ГДЕ
		|	ИзделияКомплект.Ссылка В(&мКомплекты)
		|	И втСуществующие.Ресурс ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Композиции.Ссылка,
		|	Композиции.Наименование
		|ИЗ
		|	Справочник.Композиции КАК Композиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ втСуществующие КАК втСуществующие
		|		ПО Композиции.Ссылка = втСуществующие.Ресурс
		|ГДЕ
		|	Композиции.Ссылка В(&мКомпозиции)
		|	И втСуществующие.Ресурс ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Проекты.Дизайнер КАК Дизайнер
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	Проекты.Ссылка = &Проект";
	
	Запрос.УстановитьПараметр("мКомплекты", 			мКомплекты);
	Запрос.УстановитьПараметр("мКомпозиции", 			мКомпозиции);
	Запрос.УстановитьПараметр("НаименованиеДоскиДляТЗ", ИмяДоскиДляТЗ);
	Запрос.УстановитьПараметр("Проект", 				Проект);
	
	Результат = Запрос.ВыполнитьПакет();
	
	мСтруктур = Новый Массив;
	
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		мСтруктур.Добавить(Новый Структура("Ссылка, Наименование", Выборка.Ссылка, Выборка.Наименование));
	КонецЦикла;
	
	Если мСтруктур.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;
	
	Выборка = Результат[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		Дизайнер = Выборка.Дизайнер;
	КонецЦикла;
	
	стРазделители = Новый Структура("Сотрудник, Проект", Дизайнер, Проект);
	
	омКанбанНачальноеЗаполнение.ЗаполнитьНачальноеЗаполнениеКанбана(ИмяДоскиДляТЗ, стРазделители, мСтруктур, "На основании " + ЗапускВработу); 
	
КонецПроцедуры // СоздатьЗадачиПланирования()

&НаСервереБезКонтекста
Функция СортироватьНаСервере(Знач Комплекты)
	
	тз = Комплекты.Выгрузить();
	тзКопия = тз.СкопироватьКолонки();
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК Комплект
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|ГДЕ
		|	ИзделияКомплект.Ссылка В(&Ссылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИзделияКомплект.Наименование";
	
	Запрос.УстановитьПараметр("Ссылка", тз.ВыгрузитьКолонку("Комплект"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(тзКопия.Добавить(), тз.Найти(Выборка.Комплект));
	КонецЦикла;
	
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(тзКопия);
	

КонецФункции

&НаКлиенте
Процедура Сортировать(Команда)
	
	мКомплекты = СортироватьНаСервере(Объект.Комплекты);
	
	Объект.Комплекты.Очистить();
	
	Для каждого Комплект Из мКомплекты Цикл
		
		ЗаполнитьЗначенияСвойств(Объект.Комплекты.Добавить(), Комплект);
		
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти
