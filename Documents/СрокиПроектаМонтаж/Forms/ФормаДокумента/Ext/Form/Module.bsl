&НаКлиенте
Перем мИзменений, ЕстиУдалениеКомплекта;

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗаполнитьНачальноеЗначениеТЧ();
	ЗаполнитьЗначенияСвойств(ЭтаФорма,ЗаполнитьПодпись(Объект.Проект));
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЕстиУдалениеКомплекта = Ложь;
	ЭтаФорма.ТолькоПросмотр = ЗначениеЗаполнено(ЭтаФорма.ДатаПодписи);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если мИзменений.Количество() > 0 Тогда
		
		ЗаписатьВРегистрНаСервере(мИзменений, Объект.Проект);	
		
	КонецЕсли;
	ЗаполнитьНачальноеЗначениеТЧ();
КонецПроцедуры


#КонецОбласти


#Область СобытияФормыПродолжение

&НаСервереБезКонтекста
Функция ЗаполнитьПодпись(Сделка)
	
	Если ЗначениеЗаполнено(Сделка) Тогда
		Возврат омПроекты.ПодписьСделки(Сделка);
	КонецЕсли;
	
	Возврат Новый Структура("ДатаПодписи,АвторПодписи",Дата(1,1,1),ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка"));

КонецФункции // ЗаполнитьПодпись()

#КонецОбласти


#Область СобытиеРеквизитов

&НаКлиенте
Процедура ПодписатьПриИзменении(Элемент)
	Если Объект.Подписать Тогда
		Объект.ДатаПодписи = ТекущаяДата();
		Объект.АвторПодписи = ПолучитьТекущегоПользователя();
	Иначе	
		Объект.ДатаПодписи 	= Дата(1,1,1);
		Объект.АвторПодписи	= ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СделкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Объект.Комплекты.Количество() = 0 Тогда 
		ОткрытьФормуВыбораСделки();
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаОСменеСделке",ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещения,"При смене Проекты будет очищена табличная часть. Продоллжить?", РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура СделкаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры


#КонецОбласти 


#Область СобытиеРеквизитовПродолжение

&НаКлиенте
Процедура ПослеВопросаОСменеСделке(Результат, ДополнительныйПараметр) Экспорт
    Если Результат = КодВозвратаДиалога.ДА Тогда
        ОткрытьФормуВыбораСделки();
    КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыборкаПроекта(Значение, ДополнительныйПараметр) Экспорт

    Если Значение = Неопределено Тогда  ///Если ничего не выбрать - вернется пустое значение (Неопределено)
        Возврат;
	КонецЕсли;
	
	Элементы.Проект.СписокВыбора.Очистить();
	Элементы.Проект.СписокВыбора.Добавить(Значение);
	Объект.Проект = Значение;
	Объект.Комплекты.Очистить();
	
	Если ЗначениеЗаполнено(Объект.Проект) Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма,ЗаполнитьПодпись(Объект.Проект));
		ЭтаФорма.ТолькоПросмотр = ЗначениеЗаполнено(ЭтаФорма.ДатаПодписи);
	Иначе
		ЭтаФорма.ДатаПодписи 	= Дата(1,1,1);
		ЭтаФорма.АвторПодписи 	= ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти 


#Область СобытияТЧ

&НаКлиенте
Процедура КомплектыПередУдалением(Элемент, Отказ)
	ЕстиУдалениеКомплекта = Истина;
КонецПроцедуры

&НаСервере
Функция ЗаполнитьНачальноеЗначениеТЧ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплектаМонтаж.КрайМонтажа КАК КрайМонтажа,
		|	ЛОЖЬ КАК Изменен,
		|	ДатыКомплектаМонтаж.НачалоМонтажа КАК НачалоМонтажа,
		|	СрокиСделкиМонтажКомплекты.Комплект КАК Комплект,
		|	СрокиСделкиМонтажКомплекты.Комментарий КАК Комментарий,
		|	ДатыКомплектаМонтаж.КоличествоСотрудниковМонтаж КАК КоличествоСотрудниковМонтаж,
		|	ДатыКомплектаМонтаж.КоличествоСотрудниковМонтаж * ДатыКомплектаМонтаж.НормаВремениМонтажа КАК ЧеловекоДнейМонтаж,
		|	ДатыКомплектаМонтаж.НормаВремениМонтажа КАК НормаВремениМонтажа
		|ИЗ
		|	Документ.СрокиПроектаМонтаж.Комплекты КАК СрокиСделкиМонтажКомплекты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплектаМонтаж КАК ДатыКомплектаМонтаж
		|		ПО СрокиСделкиМонтажКомплекты.Комплект = ДатыКомплектаМонтаж.Комплект
		|ГДЕ
		|	СрокиСделкиМонтажКомплекты.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатыКомплектаМонтаж.Комплект.Наименование";
	
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Объект.Комплекты.Загрузить(Запрос.Выполнить().Выгрузить());
КонецФункции // ЗаполнитьНачальноеЗначениеТЧ()    

&НаСервереБезКонтекста
Функция ПолучитьданныеКомплекта(Комплекты)

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплектаМонтаж.КрайМонтажа КАК КрайМонтажа,
		|	ДатыКомплектаМонтаж.НачалоМонтажа КАК НачалоМонтажа
		|ИЗ
		|	РегистрСведений.ДатыКомплектаМонтаж КАК ДатыКомплектаМонтаж
		|ГДЕ
		|	ДатыКомплектаМонтаж.Комплект = &Комплект";
	

 		
		Запрос.УстановитьПараметр("Комплект", Комплекты);
		тзДанныеКомплека = Запрос.Выполнить().Выгрузить();
		
		ПустаяДата = Дата(1,1,1);
		Если тзДанныеКомплека.Количество() = 0 Тогда
			стрВозврата = Новый Структура("Комплект,КрайМонтажа,НачалоМонтажа"
										,Комплекты,
										,ПустаяДата
										,ПустаяДата);	
			мВозврата = Новый Массив(1);
			мВозврата[0] = стрВозврата;
			Возврат мВозврата;
		Иначе
			Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(тзДанныеКомплека);
		КонецЕсли;

КонецФункции // ПолучитьданныеКомплекта()
 
&НаКлиенте
Процедура КомплектыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	Если Копирование Тогда
	    ПоказатьПредупреждение(,"Запрещен ввод копированием строки."); 
	ИначеЕсли ЗначениеЗаполнено(Объект.Проект) Тогда
		НС = Объект.Комплекты.Добавить();
		КомплектыКомплектНачалоВыбора(Элемент.ПодчиненныеЭлементы.КомплектыКомплект,НС,Неопределено);	
	Иначе
	    ПоказатьПредупреждение(,"Не заполнена Проект."); 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКомплекта(Значение, ДополнительныйПараметр) Экспорт

    Если Значение = Неопределено Тогда  
        Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДополнительныйПараметр.ТекущиеДанные, ПолучитьданныеКомплекта(Значение)[0]);

	ПриИзменениеДатыТЧ();
	
КонецПроцедуры

&НаКлиенте
Процедура КомплектыКрайТЗПриИзменении(Элемент)
	ПриИзменениеДатыТЧ();
КонецПроцедуры


&НаКлиенте
Процедура КомплектыПриИзменении(Элемент = Неопределено)
	Объект.КрайМонтажа = Дата(1,1,1);
	
	Для каждого х Из Объект.Комплекты Цикл
		Если х.КрайМонтажа > Объект.КрайМонтажа Тогда
			Объект.КрайМонтажа = х.КрайМонтажа;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура КомплектыКомплектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ДополнительныйПараметр = Новый Структура("ТекущиеДанные", ?(ДанныеВыбора = Неопределено, Элемент.Родитель.ТекущиеДанные, ДанныеВыбора));
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораКомплекта",ЭтаФорма,ДополнительныйПараметр);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",Ложь);
	ПараметрыФормы.Вставить("Проект",Объект.Проект);
	
	НеПоказыватьВВыборе = Новый Массив;
	
	Для каждого х Из Объект.Комплекты Цикл
	
		НеПоказыватьВВыборе.Добавить(х.Комплект);	
	
	КонецЦикла;
	ПараметрыФормы.Вставить("НеПоказыватьВВыборе",НеПоказыватьВВыборе);
	
	ОткрытьФорму("Справочник.МонтажныеКомплекты.ФормаВыбора",ПараметрыФормы,ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры



#КонецОбласти 


#Область СобытияТЧПродолжение

&НаКлиенте
Процедура ПриИзменениеДатыТЧ()

	ТД = Элементы.Комплекты.ТекущиеДанные;
	
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТД.Изменен = Истина;
	
	
КонецПроцедуры // ПриИзменениеДатыТЧ()

&НаКлиенте
Процедура КомплектыНормаВремени_МонтажаПриИзменении(Элемент)
	ПриИзменениеДатыТЧ();
	ТД = Элементы.Комплекты.ТекущиеДанные;
	ТД.ЧеловекоДнейМонтаж 	= ТД.КоличествоСотрудниковМонтаж * ТД.НормаВремениМонтажа;
	ТД.НачалоМонтажа 		= омМонтажКлиент.РасчетНачалаДатыМонтажа(ТД);
КонецПроцедуры

&НаКлиенте
Процедура КомплектыКрайМонтажаПриИзменении(Элемент)
	ПриИзменениеДатыТЧ();
	ТД = Элементы.Комплекты.ТекущиеДанные;
	ТД.НачалоМонтажа = омМонтажКлиент.РасчетНачалаДатыМонтажа(ТД);
КонецПроцедуры

&НаКлиенте
Процедура КомплектыКоличествоСотрудниковМонтажПриИзменении(Элемент)
	ПриИзменениеДатыТЧ();
	ТД = Элементы.Комплекты.ТекущиеДанные;
	ТД.ЧеловекоДнейМонтаж = ТД.КоличествоСотрудниковМонтаж * ТД.НормаВремениМонтажа;
КонецПроцедуры

#КонецОбласти 


#Область Команды

&НаКлиенте
Процедура ЗаполнитьКомплекты(Команда)
	Если ЗначениеЗаполнено(Объект.Проект) Тогда
		ЗаполнитьКомплектыНаСервере();
		КомплектыПриИзменении();
	Иначе	
	    ПоказатьПредупреждение(,"Не заполнена Проект.");
	КонецЕсли;
КонецПроцедуры


#КонецОбласти

#Область КомандыПродолжение

&НаСервере
Процедура ЗаполнитьКомплектыНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СрокиСделкиМонтажКомплекты.Комплект КАК Комплект
		|ПОМЕСТИТЬ втСуществующиеКомплекты
		|ИЗ
		|	Документ.СрокиПроектаМонтаж.Комплекты КАК СрокиСделкиМонтажКомплекты
		|ГДЕ
		|	СрокиСделкиМонтажКомплекты.Ссылка.Проект = &Проект
		|	И СрокиСделкиМонтажКомплекты.Ссылка <> &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыКомплектаПроизводство.НачалоПроизводства КАК НачалоПроизводства,
		|	ДатыКомплектаПроизводство.КрайПроизводства КАК КрайПроизводства,
		|	ИзделияКомплект.Ссылка КАК Комплект
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплектаПроизводство КАК ДатыКомплектаПроизводство
		|		ПО (ДатыКомплектаПроизводство.Комплект = ИзделияКомплект.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ втСуществующиеКомплекты КАК втСуществующиеКомплекты
		|		ПО (втСуществующиеКомплекты.Комплект = ИзделияКомплект.Ссылка)
		|ГДЕ
		|	ИзделияКомплект.Проект = &Проект
		|	И НЕ ИзделияКомплект.Ссылка В (&Комплекты)
		|	И НЕ ИзделияКомплект.ЭтоГруппа
		|	И втСуществующиеКомплекты.Комплект ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Проект", Объект.Проект);
	Запрос.УстановитьПараметр("Комплекты", Объект.Комплекты.Выгрузить().ВыгрузитьКолонку("Комплект"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Объект.Комплекты.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьКомплектыНаСервере()


#КонецОбласти

&НаСервереБезКонтекста
Процедура ЗаписатьВРегистрНаСервере(мИзменений,Сделка)

	Для каждого х Из мИзменений Цикл
	
		МенеджерЗаписей = РегистрыСведений.ДатыКомплектаМонтаж.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.Комплект = х.Комплект;
		МенеджерЗаписей.Прочитать();
		
		ЗаполнитьЗначенияСвойств(МенеджерЗаписей,х);
		МенеджерЗаписей.Проект = Сделка;
		МенеджерЗаписей.Записать();
	
	КонецЦикла;	

КонецПроцедуры // ЗаписатьВРегистрНаСервере()

 &НаСервереБезКонтекста
Процедура ПроверитьУдалениеКомплекта(мКомплекты, Сделка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплектаМонтаж.Комплект КАК Комплект
		|ИЗ
		|	РегистрСведений.ДатыКомплектаМонтаж КАК ДатыКомплектаМонтаж
		|ГДЕ
		|	ДатыКомплектаМонтаж.Проект = &Проект";
	
	Запрос.УстановитьПараметр("Проект", Сделка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если мКомплекты.Найти(Выборка.Комплект) = Неопределено Тогда
			НаборЗаписей = РегистрыСведений.ДатыКомплектаМонтаж.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Комплект.Установить(Выборка.Комплект);
			
			НаборЗаписей.Записать();
		КонецЕсли;
	КонецЦикла;
	
	
КонецПроцедуры // 	ПроверитьУдалениеКомплекта(мКомплекты);¶()

&НаСервереБезКонтекста
Функция ПолучитьТекущегоПользователя()

	Возврат ПараметрыСеанса.ТекущийПользователь;

КонецФункции // ПолучитьТекущегоПользователя()

&НаКлиенте
Процедура ОткрытьФормуВыбораСделки()
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",						Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",				Ложь);
	ПараметрыФормы.Вставить("ОтсутствующиеВПланированииСделки",	Истина);
	ПараметрыФормы.Вставить("НаименованиеСрока",				"СрокиПроектаМонтаж");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыборкаПроекта", ЭтаФорма);
	
	ОткрытьФорму("Справочник.Проекты.ФормаВыбора",ПараметрыФормы,ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры // ОткрытьФормуВыбораСделки()

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	мИзменений = Новый Массив;
	
	Для каждого х Из Объект.Комплекты Цикл
		Если х.Изменен Тогда
			стСтрока = Новый Структура("КрайМонтажа,НачалоМонтажа,Комплект,НормаВремениМонтажа,КоличествоСотрудниковМонтаж", );
			ЗаполнитьЗначенияСвойств(стСтрока, х);
			мИзменений.Добавить(стСтрока);
			х.Изменен = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
