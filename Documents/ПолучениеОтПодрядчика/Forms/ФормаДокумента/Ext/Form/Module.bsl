
&НаСервереБезКонтекста
Функция ИзделияУПодрядчикаНаСервере(Подрядчик,мИзделмяВ_ТЧ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияУПодрядчикаОстатки.ИзделиеЭлемент КАК ИзделиеЭлемент
		|ИЗ
		|	РегистрНакопления.ИзделияУПодрядчика.Остатки(
		|			,
		|			Подрядчик = &Подрядчик
		|				И НЕ ИзделиеЭлемент В (&мИзделмяВ_ТЧ)) КАК ИзделияУПодрядчикаОстатки
		|ГДЕ
		|	ИзделияУПодрядчикаОстатки.КоличествоОстаток > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИзделияУПодрядчикаОстатки.ИзделиеЭлемент.Наименование";
	
	Запрос.УстановитьПараметр("мИзделмяВ_ТЧ", мИзделмяВ_ТЧ);
	Запрос.УстановитьПараметр("Подрядчик", Подрядчик);
	      
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИзделиеЭлемент");
	
КонецФункции

&НаКлиенте
Процедура ИзделияУПодрядчика(Команда)
	Если НЕ ЗначениеЗаполнено(Объект.Подрядчик) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнен подрядчик";
		Сообщение.Поле = "Объект.Подрядчик";
		Сообщение.Сообщить();
		
	    Возврат;
	КонецЕсли;
	
	мИзделмяВ_ТЧ = Новый Массив;
	Для каждого текИзделие Из Объект.ИзделияЭлемент Цикл
		мИзделмяВ_ТЧ.Добавить(текИзделие.ИзделиеЭлемент);
	КонецЦикла;

	тзНовыеИзделия = ИзделияУПодрядчикаНаСервере(Объект.Подрядчик,мИзделмяВ_ТЧ);
	Для каждого текИзделие Из тзНовыеИзделия Цикл
	    НС = Объект.ИзделияЭлемент.Добавить();
		НС.ИзделиеЭлемент = текИзделие; 
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	омЗаполнениеРеквизиов.ЗаполнитьРеквизитыСтандартные(Объект);
КонецПроцедуры
