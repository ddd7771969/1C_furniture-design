
&НаСервереБезКонтекста
Функция ЗаполнитьПомещенияНаСервере(Сделка, мПомещения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Помещения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Помещения КАК Помещения
		|ГДЕ
		|	Помещения.Проект = &Проект
		|	И НЕ Помещения.Ссылка В (&Ссылка)
		|	И НЕ Помещения.ЭтоГруппа
		|
		|УПОРЯДОЧИТЬ ПО
		|	Помещения.НомерПомещения";
	
	Запрос.УстановитьПараметр("Проект", Сделка);
	Запрос.УстановитьПараметр("Ссылка", мПомещения);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПомещения(Команда)
	Если ЗначениеЗаполнено(Объект.Проект) Тогда
		
		мПомещения = Новый Массив;
		Для каждого х Из Объект.Помещения Цикл
		
			мПомещения.Добавить(х.Помещение);	
		
		КонецЦикла;
		
		мДобавленныеПомещения = ЗаполнитьПомещенияНаСервере(Объект.Проект, мПомещения);
		
		Для каждого х Из мДобавленныеПомещения Цикл
		
			НС = Объект.Помещения.Добавить();
			НС.Помещение = х;
		
		КонецЦикла;
	
	Иначе
	
		ПоказатьПредупреждение(, "Не заполнена Проект");	
	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СделкаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Объект.Помещения.Количество() = 0 Тогда 
		ОткрытьФормуВыбораСделки();
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаОСменеСделке",ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещения,"При смене Проекты будет очищена табличная часть. Продоллжить?", РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ПослеВопросаОСменеСделке(Результат, ДополнительныйПараметр) Экспорт
    Если Результат = КодВозвратаДиалога.ДА Тогда
		ОткрытьФормуВыбораСделки();
    КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьФормуВыбораСделки()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",						Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",				Ложь);
	ПараметрыФормы.Вставить("ОтсутствующиеВПланированииСделки", Ложь);
	ПараметрыФормы.Вставить("НаименованиеСрока",				"ЗапускВРаботу");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыборкаПроекта", ЭтаФорма);
	
	ОткрытьФорму("Справочник.Проекты.ФормаВыбора",ПараметрыФормы,ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	

КонецПроцедуры // ОткрытьФормуВыбораСделки()

&НаКлиенте
Процедура ПослеВыборкаПроекта(Значение, ДополнительныйПараметр) Экспорт

    Если Значение = Неопределено Тогда  ///Если ничего не выбрать - вернется пустое значение (Неопределено)
        Возврат;
	КонецЕсли;
	
	Объект.Проект = Значение;
	Объект.Помещения.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура СделкаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПомещенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Копирование;
	
	Если НЕ ЗначениеЗаполнено(Объект.Проект) Тогда
	    ПоказатьПредупреждение(,"Не заполнена Проект."); 
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПомещенияПомещениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(объект.Проект) Тогда
	
		ПоказатьПредупреждение(,"Не заполнена Проект");
		Возврат;
	
	КонецЕсли;
	
	ДополнительныйПараметр = Новый Структура("ТекущиеДанные", Элемент.Родитель.ТекущиеДанные);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораПомещения",ЭтаФорма,ДополнительныйПараметр);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",Ложь);
	ПараметрыФормы.Вставить("Проект",Объект.Проект);
	
	НеПоказыватьВВыборе = Новый Массив;
	
	Для каждого х Из Объект.Помещения Цикл
		НеПоказыватьВВыборе.Добавить(х.Помещение);	
	КонецЦикла;
	
	ПараметрыФормы.Вставить("НеПоказыватьПомещения",НеПоказыватьВВыборе);
	
	ОткрытьФорму("Справочник.Помещения.ФормаВыбора",ПараметрыФормы,ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПомещения(Значение, ДополнительныйПараметр) Экспорт

    Если Значение = Неопределено Тогда  
        Возврат;
	КонецЕсли;
	
	ДополнительныйПараметр.ТекущиеДанные.Помещение = Значение;

	
КонецПроцедуры

&НаКлиенте
Процедура РасчитатьКрайююДатуГотовности(Команда)
	
	мПомещения = Новый Массив;
	
	Для каждого х Из Объект.Помещения Цикл
	
		мПомещения.Добавить(х.Помещение);	
	
	КонецЦикла;
	
	омПомещения.РасчитатьКрайююДатуГотовностиНаСервере(мПомещения);
КонецПроцедуры


