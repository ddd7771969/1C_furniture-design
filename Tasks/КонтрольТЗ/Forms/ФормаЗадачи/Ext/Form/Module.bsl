//Статус 1 принят, 2 отказ, 0 Ожидание, -1 новый
&НаКлиенте                     
Перем ПолноеИмяФайла;

&НаКлиенте                     
Процедура Принять(Команда)
	
	Если НЕ КонтрольКонструкторов() Тогда
	    Возврат;
	КонецЕсли;
	
	Объект.Выполнена 	= Истина;
	Объект.Статус 		= 1;
	Записать();
	
	//стРезультат = омТЗКлиент.СохранитьФайлТЗВоВременномКаталоге(Объект.ОбъектТЗ, -1, УникальныйИдентификатор, "Выгрузить");
	//
	//Если ПустаяСтрока(стРезультат.Ошибка) Тогда
	//	
	//	Закрыть();
	//	
	//Иначе
	//	
	//	ПоказатьПредупреждение(, стРезультат.Ошибка);	
	//	
	//КонецЕсли;
	//
	
	
	ИзменитьСтатусНаСервере(Объект.Статус, Объект.ОбъектТЗ, Объект.Версия, Объект.Комментарий);
	
	КоличествоЗадач = СоздатьЗадачиПКД();
	
КонецПроцедуры

Функция СоздатьЗадачиПКД()
	
	тзКомплекты = Комплекты.Выгрузить();
	
	мКомплекты = тзКомплекты.ВыгрузитьКолонку("Комплект");
	мКомплекты.Добавить(Объект.ОбъектТЗ);
	
	мКонструкторы = тзКомплекты.ВыгрузитьКолонку("Конструктор");
	мКонструкторы.Добавить(КонструкторКомпозиции);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Задачи_ПКД.ОбъектТЗ КАК ОбъектТЗ
		|ИЗ
		|	Задача.Задачи_ПКД КАК Задачи_ПКД
		|ГДЕ
		|	Задачи_ПКД.ОбъектТЗ В(&ОбъектТЗ)
		|	И Задачи_ПКД.Основание = &Основание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(Пользователи.Ссылка, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК Пользователи,
		|	Сотрудники.Ссылка КАК Сотрудники
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО (Пользователи.Сотрудник = Сотрудники.Ссылка)
		|ГДЕ
		|	Сотрудники.Ссылка В(&мКонструкторы)";
	
	Запрос.УстановитьПараметр("Основание", 		Объект.Ссылка);
	Запрос.УстановитьПараметр("ОбъектТЗ", 		мКомплекты);
	Запрос.УстановитьПараметр("мКонструкторы", 	мКонструкторы);
	
	Результат = Запрос.ВыполнитьПакет();
	
	стПользователиОбъекта = омСотрудники.СотрудникиОбъекта(Объект.ОбъектТЗ);
	
	ВедущийКонструктор = стПользователиОбъекта.ВедущийКонструктор;
	
	мСуществующиеЗадачи 	= Результат[0].Выгрузить().ВыгрузитьКолонку("ОбъектТЗ");
	
	тзСотрудникПользователь = Результат[1].Выгрузить();
	
	Автор = ПараметрыСеанса.ТекущийПользователь;
	
	стПараметры = Новый Структура();
	стПараметры.Вставить("ВедущийКонструктор", ВедущийКонструктор);
	стПараметры.Вставить("Автор", Автор);
	стПараметры.Вставить("Композиция", ?(ТипЗнч(Объект.ОбъектТЗ) = Тип("СправочникСсылка.Композиции"), Объект.ОбъектТЗ, Справочники.Композиции.ПустаяСсылка()));
	стПараметры.Вставить("мСуществующиеЗадачи", мСуществующиеЗадачи);
	стПараметры.Вставить("тзСотрудникПользователь", тзСотрудникПользователь);
	
	КомплектыКоличество = 0;
	Результат = СоздатьЗадачуПКД(Объект.ОбъектТЗ, КонструкторКомпозиции, стПараметры); 
	КомплектыКоличество = ?(Результат, КомплектыКоличество + 1, КомплектыКоличество); 
	
	Для каждого х Из Комплекты Цикл
		
		Результат = СоздатьЗадачуПКД(х.Комплект, х.Конструктор, стПараметры); 
		КомплектыКоличество = ?(Результат, КомплектыКоличество + 1, КомплектыКоличество); 
	КонецЦикла;
	
	Возврат КомплектыКоличество;
	
КонецФункции

Функция СоздатьЗадачуПКД(ОбъектТЗ, Конструктор, стПараметры)
	
	Если НЕ стПараметры.мСуществующиеЗадачи.Найти(ОбъектТЗ) = Неопределено Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	ПользовательКонструкторЭлемент = стПараметры.тзСотрудникПользователь.Найти(Конструктор, "Сотрудники");
	
	Если ПользовательКонструкторЭлемент = Неопределено Тогда
		ПользовательКонструктор = Справочники.Пользователи.ПустаяСсылка();	
	Иначе	
		ПользовательКонструктор = ПользовательКонструкторЭлемент.Пользователи;	
	КонецЕсли;
	
	Задача = Задачи.Задачи_ПКД.СоздатьЗадачу();
    Задача.Композиция			= стПараметры.Композиция;
	Задача.Автор 				= стПараметры.Автор;
	Задача.Дата 				= ТекущаяДата();
	Задача.Наименование			= Строка(ОбъектТЗ) + ".Ожидание.ПКД";
	Задача.Исполнитель 			= ПользовательКонструктор;
	Задача.ОбъектТЗ 			= ОбъектТЗ;
	Задача.Основание 			= Объект.Ссылка;
	Задача.Проект 				= Объект.Проект;
	Задача.ТипЗадачи			= Перечисления.ТипыЗадачКонструирования.Разработка; //Разработка
	Задача.СтатусЗадачи			= Перечисления.СтатусыЗадач.Разработка; //Разработка
	
	Если ЗначениеЗаполнено(стПараметры.Композиция) И КонструкторКомпозиции = Конструктор Тогда
		
		Задача.ОбобщеннаяЗадача	= Истина;                                                   
		
	КонецЕсли;
	
	НС = Задача.Наблюдатели.Добавить();
	НС.Наблюдатель = стПараметры.ВедущийКонструктор;
	
	Задача.Записать();
	
	МенеджерРегистра = РегистрыСведений.ТаймингЗадач;
	стДанные = МенеджерРегистра.ПолучитьСтруктуруДанных(Перечисления.СостоянияЗадач.ОжиданиеКонструктором, ПользовательКонструктор, Задача.СтатусЗадачи, Задача.ТипЗадачи);
	МенеджерРегистра.УстановитьСостояниеИсполнительЗадачи(Задача.Ссылка, стДанные); 
	
	омЗадачиПКДСервер.УстановитьСтадиюКомплекта(Задача.Ссылка);
	
	УстановитьНачалоВыполненияЗадачи(Задача.Ссылка, ОбъектТЗ);
	
	Возврат Истина;
	
КонецФункции  

&НаСервереБезКонтекста
Процедура УстановитьНачалоВыполненияЗадачи(Задача, ОбъектТЗ)
	
	Запрос = Новый Запрос;
	
		
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплекта.НачалоПКД КАК НачалоПКД
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|ГДЕ
		|	ДатыКомплекта.Комплект = &ОбъектТЗ";
		
	
	Запрос.УстановитьПараметр("ОбъектТЗ", ОбъектТЗ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		НачалоПКД = ТекущаяДата();
	Иначе	
		Выборка.Следующий();
		НачалоПКД = Выборка.НачалоПКД;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция КонтрольКонструкторов()
	
	Если НЕ ВыгруженВPDM Тогда
		ПоказатьПредупреждение(, Строка(Объект.ОбъектТЗ) + " нет выгрузки в PDM.");
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КонструкторКомпозиции) Тогда
		ПоказатьПредупреждение(, Строка(Объект.ОбъектТЗ) + " нет конструктора.");
		Возврат Ложь;
	КонецЕсли;
	
	Для каждого х Из Комплекты Цикл
		
		Если НЕ х.ВыгруженВPDM Тогда
			ПоказатьПредупреждение(, "Комплект " + Строка(х.Комплект) + " не выгружен в PDM.");
			Возврат Ложь;
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(х.Конструктор) Тогда
			ПоказатьПредупреждение(, "У комплекта " + Строка(х.Комплект) + " нет конструктора.");
			Возврат Ложь;
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции // КонтрольКонструкторов()

&НаКлиенте
Процедура Отказать(Команда)
	Если ПустаяСтрока(Объект.Комментарий) Тогда
	
		ПоказатьПредупреждение(, "Для отказа необходимо заполнить комментарий");	
		
	Иначе
		
		Объект.Выполнена = Истина;
		Объект.Статус = 2;
		Записать();
		Закрыть();
		
		ИзменитьСтатусНаСервере(Объект.Статус, Объект.ОбъектТЗ ,Объект.Версия, Объект.Комментарий);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТЗ(Команда)

	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьПредупреждение(, "Перед открытием ТЗ необходимо сохранить комплект");
	Иначе
		ОткрытьФорму("РегистрСведений.ТЗ_Изделий.Форма.Форма", Новый Структура("ОбъектТЗ", Объект.ОбъектТЗ));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписанФайлТЗ" Тогда
		
		Если ПустаяСтрока(Параметр.ПолноеИмяФайла) Тогда
			ПоказатьПредупреждение(, "Не удалось получить файл ТЗ");
		ИначеЕсли Параметр.ПараметрыСохранения = "Открыть" Тогда
			ЗапуститьПриложение(Параметр.ПолноеИмяФайла);
		ИначеЕсли Параметр.ПараметрыСохранения = "Выгрузить" Тогда
			
			стПараметры = Новый Структура("ПолноеИмяФайла, Версия", Параметр.ПолноеИмяФайла, Объект.Версия);
			
			Результат = омВыгрузкаCRMНаКлиенте.ВыгрузитьВPDM(Объект.ОбъектТЗ, "ЗагрузкаТЗ", стПараметры);
			
			Если ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
				Сообщить(Формат(ТекущаяДата(), "ДЛФ=DT") + " Не удалось перенести файла ТЗ " + Параметр.ПолноеИмяФайла + ". " + Результат.ОписаниеОшибки);
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзделиеКомплектИзменено" Тогда
		
		Для каждого х Из Комплекты Цикл
			
			Если х.Комплект = Параметр Тогда
				
				Обновить(Неопределено);
				Прервать;	
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьСтатусНаСервере(Статус, ОбъектТЗ ,Версия, Комментарий)
	
	МенеджерЗаписей = РегистрыСведений.ТЗ_Изделий.СоздатьМенеджерЗаписи();
	МенеджерЗаписей.ОбъектТЗ 	= ОбъектТЗ;
	МенеджерЗаписей.Версия 		= Версия;
	
	МенеджерЗаписей.Прочитать();
	
	МенеджерЗаписей.ОбъектТЗ 	= ОбъектТЗ;
	МенеджерЗаписей.Версия 		= Версия;
	МенеджерЗаписей.Статус 		= Статус;
	МенеджерЗаписей.Комментарий = Комментарий; 
	
	МенеджерЗаписей.Записать();
	
	Если Статус = 2 Тогда //Вернуть в канбан
		омКанбанТЗ.ПереместитьРеквизитВВозврат(ОбъектТЗ, Комментарий);	
	ИначеЕсли Статус = 1 Тогда //Завершить в канбан
		омКанбанТЗ.ПеревестиРеквизитВЗавершенный(ОбъектТЗ, Комментарий);	
	КонецЕсли;
	
КонецПроцедуры // ИзменитьСтатусНаСервере()

&НаКлиенте
Процедура КомплектыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КомплектыПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	
	ТД = Элементы.Комплекты.ТекущиеДанные;
	
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(Неопределено, ТД.Комплект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомплектыПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ТипЗнч(Объект.ОбъектТЗ) = Тип("СправочникСсылка.Композиции") Тогда
		
		Комплекты.Загрузить(ЗаполнитьКомплекты(Объект.ОбъектТЗ, Истина));
		Элементы.ОбъектТЗ.Заголовок = "Композиция";
	
	Иначе 
		
	    Элементы.ГруппаКомплекты.Видимость 	= Ложь;
		Элементы.ОбъектТЗ.Заголовок = "Комплект";

	КонецЕсли;
	
	стДанные = ПолучитьКонструктораОбъекта(Объект.ОбъектТЗ);
	КонструкторКомпозиции 	= стДанные.Конструктор;
	ВыгруженВPDM			= стДанные.ВыгруженВPDM;
	
	Элементы.ДекорацияОк.Видимость 		= ВыгруженВPDM;
	Элементы.ДекорацияСтоп.Видимость 	= НЕ ВыгруженВPDM;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполнитьКомплекты(Композиция, ВозвратВТаблице)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК Комплект,
		|	ВЫБОР
		|		КОГДА ДатыКомплекта.Код_в_PDM = 0
		|				ИЛИ ДатыКомплекта.Код_в_PDM ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ВыгруженВPDM,
		|	ДанныеКомплекта.Конструктор КАК Конструктор
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|		ПО (ДатыКомплекта.Комплект = ИзделияКомплект.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних КАК ДанныеКомплекта
		|		ПО (ДанныеКомплекта.Комплект = ИзделияКомплект.Ссылка)
		|ГДЕ
		|	ИзделияКомплект.Родитель.Композиция = &Композиция
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИзделияКомплект.Наименование";
	
	Запрос.УстановитьПараметр("Композиция", Композиция);
	
	тзДанные = Запрос.Выполнить().Выгрузить();
	
	Если ВозвратВТаблице Тогда
	
		Возврат тзДанные;	
	
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(тзДанные);

КонецФункции // ЗаполнитьКомплекты()

&НаСервереБезКонтекста
Функция ПолучитьКонструктораОбъекта(ОбъектТЗ)
	
	стДанные = Новый Структура("ВыгруженВPDM, Конструктор", Ложь);
	
	Запрос = Новый Запрос;

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДатыКомплекта.Код_в_PDM = 0
		|				ИЛИ ДатыКомплекта.Код_в_PDM ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ВыгруженВPDM,
		|	ДанныеКомплектаСрезПоследних.Конструктор КАК Конструктор
		|ИЗ
		|	РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект = &ОбъектТЗ) КАК ДанныеКомплектаСрезПоследних,
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|ГДЕ
		|	ДатыКомплекта.Комплект = &ОбъектТЗ";
		
	
	Запрос.УстановитьПараметр("ОбъектТЗ", ОбъектТЗ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(стДанные, Выборка); 
	КонецЦикла;
	
    Возврат стДанные;
	
КонецФункции // ПолучитьКонструктораОбъекта()

&НаСервереБезКонтекста
Процедура НазначитьОсновногоКонструктораНаСервере(КонструкторКомпозиции, мКомплекты)

	Автор = ПараметрыСеанса.ТекущийПользователь;
	ТД = ТекущаяДата();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеКомплектаСрезПоследних.Комплект КАК Комплект,
		|	ДанныеКомплектаСрезПоследних.Конструктор КАК Конструктор,
		|	ДанныеКомплектаСрезПоследних.ТекущаяСтадия КАК ТекущаяСтадия,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаНачалоМонтажа КАК ВнешняяДатаНачалоМонтажа,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаНачалоПроектирования КАК ВнешняяДатаНачалоПроектирования,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаНачалоПроизводства КАК ВнешняяДатаНачалоПроизводства,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаНачалоСогласования КАК ВнешняяДатаНачалоСогласования,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаОкончаниеМонтажа КАК ВнешняяДатаОкончаниеМонтажа,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаОкончаниеПроектирования КАК ВнешняяДатаОкончаниеПроектирования,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаОкончаниеПроизводства КАК ВнешняяДатаОкончаниеПроизводства,
		|	ДанныеКомплектаСрезПоследних.ВнешняяДатаОкончаниеСогласования КАК ВнешняяДатаОкончаниеСогласования,
		|	ДанныеКомплектаСрезПоследних.Приоритет КАК Приоритет
		|ИЗ
		|	РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект В (&мКомплекты)) КАК ДанныеКомплектаСрезПоследних";
	
	Запрос.УстановитьПараметр("мКомплекты", мКомплекты);
	
	тзКонструкторКомплект = Запрос.Выполнить().Выгрузить();
	
	Для каждого Комплект Из мКомплекты Цикл
		
		НайденаяСтрока = тзКонструкторКомплект.Найти(Комплект, "Комплект");
		
		МенеджерЗаписей = РегистрыСведений.ДанныеКомплекта.СоздатьМенеджерЗаписи();
		
		Если НайденаяСтрока = Неопределено Тогда
			
			ТекущаяСтадия = Неопределено;
			
		ИначеЕсли НайденаяСтрока.Конструктор = КонструкторКомпозиции Тогда
			
			Продолжить;
			
		Иначе	
			
			ЗаполнитьЗначенияСвойств(МенеджерЗаписей, НайденаяСтрока);
			
		КонецЕсли;
		
		МенеджерЗаписей.Период 			= ТД;
		МенеджерЗаписей.Комплект 		= Комплект;
		МенеджерЗаписей.Конструктор 	= КонструкторКомпозиции;
		МенеджерЗаписей.ТекущаяСтадия 	= ТекущаяСтадия;
		МенеджерЗаписей.Автор 			= Автор;
		
		МенеджерЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	стДанные = ПолучитьКонструктораОбъекта(Объект.ОбъектТЗ);
	КонструкторКомпозиции 	= стДанные.Конструктор;
	ВыгруженВPDM			= стДанные.ВыгруженВPDM;
	
	Элементы.ДекорацияОк.Видимость 		= ВыгруженВPDM;
	Элементы.ДекорацияСтоп.Видимость 	= НЕ ВыгруженВPDM;
	
	мКомплекты = ЗаполнитьКомплекты(Объект.ОбъектТЗ, Ложь);
	
	Комплекты.Очистить();
	
	Для каждого х Из мКомплекты Цикл
	
		ЗаполнитьЗначенияСвойств(Комплекты.Добавить(), х);	
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КонструкторКомпозицииПриИзменении(Элемент)
	
	мОбъектов = Новый Массив;
	
	Для каждого х Из Комплекты Цикл
		
		Если НЕ ЗначениеЗаполнено(х.Конструктор) Тогда
		
			мОбъектов.Добавить(х.Комплект);	
		
		КонецЕсли;
		
	КонецЦикла;
	
	Если мОбъектов.Количество() = 0 Тогда
		
		омКонструктор.ИзменитьКонструктораНаСервере(Объект.ОбъектТЗ, КонструкторКомпозиции);
		
	Иначе
		
		мОбъектов.Добавить(Объект.ОбъектТЗ);
		омКонструктор.ИзменитьКонструктораНаСервере(мОбъектов, КонструкторКомпозиции);
		
	КонецЕсли;
	
	Обновить(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	Записать();
	Закрыть();
КонецПроцедуры


 
