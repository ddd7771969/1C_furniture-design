///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

// СтандартныеПодсистемы

// Хранилище глобальных переменных.
//
// ПараметрыПриложения - Соответствие из КлючИЗначение:
//   * Ключ - Строка - имя переменной в формате "ИмяБиблиотеки.ИмяПеременной";
//   * Значение - Произвольный - значение переменной.
//
// Пример инициализации:
//   ИмяПараметра = "СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации";
//   Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
//     ПараметрыПриложения.Вставить(ИмяПараметра, Новый СписокЗначений);
//   КонецЕсли;
//  
// Пример использования:
//   ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"].Добавить(...);
//   ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"] = ...;
Перем ПараметрыПриложения Экспорт;
Перем Б24_К_НастройкиСеанса Экспорт;
Перем мДанныеЭлемента Экспорт;
Перем глстВозврата Экспорт;
Перем глТестКаталог Экспорт;
Перем глПутьКФайлуEXE Экспорт;
Перем глНаименованиеЭлемента Экспорт;
Перем глЭлементВыгрузки Экспорт;
Перем глТипВыгрузки Экспорт;
Перем глКолЭлементов Экспорт;
Перем глНомерФайла	Экспорт;
Перем глмДанныеЭлемента Экспорт;
// Конец СтандартныеПодсистемы

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередНачаломРаботыСистемы()
	//Битрикс24
	#Если НЕ МобильныйКлиент Тогда    
		
		Б24_К_НастройкиСеанса = Б24_К_ОбщегоНазначенияКлиент.ПолучитьСтруктуруГлобальныхПеременных();
		
		#Если ВебКлиент Тогда
			
			ЗапускатьВРежимеРабочегоСтола = Б24_К_ОбщегоНазначенияВызовСервера.ПолучитьЗначениеХранилищаНастроек("ОбщиеНастройки", "ЗапускатьВРежимеРабочегоСтола");
			
			Если ЗначениеЗаполнено(ПараметрЗапуска) И ЗапускатьВРежимеРабочегоСтола = Истина Тогда
				
				НомерСимволаСессии = СтрНайти(ПараметрЗапуска, "session=");
				НомерСимволаНавигационнойСсылки = СтрНайти(ПараметрЗапуска, "_url=");	
				НомерСимволаПользователяБитрикс24 = СтрНайти(ПараметрЗапуска, "idUserB24=");
				
				Если НомерСимволаПользователяБитрикс24 > 0 ИЛИ (НомерСимволаСессии > 0 И НомерСимволаНавигационнойСсылки > 0) Тогда
					КлиентскоеПриложение.УстановитьРежимОсновногоОкна(РежимОсновногоОкнаКлиентскогоПриложения.РабочееМесто);
				КонецЕсли;
				
			КонецЕсли;
			
		#КонецЕсли
		
	#КонецЕсли
	
	
	#Если МобильныйКлиент Тогда
		Если ОсновнойСерверДоступен() = Ложь Тогда
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	// СтандартныеПодсистемы
	#Если МобильныйКлиент Тогда
		Выполнить("СтандартныеПодсистемыКлиент.ПередНачаломРаботыСистемы()");
	#Иначе
		СтандартныеПодсистемыКлиент.ПередНачаломРаботыСистемы();
	#КонецЕсли
	// Конец СтандартныеПодсистемы
	
КонецПроцедуры

Процедура ПриНачалеРаботыСистемы()
	
	// СтандартныеПодсистемы
#Если МобильныйКлиент Тогда
	Выполнить("СтандартныеПодсистемыКлиент.ПриНачалеРаботыСистемы()");
#Иначе
	СтандартныеПодсистемыКлиент.ПриНачалеРаботыСистемы();
	
#КонецЕсли
	// Конец СтандартныеПодсистемы
	
КонецПроцедуры

Процедура ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения)
	
	// СтандартныеПодсистемы
#Если МобильныйКлиент Тогда
	Выполнить("СтандартныеПодсистемыКлиент.ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения)");
#Иначе
	СтандартныеПодсистемыКлиент.ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения);
#КонецЕсли
	// Конец СтандартныеПодсистемы
	
КонецПроцедуры

Процедура ОбработкаПолученияФормыВыбораПользователейСистемыВзаимодействия(НазначениеВыбора,
			Форма, ИдентификаторОбсуждения, Параметры, ВыбраннаяФорма, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы
#Если МобильныйКлиент Тогда
	Выполнить("СтандартныеПодсистемыКлиент.ОбработкаПолученияФормыВыбораПользователейСистемыВзаимодействия(НазначениеВыбора,
		|Форма, ИдентификаторОбсуждения, Параметры, ВыбраннаяФорма, СтандартнаяОбработка)");
#Иначе
	СтандартныеПодсистемыКлиент.ОбработкаПолученияФормыВыбораПользователейСистемыВзаимодействия(НазначениеВыбора,
		Форма, ИдентификаторОбсуждения, Параметры, ВыбраннаяФорма, СтандартнаяОбработка);
#КонецЕсли
	// Конец СтандартныеПодсистемы
	
КонецПроцедуры

#КонецОбласти

#Область Битрикс24
Процедура Б24_К_ПриНачалеРаботыСистемы()

#Если НЕ МобильныйКлиент Тогда    
		
	Если Б24_К_ОбщегоНазначенияВызовСервера.ЭтотПользовательПолучаетОповещения() Тогда
		ПодключитьОбработчикОжидания("Б24_К_ОбработатьСообщенияОбОшибках", 180);
	КонецЕсли;
	
	Если НЕ Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.БазаИспользуетсяВМоделиСервиса() Тогда	
		Б24_К_ОбщегоНазначенияКлиент.ПроверитьНаОбновление();
	КонецЕсли;	

	ТелефонияБитрикс24 =  Б24_К_ТелефонияВызовСервера.ПолучитьКоличествоНастроекПодключенияСТелефонией() > 0; 
	Если ТелефонияБитрикс24 = Истина Тогда
		МодульТелефонияКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("Б24_К_ТелефонияКлиент");
		Б24_К_НастройкиСеанса.НастройкиТелефонии.МессенджерУстановлен = Б24_К_ТелефонияКлиент.ПроверитьНаличиеМессенджераБитрикс24();
	Иначе
		Б24_К_НастройкиСеанса.НастройкиТелефонии.МессенджерУстановлен = Ложь; 
	КонецЕсли;
	
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("РаботаИзОдногоОкна") Тогда
		
#Если ВебКлиент Тогда
		Если ЗначениеЗаполнено(ПараметрЗапуска) Тогда
			
			ДлинаПараметра 						= СтрДлина(ПараметрЗапуска);
			НомерСимволаСессии 					= СтрНайти(ПараметрЗапуска, "session=");
			НомерСимволаПользователяБитрикс24 	= СтрНайти(ПараметрЗапуска, "idUserB24=");
			НомерСимволаНавигационнойСсылки 	= СтрНайти(ПараметрЗапуска, "_url=");	
			НомерСимволаПортала 				= СтрНайти(ПараметрЗапуска, "_b24=");	
			
			Если НомерСимволаПользователяБитрикс24 > 0 И НомерСимволаПортала > 0 Тогда  
				
				Б24_К_НастройкиСеанса.НастройкиГорячегоЗапуска.ИдПользователяБитрикс24 = Сред(ПараметрЗапуска, НомерСимволаПользователяБитрикс24+10, НомерСимволаПортала-НомерСимволаПользователяБитрикс24-10); 
				
				Б24_К_НастройкиСеанса.НастройкиГорячегоЗапуска.Портал = Прав(ПараметрЗапуска, ДлинаПараметра - НомерСимволаПортала-4);
				
				Если ЗначениеЗаполнено(Б24_К_НастройкиСеанса.НастройкиГорячегоЗапуска.ИдПользователяБитрикс24) И ЗначениеЗаполнено(Б24_К_НастройкиСеанса.НастройкиГорячегоЗапуска.Портал) Тогда
					ПодключитьОбработчикОжидания("Б24_КБ_ПроверкаНаличиеНавигационнойСсылкиИзБитрикс24", 1);
				КонецЕсли;
				
			ИначеЕсли НомерСимволаСессии > 0 И НомерСимволаНавигационнойСсылки > 0 Тогда 
				
				СессияБитрикс24 = Сред(ПараметрЗапуска, НомерСимволаСессии+8, НомерСимволаНавигационнойСсылки-НомерСимволаСессии-8); 
				
				АдресНавигационнойСсылки = Прав(ПараметрЗапуска, ДлинаПараметра - НомерСимволаНавигационнойСсылки-4);
				АдресНавигационнойСсылки = ?(Лев(АдресНавигационнойСсылки, 1) = "#", Прав(АдресНавигационнойСсылки, СтрДлина(АдресНавигационнойСсылки)-1), АдресНавигационнойСсылки);
				
				НомерСимволаЛогина = СтрНайти(АдресНавигационнойСсылки, "&N=");
				Если НомерСимволаЛогина > 0 Тогда
					АдресНавигационнойСсылки = Лев(АдресНавигационнойСсылки, НомерСимволаЛогина-1);
				КонецЕсли;
				
				МодульРаботаИзОдногоОкнаВызовСервера = ОбщегоНазначенияКлиент.ОбщийМодуль("Б24_КБ_РаботаИзОдногоОкнаВызовСервера");
				Результат = МодульРаботаИзОдногоОкнаВызовСервера.ПроверитьНаличиеНавигационнойСсылкиИзБитрикс24ПоСессии(СессияБитрикс24);
				Если Результат <> Неопределено Тогда

					Б24_К_НастройкиСеанса.НастройкиГорячегоЗапуска.Браузер	= Результат.Браузер; 
					
					ДанныеСеанса = Б24_К_ОбщегоНазначенияКлиент.ПолучитьСтруктуруДанныхСеанса();
					ДанныеСеанса.СессияБитрикс24 			= Результат.Сессия;
					ДанныеСеанса.НастройкаПодключения 		= Результат.НастройкаПодключения;
					ДанныеСеанса.НоваяСущность 				= СтрНайти(АдресНавигационнойСсылки, "?ref") = 0 И СтрНайти(АдресНавигационнойСсылки, "/РегистрСведений.") = 0;
					ДанныеСеанса.АдресНавигационнойСсылки	= АдресНавигационнойСсылки;
					
					МодульРаботаИзОдногоОкнаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("Б24_КБ_РаботаИзОдногоОкнаКлиент");
					МодульРаботаИзОдногоОкнаКлиент.ПерейтиПоСсылке(ДанныеСеанса);
					
				КонецЕсли;
				
			КонецЕсли; 				
			
		КонецЕсли;
			
#КонецЕсли
		
	КонецЕсли;
	
#Область ПроверкаНаОбновлениеДанных
	Б24_К_ОбщегоНазначенияВызовСервера.ПроверкаОбновленияДанныхМодуляОбменаСБитрикс24(); 
#КонецОбласти  

#КонецЕсли
КонецПроцедуры

Процедура Б24_К_ОбработатьСообщенияОбОшибках() Экспорт
	Б24_К_ОбщегоНазначенияКлиент.ПроверитьНаличиеОшибокИОповестить();
КонецПроцедуры


Процедура Б24_КБ_ПроверкаНаличиеНавигационнойСсылкиИзБитрикс24() Экспорт
	
	Если Б24_К_ОбщегоНазначенияВызовСервераПовтИсп.ПодсистемаСуществует("РаботаИзОдногоОкна") Тогда
		
		МодульРаботаИзОдногоОкнаВызовСервера = ОбщегоНазначенияКлиент.ОбщийМодуль("Б24_КБ_РаботаИзОдногоОкнаВызовСервера");
		Результат = МодульРаботаИзОдногоОкнаВызовСервера.ПроверитьНаличиеНавигационнойСсылкиИзБитрикс24ПоПользователюИПорталу(Б24_К_НастройкиСеанса.НастройкиГорячегоЗапуска.ИдПользователяБитрикс24, Б24_К_НастройкиСеанса.НастройкиГорячегоЗапуска.Портал);    
		
		Если Результат <> Неопределено Тогда
			Если ЗначениеЗаполнено(Результат.Ссылка) Тогда
				
				Б24_К_НастройкиСеанса.НастройкиГорячегоЗапуска.Браузер	= Результат.Браузер;
				
				ДанныеСеанса = Б24_К_ОбщегоНазначенияКлиент.ПолучитьСтруктуруДанныхСеанса();
				ДанныеСеанса.СессияБитрикс24 			= Результат.Сессия;
				ДанныеСеанса.НастройкаПодключения 		= Результат.НастройкаПодключения;
				ДанныеСеанса.НоваяСущность 				= СтрНайти(Результат.Ссылка, "?ref") = 0 И  СтрНайти(Результат.Ссылка, "/РегистрСведений.") = 0;
				ДанныеСеанса.АдресНавигационнойСсылки	= Результат.Ссылка;
				
				МодульРаботаИзОдногоОкнаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("Б24_КБ_РаботаИзОдногоОкнаКлиент");
				МодульРаботаИзОдногоОкнаКлиент.ПерейтиПоСсылке(ДанныеСеанса);
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти
