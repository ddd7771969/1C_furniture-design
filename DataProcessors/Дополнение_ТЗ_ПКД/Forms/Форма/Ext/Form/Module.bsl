
&НаКлиенте
Перем УИД_Вызова, ФайлТЗ;

&НаСервереБезКонтекста
Функция ПрикрепитьНаСервере()
	// Вставить содержимое обработчика.
КонецФункции

&НаКлиенте
Процедура Прикрепить(Команда)
	
	стрОшибка = ПрикрепитьФайл();
	
	Если НЕ ПустаяСтрока(стрОшибка) Тогда
		ПоказатьПредупреждение(, стрОшибка);	
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Функция ПрикрепитьФайл()
	
	Если ПустаяСтрока(ОписаниеИзменения) И ПустаяСтрока(ИмяФайла) Тогда
		Возврат "Нет описания изменения или файла с корректировкой ТЗ";	
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		
		СохранитьИнформациюВРегистр(Неопределено, ОписаниеИзменения, ОбъектТЗ, ОтЗаказчика);
		
	Иначе
		
		ФайлТЗ = Новый Файл(ИмяФайла);
		
		Если НЕ ФайлТЗ.Существует() Тогда
			Возврат "Файл " + ИмяФайла + " был перемещен или удален.";	
		КонецЕсли;
		
		УИД_Вызова = Новый УникальныйИдентификатор;
		
		Элементы.ФормаПрикрепить.Доступность = Ложь;
		
		омФайлыНаКлиенте.ПереместитьФайлНаСервер(ИмяФайла, УИД_Вызова);	
		
	КонецЕсли;

КонецФункции // ПрикрепитьФайл()

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	
	Элементы.ФормаПрикрепить.Доступность = Ложь;
	ОбъектТЗ = ПредопределенноеЗначение("Справочник.Композиции.ПустаяСсылка");
	
	Если ЗначениеЗаполнено(Проект) Тогда
		Элементы.ОбъектТЗ.Доступность = Истина;
		Элементы.ОбъектТЗ.СписокВыбора.ЗагрузитьЗначения(ПолучитьОбъектыПроекта(Проект));
	Иначе
		Элементы.ОбъектТЗ.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОбъектыПроекта(Проект)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА ИзделияКомплект.Родитель.Композиция = ЗНАЧЕНИЕ(Справочник.Композиции.ПустаяСсылка)
		|			ТОГДА ИзделияКомплект.Ссылка
		|		ИНАЧЕ ИзделияКомплект.Родитель.Композиция
		|	КОНЕЦ КАК Ссылка
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|ГДЕ
		|	НЕ ИзделияКомплект.ЭтоГруппа
		|	И ИзделияКомплект.Проект = &Проект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	вт.Ссылка КАК Ссылка
		|ИЗ
		|	вт КАК вт
		|		ЛЕВОЕ СОЕДИНЕНИЕ Задача.Задачи_ПКД КАК Задачи_ПКД
		|		ПО вт.Ссылка = Задачи_ПКД.ОбъектТЗ
		|ГДЕ
		|	ВЫБОР
		|			КОГДА Задачи_ПКД.ТипЗадачи ЕСТЬ NULL
		|				ТОГДА ИСТИНА
		|			КОГДА Задачи_ПКД.ТипЗадачи В (&ТипыЗадач)
		|					И НЕ Задачи_ПКД.Выполнена
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	вт.Ссылка.Наименование";
	
	Запрос.УстановитьПараметр("Проект", 	Проект);
	Запрос.УстановитьПараметр("ТипыЗадач", 	омЗадачиПКДПовтор.ТипыЗадачКонструктора());
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции // ПолучитьОбъектыПроекта()

&НаКлиенте
Процедура ФайлОткрытие(Элемент, СтандартнаяОбработка) 
	
	СтандартнаяОбработка = Ложь;  
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие); 
	Диалог.МножественныйВыбор 	= Ложь;
	Диалог.Фильтр 				= "Файл PDF|*.pdf";  
	Диалог.Заголовок 			= "Выберите файл PDF"; 
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтаФорма); 
	Диалог.Показать(ОповещениеЗавершения)	

КонецПроцедуры  

&НаКлиенте 
Процедура ВыборФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт  
	Если НЕ ВыбранныеФайлы = Неопределено И ВыбранныеФайлы.Количество() > 0 Тогда  
		ИмяФайла = ВыбранныеФайлы[0];  
	КонецЕсли  
КонецПроцедуры

&НаКлиенте
Процедура ОбъектТЗПриИзменении(Элемент)
	Элементы.ФормаПрикрепить.Доступность = ЗначениеЗаполнено(ОбъектТЗ);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗавершенПереместитьФайлНаСервер" Тогда
	
		Если ТипЗнч(Параметр) = Тип("Структура") Тогда
		
			Если Параметр.УИД_Вызова = УИД_Вызова Тогда
			
				Если ПустаяСтрока(Параметр.Ошибка) Тогда
				
					стДанныеФайла = Новый Структура("АдресФайла, ИмяФайла, Расширение", 
										Параметр.АдресФайла, ФайлТЗ.ИмяБезРасширения, Сред(ФайлТЗ.Расширение, 2));
					
					СохранитьИнформациюВРегистр(стДанныеФайла, ОписаниеИзменения, ОбъектТЗ, ОтЗаказчика);
					Закрыть();
					
				Иначе
					
					Элементы.ФормаПрикрепить.Доступность = Истина;
					ПоказатьПредупреждение(, "Не удалось перенести файл " + Параметр.ИмяФайла  + " на сервер. "
											+ Параметр.Ошибка);
				
				КонецЕсли;	
			
			КонецЕсли;	
		
		КонецЕсли;	
	
	КонецЕсли;
	
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура СохранитьИнформациюВРегистр(стДанныеФайла, ОписаниеИзменения, ОбъектТЗ, ОтЗаказчика)

	МенеджерЗаписи = РегистрыСведений.Дополнение_к_ТЗ.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.ДатаДобавления 		= ТекущаяДата();
	МенеджерЗаписи.Комплект 			= ОбъектТЗ;
	МенеджерЗаписи.АвторДобавления 		= ПараметрыСеанса.ТекущийПользователь;
	МенеджерЗаписи.ОписаниеИзменения 	= ОписаниеИзменения;
	МенеджерЗаписи.ОтЗаказчика 			= ОтЗаказчика;
	
	Если стДанныеФайла = Неопределено Тогда
		
		МенеджерЗаписи.ЕстьФайл = Ложь;
		
	Иначе
		
		МенеджерЗаписи.ФайлТЗ 		= Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(стДанныеФайла.АдресФайла)); 
		МенеджерЗаписи.ИмяФайла 	= стДанныеФайла.ИмяФайла; 
		МенеджерЗаписи.Расширение	= стДанныеФайла.Расширение; 
		МенеджерЗаписи.ЕстьФайл 	= Истина;
		
	КонецЕсли; 
	
	МенеджерЗаписи.Записать();
        
КонецПроцедуры // СохранитьИнформациюВРегистр(Параметр.Адрес, ОписаниеИзменения, ОбъектТЗ)

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(Параметры) = Тип("ДанныеФормыСтруктура") И Параметры.Свойство("ОтЗаказчика") Тогда
		ОтЗаказчика = Параметры.ОтЗаказчика;	
	КонецЕсли;
	
	ЭтаФорма.Заголовок = ?(ОтЗаказчика, "Корректировка Заказчика", "Изменение Дизайнера");
	
КонецПроцедуры


