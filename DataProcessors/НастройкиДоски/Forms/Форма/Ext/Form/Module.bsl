
&НаКлиенте
Перем СтруктураКартинки, ИзмененаСтруктураПереходов;

#Область Команды

&НаКлиенте
Процедура ДобавитьДоску(Команда)

	ОписаниеОповещение = Новый ОписаниеОповещения("ПослеВводаНаименованиеКамбана", ЭтаФорма);
	ПоказатьВводСтроки(ОписаниеОповещение, "", "Наименование доски", 100, Истина);
	
КонецПроцедуры 

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьНаСервере(СтруктураКартинки, ИзмененаСтруктураПереходов);
КонецПроцедуры 

&НаКлиенте
Процедура ДобавитьКудаКолонки(Команда)
	
	ТД = Элементы.СписокОткудаКолонки.ТекущиеДанные;
	
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбора = Новый СписокЗначений;
	
	Для каждого х Из Колонки Цикл 
		
		Если СписокКудаКолонки.НайтиПоЗначению(х.УИД) = Неопределено Тогда
			СписокВыбора.Добавить(х.УИД, х.Наименование);
		КонецЕсли;
		
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораКудаКолонки", ЭтаФорма);
	СписокВыбора.ПоказатьВыборЭлемента(Оповещение, "Куда переместить"); 
	
КонецПроцедуры

&НаКлиенте
Процедура СледующаяКартинкаКорзины(Команда)
	КартинкаКорзиныНажатиеНаСервере(СтруктураКартинки);
КонецПроцедуры

#КонецОбласти
            

#Область ВыполнениеКоманды

&НаКлиенте
Процедура ПослеВыбораКудаКолонки(ВыборанноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыборанноеЗначение = Неопределено Тогда
	
		Возврат;	
	
	КонецЕсли;
	
	СписокКудаКолонки.Добавить(ВыборанноеЗначение.Значение, ВыборанноеЗначение.Представление);
	Заполнить_тзРазрешенныеПереходыКолонки();
	
КонецПроцедуры // ПослеВводаНаименованиеКамбана()

&НаКлиенте
Процедура ПослеВводаНаименованиеКамбана(НаименованиеДоски, ДополнительныеПараметры) Экспорт
	
    Если НЕ ЗначениеЗаполнено(НаименованиеДоски) Тогда
	
		Возврат;	
	
	КонецЕсли;  
	
	Если КонтрольДублейДоски(НаименованиеДоски) Тогда
	
		ПоказатьПредупреждение(, "Доска с именем " + НаименованиеДоски + " уже существует.");
		
	Иначе
		
		СоздатьНовуюДоску(НаименованиеДоски);
		ЗаполнитьСписокДоск(Доска);
		
	КонецЕсли;
	
КонецПроцедуры // ПослеВводаНаименованиеКамбана()

&НаКлиенте
Процедура Заполнить_тзРазрешенныеПереходыКолонки()
	
	ТД = Элементы.СписокОткудаКолонки.ТекущиеДанные;
	
	Если ТД = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	стОтбор = Новый Структура("УИДОткуда", ТД.УИД);
	
	мУдалитьСтроки = Новый Массив;
	
	Для каждого х Из тзРазрешенныеПереходы.НайтиСтроки(стОтбор) Цикл
		
		НайденаСтрока = Ложь;
		
		Для каждого у Из СписокКудаКолонки Цикл
			
			Если х.УИДКуда = у.Значение Тогда
			
				НайденаСтрока = Истина;
				Прервать;
			
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ НайденаСтрока Тогда
			мУдалитьСтроки.Добавить(х);	
		КонецЕсли;
		
	КонецЦикла;	
	
	Для каждого х Из мУдалитьСтроки Цикл
		тзРазрешенныеПереходы.Удалить(х);
		ИзмененаСтруктураПереходов = Истина;
	КонецЦикла;
	
	стОтбор.Вставить("УИДКуда",);
	
	Для каждого у Из СписокКудаКолонки Цикл
	
		стОтбор.УИДКуда = у.Значение;
		
		НайденыеСтроки = тзРазрешенныеПереходы.НайтиСтроки(стОтбор);
		
		Если НайденыеСтроки.Количество() = 0 Тогда
		
			НС = тзРазрешенныеПереходы.Добавить();
			ЗаполнитьЗначенияСвойств(НС, стОтбор);
			НС.ТребуетсяКомментарий 	= ТребуетсяКомментарийКолонки;
			НС.ЗаголовокКомментария		= ЗаголовокКомментарияКолонки;
			НС.ТребуетсяДата 			= ТребуетсяДатаКолонки;
			НС.ЗаголовокДаты 			= ЗаголовокДатыКолонки;
			
			ИзмененаСтруктураПереходов = Истина;
			
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры // 	Заполнить_тзРазрешенныеПереходыКолонки()

&НаСервереБезКонтекста
Функция СоздатьНовуюДоску(НаименованиеДоски)

	УИДДоски = Новый УникальныйИдентификатор();
	
	МенеджерЗаписей = РегистрыСведений.КанбанДоски.СоздатьМенеджерЗаписи();
	МенеджерЗаписей.УИД 			= УИДДоски;
	МенеджерЗаписей.Наименование 	= НаименованиеДоски;
	
	МенеджерЗаписей.Записать();
	
	
	МенеджерЗаписей = РегистрыСведений.КанбанПрава.СоздатьМенеджерЗаписи();
	МенеджерЗаписей.Доска 			= УИДДоски;
	МенеджерЗаписей.Пользователь 	= ПараметрыСеанса.ТекущийПользователь;
	МенеджерЗаписей.Просмотр 		= Истина;
	МенеджерЗаписей.Добавление 		= Истина;
	МенеджерЗаписей.Измениение 		= Истина;
	МенеджерЗаписей.Удаление 		= Истина;
	
	МенеджерЗаписей.Записать();
	
	

КонецФункции // СоздатьНовуюДоску()

&НаСервереБезКонтекста
Функция КонтрольДублейДоски(НаименованиеДоски)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КанбанДоски.УИД КАК УИД
		|ИЗ
		|	РегистрСведений.КанбанДоски КАК КанбанДоски
		|ГДЕ
		|	НРЕГ(КанбанДоски.Наименование) = НРЕГ(&НаименованиеДоски)";
	
	Запрос.УстановитьПараметр("НаименованиеДоски", НаименованиеДоски);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
	
		Возврат Ложь;
	
	КонецЕсли;
	
	Возврат Истина;

КонецФункции // СоздатьНовуюДоску()

Процедура СохранитьНаСервере(СтруктураКартинки, ИзмененаСтруктураПереходов)
	
	РазделитнльЧислом = ?(ИспользоватьПроекты, 1, 0) + ?(ИспользоватьСотрудников, 2, 0);
	
	МенеджерЗаписей = РегистрыСведений.КанбанДоски.СоздатьМенеджерЗаписи();
	МенеджерЗаписей.УИД = Доска;
	
	МенеджерЗаписей.Прочитать();
	
	Если НЕ (МенеджерЗаписей.Разделитель = РазделитнльЧислом И МенеджерЗаписей.ИмяКартинкиКорзины = СтруктураКартинки.ИмяКартинкиКорзины)  Тогда
	
		МенеджерЗаписей.Разделитель 		= РазделитнльЧислом;	
		МенеджерЗаписей.ИмяКартинкиКорзины 	= СтруктураКартинки.ИмяКартинкиКорзины;	
		
		МенеджерЗаписей.Записать();

	КонецЕсли;
	
	ЗаписатьКолонки();
	ЗаписатьДорожки();
	ЗаписатьПрава();
	
	Если ИзмененаСтруктураПереходов Тогда
	
		ЗаписатьРазрешенныеПереходы();
	
	КонецЕсли;
	
КонецПроцедуры 

Процедура ЗаписатьРазрешенныеПереходы()

    НаборЗаписей = РегистрыСведений.КанбанРазрешенныеПереходы.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Доска.Установить(Доска);
	
	Для каждого х Из тзРазрешенныеПереходы Цикл
	
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, х);
		НоваяЗапись.Доска = Доска;
	
	КонецЦикла;
	
	НаборЗаписей.Записать();

КонецПроцедуры


Процедура ЗаписатьПрава()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанПрава.Пользователь КАК Пользователь,
		|	КанбанПрава.Просмотр КАК Просмотр,
		|	КанбанПрава.Добавление КАК Добавление,
		|	КанбанПрава.Измениение КАК Измениение,
		|	КанбанПрава.Удаление КАК Удаление
		|ИЗ
		|	РегистрСведений.КанбанПрава КАК КанбанПрава
		|ГДЕ
		|	КанбанПрава.Доска = &Доска
		|
		|УПОРЯДОЧИТЬ ПО
		|	КанбанПрава.Пользователь.Наименование";
	
	Запрос.УстановитьПараметр("Доска", Доска);
	
	Выборка = Запрос.Выполнить().Выбрать();

	ОписаниеБулево = Новый ОписаниеТипов("Булево");
	
	тзКолонки = тзПрава.Выгрузить();
	тзКолонки.Колонки.Добавить("Соответствует", ОписаниеБулево);
	
	МаксимальныИндекс = тзКолонки.Количество() - 1;
	
	Пока Выборка.Следующий() Цикл
		
		Найден = Ложь;
		
		Для х = 0 По МаксимальныИндекс Цикл
			
			текСтрока = тзКолонки[х];

			Если НЕ текСтрока.Пользователь = Выборка.Пользователь Тогда
				Продолжить;
			КонецЕсли;
			
			текСтрока.Соответствует = Истина;
			Найден 					= Истина;
			
			Если текСтрока.Просмотр = Выборка.Просмотр  И текСтрока.Добавление = Выборка.Добавление 
				И текСтрока.Измениение = Выборка.Измениение И текСтрока.Удаление = Выборка.Удаление Тогда
				
				Прервать;
			
			КонецЕсли;
			
			МенеджерЗаписей = РегистрыСведений.КанбанПрава.СоздатьМенеджерЗаписи();
			МенеджерЗаписей.Доска 			= Доска;
			МенеджерЗаписей.Пользователь 	= текСтрока.Пользователь;
			МенеджерЗаписей.Просмотр 		= текСтрока.Просмотр;
			МенеджерЗаписей.Добавление 		= текСтрока.Добавление;
			МенеджерЗаписей.Измениение 		= текСтрока.Измениение;
			МенеджерЗаписей.Удаление 		= текСтрока.Удаление;
			
			МенеджерЗаписей.Записать();
			
			Прервать;
		
		КонецЦикла;
		
		Если НЕ Найден Тогда
		
			НаборЗаписей = РегистрыСведений.КанбанПрава.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Доска.Установить(Доска);
			НаборЗаписей.Отбор.Пользователь.Установить(Выборка.Пользователь);
			
			НаборЗаписей.Записать();
		
		КонецЕсли;
			
	КонецЦикла;
	
	Для х = 0 По МаксимальныИндекс Цикл

		текСтрока = тзКолонки[х];
		
		Если текСтрока.Соответствует Тогда
			Продолжить;
		КонецЕсли;
		
		МенеджерЗаписей = РегистрыСведений.КанбанПрава.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.Доска 			= Доска;
		МенеджерЗаписей.Пользователь 	= текСтрока.Пользователь;
		МенеджерЗаписей.Просмотр 		= текСтрока.Просмотр;
		МенеджерЗаписей.Добавление 		= текСтрока.Добавление;
		МенеджерЗаписей.Измениение 		= текСтрока.Измениение;
		МенеджерЗаписей.Удаление 		= текСтрока.Удаление;
		
		МенеджерЗаписей.Записать();
		
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ЗаписатьДорожки()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанДорожки.УИД КАК УИД,
		|	КанбанДорожки.Наименование КАК Наименование,
		|	КанбанДорожки.НомерПоПорядку КАК НомерПоПорядку,
		|	КанбанДорожки.ЛимитКарточек КАК ЛимитКарточек,
		|	КанбанДорожки.ЛимитЧасов КАК ЛимитЧасов,
		|	КанбанДорожки.ИмяСобытия КАК ИмяСобытия,
		|	КанбанДорожки.Цвет КАК Цвет,
		|	КанбанДорожки.Общая КАК Общая
		|ИЗ
		|	РегистрСведений.КанбанДорожки КАК КанбанДорожки
		|ГДЕ
		|	КанбанДорожки.Доска = &Доска";
	
	Запрос.УстановитьПараметр("Доска", Доска);
	
	Выборка = Запрос.Выполнить().Выбрать();

	ОписаниеБулево = Новый ОписаниеТипов("Булево");
	
	тзКолонки = Дорожки.Выгрузить();
	тзКолонки.Колонки.Добавить("Соответствует", ОписаниеБулево);
	
	МаксимальныИндекс = тзКолонки.Количество() - 1;
	
	Пока Выборка.Следующий() Цикл
		
		Найден = Ложь;
		
		Для х = 0 По МаксимальныИндекс Цикл
			
			текСтрока = тзКолонки[х];

			Если НЕ текСтрока.УИД = Выборка.УИД ИЛИ ПустаяСтрока(текСтрока.УИД) Тогда
				Продолжить;
			КонецЕсли;
			
			текСтрока.Соответствует = Истина;
			Найден 					= Истина;
			
			Если текСтрока.Наименование = Выборка.Наименование И х + 1 = Выборка.НомерПоПорядку И текСтрока.ИмяСобытия = Выборка.ИмяСобытия И текСтрока.Общая = Выборка.Общая 
				И текСтрока.ЛимитКарточек = Выборка.ЛимитКарточек И текСтрока.ЛимитЧасов = Выборка.ЛимитЧасов И текСтрока.Цвет = Выборка.Цвет Тогда
				
				Прервать;
			
			КонецЕсли;
			
			МенеджерЗаписей = РегистрыСведений.КанбанДорожки.СоздатьМенеджерЗаписи();
			МенеджерЗаписей.УИД 			= Выборка.УИД;
			МенеджерЗаписей.Доска 			= Доска;
			МенеджерЗаписей.НомерПоПорядку 	= х + 1;
			МенеджерЗаписей.Наименование 	= текСтрока.Наименование;
			МенеджерЗаписей.ИмяСобытия 		= текСтрока.ИмяСобытия;
			МенеджерЗаписей.ЛимитКарточек 	= текСтрока.ЛимитКарточек;
			МенеджерЗаписей.ЛимитЧасов 		= текСтрока.ЛимитЧасов;
			МенеджерЗаписей.Цвет 			= текСтрока.Цвет;
			
			МенеджерЗаписей.Записать();
			
			Прервать;
		
		КонецЦикла;
		
		Если НЕ Найден Тогда
		
			НаборЗаписей = РегистрыСведений.КанбанДорожки.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.УИД.Установить(Выборка.УИД);
			
			НаборЗаписей.Записать();
		
		КонецЕсли;
			
	КонецЦикла;
	
	Для х = 0 По МаксимальныИндекс Цикл

		текСтрока = тзКолонки[х];
		
		Если текСтрока.Соответствует Тогда
			Продолжить;
		КонецЕсли;
		
		МенеджерЗаписей = РегистрыСведений.КанбанДорожки.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.УИД 			= Новый УникальныйИдентификатор();
		МенеджерЗаписей.Доска 			= Доска;
		МенеджерЗаписей.НомерПоПорядку 	= х + 1;
		МенеджерЗаписей.Наименование 	= текСтрока.Наименование;
		МенеджерЗаписей.ИмяСобытия 		= текСтрока.ИмяСобытия;
		МенеджерЗаписей.ЛимитКарточек 	= текСтрока.ЛимитКарточек;
		МенеджерЗаписей.ЛимитЧасов 		= текСтрока.ЛимитЧасов;
		МенеджерЗаписей.Цвет 			= текСтрока.Цвет;
		
		МенеджерЗаписей.Записать();
		
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ЗаписатьКолонки()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанКолонки.УИД КАК УИД,
		|	КанбанКолонки.Наименование КАК Наименование,
		|	КанбанКолонки.НомерПоПорядку КАК НомерПоПорядку,
		|	КанбанКолонки.ЛимитКарточек КАК ЛимитКарточек,
		|	КанбанКолонки.ЛимитЧасов КАК ЛимитЧасов,
		|	КанбанКолонки.ИмяСобытия КАК ИмяСобытия,
		|	КанбанКолонки.Цвет КАК Цвет,
		|	КанбанКолонки.Окончание КАК Окончание,
		|	КанбанКолонки.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.КанбанКолонки КАК КанбанКолонки
		|ГДЕ
		|	КанбанКолонки.Доска = &Доска";
	
	Запрос.УстановитьПараметр("Доска", Доска);
	
	Выборка = Запрос.Выполнить().Выбрать();

	ОписаниеБулево = Новый ОписаниеТипов("Булево");
	
	тзКолонки = Колонки.Выгрузить();
	тзКолонки.Колонки.Добавить("Соответствует", ОписаниеБулево);
	
	МаксимальныИндекс = тзКолонки.Количество() - 1;
	
	Пока Выборка.Следующий() Цикл
		
		Найден = Ложь;
		
		Для х = 0 По МаксимальныИндекс Цикл
			
			текСтрока = тзКолонки[х];

			Если НЕ текСтрока.УИД = Выборка.УИД ИЛИ ПустаяСтрока(текСтрока.УИД) Тогда
				Продолжить;
			КонецЕсли;
			
			текСтрока.Соответствует = Истина;
			Найден 					= Истина;
			
			Если текСтрока.Наименование = Выборка.Наименование И х + 1 = Выборка.НомерПоПорядку И текСтрока.ИмяСобытия = Выборка.ИмяСобытия
				И текСтрока.ЛимитКарточек = Выборка.ЛимитКарточек И текСтрока.ЛимитЧасов = Выборка.ЛимитЧасов И текСтрока.Цвет = Выборка.Цвет 
				И текСтрока.Окончание = Выборка.Окончание И текСтрока.Статус = Выборка.Статус Тогда
				
				Прервать;
			
			КонецЕсли;
			
			МенеджерЗаписей = РегистрыСведений.КанбанКолонки.СоздатьМенеджерЗаписи();
			МенеджерЗаписей.УИД 			= Выборка.УИД;
			МенеджерЗаписей.Доска 			= Доска;
			МенеджерЗаписей.НомерПоПорядку 	= х + 1;
			МенеджерЗаписей.Наименование 	= текСтрока.Наименование;
			МенеджерЗаписей.ИмяСобытия 		= текСтрока.ИмяСобытия;
			МенеджерЗаписей.ЛимитКарточек 	= текСтрока.ЛимитКарточек;
			МенеджерЗаписей.ЛимитЧасов 		= текСтрока.ЛимитЧасов;
			МенеджерЗаписей.Окончание 		= текСтрока.Окончание;
			МенеджерЗаписей.Статус 			= текСтрока.Статус;
			МенеджерЗаписей.Цвет 			= текСтрока.Цвет;
			
			МенеджерЗаписей.Записать();
			
			Прервать;
		
		КонецЦикла;
		
		Если НЕ Найден Тогда
		
			НаборЗаписей = РегистрыСведений.КанбанКолонки.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.УИД.Установить(Выборка.УИД);
			
			НаборЗаписей.Записать();
		
		КонецЕсли;
			
	КонецЦикла;
	
	Для х = 0 По МаксимальныИндекс Цикл

		текСтрока = тзКолонки[х];
		
		Если текСтрока.Соответствует Тогда
			Продолжить;
		КонецЕсли;
		
		МенеджерЗаписей = РегистрыСведений.КанбанКолонки.СоздатьМенеджерЗаписи();
		МенеджерЗаписей.УИД 			= Новый УникальныйИдентификатор();
		МенеджерЗаписей.Доска 			= Доска;
		МенеджерЗаписей.НомерПоПорядку 	= х + 1;
		МенеджерЗаписей.Наименование 	= текСтрока.Наименование;
		МенеджерЗаписей.ИмяСобытия 		= текСтрока.ИмяСобытия;
		МенеджерЗаписей.ЛимитКарточек 	= текСтрока.ЛимитКарточек;
		МенеджерЗаписей.ЛимитЧасов 		= текСтрока.ЛимитЧасов;
		МенеджерЗаписей.Цвет 			= текСтрока.Цвет;
		
		МенеджерЗаписей.Записать();
		
	КонецЦикла;
	
	
КонецПроцедуры

#КонецОбласти


#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КонтрольДоскиПоУмолчанию();
	
	ЗаполнитьСписокДоск(Элементы.Доска.СписокВыбора);
	
	ВидимостьРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	
	ИзмененаСтруктураПереходов = Ложь;
	
	ЗаполнитьИнформациюПоКартинкам();
	ЗаполнитьЗначенияДоски(СтруктураКартинки);
	ВидимостьДатыИКомментарияКолонки();
	ВидимостьДатыИКомментарияДорожки();
	ИспользоватьКорзинуПриИзменении(Неопределено); 
	
КонецПроцедуры

#КонецОбласти 

#Область СобытияФормыПродолжение

&НаКлиенте
Процедура ЗаполнитьИнформациюПоКартинкам()

	мКартинкиКорзины = Новый Массив(5); 
	мКартинкиКорзины[0] = "Корзина1";
	мКартинкиКорзины[1] = "Корзина2";
	мКартинкиКорзины[2] = "Корзина3";
	мКартинкиКорзины[3] = "Корзина4";
	мКартинкиКорзины[4] = "Корзина5";
	
	СтруктураКартинки = Новый Структура("ИмяКартинкиКорзины, ИндексКартинкиКорзины, мКартинкиКорзины", мКартинкиКорзины[0], 0, мКартинкиКорзины);
	
КонецПроцедуры // ЗаполнитьИнформациюПоКартинкам() 


#КонецОбласти 

#Область СобытияРеквизитов

&НаКлиенте
Процедура СписокКудаКолонкиПриАктивизацииСтроки(Элемент)
	
	стОтбор = ПолучитьТекущиеДанныеПереходаКолонки();
	
	ТребуетсяКомментарийКолонки = Ложь;	
	ТребуетсяДатаКолонки		= Ложь;	
	ЗаголовокКомментарияКолонки	= "";	
	ЗаголовокДатыКолонки 		= "";	
		
	Если стОтбор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НайденыеСтроки = тзРазрешенныеПереходы.НайтиСтроки(стОтбор);
	
	Если НайденыеСтроки.Количество() > 0 Тогда
		
		ТД = НайденыеСтроки[0];
	
		ТребуетсяКомментарийКолонки = ТД.ТребуетсяКомментарий;	
		ТребуетсяДатаКолонки		= ТД.ТребуетсяДата;	
		ЗаголовокКомментарияКолонки	= ТД.ЗаголовокКомментария;	
		ЗаголовокДатыКолонки 		= ТД.ЗаголовокДаты;	
				
	КонецЕсли; 
	
	ВидимостьДатыИКомментарияКолонки();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКудаДорожкиПриАктивизацииСтроки(Элемент)
	
КонецПроцедуры


&НаКлиенте
Процедура ДоскаПриИзменении(Элемент)
	ЗаполнитьЗначенияДоски(СтруктураКартинки);
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяКомментарийПриИзменении(Элемент)
	ЗаполнитьДанныеКомментарийДата();
	ВидимостьДатыИКомментарияКолонки();
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяДатаПриИзменении(Элемент)
	ЗаполнитьДанныеКомментарийДата();
	ВидимостьДатыИКомментарияКолонки();
КонецПроцедуры

&НаКлиенте
Процедура ЗаголовокДатыКолонкиПриИзменении(Элемент)
	ЗаполнитьДанныеКомментарийДата();
КонецПроцедуры

&НаКлиенте
Процедура ЗаголовокКомментарияКолонкиПриИзменении(Элемент)
	ЗаполнитьДанныеКомментарийДата();
КонецПроцедуры 

&НаКлиенте
Процедура КолонкиПриИзменении(Элемент)
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяКомментарийДорожкиПриИзменении(Элемент)
	ВидимостьДатыИКомментарияДорожки();
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяДатаДорожкиПриИзменении(Элемент)
	ВидимостьДатыИКомментарияДорожки();
КонецПроцедуры

&НаКлиенте
Процедура СписокОткудаКолонкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры 

&НаКлиенте
Процедура СписокОткудаКолонкиПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокОткудаКолонкиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры 

&НаКлиенте
Процедура СписокКудаКолонкиПриИзменении(Элемент)
	Заполнить_тзРазрешенныеПереходыКолонки();
КонецПроцедуры

&НаКлиенте
Процедура СписокОткудаКолонкиПриАктивизацииСтроки(Элемент)

	СписокКудаКолонки.Очистить();
	
	ТД = Элемент.ТекущиеДанные;
	
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	стУИДОткуда = Новый Структура("УИДОткуда", ТД.УИД);
	
	Для каждого СтрокаОткуда Из тзРазрешенныеПереходы.НайтиСтроки(стУИДОткуда) Цикл
		
		Для каждого х Из Колонки Цикл
			
			Если СтрокаОткуда.УИДКуда = х.УИД  Тогда
			
				СписокКудаКолонки.Добавить(х.УИД, х.Наименование);
				Прервать;
			
			КонецЕсли;
		
		КонецЦикла;
	
	КонецЦикла; 
	
	ВидимостьДатыИКомментарияКолонки();

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьКорзинуПриИзменении(Элемент)
	Элементы.ГруппаДанныеКорзины.Видимость = ИспользоватьКорзину;
	
	Если ИспользоватьКорзину Тогда
		КартинкаКорзиныНажатиеНаСервере(СтруктураКартинки);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СобытияРеквизитовПродолжение

&НаСервере
Функция КартинкаКорзиныНажатиеНаСервере(СтруктураКартинки)
	
	Если СтруктураКартинки.мКартинкиКорзины.Количество() - 1 > СтруктураКартинки.ИндексКартинкиКорзины Тогда
		СтруктураКартинки.ИндексКартинкиКорзины = СтруктураКартинки.ИндексКартинкиКорзины + 1;
	Иначе	
	   СтруктураКартинки.ИндексКартинкиКорзины = 0;
   КонецЕсли;
   
   СтруктураКартинки.ИмяКартинкиКорзины = СтруктураКартинки.мКартинкиКорзины[СтруктураКартинки.ИндексКартинкиКорзины];
   Элементы.ДекорацияКорзина.Картинка = БиблиотекаКартинок[СтруктураКартинки.ИмяКартинкиКорзины];
КонецФункции

&НаКлиенте
Процедура ЗаполнитьДанныеКомментарийДата()

	стОтбор = ПолучитьТекущиеДанныеПереходаКолонки();
	
	Если стОтбор = Неопределено Тогда
		Возврат;
	КонецЕсли;

	НайденыеСтроки = тзРазрешенныеПереходы.НайтиСтроки(стОтбор);
	
	Если НайденыеСтроки.Количество() > 0 Тогда
		
		ТД = НайденыеСтроки[0];
	
		ТД.ТребуетсяКомментарий 	= ТребуетсяКомментарийКолонки;	
		ТД.ТребуетсяДата 			= ТребуетсяДатаКолонки;	
		ТД.ЗаголовокКомментария		= ?(ТребуетсяКомментарийКолонки, 	ЗаголовокКомментарияКолонки, 	"");	
		ТД.ЗаголовокДаты 			= ?(ТребуетсяДатаКолонки, 			ЗаголовокДатыКолонки, 			"");	
		
		ВидимостьДатыИКомментарияКолонки();
		
		ИзмененаСтруктураПереходов = Истина;
	КонецЕсли;

КонецПроцедуры // ЗаполнитьДанныеКомментарийДата()

#КонецОбласти

#Область ВидимостьРеквизитов

&НаКлиенте
Процедура ВидимостьДатыИКомментарияКолонки()

	Элементы.ЗаголовокДатыКолонки.Видимость 		= ТребуетсяДатаКолонки;
	Элементы.ЗаголовокКомментарияКолонки.Видимость 	= ТребуетсяКомментарийКолонки;
	
КонецПроцедуры // ВыидимостьДатыИКомментария()

&НаКлиенте
Процедура ВидимостьДатыИКомментарияДорожки()

	Элементы.ЗаголовокДатыДорожки.Видимость 		= ТребуетсяДатаДорожки;
	Элементы.ЗаголовокКомментарияДорожки.Видимость 	= ТребуетсяКомментарийДорожки;
	
КонецПроцедуры // ВыидимостьДатыИКомментария()

Процедура ВидимостьРеквизитов()

	Если РольДоступна("АдминистраторКанбан") Тогда
	
		Элементы.ДобавитьДоску.Доступность = Истина;	
	
	Иначе
	
		Элементы.ДобавитьДоску.Доступность = Ложь;	
	
	КонецЕсли;

КонецПроцедуры 

#КонецОбласти

#Область ЗаполнениеДанныхНаФорме             
	
Процедура ЗаполнитьЗначенияДоски(СтруктураКартинки)
	
	Дорожки.Очистить();
	Колонки.Очистить();
	тзПрава.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = СформироватьЗапросДляВыборкиДанныхКанбана();
	
	Запрос.УстановитьПараметр("Доска", Доска);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ЗаполнитьТаблицу(Дорожки, 				0, РезультатЗапроса);
	ЗаполнитьТаблицу(Колонки, 				1, РезультатЗапроса);
	ЗаполнитьТаблицу(тзПрава, 				2, РезультатЗапроса); 
	ЗаполнитьТаблицу(тзРазрешенныеПереходы, 3, РезультатЗапроса); 
	
	Выборка = РезультатЗапроса[4].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ИмяКартинкиКорзины 		= Выборка.ИмяКартинкиКорзины;
		Если ПустаяСтрока(ИмяКартинкиКорзины) Тогда
			ИспользоватьКорзину	= Ложь;
		Иначе
			
			ИспользоватьКорзину	= Истина;
			
			Для х = 0 По СтруктураКартинки.мКартинкиКорзины.Количество() - 1 Цикл
			
				Если СтруктураКартинки.мКартинкиКорзины[х] = ИмяКартинкиКорзины Тогда
				
					СтруктураКартинки.ИндексКартинкиКорзины = х;
					Прервать;
				
				КонецЕсли;	
			
			КонецЦикла;
			
			СтруктураКартинки.ИмяКартинкиКорзины = СтруктураКартинки.мКартинкиКорзины[СтруктураКартинки.ИндексКартинкиКорзины];
			Элементы.ДекорацияКорзина.Картинка = БиблиотекаКартинок[СтруктураКартинки.ИмяКартинкиКорзины];
			
		КонецЕсли;
		
		стРазделители 			= омКанбанНачальноеЗаполнение.ПолучитьРазделитилиИзКода(Выборка.Разделитель);
		ИспользоватьПроекты 	= стРазделители.Проект;
		ИспользоватьСотрудников = стРазделители.Сотрудник; 
		
	КонецЦикла;
	
	


КонецПроцедуры 

Функция СформироватьЗапросДляВыборкиДанныхКанбана()

	Возврат "ВЫБРАТЬ
	        |	КанбанДорожки.УИД КАК УИД,
	        |	КанбанДорожки.Наименование КАК Наименование,
	        |	КанбанДорожки.ЛимитКарточек КАК ЛимитКарточек,
	        |	КанбанДорожки.ЛимитЧасов КАК ЛимитЧасов,
	        |	КанбанДорожки.Цвет КАК Цвет,
	        |	КанбанДорожки.ИмяСобытия КАК ИмяСобытия
	        |ИЗ
	        |	РегистрСведений.КанбанДорожки КАК КанбанДорожки
	        |ГДЕ
	        |	КанбанДорожки.Доска = &Доска
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	КанбанДорожки.НомерПоПорядку
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	КанбанКолонки.УИД КАК УИД,
	        |	КанбанКолонки.Наименование КАК Наименование,
	        |	КанбанКолонки.ЛимитКарточек КАК ЛимитКарточек,
	        |	КанбанКолонки.ЛимитЧасов КАК ЛимитЧасов,
	        |	КанбанКолонки.Цвет КАК Цвет,
	        |	КанбанКолонки.ИмяСобытия КАК ИмяСобытия,
	        |	КанбанКолонки.Статус КАК Статус
	        |ИЗ
	        |	РегистрСведений.КанбанКолонки КАК КанбанКолонки
	        |ГДЕ
	        |	КанбанКолонки.Доска = &Доска
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	КанбанКолонки.НомерПоПорядку
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	КанбанПрава.Пользователь КАК Пользователь,
	        |	КанбанПрава.Просмотр КАК Просмотр,
	        |	КанбанПрава.Добавление КАК Добавление,
	        |	КанбанПрава.Измениение КАК Измениение,
	        |	КанбанПрава.Удаление КАК Удаление
	        |ИЗ
	        |	РегистрСведений.КанбанПрава КАК КанбанПрава
	        |ГДЕ
	        |	КанбанПрава.Доска = &Доска
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	КанбанРазрешенныеПереходы.УИДОткуда КАК УИДОткуда,
	        |	КанбанРазрешенныеПереходы.УИДКуда КАК УИДКуда,
	        |	КанбанРазрешенныеПереходы.ЗаголовокДаты КАК ЗаголовокДаты,
	        |	КанбанРазрешенныеПереходы.ЗаголовокКомментария КАК ЗаголовокКомментария,
	        |	КанбанРазрешенныеПереходы.ТребуетсяДата КАК ТребуетсяДата,
	        |	КанбанРазрешенныеПереходы.ТребуетсяКомментарий КАК ТребуетсяКомментарий
	        |ИЗ
	        |	РегистрСведений.КанбанРазрешенныеПереходы КАК КанбанРазрешенныеПереходы
	        |ГДЕ
	        |	КанбанРазрешенныеПереходы.Доска = &Доска
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	КанбанДоски.Разделитель КАК Разделитель,
	        |	КанбанДоски.ИмяКартинкиКорзины КАК ИмяКартинкиКорзины
	        |ИЗ
	        |	РегистрСведений.КанбанДоски КАК КанбанДоски
	        |ГДЕ
	        |	КанбанДоски.УИД = &Доска";
	
КонецФункции // СформироватьЗапросДляВыборкиДанныхКанбана()

Процедура ЗаполнитьТаблицу(текТаблица, Индекс, РезультатЗапроса)

	Выборка = РезультатЗапроса[Индекс].Выбрать();

	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(текТаблица.Добавить(), Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


#Область ПродолженияСобытияФормы 

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокДоск(ЭлементДоска)

	Для Каждого ЭлементСписка Из омКанбанДоскиНаСервере.ПолучитьДоскиСУчетомПрав() Цикл
		
		ЭлементДоска.Добавить(
								ОбщегоНазначения.СкопироватьРекурсивно(ЭлементСписка.Значение), 
								ЭлементСписка.Представление, 
								ЭлементСписка.Пометка, 
								ЭлементСписка.Картинка);
		
	КонецЦикла;

КонецПроцедуры 

Процедура КонтрольДоскиПоУмолчанию()

	ОбъектОтчета = Отчеты.ОбщиеНастройки.Создать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПроекта.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.НастройкиПроекта КАК НастройкиПроекта
		|ГДЕ
		|	НастройкиПроекта.ИмяНастройки = &ИмяНастройки
		|	И НастройкиПроекта.НомерНастройки = 0
		|	И НастройкиПроекта.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ИмяНастройки", ОбъектОтчета.НаименованиеДоскиДляТЗ());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИмяДоскиДляТЗ = "";
	
	Пока Выборка.Следующий() Цикл
		ИмяДоскиДляТЗ = Выборка.Значение;
	КонецЦикла;
	
	Если ПустаяСтрока(ИмяДоскиДляТЗ) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КанбанДоски.УИД КАК УИД
		|ИЗ
		|	РегистрСведений.КанбанДоски КАК КанбанДоски
		|ГДЕ
		|	КанбанДоски.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Наименование", ИмяДоскиДляТЗ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат;
	КонецЦикла;
	
    СоздатьНовуюДоску(ИмяДоскиДляТЗ);

КонецПроцедуры


#КонецОбласти 

#Область Переходы
	

&НаКлиенте
Процедура ДобавитьКудаДорожку(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Функция ПолучитьТекущиеДанныеПереходаКолонки()

	ТД = Элементы.СписокОткудаКолонки.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	стОтбор = Новый Структура("УИДОткуда", ТД.УИД);
	
	ТД = Элементы.СписокКудаКолонки.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	стОтбор.Вставить("УИДКуда", ТД.Значение);
	
    Возврат стОтбор;
	
КонецФункции // ПолучитьТекущиеДанныеПереходаКолонки()


#КонецОбласти 

