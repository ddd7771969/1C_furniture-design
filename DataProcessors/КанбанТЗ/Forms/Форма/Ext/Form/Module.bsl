&НаКлиенте
Перем стСтруктураКанбана, мКолонки, мДорожки, СчетчикОбращений, стШаблоны, ИзмененыНастройки, стНастройки, мДанныеДляПоиска; 

&НаКлиенте                                                
Процедура ПриОткрытии(Отказ)
	
	мДанныеДляПоиска 	= Новый Массив;
	стПараметры 		= омНастройкиПолучитьЗаписать.ПолучитьНастройкиТЗКанбан(); 
	КолонокШирина 		= стПараметры.ШиринаКолонкиТЗКанбан;
	РазмерШрифта 		= стПараметры.РазмерШрифтаТЗКанбан;
	Сортировка 			= стПараметры.СортировкаТЗКанбан;

	стНастройки = Новый Структура();
	стНастройки.Вставить("ШиринаКолонкиТЗКанбан", 	стПараметры.ШиринаКолонкиТЗКанбан);
	стНастройки.Вставить("РазмерШрифтаТЗКанбан", 	стПараметры.РазмерШрифтаТЗКанбан);
	стНастройки.Вставить("Сортировка", 				стПараметры.СортировкаТЗКанбан);
	стНастройки.Вставить("Сотрудник", 				Сотрудник);
	стНастройки.Вставить("Проект", 					Проект);
	стНастройки.Вставить("Отбор", 					"");
	
	ИзмененыНастройки = Ложь;
	
	СчетчикОбращений = 0;
	
	стСтруктураКанбана = Неопределено;
	стДанныеHTML 		= омКанбанТЗ.ПолучитьHTMLКанбанТЗ(стСтруктураКанбана, стНастройки);
	мДанныеДляПоиска 	= стДанныеHTML.мДанныеДляПоиска;
	ПолеHTML 			= стДанныеHTML.КодHTML;
	ВставитьКартинкуКорзины(стСтруктураКанбана);
	
	НаименованиеВкладкиСортировка();
	
КонецПроцедуры 

&НаКлиенте
Процедура ВставитьКартинкуКорзины(стСтруктураКанбана)
	
	АдресВХ = стСтруктураКанбана.СтруктураДоски.ПутьКФайлуКартинкиКорзины;

	Если ПустаяСтрока(АдресВХ) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		ПолноеИмяФайла = КаталогВременныхФайлов() + "КорзинаТЗ.png";
		
		Файл = Новый Файл(ПолноеИмяФайла);
		
		Если Файл.Существует() Тогда
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытиеФайлаПродолжение", ЭтаФорма, Новый Структура("АдресВХ, ПолноеИмяФайла, стСтруктураКанбана", АдресВХ, ПолноеИмяФайла, стСтруктураКанбана));
			
			Попытка
				
				НачатьУдалениеФайлов(ОписаниеОповещения, ПолноеИмяФайла);
				
			Исключение
				
				Возврат;
				
			КонецПопытки;
			
		Иначе
			
			ОткрытиеФайлаПродолжение(Новый Структура("АдресВХ, ПолноеИмяФайла, стСтруктураКанбана", АдресВХ, ПолноеИмяФайла, стСтруктураКанбана));
			
		КонецЕсли;
	
		
	Исключение
		
		
	КонецПопытки;

КонецПроцедуры // ВставитьКартинкуКорзины()

&НаКлиенте
Процедура ОткрытиеФайлаПродолжение(стПараметры) Экспорт

	
	Если ПустаяСтрока(стПараметры.АдресВХ) Тогда
	
		Возврат;	
	
	КонецЕсли;
	
	Двоичное = ПолучитьИзВременногоХранилища(стПараметры.АдресВХ);  // по адресу временного хранилища получаем двоичные данные
	Двоичное.Записать(стПараметры.ПолноеИмяФайла); 
	
	ПолеHTML = СтрЗаменить(ПолеHTML, "@the_path_to_he_bucket_image_file@", омКанбанТЗ.ПолучитьКартинкуКорзины(стПараметры.ПолноеИмяФайла));
	стПараметры.стСтруктураКанбана.СтруктураДоски.ПутьКФайлуКартинкиКорзины = стПараметры.ПолноеИмяФайла;
	

КонецПроцедуры // ОткрытиеФайлаПродолжение()

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	стНастройки.Сотрудник = Сотрудник;
	ОбновитьСтраницуОжидание();
КонецПроцедуры

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	стНастройки.Проект = Проект;
	ОбновитьСтраницуОжидание();
КонецПроцедуры 

&НаКлиенте
Процедура ПолучитьИОбработатьДанные() Экспорт
	
	ОкноДокумента = Элементы.ПолеHTML.Документ.defaultView;

	ДанныеВозврата = омКанбанНаКлиенте.ПолучитьИОбработатьДанные(ОкноДокумента, стСтруктураКанбана, СчетчикОбращений, Сотрудник, Проект, стНастройки);
	
	Если ТипЗнч(ДанныеВозврата) = Тип("Структура") Тогда
	
		Если ПустаяСтрока(ДанныеВозврата.Комментарий) Тогда
			Комментарий = "";	
		Иначе	
			Комментарий = ДанныеВозврата.ОбъектТЗНаименование + " " + ДанныеВозврата.Комментарий;	
		КонецЕсли;
	
	ИначеЕсли ДанныеВозврата Тогда
		
		ПолеHTML = "";
		ПодключитьОбработчикОжидания("ОбновитьСтраницуОжидание", 0.1, Истина);
		
	КонецЕсли; 
	
	ПодключитьОбработчикОжидания("ПолучитьИОбработатьДанные", 0.5, Истина); 

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтраницуОжидание() Экспорт
	
	стДанныеHTML 		= омКанбанТЗ.ПолучитьHTMLКанбанТЗ(стСтруктураКанбана, стНастройки);  	
	мДанныеДляПоиска 	= стДанныеHTML.мДанныеДляПоиска;
	ПолеHTML 			= стДанныеHTML.КодHTML;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаписатьДанныеВJSON(Данные) Экспорт
	Попытка
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON());     
		
		ЗаписатьJSON(ЗаписьJSON, Данные);
		
		Возврат ЗаписьJSON.Закрыть();
	Исключение
	    Сообщить(ИнформацияОбОшибке());
		Возврат Неопределено;
	КонецПопытки; 
КонецФункции

&НаКлиенте
Процедура ПолеHTMLДокументСформирован(Элемент)
	ПодключитьОбработчикОжидания("ПолучитьИОбработатьДанные", 0.5, Истина); 
КонецПроцедуры

&НаКлиенте
Процедура КолонокШиринаПриИзменении(Элемент)

	Если КолонокШирина < 50 Тогда
		КолонокШирина = 50;	
	КонецЕсли; 
	
	ИзмененыНастройки = Истина;
	стНастройки.ШиринаКолонкиТЗКанбан = КолонокШирина; 
	ОбновитьСтраницуОжидание();
	
КонецПроцедуры

&НаКлиенте
Процедура РазмерШрифтаПриИзменении(Элемент)
	
	Если РазмерШрифта < 6 Тогда
		РазмерШрифта = 6;	
	КонецЕсли;
	
	ИзмененыНастройки = Истина;
	стНастройки.РазмерШрифтаТЗКанбан = РазмерШрифта; 
	ОбновитьСтраницуОжидание();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ИзмененыНастройки И НЕ ЗавершениеРаботы Тогда
	
		стПараметры = омНастройкиПолучитьЗаписать.ПолучитьСтруктуруТЗКанбан();
		стПараметры.ШиринаКолонкиТЗКанбан 	= КолонокШирина;
		стПараметры.РазмерШрифтаТЗКанбан 	= РазмерШрифта;
		стПараметры.СортировкаТЗКанбан 		= Сортировка;
		                                   
		омНастройкиПолучитьЗаписать.ЗаписатьНастройкиКанбанТЗ(стПараметры);
		
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура КолонокШиринаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	КолонокШирина = КолонокШирина + 20 * Направление;
	
	Если КолонокШирина > 400 Тогда
		КолонокШирина = 400;
	ИначеЕсли КолонокШирина < 50 Тогда
		КолонокШирина = 50;
	КонецЕсли;
	
	стНастройки.ШиринаКолонкиТЗКанбан = КолонокШирина; 
	ОбновитьСтраницуОжидание();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Для каждого х Из мДанныеДляПоиска Цикл
	
		Если СтрНайти(НРег(х.Наименование), НРег(Текст)) > 0 Тогда
		
			ДанныеВыбора.Добавить(х.Ресурс, х.Наименование);	
		
		КонецЕсли;	
	
	КонецЦикла;
	
КонецПроцедуры   //СтрНайтиИВыделитьОформлением

&НаКлиенте
Процедура ПоискОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	стНастройки.Отбор = омКанбанТЗ.ПолучитУИДРесурса(ВыбранноеЗначение);
	ОбновитьСтраницуОжидание();

	
КонецПроцедуры

&НаКлиенте
Процедура СортировкаПриИзменении(Элемент)
	НаименованиеВкладкиСортировка();
	ИзмененыНастройки = Истина; 
	стНастройки.Сортировка = Сортировка;
	ОбновитьСтраницуОжидание();
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеВкладкиСортировка()

	ЗаголовокВкладки = "Сортировка (";
	
	Если Сортировка = 0 Тогда
	
		ЗаголовокВкладки = ЗаголовокВкладки + "В)";	
	
	ИначеЕсли Сортировка = 1 Тогда
	
		ЗаголовокВкладки = ЗаголовокВкладки + "А)";	

	Иначе
	
		ЗаголовокВкладки = ЗаголовокВкладки + "С)";
		
	КонецЕсли;
	
	Элементы.ГруппаСортировка.Заголовок = ЗаголовокВкладки;

КонецПроцедуры // НаименованиеВкладкиСортировка()

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписанФайлТЗ" Тогда
		
		Если ПустаяСтрока(Параметр.ПолноеИмяФайла) Тогда
			ПоказатьПредупреждение(, "Не удалось получить файл ТЗ");
		ИначеЕсли Параметр.ПараметрыСохранения = "Открыть" Тогда
			ЗапуститьПриложение(Параметр.ПолноеИмяФайла);
		ИначеЕсли Параметр.ПараметрыСохранения = "Выгрузить" Тогда
			
			стПараметры = Новый Структура("ПолноеИмяФайла, Версия", Параметр.ПолноеИмяФайла, Параметр.Версия);
			
			Результат = омВыгрузкаCRMНаКлиенте.ВыгрузитьВPDM(Параметр.ОбъектТЗ, "ЗагрузкаТЗ", стПараметры);
			
			Если ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
				Сообщить(Формат(ТекущаяДата(), "ДЛФ=DT") + " Не удалось перенести файла ТЗ " + Параметр.ПолноеИмяФайла + ". " + Результат.ОписаниеОшибки);
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ЗавершенПереносТЗ" Тогда
		
		ОбновитьСтраницуОжидание();
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Сотрудник") Тогда
		Если ТипЗнч(Параметры.Сотрудник) = Тип("СправочникСсылка.Пользователи") Тогда
			Сотрудник 	= омСотрудники.ПолучитьСотруднткаПоПользователю(Параметры.Сотрудник);
		Иначе
			Сотрудник 	= Параметры.Сотрудник;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Проект") И ЗначениеЗаполнено(Параметры.Проект) Тогда
		Проект.Добавить(Параметры.Проект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПриИзменении(Элемент) 
	стНастройки.Отбор = Поиск;
	ОбновитьСтраницуОжидание();
КонецПроцедуры
