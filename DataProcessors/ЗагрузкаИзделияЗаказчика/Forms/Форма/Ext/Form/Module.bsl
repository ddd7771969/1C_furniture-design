
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.текОбъект) Тогда
		ЭтаФорма.ОбъектИзделия = Параметры.текОбъект;
	ИначеЕсли ЗначениеЗаполнено(Параметры.текРодитель) Тогда
		ЭтаФорма.ОбъектИзделия = Параметры.текРодитель.Объект;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЭтаФорма.КолонкаНаименование 	= 3;
	ЭтаФорма.КолонкаЕдиница 		= 7;
	ЭтаФорма.КолонкаКоличество 		= 8;
	ЭтаФорма.ПерваяСтрока 			= 6;
КонецПроцедуры


#Область ЗагрузкаИзExcel
&НаКлиенте
Процедура ОткрытьФайл(Команда)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "Книга Excel 2007 (*.xlsx)|*.xlsx|Книга Excel 97 (*.xls)|*.xls";
	Диалог.Заголовок = НСтр("Выберите файл Excel");
	Диалог.МножественныйВыбор = Ложь;
	ОповещениеЗавершения = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтотОбъект);
	Диалог.Показать(ОповещениеЗавершения);
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыборФайлаЗавершение(ВыбранныйФайл, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйФайл = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	Элементы.ОткрытьФайл.Доступность = Ложь;
	
	ПомещенныйФайл = Ждать ПоместитьФайлНаСерверАсинх(,,,ВыбранныйФайл[0]);
	Если ПомещенныйФайл <> Неопределено И Не ПомещенныйФайл.ПомещениеФайлаОтменено Тогда
		ЗагрузкаВТабличныйДокументНаСервере(ПомещенныйФайл.Адрес, ПомещенныйФайл.СсылкаНаФайл.Расширение);
	КонецЕсли;
	
	Элементы.ОткрытьФайл.Доступность = Истина;
КонецПроцедуры 

&НаСервере
Процедура ЗагрузкаВТабличныйДокументНаСервере(АдресВременногоХранилища,Расширение)
	
	ФайлНаДиске = ПолучитьИмяВременногоФайла(Расширение);
    ДанныеФайла = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
    ДанныеФайла.Записать(ФайлНаДиске);
	табДок = Новый ТабличныйДокумент;
	
	Попытка
		табДок.Прочитать(ФайлНаДиске);
		УдалитьФайлы(ФайлНаДиске);
	Исключение
		Инфо = ИнформацияОбОшибке();
		Сообщить(НСтр("ru='Описание=';en='Description='") + Инфо.Описание + "'");
		Сообщить(НСтр("ru='ИмяМодуля=';en='ModuleName='") + Инфо.ИмяМодуля + "'");
		Сообщить(НСтр("ru='НомерСтроки=';en='LineNumber='") + Инфо.НомерСтроки + "'");
		Сообщить(НСтр("ru='ИсходнаяСтрока=';en='SourceLine='") + Инфо.ИсходнаяСтрока + "'");
		Возврат;
	КонецПопытки;
	
	текКолонкаНаименование 	= "C" + ЭтаФорма.КолонкаНаименование;
	текКолонкаКоличество 	= "C" + ЭтаФорма.КолонкаКоличество;
	текКолонкаЕдиница 		= "C" + ЭтаФорма.КолонкаЕдиница;
	
	КоличествоСтрок = табДок.ВысотаТаблицы;
	
	ЭтаФорма.ТаблицаИзделий.Очистить();
	
	Для сч = ЭтаФорма.ПерваяСтрока По КоличествоСтрок Цикл 
		
		счФорматированая = Формат(сч, "ЧГ=0;");
		Попытка
			Наименование = СокрЛП(табДок.ПолучитьОбласть("R" + счФорматированая + текКолонкаНаименование).ТекущаяОбласть.Текст);	
			Если ПустаяСтрока(Наименование) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭтаФорма.КолонкаКоличество > 0 Тогда
				стрКоличество = СокрЛП(табДок.ПолучитьОбласть("R" + счФорматированая + текКолонкаКоличество).ТекущаяОбласть.Текст);
				
				стрКоличество = СтрЗаменить(стрКоличество,Символы.НПП,"");
				
				Количество = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(стрКоличество);
				
				Если Количество = Неопределено Тогда
					Количество = 0;
				КонецЕсли;
			Иначе	
				Количество = 0;
			КонецЕсли;
			
			Единица = СокрЛП(табДок.ПолучитьОбласть("R" + счФорматированая + текКолонкаЕдиница).ТекущаяОбласть.Текст);
			Если ПустаяСтрока(Единица) Тогда
				ЕИ = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
			Иначе
				ЕИ = Справочники.ЕдиницыИзмерения.НайтиПоНаименованию(Единица);
			КонецЕсли;
			
			НС = ЭтаФорма.ТаблицаИзделий.Добавить();
			НС.Наименование 		= Наименование;
			НС.Количество 			= Количество;
			НС.ЕдиницаИзмерения 	= ЕИ; 
			//НС.НомерСтрокиВФайле 	= сч;
			
		Исключение
		КонецПопытки;
		
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СоздатьИзделияНаСервере()
	
	Для каждого текСтрока Из ЭтаФорма.ТаблицаИзделий Цикл
		Спр = Справочники.ИзделияЭлемент.СоздатьЭлемент();
		Спр.Проект 				= ЭтаФорма.ОбъектИзделия;
		Спр.Наименование 		= текСтрока.Наименование;
		Спр.Количество 			= текСтрока.Количество;
		Спр.ЕдиницаИзмерения 	= текСтрока.ЕдиницаИзмерения;
		
		Спр.Записать();
		
		Справочники.ИзделияЭлемент.ПроверитьКаталогИзделия(Спр.Ссылка,ЭтаФорма.ОбъектИзделия);		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьИзделия(Команда)
	Если ЗначениеЗаполнено(ЭтаФорма.ОбъектИзделия) Тогда
		СоздатьИзделияНаСервере();
		ЭтаФорма.Закрыть(Новый Структура("текОбъект", ЭтаФорма.ОбъектИзделия));
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Поле = "ОбъектИзделия";
		Сообщение.Текст = "Не заполнен объект.";
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
