
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьПроекты();
	
	
КонецПроцедуры

Процедура ЗаполнитьПроекты()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Проекты.Ссылка КАК Ссылка,
		|	Проекты.Наименование КАК Наименование,
		|	Проекты.Номер КАК Номер,
		|	Проекты.Префикс КАК Префикс
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	НЕ Проекты.НеВыводитьНаДиаграмму
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номер";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Элементы.Проект.СписокВыбора.Добавить(Выборка.Ссылка, 
			Формат(Выборка.Номер, "ЧГ=0") + "-" + Выборка.Наименование + " (" + Выборка.Префикс + ")");
	КонецЦикла;


КонецПроцедуры

Процедура ПроектПриИзмененииНаСервере()

	ЗаполнитьПромежуточныеДанные(ПолучитьТаблицуДанных());
	
КонецПроцедуры

Функция ЗаполнитьПромежуточныеДанные(тзДанные)
	
	ОформлениеКомпозиции = 1;
	
	мКомпозиции = ПолучитьКомпозицииИзДанных(тзДанные);
	мКомпозиции.Добавить(Справочники.Композиции.ПустаяСсылка());
	
	стОтбор = Новый Структура("Композиция", );
	
	Для каждого ТекущаяКомпозиция Из мКомпозиции Цикл
		
		Если ЗначениеЗаполнено(ТекущаяКомпозиция) Тогда
			

			НайденаяСтрока = тзДанные.Найти(ТекущаяКомпозиция, "Объект");
			НС = ТаблицаДанных.Добавить();
			ЗаполнитьЗначенияСвойств(НС, НайденаяСтрока);
			НС.ОформлениеКомпозиции = 2;
			
		Иначе
			
			ОформлениеКомпозиции = 0;
			
		КонецЕсли;
		
		стОтбор.Композиция = ТекущаяКомпозиция;
		
		Для каждого ТекущиеДанные Из тзДанные.НайтиСтроки(стОтбор) Цикл
		
			НС = ТаблицаДанных.Добавить();
			ЗаполнитьЗначенияСвойств(НС, ТекущиеДанные);
			НС.ОформлениеКомпозиции = ОформлениеКомпозиции;
		
		КонецЦикла;
		
	
	КонецЦикла;

КонецФункции // ЗаполнитьПромежуточныеДанные(тзДанные) 

Функция ПолучитьКомпозицииИзДанных(тзДанные)

	спКомпозиции = Новый СписокЗначений;
	
	Для каждого х Из тзДанные Цикл
	
		Если ЗначениеЗаполнено(х.Композиция) И спКомпозиции.НайтиПоЗначению(х.Композиция) = Неопределено Тогда
		
			спКомпозиции.Добавить(х.Композиция, х.КомпозицияНаименование);		
		
		КонецЕсли;
		
	КонецЦикла;
	
    спКомпозиции.СортироватьПоПредставлению();
	
	Возврат спКомпозиции.ВыгрузитьЗначения();

КонецФункции // ПолучитьКомпозицииИзДанных(тзДанные)

Функция ПолучитьТаблицуДанных()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзделияКомплект.Ссылка КАК Объект
		|ПОМЕСТИТЬ втОбъекты
		|ИЗ
		|	Справочник.ИзделияКомплект КАК ИзделияКомплект
		|ГДЕ
		|	ИзделияКомплект.Проект = &Проект
		|	И НЕ ИзделияКомплект.ЭтоГруппа
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Композиции.Ссылка
		|ИЗ
		|	Справочник.Композиции КАК Композиции
		|ГДЕ
		|	Композиции.Проект = &Проект
		|	И НЕ Композиции.ЭтоГруппа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыКомплекта.ДнейПКД КАК ДнейПКД,
		|	ДатыКомплекта.ДнейРКД КАК ДнейРКД,
		|	ВЫБОР
		|		КОГДА ДатыКомплекта.Код_в_PDM = 0
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Выгружен_в_PDM,
		|	ДатыКомплекта.КрайПКД КАК КрайПКД,
		|	ДатыКомплекта.КрайРКД КАК КрайРКД,
		|	ДатыКомплекта.КрайТЗ КАК КрайТЗ,
		|	ДатыКомплекта.НачалоПКД КАК НачалоПКД,
		|	ДатыКомплекта.НачалоРКД КАК НачалоРКД,
		|	ДатыКомплекта.НачалоТЗ КАК НачалоТЗ,
		|	втОбъекты.Объект.Наименование КАК КомплектНаименование,
		|	ЕСТЬNULL(ДанныеКомплектаСрезПоследних.Конструктор, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)) КАК Конструктор,
		|	Задачи_ПКД.Ссылка КАК ЗадачаПКД,
		|	ЕСТЬNULL(Задачи_ПКД.ОбобщеннаяЗадача, ЛОЖЬ) КАК ОбобщеннаяЗадача,
		|	ЛОЖЬ КАК Изменен,
		|	ДатыКомплекта.ОкончаниеПКД КАК ОкончаниеПКД,
		|	ДатыКомплекта.ОкончаниеРКД КАК ОкончаниеРКД,
		|	ДатыКомплекта.ОкончаниеТЗ КАК ОкончаниеТЗ,
		|	втОбъекты.Объект.Родитель.Композиция КАК Композиция,
		|	втОбъекты.Объект КАК Объект,
		|	втОбъекты.Объект.Родитель.Композиция.Наименование КАК КомпозицияНаименование,
		|	Задача_РКД.Ссылка КАК ЗадачаРКД
		|ИЗ
		|	втОбъекты КАК втОбъекты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|		ПО втОбъекты.Объект = ДатыКомплекта.Комплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних(, Комплект.Проект = &Проект) КАК ДанныеКомплектаСрезПоследних
		|		ПО втОбъекты.Объект = ДанныеКомплектаСрезПоследних.Комплект
		|		ЛЕВОЕ СОЕДИНЕНИЕ Задача.Задачи_ПКД КАК Задачи_ПКД
		|		ПО втОбъекты.Объект = Задачи_ПКД.ОбъектТЗ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Задача.Задача_РКД КАК Задача_РКД
		|		ПО втОбъекты.Объект = Задача_РКД.ИзделиеКомплект
		|
		|УПОРЯДОЧИТЬ ПО
		|	КомплектНаименование";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Возврат  Запрос.Выполнить().Выгрузить();

КонецФункции // ПолучитьТаблицуДанных()

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	
	ТаблицаДанных.Очистить();
	
	Если ЗначениеЗаполнено(Проект) Тогда
		
		ПроектПриИзмененииНаСервере();
		
	КонецЕсли;
	
	
КонецПроцедуры 

&НаКлиенте
Процедура ТаблицаДанныхПриИзменении(Элемент)
	
	ТД = Элементы.ТаблицаДанных.ТекущиеДанные;
	
	Если НЕ ТД = Неопределено И НЕ ТД.Изменен Тогда
		
		Элементы.ФормаСохранить.Доступность = Истина;
	
		ТД.Изменен = Истина;	
		ТД.ОформлениеКомпозиции = ТД.ОформлениеКомпозиции + 3;
		
		ИмяИзменяемойколонки = Элемент.ТекущийЭлемент.Имя; 
		
		ПустаяДата = Дата(1, 1, 1);
		
		Если ИмяИзменяемойколонки = "ТаблицаДанныхНачалоПКД" ИЛИ ИмяИзменяемойколонки = "ТаблицаДанныхДнейПКД" Тогда
			ТД.ОкончаниеПКД = омДаты.ДобавитьДниСУчетомВыходных(ТД.НачалоПКД, ТД.ДнейПКД);
			
			Если ТД.ОкончаниеРКД = ПустаяДата И НЕ(ТД.НачалоРКД = ПустаяДата ИЛИ ТД.ДнейРКД = 0) Тогда
				ТД.ОкончаниеРКД = омДаты.ДобавитьДниСУчетомВыходных(ТД.НачалоРКД, ТД.ДнейРКД);
			КонецЕсли;
		ИначеЕсли ИмяИзменяемойколонки = "ТаблицаДанныхНачалоРКД" ИЛИ ИмяИзменяемойколонки = "ТаблицаДанныхДнейРКД" Тогда
			ТД.ОкончаниеРКД = омДаты.ДобавитьДниСУчетомВыходных(ТД.НачалоРКД, ТД.ДнейРКД);
			
			Если ТД.ОкончаниеПКД = ПустаяДата И НЕ(ТД.НачалоПКД = ПустаяДата ИЛИ ТД.ДнейПКД = 0) Тогда
				ТД.ОкончаниеПКД = омДаты.ДобавитьДниСУчетомВыходных(ТД.НачалоПКД, ТД.ДнейПКД);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНаСервере(Проект, мИзмененныхДаных)
	
	МенеджерЗаписей = РегистрыСведений.ДатыКомплекта.СоздатьМенеджерЗаписи();
	
	Для каждого х Из мИзмененныхДаных Цикл
		
		МенеджерЗаписей.Комплект = х.Комплект;
		МенеджерЗаписей.Проект = Проект;		
		МенеджерЗаписей.Прочитать();
		
		ЗаполнитьЗначенияСвойств(МенеджерЗаписей, х);
		МенеджерЗаписей.Проект = Проект;
		
		МенеджерЗаписей.Записать();
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	мИзмененныхДаных = Новый Массив;
	
	Для каждого х Из ТаблицаДанных Цикл
		
		Если х.Изменен Тогда
			
			стДанных = Новый Структура();
			стДанных.Вставить("Комплект", 		х.Объект);
			стДанных.Вставить("ДнейПКД", 		х.ДнейПКД);
			стДанных.Вставить("ДнейРКД", 		х.ДнейРКД);
			стДанных.Вставить("КрайПКД", 		х.КрайПКД);
			стДанных.Вставить("КрайРКД", 		х.КрайРКД);
			стДанных.Вставить("НачалоПКД", 		х.НачалоПКД);
			стДанных.Вставить("НачалоРКД", 		х.НачалоРКД);
			стДанных.Вставить("ОкончаниеПКД", 	х.ОкончаниеПКД);
			стДанных.Вставить("ОкончаниеРКД", 	х.ОкончаниеРКД); 
			
			мИзмененныхДаных.Добавить(стДанных);
			
			х.Изменен = Ложь;
			х.ОформлениеКомпозиции = х.ОформлениеКомпозиции - 3;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СохранитьНаСервере(Проект, мИзмененныхДаных);
	
	Элементы.ФормаСохранить.Доступность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПустыеДаты(Команда)
	
	Описание = Новый ОписаниеОповещения("ПослеВыбораДат", ЭтаФорма);
	ОткрытьФорму("Обработка.ДатыНачалаПроектированияИзделий.Форма.ЗаполнениеДат", , ЭтаФорма, , , , Описание, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры  

&НаКлиенте
Процедура ПослеВыбораДат(Параметр, ДополнительныеЗначениея) Экспорт

	Если НЕ ТипЗнч(Параметр) = Тип("Структура") Тогда
		Возврат;	
	КонецЕсли;
	
	ПустаяДата = Дата(1, 1, 1);
	
	Для каждого х Из ТаблицаДанных Цикл
	
		ИзмененПКД = Ложь;
		ИзмененРКД = Ложь;
		
		Если х.НачалоПКД = ПустаяДата Тогда
		
			х.НачалоПКД = Параметр.ДатаНачала;	
			ИзмененПКД = Истина;
		
		КонецЕсли;	
		
		Если х.ДнейПКД = 0 Тогда
		
			х.ДнейПКД = Параметр.КоличествоДней;	
			ИзмененПКД = Истина;
		
		КонецЕсли;
		
		Если ИзмененПКД Тогда
			х.ОкончаниеПКД = омДаты.ДобавитьДниСУчетомВыходных(х.НачалоПКД, х.ДнейПКД);
		КонецЕсли;
		
		Если х.НачалоРКД = ПустаяДата Тогда
		
			х.НачалоРКД = Параметр.ДатаНачала;	
			ИзмененРКД = Истина;
		
		КонецЕсли;	
		
		Если х.ДнейРКД = 0 Тогда
		
			х.ДнейРКД = Параметр.КоличествоДней;	
			ИзмененРКД = Истина;
			
		КонецЕсли;
		
		Если ИзмененРКД Тогда
			
			Если х.НачалоРКД = х.НачалоПКД И х.ДнейРКД = х.ДнейПКД Тогда
				х.ОкончаниеРКД = х.ОкончаниеПКД;
			Иначе
				х.ОкончаниеРКД = омДаты.ДобавитьДниСУчетомВыходных(х.НачалоРКД, х.ДнейРКД);
			КонецЕсли;
		КонецЕсли;
		
		Если (ИзмененРКД ИЛИ ИзмененПКД) И НЕ х.Изменен Тогда
			х.Изменен = Истина;	
			х.ОформлениеКомпозиции = х.ОформлениеКомпозиции + 3;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ПослеВыбораДат()

&НаКлиенте
Процедура ТаблицаДанныхПередНачаломИзменения(Элемент, Отказ)
	
	ТД = Элементы.ТаблицаДанных.ТекущиеДанные;
	
	Если ТД = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ИмяИзменяемойколонки = Элемент.ТекущийЭлемент.Имя;
	
	Если ИмяИзменяемойколонки = "ТаблицаДанныхОбъект" Тогда
		
		Отказ = Истина;	
		
		ПоказатьЗначение(, ТД.Объект); 
		
	ИначеЕсли ИмяИзменяемойколонки = "ТаблицаДанныхЗадачаПКД" Тогда
		
		Отказ = Истина;	
		
		ПоказатьЗначение(, ТД.ЗадачаПКД); 
		
	ИначеЕсли ИмяИзменяемойколонки = "ТаблицаДанныхЗадачаРКД" Тогда
		
		Отказ = Истина;	
		
		ПоказатьЗначение(, ТД.ЗадачаРКД); 
		
	КонецЕсли;
	
КонецПроцедуры


