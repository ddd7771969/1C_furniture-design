Перем тзЦвета, ДанныеТочки, ПустаяДата;


#Область Команды
	
&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьГантНаСервере();
КонецПроцедуры

#КонецОбласти 

#Область Диаграмма
	
Процедура ОбновитьГантНаСервере()
	
	тзДанныеГанта.Загрузить(ПолучитьДанныеДиаграммы());
	
	Если Сортировка = 0 Тогда
		тзДанныеГанта.Сортировать("ПроектНаименование, 		ДатаНачала");
	ИначеЕсли Сортировка = 1 Тогда
		тзДанныеГанта.Сортировать("ИсполнительНаименование, ДатаНачала");
	Иначе
		тзДанныеГанта.Сортировать("ОбъектТЗНаименование, 	ДатаНачала");
	КонецЕсли; 
	
	стПромежуточныеДанные = ПолучитьПромежуточныеДанные();  //ДатаНачала, ДатаОкончания
	
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	
	ПостроитьДиаграммуГанта(стПромежуточныеДанные); 
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры 

Функция ПолучитьДанныеТочки()

	Серия = Диаграмма.УстановитьСерию("Данные");

	ДанныеТочки = Новый Структура();
	
	ДанныеТочки.Вставить("Точка", 								);
	ДанныеТочки.Вставить("Серия", 					Серия		);
	ДанныеТочки.Вставить("ДатаНачалаПлан", 			ПустаяДата	);
	ДанныеТочки.Вставить("ДатаОкончанияПлан", 		ПустаяДата	);
	ДанныеТочки.Вставить("Этап", 					""			);
	ДанныеТочки.Вставить("НаименованиеТочки",		""			);
	ДанныеТочки.Вставить("Проект", 								);
	ДанныеТочки.Вставить("Исполнитель",							);
	ДанныеТочки.Вставить("Продолжительность", 		0			);
	ДанныеТочки.Вставить("СсылкаЗначение", 						); 
	ДанныеТочки.Вставить("РодительДиаграммы", 					); 
	ДанныеТочки.Вставить("Редактирование", 			Ложь		); 
	ДанныеТочки.Вставить("ЭтоГруппа", 				Ложь		); 
	ДанныеТочки.Вставить("СократитьНаименование",	Ложь		); 
	ДанныеТочки.Вставить("ЕстьРасшифровка", 		Ложь		); 
	ДанныеТочки.Вставить("Задача", 								); 
	
	Возврат ДанныеТочки;	

КонецФункции // ПолучитьДанныеТочки() 

Процедура ПостроитьДиаграммуГанта(стПромежуточныеДанные)

	тзЦвета 	= омДиаграммаГанта.ПолучитьТаблицуЦветов();
	
	ПустаяДата 	= Дата(1, 1, 1);

	ДанныеТочки = ПолучитьДанныеТочки();
	
	стОтборПроект 	= Новый Структура("Проект", );
	
	Если Сортировка = 0 Тогда        
		
		стОтборИсполнитель 	= Новый Структура("Исполнитель, Проект", );
		стОтборОбъектТЗ 	= Новый Структура("ОбъектТЗ, Исполнитель, Проект", );
		
		Для каждого текПроект Из стПромежуточныеДанные.мПроект Цикл
			ВывестиПроект(текПроект, стОтборПроект);
			ДанныеТочки.РодительДиаграммы 	= текПроект;
			стОтборИсполнитель.Проект 		= текПроект; 
			стОтборОбъектТЗ.Проект 			= текПроект; 
			Для каждого текИсполнитель Из стПромежуточныеДанные.мИсполнитель Цикл
				ВывестиИсполнитель(текИсполнитель, стОтборИсполнитель);
				ДанныеТочки.РодительДиаграммы 	= текИсполнитель;
				стОтборОбъектТЗ.Исполнитель 	= текИсполнитель; 
				Для каждого текОбъектТЗ Из стПромежуточныеДанные.мОбъектТЗ Цикл
					ВывестиОбъектТЗ(текОбъектТЗ, стОтборОбъектТЗ);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;

	ИначеЕсли Сортировка = 1 Тогда
		
		Для каждого текИсполнитель Из стПромежуточныеДанные.мИсполнитель Цикл
			ВывестиИсполнитель(текИсполнитель);
			ДанныеТочки.РодительДиаграммы = текИсполнитель;
			Для каждого текПроект Из стПромежуточныеДанные.мПроект Цикл
				ВывестиПроект(текПроект, стОтборПроект);
				Для каждого текОбъектТЗ Из стПромежуточныеДанные.мОбъектТЗ Цикл
					ВывестиОбъектТЗ(текОбъектТЗ, стОтборОбъектТЗ);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;

	Иначе
		
		Для каждого текПроект Из стПромежуточныеДанные.мПроект Цикл
			Для каждого текОбъектТЗ Из стПромежуточныеДанные.мОбъектТЗ Цикл
				ВывестиОбъектТЗ(текОбъектТЗ, стОтборОбъектТЗ);
			КонецЦикла;
		КонецЦикла;

	КонецЕсли; 
	
КонецПроцедуры 

Функция ВывестиПроект(текПроект, стОтборПроект = Неопределено, Обновить = Ложь)

	Если НЕ ВыводитьПроект Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Если стОтборПроект = Неопределено Тогда
		стОтборПроект 	= Новый Структура("Проект", );
	КонецЕсли;
	
	стОтборПроект.Проект = текПроект;
	НайденыеСтроки = тзДанныеГанта.НайтиСтроки(стОтборПроект);
	
	Если НайденыеСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Обновить Тогда
		ДанныеТочки = ПолучитьДанныеТочки();
		тзЦвета 	= омДиаграммаГанта.ПолучитьТаблицуЦветов();
	КонецЕсли;
	
	ДанныеТочки.Точка 					= текПроект;
	ДанныеТочки.СсылкаЗначение 			= текПроект;
	ДанныеТочки.Редактирование 			= Ложь;
	ДанныеТочки.ЭтоГруппа 				= Ложь;
	ДанныеТочки.ЕстьРасшифровка			= Истина;
	ДанныеТочки.СократитьНаименование 	= Ложь;
	ДанныеТочки.РодительДиаграммы 		= Неопределено;
	
	ПустаяДата 		= Дата(1, 1, 1);
	ДатаНачала 		= ПустаяДата;
	ДатаОкончания 	= ПустаяДата;
	Для каждого НайденаяСтрока Из НайденыеСтроки Цикл
		
		Если НайденаяСтрока.ДатаНачалаПлан = ПустаяДата Тогда
			Продолжить;	
		КонецЕсли;
		
		Если ДатаНачала = ПустаяДата Тогда
			ДатаНачала = НайденаяСтрока.ДатаНачалаПлан;
		ИначеЕсли ДатаНачала > НайденаяСтрока.ДатаНачалаПлан Тогда
			ДатаНачала = НайденаяСтрока.ДатаНачалаПлан;
		КонецЕсли;
		
		Продолжительность = ?(НайденаяСтрока.Этап = "ПКД", НайденаяСтрока.ДнейПКД, НайденаяСтрока.ДнейРКД);
		ДатаОкончанияПлан = омДаты.ДобавитьДниСУчетомВыходных(НайденаяСтрока.ДатаНачалаПлан, Продолжительность);
		
		Если ДатаОкончанияПлан > ДатаОкончания Тогда
			ДатаОкончания = ДатаОкончанияПлан;	
		КонецЕсли;
		
		ПроектНаименование = НайденаяСтрока.ПроектНаименование;
		
	КонецЦикла; 
	
	ДанныеТочки.ДатаНачалаПлан 		= ДатаНачала;
	ДанныеТочки.ДатаОкончанияПлан 	= ДатаОкончания;
	ДанныеТочки.Продолжительность 	= Окр((ДатаОкончания - ДатаНачала) / (24 * 3600), 1);
	ДанныеТочки.НаименованиеТочки 	= "";
	ДанныеТочки.Этап 				= "Проект " + ПроектНаименование;
	
	ВывестиТочку(ДанныеТочки, , Обновить);
	
	ДанныеТочки.РодительДиаграммы = текПроект;

	
	Если Обновить Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеТочки;

КонецФункции

Функция ВывестиИсполнитель(текИсполнитель, стОтборИсполнитель = Неопределено, Обновить = Ложь)

	Если стОтборИсполнитель = Неопределено Тогда
		стОтборИсполнитель 	= Новый Структура("Исполнитель", );
	КонецЕсли;
	
	стОтборИсполнитель.Исполнитель = текИсполнитель;
	НайденыеСтроки = тзДанныеГанта.НайтиСтроки(стОтборИсполнитель);
	
	Если НайденыеСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Обновить Тогда
		ДанныеТочки = ПолучитьДанныеТочки();
		тзЦвета 	= омДиаграммаГанта.ПолучитьТаблицуЦветов();
	КонецЕсли;
	
	ДанныеТочки.Точка 					= текИсполнитель;
	ДанныеТочки.СсылкаЗначение 			= текИсполнитель;
	ДанныеТочки.Редактирование 			= Ложь;
	ДанныеТочки.ЭтоГруппа 				= Ложь;
	ДанныеТочки.ЕстьРасшифровка			= Ложь;
	ДанныеТочки.СократитьНаименование 	= Ложь;
	
	ДатаНачала 		= Дата(1, 1, 1);
	ДатаОкончания 	= ДатаНачала;
	Для каждого НайденаяСтрока Из НайденыеСтроки Цикл
		
		Если ДатаНачала = Дата(1, 1, 1) Тогда
			ДатаНачала = НайденаяСтрока.ДатаНачалаПлан;
		ИначеЕсли ДатаНачала > НайденаяСтрока.ДатаНачалаПлан Тогда
			ДатаНачала = НайденаяСтрока.ДатаНачалаПлан;
		КонецЕсли;
		
		Продолжительность = ?(НайденаяСтрока.Этап = "ПКД", НайденаяСтрока.ДнейПКД, НайденаяСтрока.ДнейРКД);
		ДатаОкончанияПлан = омДаты.ДобавитьДниСУчетомВыходных(ДатаНачала, Продолжительность);
		
		Если ДатаОкончанияПлан > ДатаОкончания Тогда
			ДатаОкончания = ДатаОкончанияПлан;	
		КонецЕсли;
		
	КонецЦикла;
	
	
	ДанныеТочки.ДатаНачалаПлан 		= ДатаНачала;
	ДанныеТочки.ДатаОкончанияПлан 	= ДатаОкончания;
	ДанныеТочки.Продолжительность 	= Окр((ДатаОкончания - ДатаНачала) / (24 * 3600), 1);
	ДанныеТочки.НаименованиеТочки 	= "";
	ДанныеТочки.Этап 				= "Конструктор " + НайденыеСтроки[0].ИсполнительНаименование;
	
	ВывестиТочку(ДанныеТочки, , Обновить);
	
	Если Обновить Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеТочки;


КонецФункции

Процедура ВывестиОбъектТЗ(текОбъектТЗ, стОтборОбъектТЗ)

	стОтборОбъектТЗ.ОбъектТЗ = текОбъектТЗ;
	НайденыеСтроки = тзДанныеГанта.НайтиСтроки(стОтборОбъектТЗ);
	
	Если НайденыеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеТочки.Точка 					= текОбъектТЗ;
	ДанныеТочки.СсылкаЗначение 			= текОбъектТЗ;
	ДанныеТочки.Редактирование 			= Истина;
	ДанныеТочки.ЭтоГруппа 				= Ложь;
	ДанныеТочки.ЕстьРасшифровка			= Истина;
	ДанныеТочки.СократитьНаименование 	= Истина;
	
	Для каждого НайденаяСтрока Из НайденыеСтроки Цикл
		
		Продолжительность = ?(НайденаяСтрока.Этап = "ПКД", НайденаяСтрока.ДнейПКД, НайденаяСтрока.ДнейРКД);
		
		ЗаполнитьЗначенияСвойств(ДанныеТочки, НайденаяСтрока); 
		ДанныеТочки.ДатаОкончанияПлан = омДаты.ДобавитьДниСУчетомВыходных(ДанныеТочки.ДатаНачалаПлан, Продолжительность);
		ДанныеТочки.Продолжительность = Продолжительность;
		ДанныеТочки.НаименованиеТочки = НайденаяСтрока.ОбъектТЗНаименование;
		
		ВывестиТочку(ДанныеТочки);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВывестиТочку(ДанныеТочки, ТекущийЦвет = Неопределено, Обновить = Ложь)

	СукундВСутках 	= 24 * 3600;
	
	Если ДанныеТочки.ДатаНачалаПлан = ПустаяДата Тогда
	
		Возврат Неопределено;	
	
	КонецЕсли;
	
	Если Обновить Тогда

		КомплектТочка 			= Диаграмма.УстановитьТочку(ДанныеТочки.СсылкаЗначение, ДанныеТочки.РодительДиаграммы);
		КомплектТочка.Текст 	= ДанныеТочки.НаименованиеТочки;
		Значение = Диаграмма.ПолучитьЗначение(КомплектТочка, ДанныеТочки.Серия);
		Если Значение.Количество() = 0 Тогда
			Возврат Неопределено;	
		КонецЕсли;
		
		Значение.Очистить();	
		
		Интервал 				= Значение.Добавить();
	Иначе
		
		КомплектТочка 			= Диаграмма.УстановитьТочку(ДанныеТочки.СсылкаЗначение, ДанныеТочки.РодительДиаграммы);
		КомплектТочка.Текст 	= ДанныеТочки.НаименованиеТочки;
		Значение = Диаграмма.ПолучитьЗначение(КомплектТочка, ДанныеТочки.Серия);
		Значение.Редактирование = ДанныеТочки.Редактирование;
		
		Интервал 				= Значение.Добавить();

	КонецЕсли;
	
	Интервал.Начало 		= ДанныеТочки.ДатаНачалаПлан;
	Интервал.Конец 			= ?(ДанныеТочки.ДатаОкончанияПлан - ДанныеТочки.ДатаНачалаПлан < СукундВСутках, ДанныеТочки.ДатаНачалаПлан + СукундВСутках, ДанныеТочки.ДатаОкончанияПлан);
	Интервал.Текст 			= ДанныеТочки.Этап;
	
	Если НЕ ТекущийЦвет = Неопределено Тогда
		Интервал.Цвет			= ТекущийЦвет;
	ИначеЕсли НЕ тзЦвета = Неопределено Тогда
		Интервал.Цвет			= омДиаграммаГанта.ПолучитьЦвет(ДанныеТочки.Этап, тзЦвета, ДанныеТочки.ЭтоГруппа, ДанныеТочки.СократитьНаименование);
	КонецЕсли;
	
	Если ДанныеТочки.ЕстьРасшифровка Тогда
		Интервал.Расшифровка 	= Новый Структура("Проект, Этап, СсылкаКомплект, Продолжительность, Исполнитель"
		, ДанныеТочки.Проект, ДанныеТочки.Этап, ДанныеТочки.СсылкаЗначение, ДанныеТочки.Продолжительность, ДанныеТочки.Исполнитель);
	Иначе
		Интервал.Расшифровка 	= Новый Структура("Продолжительность", ДанныеТочки.Продолжительность);
	КонецЕсли;
	
	Если НЕ ДанныеТочки.Задача = Неопределено Тогда
	
		Интервал.Расшифровка.Вставить("Задача", ДанныеТочки.Задача);	
	
	КонецЕсли;
	
	Если ДанныеТочки.СократитьНаименование Тогда
		Интервал.Текст = Лев(Интервал.Текст, 1);
	КонецЕсли;
	
	Значение.Текст = УстановитьТекстЗначения(ДанныеТочки.ДатаНачалаПлан, ДанныеТочки.ДатаОкончанияПлан, ДанныеТочки.Этап); 
	
	Возврат Интервал;
	
КонецФункции // ВывестиТочку()

&НаСервереБезКонтекста
Функция УстановитьТекстЗначения(ДатаНачала, ДатаОкончания, Этап)
	
	Возврат Этап + " " + Формат(ДатаНачала, "ДФ=dd.MM.yy") + " - " + Формат(ДатаОкончания, "ДФ=dd.MM.yy");
	
КонецФункции

Функция ПолучитьПромежуточныеДанные()
	
	ОписаниеЧисло = Новый ОписаниеТипов("Число");
	
	мОбъектТЗ 		= Новый Массив;
	мПроект 		= Новый Массив;
	мИсполнитель 	= Новый Массив;
	мКомплекты 		= Новый Массив;
	мКомпозиции		= Новый Массив;

	ПустаяДата = Дата(1, 1, 1); 
	
    стПромежуточныеДанные 	= Новый Структура("ДатаНачала, 	ДатаОкончания, КрайПКД, 	КрайРКД",
											   ПустаяДата,  ПустаяДата,    ПустаяДата, 	ПустаяДата); 
	
	Для каждого х Из тзДанныеГанта Цикл
		
		Если х.ДатаНачалаПлан > ПустаяДата Тогда
		
			Если стПромежуточныеДанные.ДатаНачала = ПустаяДата Тогда
			
				стПромежуточныеДанные.ДатаНачала = х.ДатаНачалаПлан;
				
			ИначеЕсли стПромежуточныеДанные.ДатаНачала > х.ДатаНачалаПлан Тогда 
				
				стПромежуточныеДанные.ДатаНачала = х.ДатаНачалаПлан;
			
			КонецЕсли;
			
			Если стПромежуточныеДанные.ДатаОкончания < х.ДатаНачалаПлан Тогда
			
				стПромежуточныеДанные.ДатаОкончания = х.ДатаНачалаПлан;	
			
			КонецЕсли;
		
		КонецЕсли;
		
		ДобавитьВМассив(х.Проект, 		мПроект);
		ДобавитьВМассив(х.Исполнитель, 	мИсполнитель);
		ДобавитьВМассив(х.ОбъектТЗ, 	мОбъектТЗ);
		
		Если ТипЗнч(х.ОбъектТЗ) = Тип("СправочникСсылка.ИзделияКомплект") Тогда
			ДобавитьВМассив(х.ОбъектТЗ,	мКомплекты);
		Иначе
			ДобавитьВМассив(х.ОбъектТЗ,	мКомпозиции);
		КонецЕсли;
	
	КонецЦикла; 
	
	//ЗаполнитьКоличествоДней(тзДанныеГанта, мКомплекты, мКомпозиции);

	стПромежуточныеДанные.Вставить("мОбъектТЗ", 	мОбъектТЗ);
	стПромежуточныеДанные.Вставить("мПроект", 		мПроект);
	стПромежуточныеДанные.Вставить("мИсполнитель", 	мИсполнитель);

	Возврат стПромежуточныеДанные; 

КонецФункции // ПолучитьпромежуточныеДанные()

Процедура ЗаполнитьКоличествоДней(тзДанныеГанта, мКомплекты, мКомпозиции)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыКомплекта.Комплект КАК ОбъектТЗ,
		|	ДатыКомплекта.ДнейПКД КАК ДнейПКД,
		|	ДатыКомплекта.ДнейРКД КАК ДнейРКД,
		|	ДатыКомплекта.КрайПКД КАК КрайПКД,
		|	ДатыКомплекта.КрайРКД КАК КрайРКД
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|ГДЕ
		|	ДатыКомплекта.Комплект В(&Комплект)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыКомплекта.Комплект КАК ОбъектТЗ,
		|	ДатыКомплекта.ДнейПКД КАК ДнейПКД,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДнейРКД,
		|	ДатыКомплекта.КрайПКД КАК КрайПКД,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК КрайРКД
		|ИЗ
		|	РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|ГДЕ
		|	ДатыКомплекта.Комплект В(&Композиция)";
	
	Запрос.УстановитьПараметр("Комплект", 	мКомплекты);
	Запрос.УстановитьПараметр("Композиция", мКомпозиции);
	
	Результат = Запрос.ВыполнитьПакет();
	
	стОтбор = Новый Структура("ОбъектТЗ", );
	
	Выборка = Результат[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		стОтбор.ОбъектТЗ = Выборка.ОбъектТЗ; 
		ЗаполнитьЗначенияСвойств(тзДанныеГанта.НайтиСтроки(стОтбор)[0], Выборка);
	КонецЦикла;
	
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		стОтбор.ОбъектТЗ = Выборка.ОбъектТЗ; 
		ЗаполнитьЗначенияСвойств(тзДанныеГанта.НайтиСтроки(стОтбор)[0], Выборка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьВМассив(Элемент, МассивДляДобавления)

	Если МассивДляДобавления.Найти(Элемент) = Неопределено Тогда
	
		МассивДляДобавления.Добавить(Элемент);	
	
	КонецЕсли;	

КонецПроцедуры

Функция ПолучитьДанныеДиаграммы()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Задачи_ПКД.Ссылка КАК Задача,
		|	Задачи_ПКД.ОбъектТЗ КАК ОбъектТЗ,
		|	Задачи_ПКД.ОбъектТЗ.Проект КАК Проект,
		|	МИНИМУМ(Подразделения.НомерСтрокиТаблицы) КАК НомерСтрокиТаблицы,
		|	Подразделения.Подразделение КАК Подразделение,
		|	Задачи_ПКД.ТипЗадачи КАК ТипЗадачи
		|ПОМЕСТИТЬ втПКД
		|ИЗ
		|	Задача.Задачи_ПКД КАК Задачи_ПКД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Подразделения КАК Подразделения
		|		ПО Задачи_ПКД.Исполнитель = Подразделения.Сотрудник
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Задачи_ПКД.ОбъектТЗ.Проект = &Проект
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Конструктор = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Задачи_ПКД.Исполнитель = &Конструктор
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Подразделения.Подразделение = &Подразделение
		|		КОНЕЦ
		|	И НЕ Задачи_ПКД.Выполнена
		|	И Задачи_ПКД.ТипЗадачи В (ЗНАЧЕНИЕ(Перечисление.ТипыЗадачКонструирования.ДоработкаКонструктором), ЗНАЧЕНИЕ(Перечисление.ТипыЗадачКонструирования.Разработка))
		|
		|СГРУППИРОВАТЬ ПО
		|	Задачи_ПКД.Ссылка,
		|	Задачи_ПКД.ОбъектТЗ,
		|	Задачи_ПКД.ОбъектТЗ.Проект,
		|	Подразделения.Подразделение,
		|	Задачи_ПКД.ТипЗадачи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПКД.Задача КАК Задача,
		|	ТаймингЗадач.Состояние КАК Состояние,
		|	ТаймингЗадач.Исполнитель КАК Исполнитель,
		|	ТаймингЗадач.Автор КАК Автор,
		|	ТаймингЗадачКомментарии.Комментарий КАК Комментарий,
		|	ДатыКомплекта.НачалоПКД КАК НачалоПКД,
		|	ДатыКомплекта.КрайПКД КАК КрайПКД,
		|	ДатыКомплекта.ОкончаниеПКД КАК ОкончаниеПКД
		|ПОМЕСТИТЬ втТайминг
		|ИЗ
		|	втПКД КАК втПКД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТаймингЗадач КАК ТаймингЗадач
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймингЗадачКомментарии КАК ТаймингЗадачКомментарии
		|			ПО ТаймингЗадач.УИД = ТаймингЗадачКомментарии.УИД
		|		ПО втПКД.Задача = ТаймингЗадач.Задача
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|		ПО втПКД.ОбъектТЗ = ДатыКомплекта.Комплект
		|ГДЕ
		|	НЕ(ДатыКомплекта.НачалоПКД = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ ДатыКомплекта.НачалоПКД ЕСТЬ NULL)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПКД.ОбъектТЗ КАК ОбъектТЗ,
		|	ЕСТЬNULL(втТайминг.Исполнитель, втПКД.Задача.Исполнитель) КАК Исполнитель,
		|	втПКД.Подразделение КАК Подразделение,
		|	ЕСТЬNULL(втТайминг.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияЗадач.ОжиданиеКонструктором)) КАК Состояние,
		|	ЕСТЬNULL(втТайминг.Комментарий, """") КАК Комментарий,
		|	ЕСТЬNULL(втТайминг.НачалоПКД, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачала,
		|	ЕСТЬNULL(втТайминг.КрайПКД, ДАТАВРЕМЯ(1, 1, 1)) КАК Край,
		|	втПКД.Проект КАК Проект,
		|	втПКД.Проект.Наименование КАК ПроектНаименование,
		|	ВЫБОР
		|		КОГДА втТайминг.Исполнитель ЕСТЬ NULL
		|			ТОГДА втПКД.Задача.Исполнитель.Наименование
		|		ИНАЧЕ втТайминг.Исполнитель.Наименование
		|	КОНЕЦ КАК ИсполнительНаименование,
		|	втПКД.Подразделение.Наименование КАК ПодразделениеНаименование,
		|	втПКД.ОбъектТЗ.Наименование КАК ОбъектТЗНаименование,
		|	""ПКД"" КАК Этап,
		|	втПКД.Задача КАК Задача,
		|	ЕСТЬNULL(втПКД.Задача.ОбобщеннаяЗадача, ЛОЖЬ) КАК ОбобщеннаяЗадача,
		|	ЕСТЬNULL(втТайминг.ОкончаниеПКД, 0) КАК ДатаОкончания
		|ИЗ
		|	втПКД КАК втПКД
		|		ЛЕВОЕ СОЕДИНЕНИЕ втТайминг КАК втТайминг
		|		ПО втПКД.Задача = втТайминг.Задача";
	
	Запрос.УстановитьПараметр("Конструктор", Конструктор);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ПолучитьДанныеДиаграммы()

#КонецОбласти

#Область ИзменениеФормы

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Сотрудник") Тогда
		Если ТипЗнч(Параметры.Сотрудник) = Тип("СправочникСсылка.Пользователи") Тогда
			Конструктор 	= омСотрудники.ПолучитьСотруднткаПоПользователю(Параметры.Сотрудник);
		Иначе
			Конструктор 	= Параметры.Сотрудник;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Проект") Тогда
		Проект 		= Параметры.Проект;
	КонецЕсли;
	
	Если РольДоступна("Конструктор") Тогда
		
		Элементы.Подразделение.Видимость 	= Ложь;
		Элементы.Конструктор.ТолькоПросмотр = Истина;
		ЭтаФорма.ТолькоПросмотр 			= Истина;
		
	КонецЕсли;
	
	Диаграмма.ОтображатьЛегенду = Ложь;
	ВыводитьПроект				= Истина;
	ОбновитьГантНаСервере();
	
КонецПроцедуры


#КонецОбласти 

#Область ИзменениеРеквизитов
	
&НаКлиенте
Процедура ВыводитьПроектПриИзменении(Элемент)
	ОбновитьГантНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СортировкаПриИзменении(Элемент)
	ОбновитьГантНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КонструкторПриИзменении(Элемент)
	ОбновитьГантНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ОбновитьГантНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	ОбновитьГантНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаПриОкончанииРедактированияИнтервала(Элемент, Интервал, ОтменаРедактирования)
	
	Интервал.Начало = НачалоДня(Интервал.Начало);
	Интервал.Конец 	= омДаты.ДобавитьДниСУчетомВыходных(Интервал.Начало, Интервал.Расшифровка.Продолжительность);
	
	Если Интервал.Расшифровка.Свойство("Задача") Тогда
		ЗаписатьИзменениеВРегистр(Интервал.Начало, Интервал.Расшифровка.Задача);
		
		Для каждого х Из тзДанныеГанта Цикл
			Если Интервал.Расшифровка.Задача = х.Задача И Интервал.Расшифровка.Этап = х.Этап Тогда
				х.ДатаНачалаПлан = Интервал.Начало;
				
				ВывестиПроект(Интервал.Расшифровка.Проект, 				, Истина);
				ВывестиИсполнитель(Интервал.Расшифровка.Исполнитель, 	, Истина);

			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 


#Область ЗаписьВРегистры

&НаСервереБезКонтекста
Процедура ЗаписатьИзменениеВРегистр(Начало, Задача)
	
	Если ТипЗнч(Задача) Тогда
	
		
	
	Иначе
	
		
	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаОбработкаРасшифровки(Элемент, Расшифровки, СтандартнаяОбработка, Дата)
	
	СтандартнаяОбработка = Ложь;
	
	Для каждого текЭлемент Из Расшифровки Цикл
		
		Если текЭлемент = Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		
		Если текЭлемент.Свойство("Задача") Тогда
			
			Если ЗначениеЗаполнено(текЭлемент.Задача) Тогда
				ПоказатьЗначение(,текЭлемент.Задача);	
			КонецЕсли;	
			
		КонецЕсли;
	
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаВыбор(Элемент, Значения, СтандартнаяОбработка, Дата)
	Если Тип("ТочкаДиаграммыГанта") = ТипЗнч(Значения) Тогда
		Если Тип("СправочникСсылка.ИзделияКомплект") = ТипЗнч(Значения.Значение) ИЛИ 
			Тип("СправочникСсылка.Композиции") = ТипЗнч(Значения.Значение) ИЛИ
			Тип("СправочникСсылка.Проекты") = ТипЗнч(Значения.Значение) Тогда
				СтандартнаяОбработка = Ложь;
				ПоказатьЗначение(,Значения.Значение);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


#КонецОбласти