Перем тзЦвета, ДанныеТочки, ПустаяДата;

#Область Команды
	
&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьГантНаСервере();
	Диаграмма.ОтображатьЛегенду = Ложь;
КонецПроцедуры

#КонецОбласти 

#Область Диаграмма
	
Процедура ОбновитьГантНаСервере()
	
	тзДанныеГанта.Загрузить(ПолучитьДанныеДиаграммы());
	
	Если Сортировка = 0 Тогда
		тзДанныеГанта.Сортировать("ПроектНаименование, 		ДатаНачала, ДатаНачалаПлан");
	ИначеЕсли Сортировка = 1 Тогда
		тзДанныеГанта.Сортировать("ИсполнительНаименование, ДатаНачала, ДатаНачалаПлан");
	Иначе
		тзДанныеГанта.Сортировать("ОбъектТЗНаименование, 	ДатаНачала, ДатаНачалаПлан");
	КонецЕсли; 
	
	стПромежуточныеДанные = ПолучитьПромежуточныеДанные();  //ДатаНачала, ДатаОкончания
	
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	
	ПостроитьДиаграммуГанта(стПромежуточныеДанные); 
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры 

Процедура ПостроитьДиаграммуГанта(стПромежуточныеДанные)
	
	тзЦвета 	= омДиаграммаГанта.ПолучитьТаблицуЦветов();
	
	ПустаяДата 	= Дата(1, 1, 1);
	
	ДанныеТочки = ПолучитьДанныеТочки();
	
	стОтборПроект 	= Новый Структура("Проект", );
	
	Если Сортировка = 0 Тогда        
		
		стОтборИсполнитель 	= Новый Структура("Исполнитель, Проект", );
		стОтборОбъектТЗ 	= Новый Структура("ОбъектТЗ, Исполнитель, Проект", );
		
		Для каждого текПроект Из стПромежуточныеДанные.мПроект Цикл
			
			ДанныеТочки.РодительДиаграммы 	= Неопределено;
			
			ВывестиПроект(текПроект, стОтборПроект);
			стОтборИсполнитель.Проект 		= текПроект; 
			стОтборОбъектТЗ.Проект 			= текПроект; 
			
			Для каждого текИсполнитель Из стПромежуточныеДанные.мИсполнитель Цикл
				
				ДанныеТочки.РодительДиаграммы 	= текПроект;
				
				стОтборИсполнитель.Исполнитель	= текИсполнитель; 
				ВывестиИсполнитель(текИсполнитель, стОтборИсполнитель);
				ДанныеТочки.РодительДиаграммы 	= текИсполнитель;
				стОтборОбъектТЗ.Исполнитель 	= текИсполнитель; 
				
				Для каждого текОбъектТЗ Из стПромежуточныеДанные.мОбъектТЗ Цикл
					ВывестиОбъектТЗ(текОбъектТЗ, стОтборОбъектТЗ);
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
	ИначеЕсли Сортировка = 1 Тогда
		
		Для каждого текИсполнитель Из стПромежуточныеДанные.мИсполнитель Цикл
			ВывестиИсполнитель(текИсполнитель);
			ДанныеТочки.РодительДиаграммы = текИсполнитель;
			Для каждого текПроект Из стПромежуточныеДанные.мПроект Цикл
				ВывестиПроект(текПроект, стОтборПроект);
				Для каждого текОбъектТЗ Из стПромежуточныеДанные.мОбъектТЗ Цикл
					ВывестиОбъектТЗ(текОбъектТЗ, стОтборОбъектТЗ);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
	Иначе
		
		Для каждого текПроект Из стПромежуточныеДанные.мПроект Цикл
			Для каждого текОбъектТЗ Из стПромежуточныеДанные.мОбъектТЗ Цикл
				ВывестиОбъектТЗ(текОбъектТЗ, стОтборОбъектТЗ);
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры 

Процедура ВывестиОбъектТЗ(текОбъектТЗ, стОтборОбъектТЗ)

	стОтборОбъектТЗ.ОбъектТЗ = текОбъектТЗ;
	НайденыеСтроки = тзДанныеГанта.НайтиСтроки(стОтборОбъектТЗ);
	
	Если НайденыеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеТочки.Точка 					= текОбъектТЗ;
	ДанныеТочки.СсылкаЗначение 			= текОбъектТЗ;
	ДанныеТочки.Редактирование 			= Истина;
	ДанныеТочки.ЭтоГруппа 				= Ложь;
	ДанныеТочки.ЕстьРасшифровка			= Истина;
	ДанныеТочки.СократитьНаименование 	= Истина;
	
	ПустаяДата = Дата(1, 1, 1);
	
	Для каждого НайденаяСтрока Из НайденыеСтроки Цикл
		
		Если ДанныеТочки.ДатаНачала = ПустаяДата Тогда
		
			Продолжить;	
		
		КонецЕсли; 
		
		ЗаполнитьЗначенияСвойств(ДанныеТочки, НайденаяСтрока); 
		ДанныеТочки.ДатаОкончания	  = НайденаяСтрока.ДатаОкончания;
		ДанныеТочки.Продолжительность = НайденаяСтрока.Продолжительность;
		ДанныеТочки.НаименованиеТочки = НайденаяСтрока.ОбъектТЗНаименование + "." + НайденаяСтрока.Состояние;
		ДанныеТочки.ЭтоПлан 		  = НайденаяСтрока.ЭтоПлан;
		ДанныеТочки.Состояние 		  = НайденаяСтрока.Состояние;
		ДанныеТочки.Приоритет 		  = НайденаяСтрока.Приоритет;
		ДанныеТочки.Переходы	  	  = НайденаяСтрока.Этап + НайденаяСтрока.Переходы;
		
		ВывестиТочку(ДанныеТочки);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьДанныеТочки()
	
	Серия = Диаграмма.УстановитьСерию("Задачи");
	
	ДанныеТочки = Новый Структура();
	
	ДанныеТочки.Вставить("Точка", 															);
	ДанныеТочки.Вставить("Серия", 					Серия									);
	ДанныеТочки.Вставить("ДатаНачала", 				ПустаяДата								);
	ДанныеТочки.Вставить("ДатаОкончания", 			ПустаяДата								);
	ДанныеТочки.Вставить("Этап", 					""										);
	ДанныеТочки.Вставить("НаименованиеТочки",		""										);
	ДанныеТочки.Вставить("Проект", 															);
	ДанныеТочки.Вставить("Исполнитель",														);
	ДанныеТочки.Вставить("Продолжительность", 		0										);
	ДанныеТочки.Вставить("СсылкаЗначение", 													); 
	ДанныеТочки.Вставить("РодительДиаграммы", 												); 
	ДанныеТочки.Вставить("Редактирование", 			Ложь									); 
	ДанныеТочки.Вставить("ЭтоГруппа", 				Ложь									); 
	ДанныеТочки.Вставить("СократитьНаименование",	Ложь									); 
	ДанныеТочки.Вставить("ЕстьРасшифровка", 		Ложь									); 
	ДанныеТочки.Вставить("Задача", 															); 
	ДанныеТочки.Вставить("Приоритет", 														); 
	ДанныеТочки.Вставить("Переходы",				0										); 
	ДанныеТочки.Вставить("ЭтоПлан", 				Ложь									); 
	ДанныеТочки.Вставить("Состояние", 				Перечисления.СостоянияЗадач.ПустаяСсылка()); 
	
	Возврат ДанныеТочки;	
	
КонецФункции // ПолучитьДанныеТочки() 

Функция ПолучитьДатаНачалаОкончания(НайденыеСтроки)
	
	СекундВСутках = 3600 * 24;
	ПустаяДата 		= Дата(1, 1, 1);
	
	стДаты = Новый Структура("ДатаНачала, ДатаОкончания", ПустаяДата, ПустаяДата);
	
	Для каждого НайденаяСтрока Из НайденыеСтроки Цикл
		
		Если НайденаяСтрока.ДатаНачала = ПустаяДата И НайденаяСтрока.ДатаНачалаПлан = ПустаяДата Тогда
			Продолжить;	
		КонецЕсли;
		
		Если стДаты.ДатаНачала = ПустаяДата Тогда
			Если НайденаяСтрока.ДатаНачала = ПустаяДата Тогда
				стДаты.ДатаНачала = НайденаяСтрока.ДатаНачалаПлан;
			ИначеЕсли НайденаяСтрока.ДатаНачалаПлан = ПустаяДата Тогда	
				стДаты.ДатаНачала = НайденаяСтрока.ДатаНачала;
			Иначе
				стДаты.ДатаНачала = Мин(НайденаяСтрока.ДатаНачала, НайденаяСтрока.ДатаНачалаПлан);
			КонецЕсли;
		Иначе
			Если стДаты.ДатаНачала > НайденаяСтрока.ДатаНачала И НайденаяСтрока.ДатаНачала > ПустаяДата Тогда
				стДаты.ДатаНачала = НайденаяСтрока.ДатаНачала;
			КонецЕсли;
			Если стДаты.ДатаНачала > НайденаяСтрока.ДатаНачалаПлан И НайденаяСтрока.ДатаНачалаПлан > ПустаяДата Тогда
				стДаты.ДатаНачала = НайденаяСтрока.ДатаНачалаПлан;
			КонецЕсли;
		КонецЕсли;
		
		Если НайденаяСтрока.Продолжительность = 0 Тогда
			ДатаОкончанияСВыходными = НайденаяСтрока.ДатаНачала + СекундВСутках
		Иначе
			ДатаОкончанияСВыходными = омДаты.ДобавитьДниСУчетомВыходных(НайденаяСтрока.ДатаНачала, НайденаяСтрока.Продолжительность);
		КонецЕсли;
		
		Если ДатаОкончанияСВыходными > стДаты.ДатаОкончания Тогда
			стДаты.ДатаОкончания = ДатаОкончанияСВыходными;	
		КонецЕсли;
		
	КонецЦикла; 
	
	Возврат стДаты;

КонецФункции // ПолучитьДатаНачалаОкончания()

Функция ВывестиПроект(текПроект, стОтборПроект = Неопределено, Обновить = Ложь)

	Если НЕ ВыводитьПроект Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Если стОтборПроект = Неопределено Тогда
		стОтборПроект 	= Новый Структура("Проект", );
	КонецЕсли;
	
	стОтборПроект.Проект = текПроект;
	НайденыеСтроки = тзДанныеГанта.НайтиСтроки(стОтборПроект);
	
	Если НайденыеСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Обновить Тогда
		ДанныеТочки = ПолучитьДанныеТочки();
		тзЦвета 	= омДиаграммаГанта.ПолучитьТаблицуЦветов();
	КонецЕсли;
	
	ДанныеТочки.Точка 					= текПроект;
	ДанныеТочки.СсылкаЗначение 			= текПроект;
	ДанныеТочки.Редактирование 			= Ложь;
	ДанныеТочки.ЭтоГруппа 				= Ложь;
	ДанныеТочки.ЕстьРасшифровка			= Истина;
	ДанныеТочки.СократитьНаименование 	= Ложь;
	ДанныеТочки.ЭтоПлан					= Ложь;
	ДанныеТочки.РодительДиаграммы 		= Неопределено;
	ДанныеТочки.Приоритет 		  		= "";
	ДанныеТочки.Переходы	  	  		= "";
	
	//ПроектНаименование = НайденыеСтроки[0].ПроектНаименование;
	
	
	стДаты = ПолучитьДатаНачалаОкончания(НайденыеСтроки); 	
	
	ДанныеТочки.ДатаНачала 			= стДаты.ДатаНачала;
	ДанныеТочки.ДатаОкончания	 	= стДаты.ДатаОкончания;
	ДанныеТочки.Продолжительность 	= Окр((стДаты.ДатаОкончания - стДаты.ДатаНачала) / (24 * 3600), 1);
	ДанныеТочки.НаименованиеТочки 	= "";
	ДанныеТочки.Этап 				= "";
	
	ВывестиТочку(ДанныеТочки, , Обновить);
	
	ДанныеТочки.РодительДиаграммы = текПроект;

	Если Обновить Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеТочки;

КонецФункции

Функция ВывестиИсполнитель(текИсполнитель, стОтборИсполнитель = Неопределено, Обновить = Ложь)

	Если стОтборИсполнитель = Неопределено Тогда
		стОтборИсполнитель 	= Новый Структура("Исполнитель", );
	КонецЕсли;
	
	стОтборИсполнитель.Исполнитель = текИсполнитель;
	НайденыеСтроки = тзДанныеГанта.НайтиСтроки(стОтборИсполнитель);
	
	Если НайденыеСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Обновить Тогда
		ДанныеТочки = ПолучитьДанныеТочки();
		тзЦвета 	= омДиаграммаГанта.ПолучитьТаблицуЦветов();
	КонецЕсли;
	
	ДанныеТочки.Точка 					= текИсполнитель;
	ДанныеТочки.СсылкаЗначение 			= текИсполнитель;
	ДанныеТочки.Редактирование 			= Ложь;
	ДанныеТочки.ЭтоГруппа 				= Ложь;
	ДанныеТочки.ЕстьРасшифровка			= Ложь;
	ДанныеТочки.СократитьНаименование 	= Ложь;
	ДанныеТочки.ЭтоПлан					= Ложь;
	ДанныеТочки.Приоритет 		  		= "";
	ДанныеТочки.Переходы	  	  		= "";
	
	стДаты = ПолучитьДатаНачалаОкончания(НайденыеСтроки); 	
		
	ДанныеТочки.ДатаНачала 			= стДаты.ДатаНачала;
	ДанныеТочки.ДатаОкончания	 	= стДаты.ДатаОкончания;
	ДанныеТочки.Продолжительность 	= Окр((стДаты.ДатаОкончания - стДаты.ДатаНачала) / (24 * 3600), 1);
	ДанныеТочки.НаименованиеТочки 	= "";
	ДанныеТочки.Этап 				= "";
	
	ВывестиТочку(ДанныеТочки, , Обновить);
	
	Если Обновить Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеТочки;


КонецФункции

Функция ВывестиТочку(ДанныеТочки, ТекущийЦвет = Неопределено, Обновить = Ложь)

	СукундВСутках 	= 24 * 3600;
	
	Если ДанныеТочки.ДатаНачала = ПустаяДата Тогда
	
		Возврат Неопределено;	
	
	КонецЕсли;
	
	Если Обновить Тогда

		КомплектТочка 			= Диаграмма.УстановитьТочку(ДанныеТочки.СсылкаЗначение, ДанныеТочки.РодительДиаграммы);
		КомплектТочка.Текст 	= ДанныеТочки.НаименованиеТочки;
		Значение = Диаграмма.ПолучитьЗначение(КомплектТочка, ДанныеТочки.Серия);
		Если Значение.Количество() = 0 Тогда
			Возврат Неопределено;	
		КонецЕсли;
		
		Значение.Очистить();	
		
		Интервал 				= Значение.Добавить();
	Иначе
		
		КомплектТочка 			= Диаграмма.УстановитьТочку(ДанныеТочки.СсылкаЗначение, ДанныеТочки.РодительДиаграммы);
		КомплектТочка.Текст 	= ДанныеТочки.НаименованиеТочки;
		Значение = Диаграмма.ПолучитьЗначение(КомплектТочка, ДанныеТочки.Серия);
		Значение.Редактирование = ДанныеТочки.Редактирование И ДанныеТочки.ЭтоПлан;
		Значение.Данные.Переходы = ДанныеТочки.Переходы;
		
		Интервал 				= Значение.Добавить();

	КонецЕсли; 
	
	Диаграмма.РазвернутьТочку(КомплектТочка, Истина);
	
	Интервал.Начало = ДанныеТочки.ДатаНачала;
	Интервал.Конец 	= ?(ДанныеТочки.ДатаОкончания - ДанныеТочки.ДатаНачала < СукундВСутках, ДанныеТочки.ДатаНачала + СукундВСутках, ДанныеТочки.ДатаОкончания);
	Интервал.Текст 	= ДанныеТочки.Этап;
	
	Если НЕ ТекущийЦвет = Неопределено Тогда
		Интервал.Цвет			= ТекущийЦвет;
	ИначеЕсли НЕ тзЦвета = Неопределено Тогда 
		Если ДанныеТочки.ЭтоПлан Тогда
			ТекущийЭтап = "ПланКонструирование";	
		Иначе
			ТекущийЭтап = ДанныеТочки.Этап;	
		КонецЕсли;
		Интервал.Цвет			= омДиаграммаГанта.ПолучитьЦвет(ТекущийЭтап, тзЦвета, ДанныеТочки.ЭтоГруппа, ДанныеТочки.СократитьНаименование);
	КонецЕсли;
	
	Если ДанныеТочки.ЕстьРасшифровка Тогда
		Интервал.Расшифровка 	= Новый Структура("Проект, Этап, СсылкаКомплект, Продолжительность, Исполнитель, Состояние"
		, ДанныеТочки.Проект, ДанныеТочки.Этап, ДанныеТочки.СсылкаЗначение, ДанныеТочки.Продолжительность
		, ДанныеТочки.Исполнитель, ДанныеТочки.Состояние);
	Иначе
		Интервал.Расшифровка 	= Новый Структура("Продолжительность", ДанныеТочки.Продолжительность);
	КонецЕсли;
	
	Если НЕ ДанныеТочки.Задача = Неопределено Тогда
	
		Интервал.Расшифровка.Вставить("Задача", ДанныеТочки.Задача);	
	
	КонецЕсли;
	
	Если ДанныеТочки.СократитьНаименование Тогда
		Интервал.Текст = Лев(Интервал.Текст, 1);
	КонецЕсли;
	
	Значение.Текст = УстановитьТекстЗначения(ДанныеТочки.ДатаНачала, ДанныеТочки.ДатаОкончания, ДанныеТочки.Этап); 
	
	Возврат Интервал;
	
КонецФункции // ВывестиТочку()

&НаСервереБезКонтекста
Функция УстановитьТекстЗначения(ДатаНачала, ДатаОкончания, Этап)
	
	Возврат Этап + " " + Формат(ДатаНачала, "ДФ=dd.MM.yy") + " - " + Формат(ДатаОкончания, "ДФ=dd.MM.yy");
	
КонецФункции

Функция ПолучитьПромежуточныеДанные()
	
	ОписаниеЧисло = Новый ОписаниеТипов("Число");
	
	мОбъектТЗ 		= Новый Массив;
	мПроект 		= Новый Массив;
	мИсполнитель 	= Новый Массив;

	ПустаяДата = Дата(1, 1, 1); 
	
    стПромежуточныеДанные 	= Новый Структура("ДатаНачала, 	ДатаОкончания, КрайПКД, 	КрайРКД",
											   ПустаяДата,  ПустаяДата,    ПустаяДата, 	ПустаяДата);
											   
	ПриостановкаКонструктором 	= Перечисления.СостоянияЗадач.ПриостановкаКонструктором;										   
	СостояниеОжидания 			= Перечисления.СостоянияЗадач.ОжиданиеКонструктором;
	
	мДобавитьСтроки = Новый Массив;
	НаименованиеКолонокТаблицы = омОбщиеФункции.КолонкиТаблицыЗначений(тзДанныеГанта);
	ТД = НачалоДня(ТекущаяДата());
	
	Для каждого х Из тзДанныеГанта Цикл 
		
		Если х.Состояние = СостояниеОжидания Тогда
		
			х.ЭтоПлан = Истина;	
		
		ИначеЕсли х.Состояние = ПриостановкаКонструктором Тогда 
		
			ДатаНачалоПаузы = ПолучтьДанныеОПаузеКомплекта(х.Задача, ПриостановкаКонструктором);
			
			Если ДатаНачалоПаузы > ПустаяДата Тогда
				
				стДанные = Новый Структура();
				
				Для каждого ИмяКолонки Из НаименованиеКолонокТаблицы Цикл
				
					Если ИмяКолонки = "Состояние"  Тогда
						стДанные.Вставить(ИмяКолонки, ПриостановкаКонструктором);	
					ИначеЕсли ИмяКолонки = "ДатаНачала"  Тогда
						стДанные.Вставить(ИмяКолонки, ДатаНачалоПаузы);	
					ИначеЕсли ИмяКолонки = "ДатаНачалаПлан"  Тогда
						стДанные.Вставить(ИмяКолонки, ДатаНачалоПаузы);	
					ИначеЕсли ИмяКолонки = "ДатаОкончания"  Тогда
						стДанные.Вставить(ИмяКолонки, ТД);	
					ИначеЕсли ИмяКолонки = "ДатаОкончанияПлан"  Тогда
						стДанные.Вставить(ИмяКолонки, ТД);	
					ИначеЕсли ИмяКолонки = "Этап"  Тогда
						стДанные.Вставить(ИмяКолонки, "Пауза");	
					Иначе
						стДанные.Вставить(ИмяКолонки, х[ИмяКолонки]);	
					КонецЕсли;
				
				КонецЦикла; 
				
				мДобавитьСтроки.Добавить(стДанные);
				
			КонецЕсли;
		
		КонецЕсли;

		
		Если х.ЭтоПлан Тогда
			
			х.ДатаНачала 	= х.ДатаНачалаПлан;	
			
		КонецЕсли;

		Если х.ДатаНачала > ПустаяДата Тогда
			х.ДатаОкончания = омДаты.ДобавитьДниСУчетомВыходных(х.ДатаНачала, х.Продолжительность); 			
		КонецЕсли;
		
		Если х.ДатаНачала > ПустаяДата Тогда
		
			Если стПромежуточныеДанные.ДатаНачала = ПустаяДата Тогда
			
				стПромежуточныеДанные.ДатаНачала = х.ДатаНачала;
				
			ИначеЕсли стПромежуточныеДанные.ДатаНачала > х.ДатаНачала Тогда 
				
				стПромежуточныеДанные.ДатаНачала = х.ДатаНачала;
			
			КонецЕсли;
			
			Если стПромежуточныеДанные.ДатаОкончания < х.ДатаОкончания Тогда
			
				стПромежуточныеДанные.ДатаОкончания = х.ДатаОкончания;	
			
			КонецЕсли;
		
		КонецЕсли; 
		
		ДобавитьВМассив(х.Проект, 		мПроект);
		ДобавитьВМассив(х.Исполнитель, 	мИсполнитель);
		ДобавитьВМассив(х.ОбъектТЗ, 	мОбъектТЗ);
		
	КонецЦикла;
	
	Для каждого ЭлементМассиваДанных Из мДобавитьСтроки Цикл
	
		ЗаполнитьЗначенияСвойств(тзДанныеГанта.Добавить(), ЭлементМассиваДанных);
	
	КонецЦикла;
	
	стПромежуточныеДанные.Вставить("мОбъектТЗ", 	мОбъектТЗ);
	стПромежуточныеДанные.Вставить("мПроект", 		мПроект);
	стПромежуточныеДанные.Вставить("мИсполнитель", 	мИсполнитель);

	Возврат стПромежуточныеДанные; 

КонецФункции // ПолучитьпромежуточныеДанные()

Функция ПолучтьДанныеОПаузеКомплекта(Задача, ПриостановкаКонструктором)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаймингЗадачСрезПоследних.Период КАК Период
		|ИЗ
		|	РегистрСведений.ТаймингЗадач.СрезПоследних(
		|			,
		|			Задача = &Задача
		|				И Состояние = &Состояние) КАК ТаймингЗадачСрезПоследних";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	Запрос.УстановитьПараметр("Состояние", ПриостановкаКонструктором);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат НачалоДня(Выборка.Период);
	КонецЦикла;
	
	Возврат Дата(1, 1, 1);	

КонецФункции // ПолучтьДанныеОПаузеКомплекта()

Процедура ДобавитьВМассив(Элемент, МассивДляДобавления)

	Если МассивДляДобавления.Найти(Элемент) = Неопределено Тогда
	
		МассивДляДобавления.Добавить(Элемент);	
	
	КонецЕсли;	

КонецПроцедуры

Функция ПолучитьДанныеДиаграммы()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Задачи_ПКД.Ссылка КАК Задача,
		|	Задачи_ПКД.ОбъектТЗ КАК ОбъектТЗ,
		|	Задачи_ПКД.ОбъектТЗ.Проект КАК Проект,
		|	ПодразделенияСрезПоследних.Подразделение КАК Подразделение,
		|	Задачи_ПКД.ТипЗадачи КАК ТипЗадачи,
		|	""ПКД"" КАК Этап
		|ПОМЕСТИТЬ втЗадачи
		|ИЗ
		|	Задача.Задачи_ПКД КАК Задачи_ПКД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Подразделения.СрезПоследних КАК ПодразделенияСрезПоследних
		|		ПО Задачи_ПКД.Исполнитель.Сотрудник = ПодразделенияСрезПоследних.Сотрудник
		|			И (ПодразделенияСрезПоследних.НомерСтрокиТаблицы = 1)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Задачи_ПКД.ОбъектТЗ.Проект = &Проект
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Конструктор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Задачи_ПКД.Исполнитель = &Конструктор
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ПодразделенияСрезПоследних.Подразделение = &Подразделение
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ПодразделенияСрезПоследних.НомерСтрокиТаблицы = 1
		|		КОНЕЦ
		|	И НЕ Задачи_ПКД.Выполнена
		|	И Задачи_ПКД.ТипЗадачи В(&ТипЗадачи)
		|	И НЕ Задачи_ПКД.ОбобщеннаяЗадача
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Задачи_РКД.Ссылка,
		|	Задачи_РКД.ОбъектТЗ,
		|	Задачи_РКД.ОбъектТЗ.Проект,
		|	ПодразделенияСрезПоследних.Подразделение,
		|	Задачи_РКД.ТипЗадачи,
		|	""РКД""
		|ИЗ
		|	Задача.Задачи_РКД КАК Задачи_РКД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Подразделения.СрезПоследних КАК ПодразделенияСрезПоследних
		|		ПО Задачи_РКД.Исполнитель.Сотрудник = ПодразделенияСрезПоследних.Сотрудник
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Задачи_РКД.ОбъектТЗ.Проект = &Проект
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Конструктор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Задачи_РКД.Исполнитель = &Конструктор
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ПодразделенияСрезПоследних.Подразделение = &Подразделение
		|		КОНЕЦ
		|	И НЕ Задачи_РКД.Выполнена
		|	И ПодразделенияСрезПоследних.НомерСтрокиТаблицы = 1
		|	И Задачи_РКД.ТипЗадачи В(&ТипЗадачи)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втЗадачи.Задача КАК Задача,
		|	ТаймингЗадач.Состояние КАК Состояние,
		|	ТаймингЗадач.Исполнитель КАК Исполнитель,
		|	ТаймингЗадачКомментарии.Комментарий КАК Комментарий,
		|	ДанныеКомплекта.ВнешняяДатаНачалоПроектирования КАК НачалоПКД,
		|	ДанныеКомплекта.ВнешняяДатаОкончаниеПроектирования КАК КрайПКД,
		|	ДанныеКомплекта.ВнешняяДатаНачалоРКД КАК НачалоРКД,
		|	ДанныеКомплекта.ВнешняяДатаОкончаниеРКД КАК КрайРКД,
		|	ТаймингЗадачКомментарии.УИД КАК УИД,
		|	ДанныеКомплекта.Приоритет КАК Приоритет
		|ПОМЕСТИТЬ втТайминг
		|ИЗ
		|	втЗадачи КАК втЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймингЗадач.СрезПоследних(
		|				,
		|				НЕ Задача.Выполнена
		|					И ТипЗадачи В (&ТипЗадачи)
		|					И ВЫБОР
		|						КОГДА &Конструктор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|							ТОГДА ИСТИНА
		|						ИНАЧЕ Задача.Исполнитель = &Конструктор
		|					КОНЕЦ
		|					И НЕ Задача.ОбобщеннаяЗадача) КАК ТаймингЗадач
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймингЗадачКомментарии КАК ТаймингЗадачКомментарии
		|			ПО ТаймингЗадач.УИД = ТаймингЗадачКомментарии.УИД
		|		ПО втЗадачи.Задача = ТаймингЗадач.Задача
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних КАК ДанныеКомплекта
		|		ПО втЗадачи.ОбъектТЗ = ДанныеКомплекта.Комплект
		|ГДЕ
		|	НЕ(ДанныеКомплекта.ВнешняяДатаНачалоПроектирования = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ ДанныеКомплекта.ВнешняяДатаНачалоПроектирования ЕСТЬ NULL)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втЗадачи.ОбъектТЗ КАК ОбъектТЗ,
		|	ЕСТЬNULL(втТайминг.Исполнитель, втЗадачи.Задача.Исполнитель) КАК Исполнитель,
		|	втЗадачи.Подразделение КАК Подразделение,
		|	ЕСТЬNULL(втТайминг.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияЗадач.ОжиданиеКонструктором)) КАК Состояние,
		|	ЕСТЬNULL(втТайминг.Комментарий, """") КАК Комментарий,
		|	ВЫБОР
		|		КОГДА втЗадачи.Этап = ""ПКД""
		|			ТОГДА ЕСТЬNULL(втТайминг.НачалоПКД, ДАТАВРЕМЯ(1, 1, 1))
		|		ИНАЧЕ ЕСТЬNULL(втТайминг.НачалоРКД, ДАТАВРЕМЯ(1, 1, 1))
		|	КОНЕЦ КАК ДатаНачала,
		|	ВЫБОР
		|		КОГДА втЗадачи.Этап = ""ПКД""
		|			ТОГДА ЕСТЬNULL(втТайминг.КрайПКД, ДАТАВРЕМЯ(1, 1, 1))
		|		ИНАЧЕ ЕСТЬNULL(втТайминг.КрайРКД, ДАТАВРЕМЯ(1, 1, 1))
		|	КОНЕЦ КАК Край,
		|	ВЫБОР
		|		КОГДА ПлановоеЗначениеПроектированияСрезПоследних.ПланРазработкиВДнях ЕСТЬ NULL
		|			ТОГДА ВЫБОР
		|					КОГДА втЗадачи.Этап = ""ПКД""
		|						ТОГДА ЕСТЬNULL(ДатыКомплекта.НормаВремени_ПКД, 0)
		|					ИНАЧЕ ЕСТЬNULL(ДатыКомплекта.НормаВремени_РКД, 0)
		|				КОНЕЦ
		|		ИНАЧЕ ПлановоеЗначениеПроектированияСрезПоследних.ПланРазработкиВДнях
		|	КОНЕЦ КАК Продолжительность,
		|	втЗадачи.Проект КАК Проект,
		|	втЗадачи.Проект.Наименование КАК ПроектНаименование,
		|	ВЫБОР
		|		КОГДА втТайминг.Исполнитель ЕСТЬ NULL
		|			ТОГДА втЗадачи.Задача.Исполнитель.Наименование
		|		ИНАЧЕ втТайминг.Исполнитель.Наименование
		|	КОНЕЦ КАК ИсполнительНаименование,
		|	втЗадачи.Подразделение.Наименование КАК ПодразделениеНаименование,
		|	втЗадачи.ОбъектТЗ.Наименование КАК ОбъектТЗНаименование,
		|	втЗадачи.Задача КАК Задача,
		|	ЕСТЬNULL(втЗадачи.Задача.ОбобщеннаяЗадача, ЛОЖЬ) КАК ОбобщеннаяЗадача,
		|	ЕСТЬNULL(ПлановоеЗначениеПроектированияСрезПоследних.ДатаНачала, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачалаПлан,
		|	ЕСТЬNULL(ПлановоеЗначениеПроектированияСрезПоследних.ПланРазработкиВДнях, 0) КАК ПланРазработкиВДнях,
		|	втЗадачи.Этап КАК Этап,
		|	втТайминг.Приоритет КАК Приоритет,
		|	ЕСТЬNULL(ДатыКомплекта.ПереходРедакция, 0) + ЕСТЬNULL(ДатыКомплекта.ПереходРекламация, 0) + 1 КАК Переходы
		|ИЗ
		|	втЗадачи КАК втЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ втТайминг КАК втТайминг
		|		ПО втЗадачи.Задача = втТайминг.Задача
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановоеЗначениеПроектирования.СрезПоследних КАК ПлановоеЗначениеПроектированияСрезПоследних
		|		ПО втЗадачи.Задача = ПлановоеЗначениеПроектированияСрезПоследних.Задача
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыКомплекта КАК ДатыКомплекта
		|		ПО втЗадачи.ОбъектТЗ = ДатыКомплекта.Комплект
		|ГДЕ
		|	(ВЫБОР
		|				КОГДА втЗадачи.Этап = ""ПКД""
		|					ТОГДА ЕСТЬNULL(втТайминг.НачалоПКД, ДАТАВРЕМЯ(1, 1, 1))
		|				ИНАЧЕ ЕСТЬNULL(втТайминг.НачалоРКД, ДАТАВРЕМЯ(1, 1, 1))
		|			КОНЕЦ > ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ЕСТЬNULL(ПлановоеЗначениеПроектированияСрезПоследних.ДатаНачала, ДАТАВРЕМЯ(1, 1, 1)) > ДАТАВРЕМЯ(1, 1, 1))";
	
	Запрос.УстановитьПараметр("Конструктор", 	Конструктор);
	Запрос.УстановитьПараметр("Подразделение", 	Подразделение);
	Запрос.УстановитьПараметр("Проект", 		Проект);
	Запрос.УстановитьПараметр("ТипЗадачи", 		омЗадачиПКДПовтор.ТипыЗадачКонструктора());
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ПолучитьДанныеДиаграммы()      

#КонецОбласти

#Область ИзменениеФормы

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Сотрудник") Тогда
		Если ТипЗнч(Параметры.Сотрудник) = Тип("СправочникСсылка.Пользователи") Тогда
			Конструктор 	= омСотрудники.ПолучитьСотруднткаПоПользователю(Параметры.Сотрудник);
		Иначе
			Конструктор 	= Параметры.Сотрудник;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Проект") Тогда
		Проект 		= Параметры.Проект;
	КонецЕсли;
	
	Если НЕ (РольДоступна("ГлавныйКонструктор") ИЛИ РольДоступна("ПолныеПрава")) Тогда
		
		Элементы.Подразделение.Видимость 	= Ложь;
		Элементы.Конструктор.ТолькоПросмотр = Истина;
		ЭтаФорма.ТолькоПросмотр 			= Истина;
		
	КонецЕсли;
	
	Диаграмма.ОтображатьЛегенду = Ложь;
	ВыводитьПроект				= Истина;
	ОбновитьГантНаСервере();

КонецПроцедуры

#КонецОбласти 

#Область ИзменениеРеквизитов
	
&НаКлиенте
Процедура ВыводитьПроектПриИзменении(Элемент)
	ОбновитьГантНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СортировкаПриИзменении(Элемент)
	ОбновитьГантНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КонструкторПриИзменении(Элемент)
	ОбновитьГантНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ОбновитьГантНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	ОбновитьГантНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаПриОкончанииРедактированияИнтервала(Элемент, Интервал, ОтменаРедактирования)
	
	Интервал.Начало = НачалоДня(Интервал.Начало);
	
	Если Интервал.Начало < НачалоДня(ТекущаяДата()) Тогда
		ОтменаРедактирования = Истина;		
		Возврат;
	КонецЕсли;
	
	Продолжительность = ?(Интервал.Расшифровка.Продолжительность = 0, 1, Интервал.Расшифровка.Продолжительность);
	Интервал.Конец 	= омДаты.ДобавитьДниСУчетомВыходных(Интервал.Начало, Продолжительность);
	
	Если Интервал.Расшифровка.Свойство("Задача") Тогда
		ЗаписатьИзменениеВРегистр(Интервал.Начало, Интервал.Расшифровка.Задача, Продолжительность);
		
		Для каждого х Из тзДанныеГанта Цикл
			Если Интервал.Расшифровка.Задача = х.Задача И Интервал.Расшифровка.Этап = х.Этап Тогда
				х.ДатаНачала = Интервал.Начало;
				
				ВывестиПроект(Интервал.Расшифровка.Проект, 				, Истина);
				ВывестиИсполнитель(Интервал.Расшифровка.Исполнитель, 	, Истина);

			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область ЗаписьВРегистры

&НаСервереБезКонтекста
Процедура ЗаписатьИзменениеВРегистр(Начало, Задача, Продолжительность)
	
	МенеджерЗаписи = РегистрыСведений.ПлановоеЗначениеПроектирования.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Задача 				= Задача;
	МенеджерЗаписи.Период 				= ТекущаяДата();
	МенеджерЗаписи.Автор 				= ПараметрыСеанса.ТекущийПользователь;
	МенеджерЗаписи.ДатаНачала 			= Начало;
	МенеджерЗаписи.ПланРазработкиВДнях 	= Продолжительность;
	
	МенеджерЗаписи.Записать();
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаОбработкаРасшифровки(Элемент, Расшифровки, СтандартнаяОбработка, Дата)
	
	СтандартнаяОбработка = Ложь;
	
	Для каждого текЭлемент Из Расшифровки Цикл
		
		Если текЭлемент = Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		
		Если текЭлемент.Свойство("Задача") Тогда
			
			Если ЗначениеЗаполнено(текЭлемент.Задача) Тогда
				ПоказатьЗначение(,текЭлемент.Задача);	
			КонецЕсли;	
			
		КонецЕсли;
	
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаВыбор(Элемент, Значения, СтандартнаяОбработка, Дата)
	Если Тип("ТочкаДиаграммыГанта") = ТипЗнч(Значения) Тогда
		Если Тип("СправочникСсылка.ИзделияКомплект") = ТипЗнч(Значения.Значение) ИЛИ 
			Тип("СправочникСсылка.Композиции") = ТипЗнч(Значения.Значение) ИЛИ
			Тип("СправочникСсылка.Проекты") = ТипЗнч(Значения.Значение) Тогда
				СтандартнаяОбработка = Ложь;
				ПоказатьЗначение(,Значения.Значение);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


#КонецОбласти