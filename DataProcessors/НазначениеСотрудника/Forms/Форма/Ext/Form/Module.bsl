
Процедура СохранитьНаСервере()
	
	ДОбъект = РеквизитФормыВЗначение("ДеревоСотрудники");
	
	Для каждого строкаКомпозиция Из ДОбъект.Строки Цикл
	
		Если строкаКомпозиция.Изменен Тогда
			УстановитьСотрудников(строкаКомпозиция.Композиция, строкаКомпозиция);
			строкаКомпозиция.Изменен = Ложь;
		КонецЕсли;
		
		Для каждого строкаКомплекты Из строкаКомпозиция.Строки Цикл
			
			Если строкаКомплекты.Изменен Тогда
				УстановитьСотрудников(строкаКомплекты.Комплект, строкаКомплекты);	
				строкаКомплекты.Изменен = Ложь;
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЦикла; 
	
	ЗначениеВРеквизитФормы(ДОбъект, "ДеревоСотрудники");
	
КонецПроцедуры

Процедура УстановитьСотрудников(ОбъектИзменения, строкаДанных)

	МенеджерЗаписи = РегистрыСведений.ИзменениеСотрудника.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ОбъектИзменения = ОбъектИзменения;
	МенеджерЗаписи.Прочитать();
	
	Если ЗначениеЗаполнено(строкаДанных.Менеджер) Тогда
		МенеджерЗаписи.Менеджер = строкаДанных.Менеджер;
	ИначеЕсли НЕ ЗначениеЗаполнено(МенеджерЗаписи.Менеджер) Тогда
		МенеджерЗаписи.Менеджер = Менеджер;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(строкаДанных.ВедущийКонструктор) Тогда
 	   МенеджерЗаписи.ВедущийКонструктор = строкаДанных.ВедущийКонструктор;
	ИначеЕсли НЕ ЗначениеЗаполнено(МенеджерЗаписи.ВедущийКонструктор) Тогда
		МенеджерЗаписи.ВедущийКонструктор = ВедущийКонструктор;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(строкаДанных.Дизайнер) Тогда
 	   МенеджерЗаписи.Дизайнер = строкаДанных.Дизайнер;
	ИначеЕсли НЕ ЗначениеЗаполнено(МенеджерЗаписи.Дизайнер) Тогда
		МенеджерЗаписи.Дизайнер = Дизайнер;
	КонецЕсли;
	
    МенеджерЗаписи.ОбъектИзменения = ОбъектИзменения;
	МенеджерЗаписи.Период = ТекущаяДата();
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьНаСервере();
КонецПроцедуры

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	Дата = ТекущаяДата();
	
	ДоступностьКолонок();
	
КонецПроцедуры 

Процедура ДоступностьКолонок()
	
	Если НЕ (РольДоступна("АдминистраторСистемы") ИЛИ РольДоступна("ПолныеПрава")) Тогда
		
		Если НЕ РольДоступна("Дизайнер") Тогда
			Элементы.ТаблицаСотрудникиДизайнер.ТолькоПросмотр = Истина;
		КонецЕсли;  
		
		Если НЕ РольДоступна("ГлавныйКонструктор") Тогда
			Элементы.ТаблицаСотрудникиВедущийКонструктор.ТолькоПросмотр = Истина;
		КонецЕсли;  
		
		Элементы.ТаблицаСотрудникиМенеджер.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроектПриИзмененииНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИзделияКомплект.Ссылка КАК Комплект,
	|	ИзделияКомплект.Родитель.Композиция КАК Композиция,
	|	ИзменениеСотрудникаСрезПоследних.Менеджер КАК Менеджер,
	|	ИзменениеСотрудникаСрезПоследних.ВедущийКонструктор КАК ВедущийКонструктор,
	|	ИзменениеСотрудникаСрезПоследних.Дизайнер КАК Дизайнер
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	Справочник.ИзделияКомплект КАК ИзделияКомплект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзменениеСотрудника.СрезПоследних(&Дата, ) КАК ИзменениеСотрудникаСрезПоследних
	|		ПО ИзделияКомплект.Ссылка = ИзменениеСотрудникаСрезПоследних.ОбъектИзменения
	|ГДЕ
	|	ИзделияКомплект.Проект = &Проект
	|	И НЕ ИзделияКомплект.ЭтоГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Проекты.Дизайнер КАК Дизайнер,
	|	Проекты.Менеджер КАК Менеджер,
	|	Проекты.ВедущийКонструктор КАК ВедущийКонструктор,
	|	Проекты.Сметчик КАК Сметчик
	|ИЗ
	|	Справочник.Проекты КАК Проекты
	|ГДЕ
	|	Проекты.Ссылка = &Проект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК Флаг,
	|	вт.Комплект КАК Комплект,
	|	вт.Композиция КАК Композиция,
	|	ВЫБОР
	|		КОГДА ИзменениеСотрудникаСрезПоследних.Менеджер ЕСТЬ NULL
	|			ТОГДА вт.Менеджер
	|		ИНАЧЕ ИзменениеСотрудникаСрезПоследних.Менеджер
	|	КОНЕЦ КАК Менеджер,
	|	ВЫБОР
	|		КОГДА ИзменениеСотрудникаСрезПоследних.ВедущийКонструктор ЕСТЬ NULL
	|			ТОГДА вт.ВедущийКонструктор
	|		ИНАЧЕ ИзменениеСотрудникаСрезПоследних.ВедущийКонструктор
	|	КОНЕЦ КАК ВедущийКонструктор,
	|	ВЫБОР
	|		КОГДА ИзменениеСотрудникаСрезПоследних.Дизайнер ЕСТЬ NULL
	|			ТОГДА вт.Дизайнер
	|		ИНАЧЕ ИзменениеСотрудникаСрезПоследних.Дизайнер
	|	КОНЕЦ КАК Дизайнер
	|ИЗ
	|	вт КАК вт
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзменениеСотрудника.СрезПоследних(&Дата, ) КАК ИзменениеСотрудникаСрезПоследних
	|		ПО вт.Композиция = ИзменениеСотрудникаСрезПоследних.ОбъектИзменения
	|
	|УПОРЯДОЧИТЬ ПО
	|	вт.Композиция.Наименование,
	|	вт.Комплект.Наименование
	|ИТОГИ
	|	ИСТИНА КАК Флаг
	|ПО
	|	Композиция";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	Запрос.УстановитьПараметр("Дата", 	КонецДня(Дата));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[2].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам); // Выгрузка в дерево значений!
	
	// Выгрузка результата запроса в дерево
	КоллекцияЭлементовДерева = ДеревоСотрудники.ПолучитьЭлементы();
	КоллекцияЭлементовДерева.Очистить();
	ОбщегоНазначения.ЗаполнитьКоллекциюЭлементовДереваДанныхФормы(КоллекцияЭлементовДерева, Выборка);
	
	Выборка = РезультатЗапроса[1].Выбрать();
	
	ПустойСотрудник 	= Справочники.Сотрудники.ПустаяСсылка();
	ВедущийКонструктор 	= ПустойСотрудник; 
	Дизайнер 			= ПустойСотрудник; 
	Менеджер 			= ПустойСотрудник; 
	Сметчик 			= ПустойСотрудник; 
	
	
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Выборка);	
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	ПроектПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСотрудникиПередНачаломИзменения(Элемент, Отказ)
	ТД = Элементы.ДеревоСотрудники.ТекущиеДанные;
	
	Отказ = НЕ (ЗначениеЗаполнено(ТД.Композиция) ИЛИ ЗначениеЗаполнено(ТД.Комплект));
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСотрудникиПриИзменении(Элемент)
	
	ТД = Элементы.ДеревоСотрудники.ТекущиеДанные;
	
	Если НЕ ТД = Неопределено Тогда
		ТД.Изменен = Истина;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСотрудникиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСотрудникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ПроектПриИзмененииНаСервере();
КонецПроцедуры

