&НаКлиенте
Перем ПодчиненыеЭлеметыСтрокиДерева;

Процедура СохранитьНаСервере()
	
	Автор = ПараметрыСеанса.ТекущийПользователь;
	ДОбъект = РеквизитФормыВЗначение("ДеревоСотрудники");
	
	Для каждого строкаКомпозиция Из ДОбъект.Строки Цикл
	
		Для каждого строкаКомплекты Из строкаКомпозиция.Строки Цикл
			
			Если строкаКомплекты.Изменен Тогда
				
				Если ЗначениеЗаполнено(строкаКомплекты.Комплект) Тогда
					УстановитьСотрудников(строкаКомплекты.Комплект, 	Автор, строкаКомплекты);	
				Иначе
					УстановитьСотрудников(строкаКомплекты.Композиция, 	Автор, строкаКомплекты);	
				КонецЕсли;
				строкаКомплекты.Изменен = Ложь;
				
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЦикла; 
	
	ЗначениеВРеквизитФормы(ДОбъект, "ДеревоСотрудники");
	
КонецПроцедуры

Процедура УстановитьСотрудников(ОбъектИзменения, Автор, строкаДанных)

	МенеджерЗаписи = РегистрыСведений.ИзменениеСотрудника.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ОбъектИзменения = ОбъектИзменения;
	МенеджерЗаписи.Прочитать();
	
	Если ЗначениеЗаполнено(строкаДанных.Менеджер) Тогда
		МенеджерЗаписи.Менеджер = строкаДанных.Менеджер;
	ИначеЕсли НЕ ЗначениеЗаполнено(МенеджерЗаписи.Менеджер) Тогда
		МенеджерЗаписи.Менеджер = Менеджер;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(строкаДанных.ВедущийКонструктор) Тогда
 	   МенеджерЗаписи.ВедущийКонструктор = строкаДанных.ВедущийКонструктор;
	ИначеЕсли НЕ ЗначениеЗаполнено(МенеджерЗаписи.ВедущийКонструктор) Тогда
		МенеджерЗаписи.ВедущийКонструктор = ВедущийКонструктор;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(строкаДанных.Дизайнер) Тогда
 	   МенеджерЗаписи.Дизайнер = строкаДанных.Дизайнер;
	ИначеЕсли НЕ ЗначениеЗаполнено(МенеджерЗаписи.Дизайнер) Тогда
		МенеджерЗаписи.Дизайнер = Дизайнер;
	КонецЕсли;
	
    МенеджерЗаписи.ОбъектИзменения 	= ОбъектИзменения;
    МенеджерЗаписи.Автор 			= Автор;
	МенеджерЗаписи.Период 			= ТекущаяДата();
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьНаСервере();
КонецПроцедуры

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	Дата = ТекущаяДата();
	
	ДоступностьКолонок();
	
КонецПроцедуры 

Процедура ДоступностьКолонок()
	
	Если НЕ (РольДоступна("АдминистраторСистемы") ИЛИ РольДоступна("ПолныеПрава")) Тогда
		
		Если НЕ РольДоступна("Дизайнер") Тогда
			Элементы.ТаблицаСотрудникиДизайнер.ТолькоПросмотр = Истина;
		КонецЕсли;  
		
		Если НЕ РольДоступна("ГлавныйКонструктор") Тогда
			Элементы.ТаблицаСотрудникиВедущийКонструктор.ТолькоПросмотр = Истина;
		КонецЕсли;  
		
		Элементы.ТаблицаСотрудникиМенеджер.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроектПриИзмененииНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИзделияКомплект.Ссылка КАК Комплект,
	|	ИзделияКомплект.Родитель.Композиция КАК Композиция,
	|	ВЫБОР
	|		КОГДА ИзменениеСотрудникаСрезПоследних.Менеджер ЕСТЬ NULL
	|			ТОГДА ИзделияКомплект.Проект.Менеджер
	|		ИНАЧЕ ИзменениеСотрудникаСрезПоследних.Менеджер
	|	КОНЕЦ КАК Менеджер,
	|	ВЫБОР
	|		КОГДА ИзменениеСотрудникаСрезПоследних.ВедущийКонструктор ЕСТЬ NULL
	|			ТОГДА ИзделияКомплект.Проект.ВедущийКонструктор
	|		ИНАЧЕ ИзменениеСотрудникаСрезПоследних.ВедущийКонструктор
	|	КОНЕЦ КАК ВедущийКонструктор,
	|	ВЫБОР
	|		КОГДА ИзменениеСотрудникаСрезПоследних.Дизайнер ЕСТЬ NULL
	|			ТОГДА ИзделияКомплект.Проект.Дизайнер
	|		ИНАЧЕ ИзменениеСотрудникаСрезПоследних.Дизайнер
	|	КОНЕЦ КАК Дизайнер,
	|	ДанныеКомплектаСрезПоследних.Конструктор КАК Конструктор
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	Справочник.ИзделияКомплект КАК ИзделияКомплект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзменениеСотрудника.СрезПоследних(
	|				&Дата,
	|				ОбъектИзменения В
	|					(ВЫБРАТЬ
	|						ИзделияКомплект.Ссылка
	|					ИЗ
	|						Справочник.ИзделияКомплект КАК ИзделияКомплект
	|					ГДЕ
	|						ИзделияКомплект.Проект = &Проект
	|						И НЕ ИзделияКомплект.ЭтоГруппа)) КАК ИзменениеСотрудникаСрезПоследних
	|		ПО ИзделияКомплект.Ссылка = ИзменениеСотрудникаСрезПоследних.ОбъектИзменения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних(
	|				&Дата,
	|				Комплект В
	|					(ВЫБРАТЬ
	|						ИзделияКомплект.Ссылка
	|					ИЗ
	|						Справочник.ИзделияКомплект КАК ИзделияКомплект
	|					ГДЕ
	|						ИзделияКомплект.Проект = &Проект
	|						И НЕ ИзделияКомплект.ЭтоГруппа)) КАК ДанныеКомплектаСрезПоследних
	|		ПО (ДанныеКомплектаСрезПоследних.Комплект = ИзделияКомплект.Ссылка)
	|ГДЕ
	|	ИзделияКомплект.Проект = &Проект
	|	И НЕ ИзделияКомплект.ЭтоГруппа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ИзделияКомплект.ПустаяСсылка),
	|	Композиции.Ссылка,
	|	ВЫБОР
	|		КОГДА ИзменениеСотрудникаСрезПоследних.Менеджер ЕСТЬ NULL
	|			ТОГДА Композиции.Проект.Менеджер
	|		ИНАЧЕ ИзменениеСотрудникаСрезПоследних.Менеджер
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИзменениеСотрудникаСрезПоследних.ВедущийКонструктор ЕСТЬ NULL
	|			ТОГДА Композиции.Проект.ВедущийКонструктор
	|		ИНАЧЕ ИзменениеСотрудникаСрезПоследних.ВедущийКонструктор
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИзменениеСотрудникаСрезПоследних.Дизайнер ЕСТЬ NULL
	|			ТОГДА Композиции.Проект.Дизайнер
	|		ИНАЧЕ ИзменениеСотрудникаСрезПоследних.Дизайнер
	|	КОНЕЦ,
	|	ДанныеКомплектаСрезПоследних.Конструктор
	|ИЗ
	|	Справочник.Композиции КАК Композиции
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзменениеСотрудника.СрезПоследних(
	|				&Дата,
	|				ОбъектИзменения В
	|					(ВЫБРАТЬ
	|						Композиции.Ссылка
	|					ИЗ
	|						Справочник.Композиции КАК Композиции
	|					ГДЕ
	|						Композиции.Проект = &Проект
	|						И НЕ Композиции.ЭтоГруппа)) КАК ИзменениеСотрудникаСрезПоследних
	|		ПО Композиции.Ссылка = ИзменениеСотрудникаСрезПоследних.ОбъектИзменения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеКомплекта.СрезПоследних(
	|				&Дата,
	|				Комплект В
	|					(ВЫБРАТЬ
	|						Композиции.Ссылка
	|					ИЗ
	|						Справочник.Композиции КАК Композиции
	|					ГДЕ
	|						Композиции.Проект = &Проект
	|						И НЕ Композиции.ЭтоГруппа)) КАК ДанныеКомплектаСрезПоследних
	|		ПО (ДанныеКомплектаСрезПоследних.Комплект = Композиции.Ссылка)
	|ГДЕ
	|	Композиции.Проект = &Проект
	|	И НЕ Композиции.ЭтоГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Проекты.Дизайнер КАК Дизайнер,
	|	Проекты.Менеджер КАК Менеджер,
	|	Проекты.ВедущийКонструктор КАК ВедущийКонструктор,
	|	Проекты.Сметчик КАК Сметчик
	|ИЗ
	|	Справочник.Проекты КАК Проекты
	|ГДЕ
	|	Проекты.Ссылка = &Проект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК Флаг,
	|	вт.Комплект КАК Комплект,
	|	вт.Композиция КАК Композиция,
	|	вт.Менеджер КАК Менеджер,
	|	вт.ВедущийКонструктор КАК ВедущийКонструктор,
	|	вт.Дизайнер КАК Дизайнер,
	|	вт.Конструктор КАК Конструктор
	|ИЗ
	|	вт КАК вт
	|
	|УПОРЯДОЧИТЬ ПО
	|	вт.Композиция.Наименование,
	|	вт.Комплект.Наименование
	|ИТОГИ
	|	ИСТИНА КАК Флаг
	|ПО
	|	Композиция";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	Запрос.УстановитьПараметр("Дата", 	КонецДня(Дата));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[2].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам); // Выгрузка в дерево значений!
	
	// Выгрузка результата запроса в дерево
	КоллекцияЭлементовДерева = ДеревоСотрудники.ПолучитьЭлементы();
	КоллекцияЭлементовДерева.Очистить();
	ОбщегоНазначения.ЗаполнитьКоллекциюЭлементовДереваДанныхФормы(КоллекцияЭлементовДерева, Выборка);
	
	Выборка = РезультатЗапроса[1].Выбрать();
	
	ПустойСотрудник 	= Справочники.Сотрудники.ПустаяСсылка();
	ВедущийКонструктор 	= ПустойСотрудник; 
	Дизайнер 			= ПустойСотрудник; 
	Менеджер 			= ПустойСотрудник; 
	Сметчик 			= ПустойСотрудник; 
	
	
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Выборка);	
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	ПроектПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСотрудникиПередНачаломИзменения(Элемент, Отказ)
	
	ТД = Элементы.ДеревоСотрудники.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТД.Композиция) Тогда
		ПодчиненыеЭлеметыСтрокиДерева = Элементы.ДеревоСотрудники.ТекущиеДанные.ПолучитьЭлементы();
	Иначе
		Отказ= Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСотрудникиПриИзменении(Элемент)
	
	ТД = Элементы.ДеревоСотрудники.ТекущиеДанные;
	
	Если НЕ ТД = Неопределено Тогда
		
		ТекущаяКомпозиция = ТД.Композиция;
		ТекущийКонструктор = Неопределено;
		
		Если ЗначениеЗаполнено(ТекущаяКомпозиция) Тогда
			
			Если ПодчиненыеЭлеметыСтрокиДерева.Количество() > 0 Тогда
				
				ПустойКомплект = ПредопределенноеЗначение("Справочник.ИзделияКомплект.ПустаяСсылка");
				
				
				
				Для каждого СтрокаДерева Из ПодчиненыеЭлеметыСтрокиДерева Цикл
					
					Если СтрокаДерева.Комплект = ПустойКомплект И ТекущаяКомпозиция = СтрокаДерева.Композиция  Тогда
						ТекущийКонструктор = СтрокаДерева.Конструктор; 
						Прервать;
					КонецЕсли;	
					
				КонецЦикла;
				
			Иначе	
				ТД.Изменен = Истина;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекущийКонструктор) Тогда
				
				ИмяКолонки = СтрЗаменить(Элемент.ТекущийЭлемент.Имя, "ТаблицаСотрудники", "");
			
				Для каждого СтрокаДерева Из ПодчиненыеЭлеметыСтрокиДерева Цикл
					Если СтрокаДерева.Конструктор 	= ТекущийКонструктор Тогда
						СтрокаДерева.Изменен 		= Истина;
						СтрокаДерева[ИмяКолонки] 	= ТД[ИмяКолонки];
					КонецЕсли;
				КонецЦикла;
				
				ТД[ИмяКолонки] = Неопределено;
			
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСотрудникиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСотрудникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ПроектПриИзмененииНаСервере();
	
	Элементы.ФормаСохранить.Доступность = Дата = НачалоДня(ТекущаяДата());
	
КонецПроцедуры

